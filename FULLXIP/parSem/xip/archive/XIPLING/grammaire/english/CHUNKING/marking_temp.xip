
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2004 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : English Normalization Temporal XIP grammar
//!   filename   : marking_temp.xip
//!   Date       : December 2004
//!   Author     : Caroline Hagege
//!   Description : Marking rules for complex Temporal expressios
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!





// Marking with specific features complex temporal NPs coming from reshuffling rules or not
// adding feature c_set when the complex NP contains num period each period
//40> NP[c_set=+]{num, noun[period], quant[lemme:each], noun[period]}.


DependencyRules


| NP[tempexpr=+]{?*,nadj[period,last]} |
	~
| PP[tempexpr=+]{prep, NP{?*,nadj[period,last:+]}} |
	~

// Marking certain elements with temporal features in a certain context
// e.g. quarter
| NP[tempexpr=+]{?*,noun[lemme:quarter, tempexpr=+, period=+, c_relat=+]}, PP{prep[lemme:to], NP{?*,?[num]}} |
	~

| NP[tempexpr=+]{?[lemme:this],noun[lemme:quarter, tempexpr=+, period=+, c_relat=+]} |
	~
| NP[tempexpr=+]{(det),?[preperiod:+],noun[lemme:quarter, tempexpr=+, period=+, c_relat=+]} |
	~
| NP[tempexpr=+]{?*,num[ord],noun[lemme:quarter, tempexpr=+, period=+, c_relat=+]}, PP[tempexpr] |
	~


// When it is a date:+, it is also a c_absolut
| NP[tempexpr, c_absolut=+]{noun[mwe:+,date:+]} |
  ~

// When a temporal expression is an interval then it is a duration
| PP[tempexpr, interval, c_dur=+] | 
  ~


// num <period> each <period> has to bear feature c_set (from reshuffling)
| NP[tempexpr,c_set=+]{?[card:+], noun[period], quant[lemme:each], noun[period]} |
  ~


/// Marking temporal referential expressions
| PP[tempexpr=+,reftime=+,before=+]{prep[reftime:+,before:+], ?[lemme:that]} | 
  ~
| PP[tempexpr=+,reftime=+,after=+]{prep[reftime:+,after:+], ?[lemme:that], (?[period])} | 
  ~
| PP[tempexpr=+,reftime=+,at=+]{prep[reftime:+,at:+], ?[lemme:that]} | 
~


| PP[tempexpr=+,reftime=+,before=+]{prep[reftime:+,before:+], NP[tempexpr]{?[lemme:that], ?*, noun[period,last:+]}} | 
  ~
| PP[tempexpr=+,reftime=+,after=+]{prep[reftime:+,after:+], NP[tempexpr]{?[lemme:that], ?*, noun[period,last:+]}} | 
  ~
| PP[tempexpr=+,reftime=+,at=+]{prep[reftime:+,at:+], NP[tempexpr]{?[lemme:that], ?*, noun[period,last:+]}} | 
~


// from that time on
| PP[tempexpr=+,reftime=+,after=+]{prep[lemme:from], NP[tempexpr]{?[lemme:that], ?*, noun[period,last:+]}} |
~



// Marking incomplete tempexpr (need another reference)
| PP[tempexpr=+, miss_year=+]{prep, NP{?*,noun[last:+,month]}} |
~


