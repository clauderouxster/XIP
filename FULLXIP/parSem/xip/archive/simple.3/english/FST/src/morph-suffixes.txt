!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!  suffixes                                                   
!
!  Description: Suffixes for the English morphological analyzer
!
!  Author: Ágnes Sándor
!
!  Date: September 2002
!
!  Copyright (c) 2002 Xerox Corporation.  All Rights Reserved.
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!# new lexicon defintions by A. Schiller

LEXICON NOUNACRON	 ! e.g. API -> APIs, API's
 < %+Noun:0 %+Sg:0 (%+Gen:%^GenSg) %+Acron:0 >			#;
 < %+Noun:0 (0:%') %+Pl:s (%+Gen:%^GenPl) %+Acron:0 >		#;

LEXICON NOUNACRON_S	  ! e.g. BIOS -> BIOSs/BIOSes/BIOS's
 < %+Noun:0 %+Sg:0 (%+Gen:%^GenSg) %+Acron:0 >			#;
 < %+Noun:0 (0:[%'|e]) %+Pl:s (%+Gen:%^GenPl) %+Acron:0 >	#;

! ------------------------------------------------------------------------------

LEXICON SGPL
+Sg:0 	SGENDING ;
+Pl:s%}	PLENDING ;

LEXICON SGENDING
	# ;
+Gen:'s	# ;

LEXICON PLENDING
	# ;
+Gen:'	# ;


LEXICON PROPPARTICLE		!!AnS!!
+Prop+Particle:0	#;

LEXICON PROPABBR
+Prop+Abbr:0	# ;

LEXICON ADJABBR
+Adj+Abbr:0	# ;

LEXICON DEGABBR
+Deg+Abbr:0	# ;

LEXICON NOUNABBR
+Noun+Sg+Abbr:0	# ;
+Noun+Pl+Abbr:s	# ;


LEXICON ADVABBR
+Adv+Abbr:0	# ;



LEXICON PROPMISCSG
+Prop+Misc:0		SGENDING ;


LEXICON PROPMISCPL
+Prop+Misc:0		# ;
+Prop+Misc+Gen:'		# ;
+Prop+Misc+Gen:'s		# ;

LEXICON PROPMISCABBR
+Prop+Misc+Abbr:0		# ;

LEXICON PROPMISCACRON
+Prop+Misc+Acron:0		SGENDING ;






LEXICON LET
+Let+Sg:0		# ;
+Let+Pl:'s		# ;

LEXICON INIT
+Init:0		# ;



LEXICON FOR
+For:0		# ;

LEXICON INTERJ
+Interj:0		# ;

LEXICON DETINDEFSG
+Det+Indef+Sg:0		# ;

LEXICON DETWHSP
+Det+Wh+SP:0		# ;

LEXICON DETDEFSP
+Det+Def+SP:0		# ;

LEXICON DETPL
+Det+Pl:0		# ;

LEXICON DETSG
+Det+Sg:0		# ;

LEXICON DETSP
+Det+SP:0		# ;

LEXICON ONOM
+Onom:0		# ;

LEXICON PREP
+Prep:0		# ;

LEXICON CONJSUB
+Conj+Sub:0		# ;

LEXICON CONJCOORD
+Conj+Coord:0		# ;

LEXICON NOUNSGPL
+Noun:0		SGPL ;

LEXICON NOUNSING
+Noun+Sg:0		SGENDING ;

LEXICON NOUNPLUR
+Noun+Pl:0	# ;
+Noun+Pl+Gen:'^S	# ;

LEXICON NOUNSP
+Noun+SP:0	# ;
+Noun+SP+Gen:'^S	# ;



LEXICON NOUNSGPL-AP
+Noun:0		SGPL-AP ;

LEXICON SGPL-AP
+Sg:0	SGENDING ;
	NOUNPL-AP ;

LEXICON NOUNPL-AP
+Pl:'s%}	PLENDING ;

LEXICON ADJ
+Adj:0	# ;

LEXICON ADV
+Adv:0		# ;

LEXICON ADJ-ADV	  ! AnS!
	ADJ;
	ADV;

LEXICON ADVWH
+Adv+Wh:0		# ;

LEXICON ADVCOMP
+Adv+Comp:er%}	# ;

LEXICON ADVSUP
+Adv+Sup:est%}	# ;

LEXICON ADJ-NOUNSGPL
+NAdj+Sg:0		# ;
+Noun+Sg+Gen:'s		# ;
+Noun+Pl:s%}		PLENDING ;
			
LEXICON ADJ-NOUNSP	!AnS
+NAdj+SP:0		# ;
+Noun+SP+Gen:'s		# ;


LEXICON ADJ-NOUNSG
+NAdj+Sg:0		# ;
+Noun+Sg+Gen:'s		# ;

LEXICON ADJCOMP
+Adj+Comp:er%}	# ;

LEXICON ADJSUP
+Adj+Sup:est%}	# ;

LEXICON ADJCOMP-SUP
		ADJCOMP ;
		ADJSUP ;



LEXICON ADJVPAP	    !AnS!
+Adj+VPap:0	#;


LEXICON EDDERIV
ed}		ADJVPAP ;	!AnS!
ed}ly}		ADV ;
ed}ness}		NOUNSGPL ;

LEXICON PASTBOTH123
+Verb+PastBoth+123SP:ed%}              # ;

LEXICON PRES3
+Verb+Pres+3sg:s%}            # ;

LEXICON PRESN3
+Verb+Pres+Non3sg:0         # ;

LEXICON PROG
+Verb+Prog:ing%}                # ;

LEXICON PRES-PROG
		PRES3 ;
		PRESN3 ;
		PROG ;

LEXICON VERB4
		PASTBOTH123 ;
		PRES-PROG ;

LEXICON LY-NESS
ly}		ADV ;
ness}		NOUNSGPL ;

LEXICON INGDERIV
0:ing}		ING-ADJ-NOUNSGPL ;
ing}		LY-NESS ;

LEXICON ING-ADJ-NOUNSGPL
+Adj+VProg:0	# ;
+Noun+VProg+Sg:0	SGENDING ;
+Noun+VProg+Pl:s%}	PLENDING ;

LEXICON VERB4-ED-ING
		VERB4 ;
		EDDERIV ;
		INGDERIV ;

LEXICON ADJVPAP-LY-NESS		!AnS!
		ADJVPAP ;
		LY-NESS ;


LEXICON ADJ-LY-NESS
		ADJ ;
		LY-NESS ;

LEXICON ABLEDERIV
able}		ADJ-LY-NESS ;
able}ity}		NOUNSGPL ;

LEXICON ISM-IST
ism}		NOUNSGPL ;
ist}		NOUNSGPL ;

LEXICON ALDERIV
al}		ADJ-LY-NESS ;
al}		ISM-IST ;
al}ity}		NOUNSGPL ;
al}ize}		VERB4-ED-ING ;
al}ise}		VERB4-ED-ING ;
al}ize}		ABLEDERIV ;
al}ise}		ABLEDERIV ;
al}ize}er}	NOUNSGPL ;
al}ise}er}	NOUNSGPL ;
al}ize}ion}	NOUNSGPL ;
al}ise}ion}	NOUNSGPL ;

LEXICON IZEDERIV
ize}		VERB4-ED-ING ;
ise}		VERB4-ED-ING ;
ize}		ABLEDERIV ;
ise}		ABLEDERIV ;
ize}er}		NOUNSGPL ;
ise}er}		NOUNSGPL ;
ize}ion}	NOUNSGPL ;
ise}ion}	NOUNSGPL ;

LEXICON IFYDERIV
ify}		VERB4-ED-ING ;
ify}		ABLEDERIV ;
ify}er}		NOUNSGPL ;
ify}ion}	NOUNSGPL ;
ify}ive}	ADJ-LY-NESS ;
ify}ive}	ISM-IST ;
ify}ory}	ADJ-LY-NESS ;
ify}or}		NOUNSGPL ;
ify}or}ial}	ADJ ;
ify}or}ial}ly}	ADV ;

LEXICON ATEDERIV
ate}			VERB4-ED-ING ;
ate}ion}		NOUNSGPL ;
ate}ion}al}		ADJ-LY-NESS ;
ate}ion}al}		ISM-IST ;
ate}ion}al}ity}		NOUNSGPL ;
ate}ion}al}ize}		VERB4 ;
ate}ion}al}ise}		VERB4 ;
ate}ion}al}ize}		ABLEDERIV ;
ate}ion}al}ise}		ABLEDERIV ;
ate}ion}al}ize}ed}	ADJVPAP-LY-NESS ;
ate}ion}al}ise}ed}	ADJVPAP-LY-NESS ;
ate}ion}al}ize}er}	NOUNSGPL ;
ate}ion}al}ise}er}	NOUNSGPL ;
ate}ion}al}ize}		INGDERIV ;
ate}ion}al}ise}		INGDERIV ;
ate}ion}al}ize}ion}	NOUNSGPL ;
ate}ion}al}ise}ion}	NOUNSGPL ;
ate}ive}		ADJ-LY-NESS ;
ate}ive}		ISM-IST ;
ate}ive}ity}		NOUNSGPL ;
ate}ory}		ADJ-LY-NESS ;
ate}or}			NOUNSGPL ;

LEXICON IONDERIV
ion}		NOUNSGPL ;
!ion}al}		ADJ-NOUNSGPL ;
ion}al}		ADJ ;
ion}al}		ISM-IST ;
ion}al}ity}	NOUNSGPL ;
ion}al}		IZEDERIV ;
ion}al}		LY-NESS ;
ion}		ISM-IST ;


!LEXICON ATIONDERIV
!ation}		NOUNSGPL ;
!ation}al}		ADJ-NOUNSGPL ;
!ation}al}		ISM-IST ;
!ation}al}ity}	NOUNSGPL ;
!ation}al}		IZEDERIV ;
!ation}al}		LY-NESS ;
!ation}		ISM-IST ;


LEXICON YDERIV
y}		ADJ-LY-NESS ;
y}		ADJCOMP-SUP ;

!LEXICON IVEDERIV-ISM-IST
!ive}		ADJ-LY-NESS ;
!ive}		ISM-IST ;
!ive}ity}	NOUNSGPL ;

LEXICON IVEDERIV
ive}		ADJ-LY-NESS ;
ive}		ISM-IST ;

LEXICON ORDERIV
or}		NOUNSGPL ;
or}ial}		ADJ ;
or}ial}ly}	ADV ;

LEXICON ICDERIV
ic}		ADJ ;
ic}al}ly}	ADV ;
ic}ness}	NOUNSGPL ;

LEXICON IBLEDERIV
ible}		ADJ-LY-NESS ;
ible}ity}	NOUNSGPL ;

LEXICON LYDERIV
ly}		ADJ ;
ly}		ADJCOMP-SUP ;
ly}ness}	NOUNSGPL ;

LEXICON ISTDERIV
ist}		NOUNSGPL ;
ist}		ICDERIV ;

LEXICON NOUN-VERB
		NOUNSGPL ;
		VERB4-ED-ING ;

LEXICON ADJ-NOUN-VERB
		ADJ-NOUNSGPL ;
		VERB4-ED-ING ;

LEXICON NOUN-VERB-ABLE
		NOUN-VERB ;
		ABLEDERIV ;

LEXICON NOUN-VERB-ER
		NOUN-VERB ;
er}		NOUNSGPL ;

LEXICON NOUN-VERB-ABLE-ER
		NOUN-VERB-ABLE ;
er}		NOUNSGPL ;

LEXICON VERB-ABLE
		VERB4-ED-ING ;
		ABLEDERIV ;

LEXICON VERB-ABLE-ER
		VERB-ABLE ;
er}		NOUNSGPL ;

LEXICON VERB-ER
		VERB4-ED-ING ;
er}		NOUNSGPL ;

LEXICON VERB-ABLE-ER-MENT
		VERB-ABLE-ER ;
ment}		NOUNSGPL ;

LEXICON ADJ-LY
		ADJ ;
ly}		ADV ;

LEXICON ADJ-LY-NESS-ITY
		ADJ-LY-NESS ;
ity}		NOUNSGPL ;

LEXICON ED-ING 
		EDDERIV ;
		INGDERIV ;

LEXICON ADJ-LY-NESS-ITY-ISM-IST-IZE
		ADJ-LY-NESS-ITY ;
		ISM-IST ;
		IZEDERIV ;

LEXICON VERB-ION
		VERB4-ED-ING ;
ion}		NOUNSGPL ;

LEXICON VERB-ABLE-ER-ION
		VERB-ABLE-ER ;
ion}		NOUNSGPL ;

LEXICON ABLE-ED
		ABLEDERIV ;
ed}		ADJVPAP ;

LEXICON IVE-ITY
		IVEDERIV ;
ive}ity}	NOUNSGPL ;

LEXICON IVE-ITY-OR
		IVE-ITY ;
or}		NOUNSGPL ;

LEXICON VERB-ION-IVE-ITY-OR
		VERB-ION ;
		IVE-ITY-OR ;

LEXICON VERB-ER-MENT
		VERB-ER ;
ment}		NOUNSGPL ;

LEXICON VERB-ION-OR
		VERB-ION ;
or}		NOUNSGPL ;

LEXICON VERB-ABLE-ER-ION-IVE-ORY
		VERB-ABLE-ER-ION ; 
		IVEDERIV ; 
ory}		ADJ-LY-NESS ;

LEXICON CEDERIV
ce}		NOUNSGPL ;
ce}y}		NOUNSGPL ;

LEXICON ADJ-LY-NESS-CEDERIV 
		ADJ-LY-NESS ;
		CEDERIV ;

LEXICON ADJ-NOUNSGPL-ISM-IST
		ADJ-NOUNSGPL ;
		ISM-IST ;

LEXICON ADJ-NOUNSGPL-ISM-IST-ITY
		ADJ-NOUNSGPL-ISM-IST ;
ity}		NOUNSGPL ;

!LEXICON ADJ-NOUNSGPL-ISM-IST-ITY-IZE
!		ADJ-NOUNSGPL-ISM-IST-ITY ;
!		IZEDERIV ;

LEXICON ADJ-ISM-IST-ITY-IZE-LY-NESS
		ADJ-LY-NESS ;
		ISM-IST ;
ity}		NOUNSGPL ;
		IZEDERIV ;


LEXICON ADJ-NOUN-ISM-IST-ITY-IZE-LY-NESS
		ADJ-NOUNSGPL ;
		LY-NESS ;
		ISM-IST ;
ity}		NOUNSGPL ;
		IZEDERIV ;


LEXICON ADJ-LY-NESS-ADJCOMP-SUP
		ADJ-LY-NESS ;
		ADJCOMP-SUP ;

LEXICON ED-ING-ER
		ED-ING ; 
er}		NOUNSGPL ;

LEXICON ABLE-ED-ING-ER
		ABLEDERIV ;
		ED-ING-ER ;

LEXICON ADJ-NOUNSGPL-ITY
	ADJ-NOUNSGPL ; 
ity}	NOUNSGPL ;

LEXICON ADJ-ITY-LY-NESS
	ADJ ; 
	LY-NESS ;
ity}	NOUNSGPL ;

LEXICON ADJ-NOUNSGPL-ITY-LY-NESS
	ADJ-NOUNSGPL ; 
	LY-NESS ;
ity}	NOUNSGPL ;

LEXICON ADJ-NOUNSGPL-LY-NESS
	ADJ-NOUNSGPL ; 
	LY-NESS ;

LEXICON ADJ-LY-NESS-CE
		ADJ-LY-NESS ;
ce}		NOUNSGPL ;

LEXICON VERB-ER-ION
	VERB-ER ;
ion}	NOUNSGPL ;

LEXICON ED-ING-ION
		ED-ING ;
ion}		NOUNSGPL ;

LEXICON ED-ING-ION-OR
		ED-ING-ION ;
or}		NOUNSGPL ;

LEXICON ADJ-NESS
		ADJ ;
ness}		NOUNSGPL ;

LEXICON ADJ-ALLY
		ADJ ;
ally}		ADV ;

LEXICON ADJ-ALLY-NESS
		ADJ-ALLY ;
ness}		NOUNSGPL ;

LEXICON QUANT
+Quant:0	# ;
