##################################################################
### Urls, Emails adresses and Phone number recognition
### within  NTM 
### C. Brun August 2003 (taken from N. Guerin, with some additions)
##################################################################
   

# Include basic definitions 

#Commented for Makefile  
#source ./def-chars.xfst 

# Domain name:  "xerox.fr", "cnn.news" (future??) , "toto.titi.tutu.tata.org" (but not more!)

define domainName [ [ Char | %_ | %- ]+ %. ]^{1,4} Let^{2,4} ;

# email address
# Generally email adresses only include alphanumerics, periods, the @ symbol, the underscore, and the hyphen.
#
# e.g.:
# anyletters@toto.com

define email [ Char | %_ | %- | %. ]+ %@ domainName ;

define email2  [ %{ [ [ Char | %_ | %- | %. | %, | % ] ]+ %} ] %@ domainName ; 

# URL
#  
# e.g.s
# http://user:passwd@www.xrce.xerox.com/dscgi/ds.py/View/Collection-1236

# Any (?) character but blank, < " '
define AnyChar [ 
    Char 
    | %& | %~ | %# | %{ | %( | %[ | %- | %| | %_ | %\ | %*
    | %^ | %@ | %) | %] | %= | %° | %+ | %² | %$ | %£ | %¤ 
    | %µ | %% | %! | %§ | %: | %/ | %; | %. | %, | %? | %}
 ] ;

define URL [ [ {http} | {ftp} | {news} | {nntp} | {telnet} 
    | {wais} | {file} | {mailto} | {gopher} | {prospero} ]
    [ {://} | {:/} ]
    ( AnyChar+ %: AnyChar+ %@ )
    domainName ( %/ [ AnyChar ]+ ) | {www.}  ( AnyChar+ %: AnyChar+ %@ )
    domainName ( %/ [ AnyChar ]+ )  ] ;

# Telephone/Fax number 
# e.g. "Tel : 1/800-ANALOGD"
# Tel: (212) 599-5656
# Fax: (212) 889-6689
# Tel: 202.331.2121
# Fax: 202.331.2496
# Tel: +972.9.740.9133
# Fax: 972.3.634.2927
# Tel (06) 869 0501
# Tel: 81 3-5371-1921
# tel: 415-593-7430
# tel: +44 (0)20 8232-1600 x276
# tel: + 44 (0) 207 7375 7572
# Tel.:5449083,5448108 (Corp.Off.) Fax.:5448108
# Tel = +33-6-09.14.83.53
# CB ADDED : Phone Freephone etc should be possible 
# Must:
# - start with "Tel" or something equivalent
# - contain at least one digit
# - end with a digit or a capital letter
 
##### Additions Needed ?
### freephone helpline 08 626811 ????
### phone/freephone number ?
### Toll-free : 0-888-397-3742 
### free phone  

define Telephone [ [ {Tel} | {Tél} | {TEL} | {TÉL}  | {tel} | {tél} 
         | {Telephone} | {TELEPHONE} | {Téléphone} | {telephone} | {Fax} | {FAX} 
         | {ISDN} | {fax} | {Facsimile} | {facsimile} | {PHONE} | {Phone} | {phone} 
         | {FreePhone} | {FREEPHONE} | {Freephone} | {freephone} |  {Tél/fax} 
         | {toll-free}| {Toll-free} | {TOLL-FREE} | {Toll-Free} 
     ]
    ( %. )
    ( SP ) ( %: | %= ) SP* 
    [ Digit | Up | %( | %) | %+ | %.| %/ | %- | %  ]+
      Digit
    [ Digit | Up | %( | %) | %+ | %.| %/ | %- | %  ]+
    [ Digit | Up] ]  ; 

define someadresses [ {pagesjaunes.fr} | {google.com} | {wikipedia.com} | {wikipedia.fr}] ;


####  Final transducer
####  CB: add the tag noun to be compatible with XIP
####  Are they really nouns ?

regex [ email "+NoDict":0 "+Email":0 "+NOUN":0   |
        email2  "+NoDict":0 "+Email":0 "+NOUN":0   |
        URL   "+NoDict":0 "+URL":0 "+NOUN":0 | 
	someadresses "+NoDict":0 "+URL":0 "+NOUN":0 | 
        Telephone   "+NoDict":0 "+Tel":0 "+NOUN":0 
];

#commented for makefile
#save /home/brun/test.fst
#exit


define SPEC

define DASH [
"\u2012" 	%+Punct:0 					# FIGURE DASH
| "\u2013"	%+Punct:0 					# EN DASH
| "\u2014"	%+Punct:0 					# EM DASH
| "\u2015"      %+Punct:0					# HORIZONTAL BAR
] %+PUNCT:0 ; 

## add quotes   (AS@17-Oct-2007)


define QUOTE [
  "\u0022"	%+Punct:0 %+Quote:0 		# QUOTATION MARK
| "\u00AB"	%+Punct:0 %+Quote:0		# LEFT-POINTING DOUBLE ANGLE QUOTATION MARK
| "\u00BB"	%+Punct:0 %+Quote:0		# RIGHT-POINTING DOUBLE ANGLE QUOTATION MARK
| "\u2018"	%+Punct:0 %+Quote:0		# LEFT SINGLE QUOTATION MARK
| "\u2019"	%+Punct:0 %+Quote:0		# RIGHT SINGLE QUOTATION MARK
| "\u201A"	%+Punct:0 %+Quote:0		# SINGLE LOW-9 QUOTATION MARK
| "\u201B"	%+Punct:0 %+Quote:0		# SINGLE HIGH-REVERSED-9 QUOTATION MARK
| "\u201C"	%+Punct:0 %+Quote:0		# LEFT DOUBLE QUOTATION MARK
| "\u201D"	%+Punct:0 %+Quote:0		# RIGHT DOUBLE QUOTATION MARK
| "\u201E"	%+Punct:0 %+Quote:0		# DOUBLE LOW-9 QUOTATION MARK
| "\u201F"	%+Punct:0 %+Quote:0		# DOUBLE HIGH-REVERSED-9 QUOTATION MARK
| "\u2039"	%+Punct:0 %+Quote:0		# SINGLE LEFT-POINTING ANGLE QUOTATION MARK
| "\u203A"	%+Punct:0 %+Quote:0		# SINGLE RIGHT-POINTING ANGLE QUOTATION MARK
];

define SYMBOL [
  "\u20A0"	%+Symbol:0 %+Curr:0 		# EURO-CURRENCY SIGN
| "\u20A1"	%+Symbol:0 %+Curr:0		# COLON SIGN
| "\u20A2"	%+Symbol:0 %+Curr:0		# CRUZEIRO SIGN
| "\u20A3"	%+Symbol:0 %+Curr:0		# FRENCH FRANC SIGN
| "\u20A4"	%+Symbol:0 %+Curr:0		# LIRA SIGN
| "\u20A5"	%+Symbol:0 %+Curr:0		# MILL SIGN
| "\u20A6"	%+Symbol:0 %+Curr:0		# NAIRA SIGN
| "\u20A7"	%+Symbol:0 %+Curr:0		# PESETA SIGN
| "\u20A8"	%+Symbol:0 %+Curr:0		# RUPEE SIGN
| "\u20A9"	%+Symbol:0 %+Curr:0		# WON SIGN
| "\u20AA"	%+Symbol:0 %+Curr:0		# NEW SHEQEL SIGN
| "\u20AB"	%+Symbol:0 %+Curr:0		# DONG SIGN
| "\u20AC"	%+Symbol:0 %+Curr:0		# EURO SIGN
| "\u20AD"	%+Symbol:0 %+Curr:0		# KIP SIGN
| "\u20AE"	%+Symbol:0 %+Curr:0		# TUGRIK SIGN
| "\u20AF"	%+Symbol:0 %+Curr:0		# DRACHMA SIGN
| "\u20B0"	%+Symbol:0 %+Curr:0		# GERMAN PENNY SIGN
| "\u20B1"	%+Symbol:0 %+Curr:0		# PESO SIGN
| "\u2109"	%+Symbol:0 %+Unit:0		# DEGREE FAHRENHEIT
| "\u2125"	%+Symbol:0 %+Unit:0		# OUNCE SIGN
| "\u2126"	%+Symbol:0 %+Unit:0		# OHM SIGN
| "\u2127"	%+Symbol:0 %+Unit:0		# INVERTED OHM SIGN
| "\u212A"	%+Symbol:0 %+Unit:0		# KELVIN SIGN
| "\u212B"	%+Symbol:0 %+Unit:0		# ANGSTROM SIGN
] %+NOUN%_INV:0;

define BD	" " | "\n" | " " | "\t" | EOT | BOT ;

define NEWQUOTE	QUOTE %+PUNCT:0	( [ %+LeftSpace:0	LC BD 		
			  	| %+RightSpace:0	RC BD ] );


# combine:

regex [ SPEC | NEWQUOTE | SYMBOL | DASH ];
