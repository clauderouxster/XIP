# ============================================================================
# CONTENT:	(x)fst script for morphological guesser
#
# LANGUAGE:	ENGLISH (ISO-8859-1)
#
# AUTHOR:	A. Schiller
#
# CREATED:	16-Aug-1999
#
# ============================================================================
# USAGE:	xfst -l [ThisFile]
# ============================================================================


define LowChar	[a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|x|y|z|å|ä|ö];
define UppChar	[A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Å|Ä|Ö];
define Char	[ LowChar | UppChar ];

define Digit    [ %0|1|2|3|4|5|6|7|8|9 ];
define Op       [ %+|%-|%*|%/|%=|%& ];
define Curr	[ (M | U S | C a n ) "$" | "£" | "¥" | "¢" | "\u20AC"
		| D M | F F | W o n | Y e n ];
define CurrDig	[ m | M | b n ];

echo >>> define stems
define LowStem	[ LowChar ]+;		# all characters lower case
define UpStem   [ UppChar ]+;		# all characters upper case
define MixStem	[ Char | %-  ]+;

define CapStem	UppChar LowStem;		
define PropStem UppChar MixStem;

define Trunc	Char [ Char | Digit | Op]* %- ;
 
define Card	Digit+ [Op Digit+]*;

echo >>> define special strings
define StemIng		LowStem {ing} ;
define StemIon		LowStem {ion} ;
define StemEd		LowStem {ed} ;

define StemOther	LowStem - [ ?* [ {ing} | {ed} | {ion} ] ];


# =============================================================
read regex
[
# numerals
  Card ("%"|"$")			%+Dig:0 %+Card:0
| Curr Digit+ [["."|","] Digit+]* (CurrDig)	%+Dig:0 %+DlrAmt:0	# currency + amount
| Digit+ (%-) [Char - s]		%+Dig:0 %+Card:0	# 10a
| Digit+ (%') s			        %+Noun:0 %+Pl:0		# the 1960s

# adverbs
| LowStem l y				%+Adv:0

# some nouns
| (Trunc) StemIon			%+NAdj:0 %+Sg:0
| (Trunc) StemIon 0:s			%+Noun:0 %+Pl:0

# -ing forms
| StemIng				[ %+Verb:0 %+Prog:0 ]
| StemIng				[ %+Noun:0 %+VProg:0 %+Sg:0 | %+Adj:0 %+VProg:0 ]
| StemIng 0:s				[ %+Noun:0 %+VProg:0 %+Pl:0 ]

# adjectives in -ed
| Trunc StemEd				%+Adj:0 
| StemEd				[ %+Verb:0 %+PastBoth:0 %+123SP:0 |  %+Adj:0 %+VPap:0 ]

# adjectives loke "10-years-old"
| Card %- (Trunc) LowStem		%+NAdj:0 %+Sg:0		# 10-years-old

# compounds
| (Trunc) StemOther			[ %+NAdj:0 %+Sg:0 ]
| (Trunc) StemOther 0:s			[ %+Noun:0 %+Pl:0 ]
| Trunc StemIng				[ %+NAdj:0 %+Sg:0 ]
| Trunc StemIng 0:s			[ %+Noun:0 %+Pl:0 ]

# Capitalized words => proper nouns
| (Trunc) PropStem (%.)			%+Prop:0
| [ UppChar %. ]+			%+Prop:0
| UppChar+ [ Op UppChar+]*		%+Prop:0

# words ending in %.
|  LowStem %.				%+Adv:0 %+Abbr:0

# interjections
| LowChar+ [a|e|i|o|u]+ h+		%+Interj:0

];
