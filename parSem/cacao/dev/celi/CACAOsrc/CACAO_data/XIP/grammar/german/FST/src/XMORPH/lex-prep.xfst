#! -------------------------------------------------------------------------
#! TYPE:	xfst
#! CONTENT:	German morphology : prepostions
#!
#! AUTHOR:	Anne Schiller, XRCE
#! CREATED:	30-Jun-2006
#!		- derived from former flex-func.lexc
#! -------------------------------------------------------------------------

!===================================================================================
! References:
! DG == Duden (1984)
!	"Die Grammatik", Dudenverlag
! DR == Duden (1996)
!	"Die deutsche Rechtschreibung", Dudenverlag
! HB == Helbig/Buscha (1993)
!	"Deutsche Grammatik", Langenscheidt
! HG ==	Heinz Griesbach (1986): 
!	"Neue Deutsche Grammatik", Langenscheidt
!===================================================================================

# some general definitions

# [A] boundary symbols to be kept on upper (lexical) side
define AND	"^&":0 ;		# boundary for contracted forms (an^&dem)
define SEP	"^|":0 ;		# morpheme boundary (dar^|an)

# [B] spelling markers
define NEW	"^New":0 ;
define OLD	"^Old":0 ;
define ERR	"^Err":0 ;


# ------------------------------------------------------------------------------
# MAIN PART
# ------------------------------------------------------------------------------
 
! ------------------------------------------------------------------------------
! prepositions with determiners
! ------------------------------------------------------------------------------


define PDARTM 	0:m 	[ %+PrepDet %+Masc %+Sg %+Dat %+AgrWk
			| %+PrepDet %+Neut %+Sg %+Dat %+AgrWk ]:0;
define PDARTR	0:r	[ %+PrepDet %+Fem  %+Sg %+Dat %+AgrWk ]:0;
define PDARTS	0:s	[ %+PrepDet %+Neut %+Sg %+Acc %+AgrWk ]:0;
define PDARTN	0:n	[ %+PrepDet %+Masc %+Sg %+Acc %+AgrWk ]:0;
define PDARTMNS	PDARTM | PDARTN |PDARTS ;

define PREP	[ %+Prep ]:0;
define PREPDET	[ %+PrepDet ]:0;
define PSTP	[ %+Postp ]:0;
define CRCP1	[ %+Prep %+Crc ]:0;
define CRCP2	[ %+Postp %+Crc ]:0;

define ABBR	%+Abbr:0 ;

define ADPOS [
  {ab}			PREP %+AccDat:0		# "~ ersten Mai; ~ erstem Mai
| {ab} 			CRCP2			# "von Montag ~
| {abseits}		PREP %+Gen:0		# "~ der Straße
| {abzüglich}		PREP %+Gen:0		# "~ des Rabatts
| {abzüglich}:{abzgl.}	PREP %+Gen:0 ABBR
| {an}			CRCP2			# "von Montag ~
| {an}			PREP %+AccDat:0		# "~ die Wand; ~ der Wand
| {an}			PDARTS		# er geht ans Haus
| {an}:{a} 		PDARTM		# er steht am Weg 
| {anfangs}		PREP %+Gen:0		# "~ der Zielgeraden
| {angesichts}		PREP %+Gen:0		# "~ des Problems
| {anhand}		PREP %+Gen:0		# "~ der Unterlagen
| {anlässlich}		PREP %+Gen:0 NEW	# "~ der Tagung
| {anläßlich}		PREP %+Gen:0 OLD	# "~ der Tagung
| {anstatt}		PREP %+DatGen:0		# "~ vielen Worten; ~ des Wortes
| {anstelle}		PREP %+Gen:0		# "~ des Lehrers
| {auf Seiten}		PREP %+Gen:0 NEW	# @O=auf_seiten
| {auf seiten}		PREP %+Gen:0 OLD	# @N=aufseiten
| {auf}			CRCP2			# "von Jugend auf
| {auf}			PREP %+AccDat:0		# "~ den Tisch; ~ dem Tisch
| {auf}			PDARTS		# er klettert aufs Dach
| {aufgrund}		PREP %+Gen:0		# "~ der Anfrage
| {aufseiten}		PREP %+Gen:0 NEW	# @O=auf_seiten
| {aus}			CRCP2			# "von mir ~
| {aus}			PREP %+Dat:0		# "~ dem Haus
| {ausgangs}		PREP %+Gen:0		# "~ Zielgeraden
| {ausschließlich}	PREP %+Gen:0
| {außer}		PREP %+DatGen:0		# "~ meinem Vater; ~ Landes 
| {außerhalb}		PREP %+Gen:0		# "~ Berlins
| {bei}			PREP %+Dat:0		# "~ mir
| {bei}			PDARTM		# er ist beim Arzt
| {bei}:{b.}		PREP %+Dat:0 ABBR
| {beiderseits}		PREP %+Gen:0		# "~ der Straße
| {beidseits}		PREP %+Gen:0		# "~ der Straße
| {betreffs}		PREP %+Gen:0
| {betreffs}:{betr.}	PREP %+Gen:0 ABBR
| {bezüglich}		PREP %+Gen:0
| {bezüglich}:{bez.}	PREP %+Gen:0 ABBR
| {bezüglich}:{bzgl.}	PREP %+Gen:0 ABBR
| {binnen}		PREP %+DatGen:0		# "~ einem Jahr; ~ eines Jahres
| {bis}			PREP %+Preprep:0		# bis zu dieser Stelle; bis auf ihn
| {bis}			PREP %+Acc:0		# "~ morgen
| {dank}		PREP %+DatGen:0		# "~ seinem Fleiße; ~ guten Willens
| {diesseits}		PREP %+Gen:0		# "~ der Grenze
| {durch}		PREP %+Acc:0		# "~ das Tal
| {durch}		PDARTS		# er sieht durchs Fenster
| {eingangs}		PREP %+Gen:0
| {eingedenk}		PREP %+Gen:0
| {eingedenk}		PSTP %+Gen:0
| {einschl}{ießlich}:{.} PREP %+DatGen:0 ABBR
| {einschließlich}	PREP %+DatGen:0
| {entgegen}		PREP %+Dat:0		# "~ aller Hoffnung
| {entgegen}		PSTP %+Dat:0		# "der Sonne ~
| {entlang}		PREP %+DatGen:0		# "~ des Weges
| {entlang}		PSTP %+AccDat:0	# "den Weg ~
| {entspr}{echend}:{.}	PREP %+Dat:0 ABBR
| {entsprechend}	PREP %+Dat:0		# "~ den Anweisungen
| {entsprechend}	PSTP %+Dat:0		# "der Regel ~
| {exklusive}		PREP %+DatGen:0
| {fern}		PREP %+DatGen:0
| {für}			PREP %+Acc:0
| {für}  		PDARTS		# fürs Erste
| {für}:{f.}		PREP %+Acc:0 ABBR
| {gegen}		PREP %+Acc:0
| {gegenüber}		PREP %+Dat:0		# "~ dem Dom
| {gegenüber}		PSTP %+Dat:0		# "meinem Vater ~
| {gemäß}		PREP %+Dat:0		# "~ den Regeln
| {gemäß}		PSTP %+Dat:0		# "dem Gesetz ~
| {gemäß}:{gem.}	PREP %+Dat:0 ABBR
| {gen}			PREP %+Acc:0
| {gleich}		PREP %+Dat:0
| {halber}		PSTP %+Gen:0
| {hinsichtlich}	PREP %+Gen:0
| {hinter}		PREP %+AccDat:0
| {hinter} 		PDARTMNS		# hinterm Haus; hintern Zaun, hinters Haus
| {in Bezug auf}	PREP %+Dat:0 NEW	# @O=in_bezug_auf
| {in bezug auf}	PREP %+Dat:0 OLD	# @N=in_Bezug_auf
| {in}			PREP %+AccDat:0
| {in} 			PDARTS		# er geht ins Haus
| {in}:{i.}		PREP %+AccDat:0 ABBR
| {in}:{i}		PDARTM		# er steht im Weg 
| {inclusive}		PREP %+DatGen:0
| {inclusive}:{incl.}	PREP %+DatGen:0 ABBR
| {infolge}		PREP %+Gen:0
| {inklusive}		PREP %+DatGen:0
| {inklusive}:{inkl.}	PREP %+DatGen:0 ABBR
| {inmitten}		PREP %+Gen:0
| {innerhalb}		PREP %+DatGen:0		# "~ vier Jahren; ~ eines Jahres
| {je}			PREP %+NomAcc:0		# "~ Patient; ~ angefangenen Monat
| {je}:{j.}		PREP %+NomAcc:0 ABBR
| {jenseits}		PREP %+Gen:0
| {kontra}		PREP %+Acc:0
| {kraft}		PREP %+Gen:0		# "~ Amtes
| {laut}		PREP %+DatGen:0		# "~ neuem Befehl; ~ neuen Befehls
| {laut}:{lt.}		PREP %+DatGen:0 ABBR
| {links}		PREP %+Gen:0		# "~ des Hauses
| {längs}		PREP %+DatGen:0		# "~ dem Wege; ~ des Weges
| {längsseits}		PREP %+Gen:0
| {mangels}		PREP %+DatGen:0		# "~ Beweisen; ~ Geldes
| {minus}		PREP %+Gen:0
| {mit Hilfe}		PREP %+Gen:0
| {mit}			PREP %+Dat:0
| {mit}:{m.}		PREP %+Dat:0 ABBR
| {mithilfe}		PREP %+Gen:0 NEW	# @O=mit_Hilfe
| {mitsamt}		PREP %+Dat:0
| {mittels}		PREP %+DatGen:0		# "~ Drähten; ~ eines Drahtes 
| {nach}		PREP %+Dat:0		# "~ dem Spiel
| {nach}		PSTP %+Dat:0		# "der Nase ~
| {nach}:{n.}		PREP %+Dat:0 ABBR
| {nah}			PREP %+Dat:0		# "~ dem Ufer
| {nahe}		PREP %+Dat:0		# "~ dem Ufer
| {namens}		PREP %+NomGen:0		# "~ Otto; ~ der Regierung
#| {namens}		PREP %+Gen:0
| {neben}		PREP %+AccDat:0
| {nebst}		PREP %+Dat:0
| {nordwestlich}	PREP %+Gen:0
| {nordöstlich}		PREP %+Gen:0
| {nächst}		PREP %+Dat:0		# "~ dem Hause
| {nördlich}		PREP %+Gen:0
| {ob}			PREP %+Gen:0		# "~ des Glücks|%vrlt.
| {oberhalb}		PREP %+Gen:0
| {ohne}		PREP %+Acc:0
| {ohne}:{o.}		PREP %+Acc:0 ABBR
| {per}			PREP %+Acc:0		# "~ ersten Januar
| {pro}			PREP %+NomAcc:0		# "~ Patient(en)
| {pro}:{p.}		PREP %+NomAcc:0 ABBR
| {rechts}		PREP %+Gen:0
| {samt}		PREP %+Dat:0
| {seit}		PREP %+Dat:0
| {seitens}		PREP %+Gen:0
| {seitlich}		PREP %+Gen:0
| {statt}		PREP %+DatGen:0
| {südlich}		PREP %+Gen:0
| {südwestlich}		PREP %+Gen:0
| {südöstlich}		PREP %+Gen:0
| {trotz}		PREP %+DatGen:0
| {um}			CRCP1 %+Gen:0		# "~ Himmels willen
| {um}			PREP %+Acc:0		# "~ den heißen Brei
| {um} 			PDARTS		# es geht ums Ganze
| {unbeschadet}		PREP %+Gen:0
| {unerachtet}		PREP %+Gen:0
| {ungeachtet}		PREP %+Gen:0		# "dessen ~
| {ungeachtet}		PSTP %+Gen:0		# "~ dessen
| {unter}		PREP %+AccDat:0
| {unter}		PDARTM		# unterm Bett; untern Tisch; unters Dach
| {unter}:{u.}		PREP %+AccDat:0 ABBR
| {unterhalb}		PREP %+Gen:0
| {unweit}		PREP %+Gen:0
| {vermittels}		PREP %+Gen:0
| {vermöge}		PREP %+Gen:0
| {versus}		PREP %+Acc:0
| {versus}:{vs.}	PREP %+Acc:0 ABBR
| {via}			PREP %+Acc:0
| {voller}		PREP %+Acc:0		# "~ Wasser 
| {voller}		PREP %+Gen:0		# "~ grober Fehler 
| {von Seiten}		PREP %+Gen:0 NEW	# @O=von_seiten
| {von seiten}		PREP %+Gen:0 OLD	# @N=von_Seiten
| {von}			PREP %+Dat:0
| {von}:{v.}		PREP %+Dat:0 ABBR
| {von}:{vo}		PDARTM		# er fällt vom Dach
| {vonseiten}		PREP %+Gen:0 NEW	# @O=von_seiten
| {vor}			PREP %+AccDat:0		# "~ die Tür; ~ der Tür 
| {vor} 		PDARTMNS		# vorm Bett; vorn Tisch; vors Haus
| {vorbehaltlich}	PREP %+Gen:0
| {wegen}		PREP %+DatGen:0
| {wegen}		PSTP %+Gen:0		# "der Sache ~
| {wegen}:{w.}		PREP %+DatGen:0 ABBR
| {wegen}:{wg.}		PREP %+DatGen:0 ABBR
| {westlich}		PREP %+Gen:0
| {wider}		PREP %+Acc:0		# "~ den Stachel
| {willen}		CRCP2			# "um Himmels ~
| {während}		PREP %+DatGen:0
| {zeit}		PREP %+Gen:0		# "~ seines Lebens
| {zu Seiten}		PREP %+Gen:0 NEW	# @O=zu_seiten
| {zu seiten}		PREP %+Gen:0 OLD	# @N=zu_Seiten
| {zu}			PREP %+Dat:0
| {zu} 			PDARTM		# er geht zum Arzt
| {zu} 			PDARTR		# er geht zur Schule
| {zu}:{z.}		PREP %+Dat:0 ABBR
| {zufolge}		PREP %+Gen:0
| {zufolge}		PSTP %+Dat:0
| {zugunsten}		PREP %+Gen:0
| {zulasten}		PREP %+Gen:0
| {zuliebe}		PSTP %+Dat:0
| {zunächst}		PREP %+Dat:0
| {zunächst}		PSTP %+Dat:0		# "[DG]: dem Hause ~
| {zuseiten}		PREP %+Gen:0 NEW	# @O=zu_seiten
| {zuungunsten}		PREP %+Gen:0
| {zuwider}		PSTP %+Dat:0
| {zuzüglich}		PREP %+Gen:0
| {zuzüglich}:{zzgl.}	PREP %+Gen:0 ABBR
| {zwecks}		PREP %+Gen:0
| {zwischen}		PREP %+AccDat:0
| {à la}		PREP %+NomAcc:0		# "~ Mayer
| {östlich}		PREP %+Gen:0
| {über}		PREP %+AccDat:0
| {über}		PDARTMNS		# überm Bett; übern Zaun; übers Land
| {über}		PSTP %+Acc:0		# "den Tag ~

];

# pronominal adverbs

define PRONADV [
  {dar} SEP {an}	%+Pronadv:0
| {dar} SEP {auf}	%+Pronadv:0
| {dar} SEP {aus}	%+Pronadv:0
| {dar} SEP {in}	%+Pronadv:0
| {dar} SEP {nach}	%+Pronadv:0	# veraltet [D]
| {dar} SEP {neben}	%+Pronadv:0	# veraltet [D]
| {dar} SEP {ob}	%+Pronadv:0	# veraltet [D]
| {dar} SEP {um}	%+Pronadv:0
| {dar} SEP {unter}	%+Pronadv:0
| {dar} SEP {über}	%+Pronadv:0
| {da} SEP {bei}	%+Pronadv:0
| {da} SEP {durch}	%+Pronadv:0
| {da} SEP {für}	%+Pronadv:0
| {da} SEP {gegen}	%+Pronadv:0
| {da} SEP {hinter}	%+Pronadv:0
| {da} SEP {mit}	%+Pronadv:0
| {da} SEP {nach}	%+Pronadv:0
| {da} SEP {neben}	%+Pronadv:0
| {da} SEP {von}	%+Pronadv:0
| {da} SEP {vor}	%+Pronadv:0
| {da} SEP {wider}	%+Pronadv:0
| {da} SEP {zu}		%+Pronadv:0
| {da} SEP {zwischen}	%+Pronadv:0
| {hier} SEP {an}	%+Pronadv:0
| {hier} SEP {auf}	%+Pronadv:0
| {hier} SEP {aus}	%+Pronadv:0
| {hier} SEP {bei}	%+Pronadv:0
| {hier} SEP {durch}	%+Pronadv:0
| {hier} SEP {für}	%+Pronadv:0
| {hier} SEP {gegen}	%+Pronadv:0
| {hier} SEP {hinter}	%+Pronadv:0
| {hier} SEP {in}	%+Pronadv:0
| {hier} SEP {mit}	%+Pronadv:0
| {hier} SEP {nach}	%+Pronadv:0
| {hier} SEP {neben}	%+Pronadv:0
| {hier} SEP {um}	%+Pronadv:0
| {hier} SEP {unter}	%+Pronadv:0
| {hier} SEP {von}	%+Pronadv:0
| {hier} SEP {vor}	%+Pronadv:0
| {hier} SEP {wider}	%+Pronadv:0
| {hier} SEP {zu}	%+Pronadv:0
| {hier} SEP {über}	%+Pronadv:0
| {hier} SEP {zwischen}	%+Pronadv:0
| {wor} SEP {an} 	%+Pronadv:0 %+Wh:0
| {wor} SEP {auf} 	%+Pronadv:0 %+Wh:0
| {wor} SEP {aus} 	%+Pronadv:0 %+Wh:0
| {wor} SEP {in} 	%+Pronadv:0 %+Wh:0
| {wor} SEP {um} 	%+Pronadv:0 %+Wh:0
| {wor} SEP {unter} 	%+Pronadv:0 %+Wh:0
| {wor} SEP {über} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {bei} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {durch} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {für} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {gegen} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {hinter} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {mit} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {nach} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {neben} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {von} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {vor} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {wider} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {zu} 	%+Pronadv:0 %+Wh:0
| {wo} SEP {zwischen} 	%+Pronadv:0 %+Wh:0


]
.o. # daran/dran
[ 
 	a (->) 0 || .#. d _ r 
];


read regex [ ADPOS | PRONADV ];
