//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : Culture.xip                                                 !
//!   Date       : 1007/April                                                  !
//!   Author     : João Loureiro                                               !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!  Identifies titles of works of art                                         !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Sequence:

////////////////////////////////////////////////////////////
// Obras Reproduzidas                                     //
////////////////////////////////////////////////////////////

// Example: [Fados de Coimbra], [Traz Outro Amigo também] e [Cantigas de Maio], (da autoria) de José Afonso
// Example: [Waking Up The Neighbours], de Bryan Adams
//
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right],
                       (punct[comma];?[lemma:e], punct[bracket,left], ?[maj], ?*, punct[bracket,right])+, punct[comma],
                       (?[lemma:de], ?[surface:a], ?[lemma:autoria]),
                       ?[lemma:de], (?[lemma:o]), ?+[maj], (prep, art*, ?+[maj])*|.
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right],
                       punct[comma], (?[lemma:de], ?[surface:a], ?[lemma:autoria]),
                       ?[lemma:de], (?[lemma:o]), ?+[maj], (prep, art*, ?+[maj])*|.


// Example: [Título], o mais recente trabalho
//
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right], punct[comma], ?[lemma:o], ((adv), ?), ?[lemma:trabalho]|.


// Example: (escreveu) O Principezinho
// Example: O Principezinho (escrito por "ou" foi escrito)
// Example: O Principezinho (o mais recente livro)
// Example: o livro [O Principezinho]
// Example: escreve Caine em [What s it All About]
//
1> noun[culture=+,inquote=+] @= |?[lemma:escrever], punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right]|.
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right], ?[lemma:ser];punct[comma], ?[lemma:escrever]|.
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right], punct[comma], ?[lemma:o], ((adv), ?), ?[lemma:livro]|.
1> noun[culture=+,inquote=+] @= |?[lemma:o], ?[lemma:livro], punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right]|.
1> noun[culture=+,inquote=+] @= |?[lemma:escrever], ?+[maj], (prep,?+[maj])*, ?[lemma:em], punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right]|.


// Example: (realizou) O Titanic
// Example: O Titanic (realizado por "ou" foi realizado)
// Example: O Titanic (o mais recente fime)
// Example: o filme [O Titanic]
//
1> noun[culture=+,inquote=+] @= |?[lemma:realizar], punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right]|.
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right], ?[lemma:ser];punct[comma], ?[lemma:realizar]|.
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right], punct[comma], ?[lemma:o], ((adv), ?), ?[lemma:filme]|.
1> noun[culture=+,inquote=+] @= |?[lemma:o], ?[lemma:filme], punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right]|.


// Example: (protagonizou) O Titanic
// Example: O Titanic (protagonizado por "ou" foi protagonizado)
//
1> noun[culture=+,inquote=+] @= |?[lemma:protagonizar], punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right]|.
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right], ?[lemma:ser];punct[comma], ?[lemma:protagonizar]|.


// Example: (produziu) O Titanic
// Example: O Titanic (produzido por "ou" foi produzido)
//
1> noun[culture=+,inquote=+] @= |?[lemma:produzir], punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right]|.
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right], ?[lemma:ser];punct[comma], ?[lemma:produzir]|.


// Example: (cantou) [Todo O Tempo] Do Mundo]
// Example: [Todo O Tempo Do Mundo] (cantado por "ou" foi cantado)
// Example: [Todo O Tempo Do Mundo] (a mais recente música)
// Example: [Momento] (o mais recente álbum)
// Example: [Momento] (o mais recente disco)
// Example: a música [Todo o Tempo do Mundo]
// Example: o álbum [Momento]
// Example: o disco [Momento]
//
1> noun[culture=+,inquote=+] @= |?[lemma:cantar], punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right]|.
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right], ?[lemma:ser];punct[comma], ?[lemma:cantar]|.
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right], punct[comma], ?[lemma:o], ((adv), ?), ?[lemma:música]|.
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right], punct[comma], ?[lemma:o], ((adv), ?), ?[lemma:álbum]|.
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right], punct[comma], ?[lemma:o], ((adv), ?), ?[lemma:disco]|.
1> noun[culture=+,inquote=+] @= |?[lemma:o], ?[lemma:música], punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right]|.
1> noun[culture=+,inquote=+] @= |?[lemma:o], ?[lemma:álbum], punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right]|.
1> noun[culture=+,inquote=+] @= |?[lemma:o], ?[lemma:disco], punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right]|.

// Example: (interpretou) Todo O Tempo Do Mundo
// Example: Todo O Tempo Do Mundo (interpretado por "ou" foi interpretado)
//
1> noun[culture=+,inquote=+] @= |?[lemma:interpretar], punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right]|.
1> noun[culture=+,inquote=+] @= |punct[bracket,left]| ?[maj], ?*[bracket:~] |punct[bracket,right], ?[lemma:ser];punct[comma], ?[lemma:interpretar]|.

// Camões(1554)
//
1> noun[culture=+, inquote=+] @= ?+[maj], (prep, (art), ?+[maj]) |punct[left, paren], num[dig], punct[right, paren]|.


////////////////////////////////////////////////////////////
// Obras de Arte                                          //
////////////////////////////////////////////////////////////

// Muralha da China
// Cristo Redentor
// Machu Picchu
//CHichén Itzá
// Taj Mahal
// Coliseu Romano
// Petra
//
3> noun[culture=+, monument=+] @= ?[lemma:muralha], prep[lemma:de], ?[lemma:o], ?[surface:China].
3> noun[culture=+, monument=+] @= ?[lemma:cristo], ?[lemma:redentor].
3> noun[culture=+, monument=+] @= ?[surface:Machu], ?[surface:Picchu].
3> noun[culture=+, monument=+] @= ?[surface:Chichén], ?[surface:Itzá].
3> noun[culture=+, monument=+] @= ?[surface:Taj], ?[surface:Mahal].
3> noun[culture=+, monument=+] @= ?[lemma:coliseu], ?[lemma:romano].
3> ?[culture=+, monument=+] = ?[surface:Petra].



// Igreja de...
// Igreja Paroquial de...
//
4> noun[culture=+, monument=+] @= ?[lemma:igreja], (?[lemma:paroquial]), (prep[lemma:de], (?[lemma:o])), ?+[maj], (prep, (art), ?+[maj])*.

// Cruzeiro de...
// Cruzeiro Paroquial de...
//
4> noun[culture=+, monument=+] @= ?[lemma:cruzeiro], (?[lemma:paroquial]), (prep[lemma:de], (?[lemma:o])), ?+[maj], (prep, (art), ?+[maj])*.

// lápide / pelourinho / ponte / capela / palacete / aqueduto / convento / santuário / chafariz / ermida / ruínas / coliseu de...
//
4> noun[culture=+, monument=+] @= ?[lemma:lápide];
                          ?[lemma:pelourinho];
                          ?[lemma:ponte];
                          ?[lemma:capela];
                          ?[lemma:palacete];
                          ?[lemma:aqueduto];
                          ?[lemma:convento];
                          ?[lemma:santuário];
                          ?[lemma:chafariz];
                          ?[lemma:ermida];
                          ?[lemma:ruína, pl];
                          ?[lemma:coliseu],
                          (prep[lemma:de], (?[lemma:o])), ?+[maj], (prep, (art), ?+[maj])*.


////////////////////////////////////////////////////////////
// Brands                                                 //
////////////////////////////////////////////////////////////

6> noun[brand=+] = ?[surface:Harley], ?[surface:Davidson].
6> noun[brand=+] = ?[Surface:Rolls], ?[surface:Royce].
6> noun[brand=+] = ?[surface:City], ?[surface:Desk].
6> noun[brand=+] = ?[surface:Konica], ?[surface:Minolta].
6> noun[brand=+] = ?[surface:Sony], ?[surface:Ericsson].
6> noun[brand=+] = ?[surface:Coca], ?[surface:Cola].


////////////////////////////////////////////////////////////
// Obras do tipo Produto                                  //
////////////////////////////////////////////////////////////

// Fiat Punto
// Nokia N95
// Samsung 793DF
// Nokia 6300
//
8> noun[culture=+, product=+] @= ?[brand], ?[maj], (?[maj]).
8> noun[culture=+, product=+] @= ?[brand], ?[maj], num[dig].
8> noun[culture=+, product=+] @= ?[brand], num[dig], ?[maj].
8> noun[culture=+, product=+] @= ?[brand], num[dig].


////////////////////////////////////////////////////////////
// Publicações                                            //
////////////////////////////////////////////////////////////

// Decreto-Lei nº31/345 de 11 de Maio de 1005
// Decreto Lei nº31/345 de 11 de Maio de 1005
//
10> noun[culture=+, publication=+] @= ?[lemma:decreto-lei],(?[surface:n], ?[surface:º]), num[dig], symbol[slash], num[dig], (punct[comma]), prep[lemma:de], num[dig], prep[lemma:de], ?[t-month], prep[lemma:de], num[dig].
10> noun[culture=+, publication=+] @= ?[lemma:decreto], ?[lemma:lei], (?[surface:n], ?[surface:º]), num[dig], symbol[slash], num[dig], (punct[comma]), prep[lemma:de], num[dig], prep[lemma:de], ?[t-month], prep[lemma:de], num[dig].

// Decreto-Lei nº31/345 de Maio de 1005
// Decreto Lei nº31/345 de Maio de 1005
//
10> noun[culture=+, publication=+] @= ?[lemma:decreto-lei],(?[surface:n], ?[surface:º]), num[dig], symbol[slash], num[dig], (punct[comma]), prep[lemma:de], ?[t-month], prep[lemma:de], num[dig].
10> noun[culture=+, publication=+] @= ?[lemma:decreto], ?[lemma:lei], (?[surface:n], ?[surface:º]), num[dig], symbol[slash], num[dig], (punct[comma]), prep[lemma:de], ?[t-month], prep[lemma:de], num[dig].

// Decreto-Lei nº31/345 de 1005
// Decreto Lei nº31/345 de 1005
//
10> noun[culture=+, publication=+] @= ?[lemma:decreto-lei],(?[surface:n], ?[surface:º]), num[dig], symbol[slash], num[dig], (punct[comma]), prep[lemma:de], num[dig].
10> noun[culture=+, publication=+] @= ?[lemma:decreto], ?[lemma:lei], (?[surface:n], ?[surface:º]), num[dig], symbol[slash], num[dig], (punct[comma]), prep[lemma:de], num[dig].

// Decreto Lei 154/94
// Decreto Lei nº31/345
//
10> noun[culture=+, publication=+] @= ?[lemma:decreto-lei], (?[surface:n], ?[surface:º]), num[dig], symbol[slash], num[dig].
10> noun[culture=+, publication=+] @= ?[lemma:decreto], ?[lemma:lei], (?[surface:n], ?[surface:º]), num[dig], symbol[slash], num[dig].
