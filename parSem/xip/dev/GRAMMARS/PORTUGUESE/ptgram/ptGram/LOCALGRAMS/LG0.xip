//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : LG0.xip                                                     !
//!   Date       : 2007/Maio                                                   !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!  Dealing with verbs that have different properties if they are used as     !
//!  base form or with a refl pronoun.                                         !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Sequence:

////////////////////////////////////////////////////////////////////////////////
// verbs
////////////////////////////////////////////////////////////////////////////////

// TORNAR-SE
// TORNAR-SE has to bear the feature cop:+, when TORNAR alone has not.
// We also have to deal the cases where the reflex is on the left of the verb
//
// Example: Ele tornou-se presidente.
//
1> pastpart[cop=+] = #2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos], pastpart#1[lemma:tornar,!number:!,!gender:!,!person:!],
                     where(#1[number]:#2[number] & #1[person]:#2[person]).
1> verb[cop=+]     = #1[lemma:tornar,!number:!,!gender:!,!person:!], #2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos],
                     where(#1[number]:#2[number] & #1[person]:#2[person]).
1> verb[cop=+]     = #2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos], #1[lemma:tornar,!number:!,!gender:!,!person:!],
                     where(#1[number]:#2[number] & #1[person]:#2[person]).


// FAZER-SE
// FAZER-SE before an adjective has to be taken as a copula.
// Example: Ela fez-se bonita
//
3> pastpart[cop=+] = #2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos], pastpart#1[lemma:fazer,!number:!,!gender:!,!person:!] | (pron[quant];adv), adj |,
                     where(#1[number]:#2[number] & #1[person]:#2[person]).
3> verb[cop=+]     = #1[lemma:fazer,!number:!,!gender:!,!person:!], #2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos] | (pron[quant];adv), adj |,
                     where(#1[number]:#2[number] & #1[person]:#2[person]).
3> verb[cop=+]     = #2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos], #1[lemma:fazer,!number:!,!gender:!,!person:!] | (pron[quant];adv), adj |,
                     where(#1[number]:#2[number] & #1[person]:#2[person]).


// DEITAR-SE A + INF
// Deitar-se is an aspectual form and has to bear feature aInfInc:+
//
// Example: Ele deitou-se a comer.
//
5> pastpart[aInfInc=+] = #2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos], pastpart#1[lemma:deitar,!number:!,!gender:!,!person:!] | ?[lemma:a], verb[inf] |,
                         where(#1[number]:#2[number] & #1[person]:#2[person]).
5> verb[aInfInc=+]     = #1[lemma:deitar,!number:!,!gender:!,!person:!], #2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos] | ?[lemma:a], verb[inf] |,
                         where(#1[number]:#2[number] & #1[person]:#2[person]).
5> verb[aInfInc=+]     = #2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos], #1[lemma:deitar,!number:!,!gender:!,!person:!] | ?[lemma:a], verb[inf] |,
                         where(#1[number]:#2[number] & #1[person]:#2[person]).
6> ?[aInfInc=~] = verb#1[lemma:deitar] |#2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos], (?[lemma:a]), verb[inf] |,
       where(#1[number]~:#2[number] | #1[person]~:#2[person]).


// POR-SE A + INF
// pôr-se is has to bear the feature aInfInc:+
//
// Examples: Eu tinha-me posto a chorar
// Examples: Eu pus-me a chorar
// Examples: Eu pus-me chorando
//
7> pastpart[aInfInc=+,noGerInc=+] = #2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos], pastpart#2[lemma:pôr,!number:!,!gender:!,!person:!] | (?[lemma:a]), verb[gerund];verb[inf] |,
                                    where(#1[number]:#2[number] & #1[person]:#2[person]).
7> verb[aInfInc=+,noGerInc=+]     = #1[lemma:pôr,!number:!,!gender:!,!person:!], #2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos] | (?[lemma:a]), verb[gerund];verb[inf] |,
                                    where(#1[number]:#2[number] & #1[person]:#2[person]).
7> verb[aInfInc=+,noGerInc=+]     = #2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos], #2[lemma:pôr,!number:!,!gender:!,!person:!] | (?[lemma:a]), verb[gerund];verb[inf] |,
                                    where(#1[number]:#2[number] & #1[person]:#2[person]).

7> ? = #1[lemma:pôr,aInfInc=~,noGerInc=~] |#2[surface:me];#2[surface:te];#2[surface:se];#2[surface:nos];#2[surface:vos], (?[lemma:a]), verb[gerund];verb[inf] |,
       where(#1[number]~:#2[number] | #1[person]~:#2[person]).




// A GENTE is personal pronoun that accords inflexion pers=3, Gender=f, Number=sing
// but it is interpreted as pers=1, Number=plural (the same as "nós")
//
// This rule should be coded in RuDriCo as soon as negation is available there!!!!!!
//
// Example: A falar é que a gente se entende
// Example: Se a gente vier a esta hora
// Example: A gente nova não sabe viver (it is not a pronoun)
// Example: A gente do teatro ... (it is not a pronoun)
//
7> pron[pers=+,fem=+,sg=+] = ?[surface:a], ?[lemma:gente] | ?[adj:~,surface:~"de"] | .
