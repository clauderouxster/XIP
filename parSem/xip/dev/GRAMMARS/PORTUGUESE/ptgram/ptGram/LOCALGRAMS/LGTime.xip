//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : LGTime.xip                                                  !
//!   Date       : 2006/October                                                !
//!   Author     : João Loureiro  - C. Hagege  (For HAREM2)                    !
//!   Altered by : Diogo Oliveira   (2010/September)                           !
//!   Rebuilt by : Andreia Maurício (2010-2011)                                !
//!   Revised by : Nuno Mamede & Jorge Baptista (2012/May)                     !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!  Identifies Time expressions                                               !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Sequence:

1>  ? = noun[surface:"AK-47",t-interval=~,time=~].



//////////////////////////////////////////////////////
// quantidades de tempo                             //
//////////////////////////////////////////////////////

//ANDREIA, 2011
//Example: a manhã passada
//Example: o dia que passou
//
1> noun[time=+,t-tempref=enunc,timeref:-]  = ?[t-meas];?[t-weekday];?[t-part-of-day];?[t-as-a-meas], ?[lemma:que], ?[surface:passou].
1> noun[time=+,t-tempref=enunc,timeref:-]  = ?[t-meas];?[t-weekday];?[t-part-of-day];?[t-as-a-meas], ?[lemma:passado];?[lemma:passada];?[lemma:anterior].
1> noun[time=+,t-tempref=text,timeref:+]   = ?[t-meas];?[t-weekday];?[t-part-of-day];?[t-as-a-meas], ?[lemma:seguinte].
1> noun[time=+,t-tempref=enunc,timeref:+]  = ?[t-meas];?[t-weekday];?[t-part-of-day];?[t-as-a-meas], ?[lemma:que], ?[surface:vem].
1> noun[time=+,t-tempref=enunc,timeref:+,t-uncertain=+]  = ?[t-meas];?[time,quant,t-date], ?[lemma:que], ?[lemma:"haver"], ?[lemma:de], ?[surface:vir].
1> noun[time=+,t-tempref=enunc,timeref:+,t-uncertain=+,t-no-normalization=+]  = ?[t-weekday];?[t-part-of-day];?[t-as-a-meas], ?[lemma:que], ?[lemma:"haver"], ?[lemma:de], ?[surface:vir].


////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
// Intervalos                                                                 //
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

// São intervalos na terminologia do HAREM
//
//ATENÇÃO: estas regras podem ter de ser deslocadas para o pós-chunker,
//         pois alguns dos elementos não foram ainda calculados ?!?!?!?!?!?
//
4> pp[time=+,t-interval=+] = ?[lemma:entre], (art[def]), ?[time, t-date];?[t-century], conj[lemma:e],  (art[def]), ?[time, t-date];?[t-century], (?[t-era]).
4> pp[time=+,t-interval=+] = ?[lemma:entre], (art[def]), ?[t-month], conj[lemma:e],  (art[def]), ?[t-month].

4> pp[time=+,t-interval=+] = prep[lemma:de], ?[time, t-date], ?[surface:a],   ?[time, t-date], (?[t-era]).
4> pp[time=+,t-interval=+] = prep[lemma:de], ?[time, t-date], ?[surface:até], ?[time, t-date], (?[t-era]).
4> pp[time=+,t-interval=+] = ?[lemma:desde], ?[time, t-date], ?[lemma:até],   ?[time, t-date], (?[t-era]).

4> pp[time=+,t-interval=+] @= ?[lemma:entre], ?[t-data-string], (?[t-hora-string]), conj[lemma:e], ?[t-data-string], (?[t-hora-string]).
4> pp[time=+,t-interval=+] @= prep[lemma:de], ?[t-data-string], (?[t-hora-string]), ?[surface:a],  ?[t-data-string], (?[t-hora-string]).
4> pp[time=+,t-interval=+] @= prep[lemma:de], ?[t-data-string], (?[t-hora-string]), ?[surface:até],?[t-data-string], (?[t-hora-string]).
4> pp[time=+,t-interval=+] @= ?[lemma:desde], ?[t-data-string], (?[t-hora-string]), ?[lemma:até],  ?[t-data-string], (?[t-hora-string]).

4> pp[time=+,t-interval=+] @= ?[lemma:entre], (?[t-data-string]), ?[t-hora-string], conj[lemma:e], (?[t-data-string]), ?[t-hora-string].
4> pp[time=+,t-interval=+] @= prep[lemma:de], (?[t-data-string]), ?[t-hora-string], ?[surface:a],  (?[t-data-string]), ?[t-hora-string].
4> pp[time=+,t-interval=+] @= prep[lemma:de], (?[t-data-string]), ?[t-hora-string], ?[surface:até],(?[t-data-string]), ?[t-hora-string].
4> pp[time=+,t-interval=+] @= ?[lemma:desde], (?[t-data-string]), ?[t-hora-string], ?[lemma:até],  (?[t-data-string]), ?[t-hora-string].


4> pp[time=+,t-interval=+, t-tempref=absolut] = ?[lemma:em], art[def], noun[lemma:"ano %d+\-%d+",t-year], (?[t-era]).
4> pp[time=+,t-interval=+,  t-tempref=absolut] = ?[lemma:em], art[def], ?[lemma:século], ?[lemma:"%c+\-%c+"];?[lemma:"%C+\-%C+"], (?[t-era]).
4> pp[time=+,t-interval=+,  t-tempref=absolut] = ?[lemma:em], art[def], ?[t-century], ?[surface:a], ?[rom], (?[t-era]).

4> pp[time=+,t-interval=+] = ?[lemma:em], art[def], ?[time, t-date], ?[surface:a], ?[time, t-date];?[num], (?[t-era]).

4> pp[time=+,t-interval=+] = ?[lemma:de],    num, ?[surface:a],   ?[time,quant,t-date], (?[t-era]).
4> pp[time=+,t-interval=+] = ?[lemma:de],    num, ?[surface:até], ?[time,quant,t-date], (?[t-era]).
4> pp[time=+,t-interval=+] = ?[lemma:desde], num, ?[surface:até], ?[time,quant,t-date], (?[t-era]).
4> pp[time=+,t-interval=+] = ?[lemma:entre], num, ?[surface:e],   ?[time,quant,t-date], (?[t-era]).




/////////////////////////////////////////////////////////////////////////////
// THINGS TO ADD - PLEASE KEEP IT HERE FOR REMEMBERING
//CH, 2008/02/15
/////////////////////////////////////////////////////////////////////////////

// TO DO - Only possible with scripting because we cannot have the values of years of the second expression
// Coordination of dates
// Nos dias 17 e 18 de Novembro
// Two structures have been constructed, we cannot apply sequence rules
//| noun#1[time,t-tempref:absolut,t-monthday], conj[coord,lemma:e], noun#2[t-tempref:absolut]{?*,#3[t-month:1]} |

// Nos dias 16, 17 e 18 de Novembro
// two structures also but in the first one the coordination is present
// each member of the first complex noun must have new features added on them
//| noun#1[time,t-tempref:absolut,t-monthday]{?*, num[dig,t-date], ?*, num[dig:date]}, conj[coord,lemma:e], noun#2[t-tempref:absolut]{?*,#3[t-month:1]} |
