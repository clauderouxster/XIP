//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : disamb_after.xip                                            !
//!   Date       : 2006/March                                                  !
//!   Author     : Nuno Mamede                                                 !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!   POS disambiguation                                                       !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Tagging:


//========================================================
// upper case words (other than at the beginning of a sentence) must be a  proper name
//========================================================
// Example:
1> ?<maj,start:~> = ?<proper> .


//========================================================
// Deleting tag ADJ for past-participle
//========================================================
// Example:
2> adj, pastpart = ?[adj=~].


//========================================================
// FRACTION-ADJ-NOUN ambiguity
//========================================================
// Example: meia (2 canecas e meia (um quarto))
2> num,adj,noun = | num, adj;noun, ?[lemma:e], (art) | num .


//========================================================
// VERB inf-conjuntivo ambiguity
//========================================================
// Se houver uma preposição por perto à esquerda e uma forma ambigua verbal
// conjuntivo, infinitivo então escolha-se a forma infinitiva
// Example:para conseguir
2> verb<inf>, verb<subj,fut> = | prep, (art), (adj), (noun), (adv) | verb[inf] .


//========================================================
// AUXILIAR seguido de VERB inf
//========================================================
// Se uma flexão ambígua de um lema verbal puder ser um infinitivo não flexionado, 
// e se vier a seguir à prep pedida pel auxiliar), então, 
// descarta as outras etiquetas desse lema e escolhe o infinitivo não flexionado
2> verb<inf>, verb<inf:~> = | ?[aInfReal],     (pron,adv), prep[lemma:a],    (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[aInfDur],      (pron,adv), prep[lemma:a],    (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[aInfIte],      (pron,adv), prep[lemma:a],    (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[aInfInc],      (pron,adv), prep[lemma:a],    (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[aInfIrreal],   (pron,adv), prep[lemma:a],    (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[aInfFut],      (pron,adv), prep[lemma:a],    (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[aInfTer],      (pron,adv), prep[lemma:a],    (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[deInfImp],     (pron,adv), prep[lemma:de],   (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[deInfObr],     (pron,adv), prep[lemma:de],   (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[deInfIrreal],  (pron,adv), prep[lemma:de],   (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[deInfTer],     (pron,adv), prep[lemma:de],   (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[deInfDeo],     (pron,adv), prep[lemma:de],   (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[deInfDeoImpf], (pron,adv), prep[lemma:de],   (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[deInfFre],     (pron,adv), prep[lemma:de],   (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[emInfIrreal],  (pron,adv), prep[lemma:em],   (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[emInfInc],     (pron,adv), prep[lemma:em],   (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[porInfTer],    (pron,adv), prep[lemma:por],  (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[porInfInc],    (pron,adv), prep[lemma:por],  (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[porInfImpP],   (pron,adv), prep[lemma:por],  (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[paraInfImp],   (pron,adv), prep[lemma:para], (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[paraInfInc],   (pron,adv), prep[lemma:para], (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[queInfObr],    (pron,adv),    ?[lemma:que],  (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[queInfDeoImps],(pron,adv),    ?[lemma:que],  (pron)| verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[noInfIrreal],  (pron,adv)|                           verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[noInfFre],     (pron,adv)|                           verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[noInfDur],     (pron,adv)|                           verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[noInfDeo],     (pron,adv)|                           verb[inf] .
2> verb<inf>, verb<inf:~> = | ?[noInfFut],     (pron,adv)|                           verb[inf] .



//========================================================
// NOUN-ADJ ambiguity
//========================================================
// Example: Os empresários tomarenses
//10> noun, adj = adj | noun[verb:~] | .


//========================================================
// INFINITIVE vs NOUN ambiguity
//========================================================
// After an aspectual verb followed by a prep, then it is an non-flexioned infinitive
4> noun, verb<inf> = | verb[vasp], (adv), prep |  verb[inf] .


// If we have an ambiguous verb[inf],noun followed by an infinitive verb
// And if this verb is also preceeded by a preposition
// Then it is an infinitive and not a noun
// Example: Vim para o Pedro poder comer o bolo.
5> noun, verb<inf> = | prep, (art;pron[quant]), (pron[poss]), (adj), noun | verb[inf] | (prep), verb[inf] | .


//========================================================
// PRON-ADV ambiguity
//========================================================
//NJM+JB, 2010/04/05
// removed because there are equivalent rules in RuDriCo
//
//15> pron<lemma:"muito",sg,masc>    %= | verb | adv | prep | .
//15> pron<lemma:"pouco",sg,masc>    %= | verb | adv | prep | .
//15> pron<lemma:"nada">                 %= | verb | adv | prep | .


// Jorge: afirma que a regra está errada. Exemplo: "Esforçarmo-nos demasiado cansa".
//
// A tirar quando se alterar o PALAVROSO que devia dar a etiqueta pretendida
// (pode faltar depois a regra de dasambiguação)
//
// Example: Eles começaram a reunir-se
//20> verb[infflex] = verb[inf=+,infflex:~] |pron[lemma:"se",cli]|.


//========================================================
// PRON PREP+DETPL ambiguity
//========================================================
// Example: nos
//30> pron[surface:"nos"] %= prep | noun | .
//30> noun[surface:"nos"] %= prep | noun | .


//========================================================
// ADJ Verb first person sigular
//========================================================
// if we have a ambiguous verb[1st person]/adj that just follows a singular noun,
// then it is an adj and not a verb
// Example: financiamento oculto
7> adj<sg>, verb<pres,1p> = | noun[sg] | adj .

//========================================================
// NOUN Verb first person sigular
//========================================================
// if we have a ambiguous verb[1st person]/noun that just follows determiners/adj
// then it is an noun and not a verb
// Example: Os seu belo pupilo
7> noun<sg>, verb<pres,1p> = | art;pron[dem], (pron[poss];num), adj[sg] | noun .


//========================================================
// NOUN/VERB ambiguity
//========================================================
// If we have the lemma "que" followed possibly by an adverb and then by an ambiguous
// noun/verb
// Then we chose the reading verb and not noun
// Example: diz que ja veio[VERB] na regiao ha 18 anos.
7> noun<sg>, verb = | ?[lemma:que], (pron), (adv) | verb .

//========================================================
// VERB[fut,subj]/VERB[inf] ambiguity
//========================================================
// If we have on the left of an ambiguous verb[fut,subj]/infinitive verb
// AND if on the left of this verb we have another verb bearing feature
// s_np_inf, then we chose the infinitive form
7> verb<inf>,verb<subj,fut> = | verb[s_np_inf], ?* | verb[inf] .


//========================================================
// ART[surface=a]/PREP[surface=a] ambiguity
//========================================================
//
// Example: foi/veio a Lisboa
//
8> art<surface:a>, prep<surface:a> = prep | noun[introd_street];noun[city] | .


//geographic=+,mountain
//island=+].capital, region, division mountain
//geographic,water-masscity country continent



//========================================================
// for the rest: take preferred readings from HMM tagger
//========================================================
9> ? = ?[hmmselection].



//========================================================
// Regras apos a seleccao do HMM
//========================================================

// VINF apanhado como Verbo finito
// Example: Vim para poder o Pedro COMER[vinf] o bolo
10> verb[fut,subj,1p] %= | verb[noinfdeo], (art;pron[quant]), (pron[poss]), (adj), noun | verb[verb,inf] .


// VINF taken as masculine noun. This is only done for very constrained constructions.
//
// Example: Para ele, o PCD é o CONTINUAR(as noun) da esperança de os cabo-verdianos
// poderem resolver problemas.
// Check if not introducing problems.
10> verb[inf] %= | verb[lemma:ser], art[lemma:o,sg,masc] | noun[masc,sg,inf] .

// Same rule as above when the article is the first element of the sentence
//
// Example: O continuar da esperança de os cabo-verdianos poderem resolver problemas...
10> verb[inf] %= | art[lemma:o,sg,masc,start] | noun[masc,sg,inf] .
