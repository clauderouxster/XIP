//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2005 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : Portuguese XIP grammar
//!   filename   : desamb_for_fst.xip
//!   Date       : Feb. 2005
//!   Author     : Caroline Hagege
//!   Description : Words that have to be added or modified in the FST.
//! 		    When the modification cannot be performed through a lexicon file
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!



Tagging:

// SIDA : changing a guessed pastpart into a feminine noun
10> pastpart<surface:sida> %= noun[fem:+,sg:+] .


// the word "estaca" is a feminine noun (and not only a verb). Has to be added to the fst.
// in plural form destroy the interpretation verb if preceeded by a feminine determiner
10> verb<lemma:estacar,2p:+> %= | art[pl:+];num[card:+];adj[pl:+,fem:+] | noun[noun:+,fem:+,pl:+] .


// the word "palestra" is a feminine noun and not only a verb
10> verb<lemma:palestrar,3p:+,sg:+> %= | art[sg:+,fem:+], (adj) | noun[noun:+,fem:+,sg:+,lemma:palestra] .
10> verb<lemma:palestrar,2p:+,sg:+> %= | art[pl:+];num[card:+];adj[pl:+,fem:+] | noun[noun:+,fem:+,pl:+] .


// The word "recompensa" is a feminine noun (and not only a verb). Has to be added to the fst.
// in plural form destroy the interpretation verb if preceeded by a feminine determiner
10> verb<lemma:recompensar,2p:+> %= | art[pl:+];num[card:+];adj[pl:+,fem:+] | noun[noun:+,fem:+,pl:+] .

// in singular form, destroy the interpretation verb if at the beginning of the sentence 
// and preceeded by "a" or "uma"
10> verb<lemma:recompensar, 3p:+, pres:+> %= | art[sg:+,fem:+,start:+] | noun[noun:+,fem:+,sg:+] .

// the word "oculto" is an adjective (to add in the fst lexicon)
10> verb<lemma:ocultar,1p:+> %= | noun[sg:+] | adj[sg:+,masc:+,adj:+] .

// The word "pupilo" is also a noun (only coded as verb in FST lexicon) - To ADD
10> verb<lemma:pupilar, 1p:+> %= | art;pron[dem:+], (pron[poss:+]), (num), adj*, (noun) | noun[noun:+,masc:+,sg:+] .

// the word "mÃºsica" is only a noun and not an adjective and a noun
10> noun[lemma:mÃºsica] %= noun .

// the word "fixe" can be an adjective when preceeded by "ser". At this time only verb "fixar"
10> verb<surface:fixe> %= | verb[lemma:ser], (adv) | adj. 


// the word "ribeira" can be a nounalso (only verb at this stage). Add in the fst
//10> verb<surface="Ribeira"> %= noun[fem:+,sg:+,geographic:+,location:+,maj:+] | (prep[lemma:de]), ?[maj,toutmaj:~] | .

// word "Exame" when preceeding by a word with lower case and followed by a word in lower case is newspaper name
10> noun<lemma:exame,maj:+> %= | ?[maj:~] | noun[proper:+,jornal:+,org:+,company:+] | ?[maj:~] | .
10> noun<lemma:"visão",maj:+> %= | ?[maj:~] | noun[proper:+,jornal:+,org:+,company:+] | ?[maj:~] | .
