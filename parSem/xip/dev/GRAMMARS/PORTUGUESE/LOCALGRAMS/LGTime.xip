//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! L2F - Spoken Language Systems Laboratory / INESC-ID Lisboa                 !
//!                                                                            !
//! Copyright (c) 2006 INESC-ID Lisboa.  All Rights Reserved.                  !
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : LGTime.xip                                                  !
//!   Date       : 2006/October                                                !
//!   Author     : João Loureiro  - C. Hagege  (For HAREM2)                    !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!  Identifies Time expressions                                               !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

// $Id: LGTime.xip,v 1.2 2008/10/21 15:20:04 chagege Exp $


Sequence:

////////////////////////////////////////////////////////////////////////////////
// Time and Dates                                                             //
////////////////////////////////////////////////////////////////////////////////

// segunda feira (sem hífen)
// Used by XEROX (do not erase)
// 2ª feira
2> noun[time=+, weekday=2] = noun[weekday:2], ?[lemma:feira].
2> noun[time=+, weekday=3] = noun[weekday:3], ?[lemma:feira].
2> noun[time=+, weekday=4] = noun[weekday:4], ?[lemma:feira].
2> noun[time=+, weekday=5] = noun[weekday:5], ?[lemma:feira].
2> noun[time=+, weekday=6] = noun[weekday:6], ?[lemma:feira].

2> noun[time=+, weekday=2] = ?[surface:2ª], ?[lemma:feira].
2> noun[time=+, weekday=3] = ?[surface:3ª], ?[lemma:feira].
2> noun[time=+, weekday=4] = ?[surface:4ª], ?[lemma:feira].
2> noun[time=+, weekday=5] = ?[surface:5ª], ?[lemma:feira].
2> noun[time=+, weekday=6] = ?[surface:6ª], ?[lemma:feira].

////////////////////////////////////////////////////////////////////////////////
// NOUNS                                                                      //
////////////////////////////////////////////////////////////////////////////////
// change for XEROX ?[surface:a] never corresponds to definite article for Xerox
2> noun[time:+, part_of_day:+] = ?[surface:turno];?[surface:período], ?[lemma:de], art[def],
                                 ?[lemma:manhã];?[lemma:tarde];?[lemma:noite].
2> noun[time=+, part_of_day=+]   = | conj | ?[time_focus], prep[lemma:de], (?[lemma:o]), ?[time_meas];?[part_of_day].

// Example: século XX
// Example: segunda metade do sec. XX
2> noun[time=+,date=+,tipo_tempref=absolut, century=+] @= (num[ord], ?[lemma:metade];?[lemma:quarto], prep[lemma:de], art[def]), ?[lemma:século];?[lemma:"séc."];?[surface:"sec."];?[surface:"sec"];?[lemma:"séc"], num[rom];num[card, frac:~], (?[lemma:dC];?[lemma:aC];?[lemma:AD]).

// Example: no 1º lustro da década de 1930
2> noun[time=+,date=+,tipo_tempref=absolut]  @= (?[def]), ?[lemma:primeiro];?[lemma:último];?[lemma:segundo];num[ord], ?[surface:lustro], (prep[lemma:de], ?[def], ?[lemma:década]), prep[lemma:de], ?[num].
2> noun[time=+,date=+,tipo_tempref=absolut]  @= (?[def]), ?[lemma:primeiro];?[lemma:último];?[lemma:segundo];num[ord], ?[surface:lustro], (prep[lemma:de], ?[def], ?[lemma:década]), prep[lemma:de], ?[num].

// Example: na primeira metade do século XV
2> noun[time=+,date=+,tipo_tempref=absolut]  = ?[def], ?[lemma:primeiro];?[lemma:último];?[lemma:segundo], ?[lemma:metade];?[lemma:década], prep[lemma:de], ?[def], ?[century].
2> noun[time=+,date=+,tipo_tempref=absolut]  = ?[lemma:primeiro];?[lemma:último];?[lemma:segundo], ?[lemma:metade];?[lemma:década], prep[lemma:de], ?[def], ?[century].
2> noun[time=+,date=+,tipo_tempref=absolut]  = ?[surface:meados], prep[lemma:de], ?[def], ?[century].

// Example: no 1º quartel do século XX
2> noun[time=+,date=+,tipo_tempref=absolut]  @= (?[def]), ?[lemma:primeiro];?[lemma:último];?[lemma:segundo];num[ord], ?[surface:quartel], prep[lemma:de], ?[def], ?[century].
2> noun[time=+,date=+,tipo_tempref=absolut]  @= (?[def]), ?[lemma:primeiro];?[lemma:último];?[lemma:segundo];num[ord], ?[surface:quartel], prep[lemma:de], ?[def], ?[century].


// Example: nas primeiras décadas
14> adv[advtimetemp=+,tipo_tempref=enun,ref-before=+] = prep[lemma:em], ?[def];?[proxdem], ?[ord], ?[time_meas,pl];?[lemma:altura,pl];?[lemma:momento,pl];?[lemma:ocasião,pl];?[lemma:instante,pl];?[lemma:tempo,pl].


////////////////////////////////////////////////////////////////////////////////
// CH - Complex temporal triggers that are not tokenized as a whole           //
////////////////////////////////////////////////////////////////////////////////
// a seguir a...
2> prep[ref-after=+] = ?[lemma:a], ?[lemma:seguir], prep[lemma:a].
2> adv[ref-after=+] = | ?[time:+,meas:+] | ?[lemma:a], ?[lemma:seguir].

/// SPECIAL XEROX TO AVOID CODING SO MANY ADVERBIALS
// COMPLEX TEMPORAL ADVERBS FROM JB list. CH, march
// Are tokenized differently at XRCE. Try to catch them throu the following rules
2> adv[advtimetemp=+] @= prep[lemma:por], ?[lemma:volta], prep[lemma:de], art[def], ?[part_of_day].
2> adv[advtimetemp=+] @= prep[lemma:de], ?[part_of_day].
2> adv[advtimetemp=+] = prep[lemma:a], art[def], ?[part_of_day].
2> adv[advtimetemp=+] @= prep[lemma:por], art[def], ?[part_of_day], (?[lemma:dentro];?[surface:adentro])  . 

// as vezes;todas as vezes
2> adv[advtimefreq=+] @= prep[lemma:a];?[lemma:todo,pl:+], art[def,pl], noun[lemma:vez,pl:+] .
// por vezes;algumas vezes
2> adv[advtimefreq=+] @= ?[lemma:por];pron[lemma:algum,fem,pl], noun[lemma:vez,pl:+] .
// de quando em quando
2> adv[advtimefreq=+] @= prep[lemma:de], ?[lemma:quando], prep[lemma:em], ?[lemma:quando] .
// 
2> adv[advtimefreq=+] @= noun#1[time_meas:+], ?[lemma:sim], noun#2[time_meas:+], adv[lemma:"não"], where(#1[lemma]:#2[lemma]) .
// uma vez (por;ou) outra
2> adv[advtimefreq=+] @= art[indef], noun[lemma:vez,sg], ?[lemma:por];?[lemma:ou], ?[lemma:outro,fem,sg] .

// hoje em dia
2> adv[advtimetemp=+,ref-simult=+,tref-enunc=+] @= ?[lemma:hoje], prep[lemma:em], ?[lemma:dia] .

// na vespera
2> adv[advtimetemp=+,ref-before=+,tref-text=+] @= prep[lemma:em], art[def], noun[lemma:véspera], prep[lemma:de], (art[def]), ?[one_day];?[date]  .
2> adv[advtimetemp=+,ref-before=+,tref-text=+] @= prep[lemma:em], art[def], noun[lemma:véspera]  .

// no dia anterior
2> adv[advtimetemp=+,ref-before=+,tref-text=+] @= prep, art[def], ?[time_meas], ?[lemma:anterior] .

// ao cair;romper do dia;noite
2> adv[advtimetemp=+] @= prep[lemma:a], art[def,masc], ?[lemma:cair];?[lemma:romper], prep[lemma:de], art[def], ?[part_of_day,time] .

////////////////////////////////////////////////////////////////////////////////
// Marking nums with the feature of the measure to enable its normalization   //
////////////////////////////////////////////////////////////////////////////////

//3> ? @= num[second=+];?[lemma:um,second=+], ?[lemma:segundo].
//3> ? @= num[minute=+];?[lemma:um,minute=+], ?[lemma:minuto].
//3> ? @= num[hour=+];?[lemma:um,hour=+], ?[lemma:hora].
//3> ? @= num[monthday=+];?[lemma:um,monthday=+], ?[lemma:dia].
//3> ? @= num[week=+];?[lemma:um,week=+],    ?[lemma:semana].
//3> ? @= num[month=+];?[lemma:um,month=+],  ?[lemma:mês].
//3> ? @= num[year=+];?[lemma:um,year=+],    ?[lemma:ano].

//3> ? @= num[year=+,h_to_compute=+];?[lemma:um,year=+,h_to_compute=+],   ?[lemma:década].
//3> ? @= num[year=+,h_to_compute=+];?[lemma:um,year=+,h_to_compute=+],   ?[lemma:século].
//3> ? @= num[year=+,h_to_compute=+];?[lemma:um,year=+,h_to_compute=+],   ?[lemma:milénio].

//3> ? @= num[week=+,h_to_compute=+];?[lemma:um,week=+,h_to_compute=+],   ?[lemma:quinzena].

//3> ? @= num[month=+,h_to_compute=+];?[lemma:um,month=+,h_to_compute=+],   ?[lemma:trimestre].
//3> ? @= num[month=+,h_to_compute=+];?[lemma:um,month=+,h_to_compute=+],   ?[lemma:semestre].




////////////////////////////////////////////////////////////////////////////////
// Frequency                                                                  //
////////////////////////////////////////////////////////////////////////////////

// 2 vezes por semana
// 2 vezes ao dia
// 4 dias por mês
//
4> noun[time=+, frequency=+] = num[dig];num[card, frac:~];?[lemma:um], ?[lemma:vez], ?[surface:a], ?[lemma:o], ?[time, meas].
4> noun[time=+, frequency=+] = num[dig];num[card, frac:~];?[lemma:um], ?[lemma:vez], ?[lemma:por], ?[time, meas].
4> noun[time=+, frequency=+] = num;?[lemma:um], ?[time, meas], ?[lemma:por], ?[time, meas].

// de 2 em 2 dias
4> pp[time=+, frequency=+] = ?[lemma:de], num[dig];num[card, frac:~], ?[lemma:em], num[dig];num[card, frac:~], ?[time_meas].

////////////////////////////////////////////////////////////////////////////////
// Relative Dates                                                             //
////////////////////////////////////////////////////////////////////////////////

// MW expressions (XEROX ONLY)
6> adv[time=+, uncertain=+, timeref=+, timeval=2, tipo_tempref=enun] = prep[lemma:"depois_de"], ?[lemma:amanhã].
6> adv[time=+, uncertain=+, timeref=-, timeval=2, tipo_tempref=enun] = prep[lemma:"antes_de"], ?[lemma:ontem].

// daqui a X dias | semanas | meses | anos
// PROBLEMA : nuno constroi PP mas perde-se um traco depois na NE
8> adv[time=+, date=+, date_aux=+, uncertain=+,tipo_tempref=enun,ref-after=+] @= prep[lemma:de], ?[lemma:aqui], ?[surface:a], ?[lemma:um];num[frac:~, ord:~], ?[time_meas], ?[lemma:e], ?[lemma:um], ?[lemma:quarto] .
8> adv[time=+, date=+, date_aux=+, uncertain=+,tipo_tempref=enun,ref-after=+] @= prep[lemma:de], ?[lemma:aqui], ?[surface:a], ?[lemma:um];num[frac:~, ord:~], ?[time_meas], (?[lemma:e], ?[lemma:meio]).

// de amanhã a X dias | semanas | meses | anos
// nuno has put pp, but then the rule for extracting them as NER implies that it is an adverb
8> adv[time=+, date=+, date_aux=+, uncertain=+,tipo_tempref=enun,ref-after=+] = prep[lemma:de], 
                                                                                 ?[lemma:amanhã];?[lemma:hoje];?[lemma:ontem];?[lemma:anteontem];?[lemma:"depois de amanhã"],
                                                                                 ?[surface:a], ?[lemma:um];num[frac:~, ord:~], ?[lemma:dia];?[lemma:semana];?[lemma:mês];?[lemma:ano].



// de sábado a X dias | semanas | meses | anos
10> pp[time=+, date=+, date_aux=+, uncertain=+] @= prep[lemma:de], (?[lemma:o], ?[lemma:próximo]), ?[weekday], ?[surface:a], ?[lemma:um];num[frac:~, ord:~], ?[lemma:dia];?[lemma:semana];?[lemma:mês];?[lemma:ano].



////////////////////////////////////////////////////////////////////////////////
// Dates                                                                      //
////////////////////////////////////////////////////////////////////////////////

// CH modification. Catch the longest expression first
// (dias) 4, 5 e 6 de Janeiro de 1995
12> noun[date=+, monthday=+] @= ?[lemma:dia], num[dig];num[card, frac:~], (punct[comma], num[dig];num[card, frac:~])+ .


// próxima quarta-feira
// quarta-feira seguinte
// primeira quarta-feira
14> noun[time=+, weekday=+, uncertain=+] = ?[timeref], ?[weekday].
14> noun[time=+, weekday=+, uncertain=+] = ?[weekday], ?[timeref].
14> noun[time=+, weekday=+] = num[ord], ?[weekday].

// CH added March 2008
// dia 3 deste mes
// este ano
// esta semana
// dia <num> deste mes/semana/
// This is a referential date !!!
14>noun[time=+,date=+,tipo_tempref=enun,uncertain=+] @= ?[lemma:dia], num, prep[lemma:de], (?[dem]), ?[time_meas:+] .

14> noun[time=+,date=+,tipo_tempref=enun,ref-simult=+,ref-after=+,uncertain=+] @= ?[dem], ?[time_meas:+] .


// próximo dia 2/primeiro
// dia 2/primeiro
// próximo dia
// dia anterior
// (próximo) primeiro dia
// (de) 2 (a 4 de Março)
// primeiro (de Abril)
// 1 (do 2)
// 1 (de Abril)
// 1 (do [próximo] mês)

// CH TO MOVE move uncertain after having calculated date e.g. no passado dia 7 de dezembro de 2003, was badly tokenized
14> noun[time=+, monthday=+] = ?[lemma:dia], num[dig];num[card, frac:~];num[ord].
14> noun[time=+, monthday=+] = (?[timeref]), num[ord], ?[lemma:dia].


// CH transformed the following rules in order to avoid the structure noun{num}
14> ? @= |prep[lemma:de]| num[dig,time=+,monthday=+];num[card, frac:~,time=+,monthday=+] |?[surface:a], num[dig];num[card, frac:~], prep[lemma:de], ?[month]|.
// acrescentar o traço uncertain às regras em baixo com o traço timeref qd a sua normalização estiver a ser feita
14> noun[time=+, monthday=+] = ?[timeref], num[ord] |?[surface:de], ?[month]|.
14> ? @= num[ord,time=+,monthday=+,timeref=+] |?[surface:de], ?[month]|.
14> noun[time=+, monthday=+] = ?[timeref], num[dig];num[card, frac:~] |?[surface:de], ?[month]|.
14> ? @= num[dig,time=+,monthday=+];num[card,frac:~,time=+,monthday=+] | (?[surface:de]), ?[month] |.
14> noun[time=+, monthday=+] = ?[timeref], num[dig];num[card, frac:~] |?[surface:de], ?[surface:o], num[dig];num[card, frac:~]|.
14> ? @= num[dig,time=+,monthday=+];num[card, frac:~,time=+,monthday=+] |?[surface:de], ?[surface:o], num[dig];num[card, frac:~]|.
14> noun[time=+, monthday=+] = ?[timeref], num[dig];num[card, frac:~] |?[surface:de], ?[surface:o], (?[timeref]), ?[lemma:mês]|.
14> ? @= num[dig,time=+,monthday=+];num[card, frac:~,time=+,monthday=+] |?[surface:de], ?[surface:o], (?[timeref]), ?[lemma:mês]|.
14> ?[time=+, monthday=+] = |prep[lemma:de]| num[dig];num[card, frac:~] |?[surface:a], num[dig];num[card, frac:~], prep[lemma:de], ?[month]|.


// próximo mês de Maio
// mês de Maio
// (próximo) mês 12
// próximo mês
// mês anterior
// primeiro mês
// (2 do) 3 (de 2002)
// (2 do) 3 (do [próximo] ano)
// (2 do) 3
// 3 (de 2002)
// 3 (do [próximo] 2002)
14> noun[time=+, month=+, uncertain=+] = ?[timeref], ?[lemma:mês], ?[lemma:de], ?[month].
14> noun[time=+, month=+] = ?[lemma:mês], ?[lemma:de], ?[month].
14> noun[time=+, month=+, uncertain=+] = ?[timeref], ?[lemma:mês], num[dig];num[card, frac:~].
14> noun[time=+, month=+] = ?[lemma:mês], num[dig];num[card, frac:~].
14> noun[time=+, month=+, uncertain=+] = ?[timeref,tipo_tempref:~disc], ?[lemma:mês].
14> noun[time=+, month=+, uncertain=+] = ?[lemma:mês], ?[timeref,tipo_tempref:~disc].
14> noun[time=+, month=+] = num[ord], ?[lemma:mês].
14> ?[time=+, month=+] = |num[dig];num[card, frac:~], ?[surface:de], ?[surface:o]| num[dig];num[card, frac:~] |?[surface:de], num[dig];num[card, frac:~]|.
14> ?[time=+, month=+] = |num[dig];num[card, frac:~], ?[surface:de], ?[surface:o]| num[dig];num[card, frac:~] |?[surface:de], ?[surface:o], ?[timeref], ?[lemma:ano]|.
14> ?[time=+, month=+] = |num[dig];num[card, frac:~], ?[surface:de], ?[surface:o]| num[dig];num[card, frac:~].
14> ?[time=+, month=+] = num[dig];num[card, frac:~]  |?[surface:de], num[dig];num[card, frac:~]|.
14> ?[time=+, month=+] = num[dig];num[card, frac:~]  |?[surface:de], ?[surface:o], ?[timeref], ?[lemma:ano]|.


// 1999
// (em...) 1999
// <evento> de 1999
// (próximo) ano (de) 2001
// próximo ano
// ano seguinte
14> ?[time=+, year=+] = |?[month];?[lemma:mês], (?[surface:de];punct[comma])| num[dig];num[card, frac:~].
14> ?[time=+, year=+] = |num[dig];num[card, frac:~], ?[surface:de]| num[dig];num[card, frac:~].
14> ?[time=+, year=+] = |?[lemma:em]| num[dig,monthday:~] |?[noun:~, num:~,adj:~]|.
14> ?[time=+, year=+] = |?[event], ?[lemma:de]| num[dig].
14> noun[time=+, year=+] = (?[timeref]), ?[lemma:ano], (?[surface:de]), num[dig];num[card, frac:~].
14> noun[time=+, year=+, uncertain=+,tipo_tempref=enun] = ?[timeref,tipo_tempref:~disc], (num), ?[lemma:ano].
14> noun[time=+, year=+, uncertain=+,tipo_tempref=disc] = ?[lemma:ano], ?[timeref,tipo_tempref:~enun,tipo_tempref:~disc].

// próxima primavera
// próximo ano
// ano seguinte
14> noun[time=+, tipo_tempref=disc, timeref=+] = ?[tipo_tempref:disc,timeref:+], ?[time_meas];?[season].
14> noun[time=+, tipo_tempref=disc, timeref=+] = ?[time_meas];?[season], ?[tipo_tempref:disc,timeref:+].
14> noun[time=+, tipo_tempref=disc, timeref=-] = ?[tipo_tempref:disc,timeref:-], ?[time_meas];?[season].
14> noun[time=+, tipo_tempref=disc, timeref=-] = ?[time_meas];?[season], ?[tipo_tempref:disc,timeref:-].
14> noun[time=+, tipo_tempref=enun, timeref=+] = ?[tipo_tempref:enun,timeref:+], ?[time_meas];?[season].
14> noun[time=+, tipo_tempref=enun, timeref=+] = ?[time_meas];?[season], ?[tipo_tempref:enun,timeref:+].
14> noun[time=+, tipo_tempref=enun, timeref=-] = ?[tipo_tempref:enun,timeref:-], ?[time_meas];?[season].

// ADDED CH
// if we have a possible date (4 digits starting by 1[09] or 20 between parenthesis,
// we mark it as a date:+,year:+
14> ?[time=+,year=+] = | punct[left,paren] | num[lemma:"1%d%d%d"] | punct[right,paren] | .
14> ?[time=+,year=+] = | punct[left,paren] | num[lemma:"20%d%d"] | punct[right,paren] | .
// depois de + digit
// antes de + digit
14> ?[time=+,year=+] = | prep[lemma:"antes_de"] | num[lemma:"1%d%d%d"] | ?[noun:~,adj:~] | .
14> ?[time=+,year=+] = | prep[lemma:"depois_de"] | num[lemma:"1%d%d%d"] | ?[noun:~,adj:~] | .
14> ?[time=+,year=+] = | prep[lemma:"antes_de"] | num[lemma:"20%d%d"] | ?[noun:~,adj:~] | .
14> ?[time=+,year=+] = | prep[lemma:"depois_de"] | num[lemma:"20%d%d"] | ?[noun:~,adj:~] | .



// século XX
// próximo século
// século anterior
// CH1 - NUNO has added tipo-tempref=absolut...for the two last expresssion and this is not the case....
// CH2 - the first rule is broadened in order to take into account
// "segunda metade do sec. XX"
14> noun[time=+, date=+, tipo_tempref=absolut, century=+] @= (num[ord], ?[lemma:metade];?[lemma:quarto], prep[lemma:de], art[def]), ?[lemma:século];?[lemma:"séc."];?[surface:"sec."];?[surface:"sec"];?[lemma:"séc"], num[rom];num[card, frac:~], (?[lemma:dC];?[lemma:aC];?[lemma:AD]).
//14> noun[time=+, century=+,tipo_tempref=enun,ref-after=+] = ?[timeref], ?[lemma:século]. // acrescentar o traço uncertain quando a normalização estiver feita
//14> noun[time=+, century=+,tipo_tempref=disc,ref-before=+] = ?[lemma:século], ?[timeref]. // acrescentar o traço uncertain quando a normalização estiver feita


// a. C. / d. C.
14> ?[time=+, timeref=+] = ?[surface:"a.C."];?[surface:"d.C."].



////////////////////////////////////////////////////////////////////////////////
// Other kind of dates                                                        //
////////////////////////////////////////////////////////////////////////////////

// 04/02
// 02/Janeiro
// Jan/02
// 4/1/1920
// 4/Jan/1920
16> noun[time=+, data_complex=+] = num[dig], ?[slash], num[dig];?[month], ?[slash], num[dig].
16> noun[time=+, data_complex=+] = num[dig], ?[slash], num[dig].
16> noun[time=+, data_complex=+] = num[dig], ?[slash], ?[month].
16> noun[time=+, data_complex=+] = ?[month], ?[slash], num[dig].



////////////////////////////////////////////////////////////////////////////////
// combinação dos chunks anteriores - dia da semana + dia do mês + mês + ano) //
////////////////////////////////////////////////////////////////////////////////
// quarta-feira, 22 de Março de 1998 
// quarta-feira, 22 de Março 
// quarta-feira, dia 22
// dia 22 de Março de 1998
// dia 22 de Março
// Março de 1998
// quarta-feira <- atribuição do traço date
// dia 22 <- atribuição do traço date
// Janeiro <- atribuição do traço date
// ano de 1998 <- atribuição do traço date
//
18> noun[time=+,date=+,tipo_tempref=absolut] @= ?[weekday], (punct[comma]), ?[monthday], (?[lemma:de], (?[lemma:o])), ?[month], (?[lemma:de], (?[lemma:o]), ?[year]), (?[timeref]).
18> noun[time=+,date=+,tipo_tempref=absolut] @= ?[monthday], (?[lemma:de], (?[lemma:o])), ?[month], (?[lemma:de], (?[lemma:o]), ?[year]), (?[timeref]), (punct[comma]), (?[lemma:em], art), ?[weekday].
18> noun[time=+,date=+,tipo_tempref=absolut] @= ?[weekday], (punct[comma]), ?[monthday].
18> noun[time=+,date=+,tipo_tempref=absolut] @= ?[monthday], (?[lemma:de], (?[lemma:o])), ?[month], (?[lemma:de], (?[lemma:o]), ?[year]), (?[timeref]).
18> noun[time=+,date=+,tipo_tempref=absolut] @= ?[monthday], ?[lemma:de], (?[lemma:o]), ?[timeref], ?[lemma:mês].
18> noun[time=+,date=+,tipo_tempref=absolut] @= ?[monthday], ?[lemma:de], (?[lemma:o]), ?[lemma:mês], ?[timeref].
18> noun[time=+,date=+,tipo_tempref=absolut] @= ?[month], ?[lemma:de], (?[lemma:o]), ?[year], (?[timeref]).



// Added CH
// Season followed by year
18> noun[time=+, date=+, tipo_tempref=absolut] @= ?[season], ?[lemma:de], (?[lemma:o]), num[dig,year=+].

// When the date is in american format
// Quarta, Julho 3 de 2005 - Have to clean the features month and year that have been attached
// previously on the monthday
18> noun[time=+, date=+, tipo_tempref=absolut] @= ?[weekday], punct[comma], noun[month], num[month=~,year=~,monthday=+], (prep[lemma:de]), ?[year] .

// Janeiro, 3 de 2004
// Similar treatment - cleaning previously attributed features.
18> noun[time=+, date=+, tipo_tempref=absolut] @= noun[month], (punct[comma]), num[month=~,year=~,monthday=+], (prep[lemma:de]), ?[year] .

// NJM (muito duvidoso ....)
18> ?[date=+] @ = ?[weekday].
//18> ?[date=+,tipo_tempref=absolut] @ = ?[monthday].
//18> ?[date=+, tipo_tempref=absolut] @ = ?[month].
//18> ?[date=+,tipo_tempref=absolut] @ = ?[year].




////////////////////////////////////////////////////////////////////////////////
// Hours                                                                      //
////////////////////////////////////////////////////////////////////////////////


/////////////////////////////
// Marking rules that will mark the nodes containing hours information and minutes information
19> ? @= ?[lemma:meia-noite,hour=+] .
19> ? @= ?[lemma:meio-dia,hour=+] .

//19> ? @= num[minute=+];?[lemma:um,minute=+], ?[lemma:minuto] .
//20> ? @= num[hour=+];?[lemma:um,hour=+], ?[lemma:hora] .
20> ? @= ?[lemma:hora], ?[lemma:e], (?[lemma:um]), ?[lemma:meio,minute=+];?[lemma:quarto,minute=+] .

20> ? @= num[hour=+];?[lemma:um,hour=+], ?[lemma:e], num[minute=+] .
20> ? @= prep[lemma:a], (art[def]),  num[hour=+];?[lemma:um,hour=+], ?[lemma:e], num[minute:~, minute=+] .


//////////////////////////////////////////////////////
// 1-Hours (às | antes das | depois das | cerca das | por volta das) //FALTA
//////////////////////////////////////////////////////
// 20 para as 5
// 20 para as 5 da manhã / tarde / noite / madrugada
//
25> noun[hora=+,time=+] @= num[frac:~,ord:~,minute=+], (?[lemma:minuto]),
                            ?[lemma:para], (?[lemma:o]), num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], ?[lemma:hora];?[lemma:h], (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp,timeval:~]).
25> noun[hora=+,time=+] @= num[lemma:"um quarto"], (?[lemma:minuto]),
                            ?[lemma:para], (?[lemma:o]), num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], ?[lemma:hora];?[lemma:h], (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp,timeval:~]).


/////////////////////////////
// 20 menos 5
// 20 menos 5 da manhã / tarde / noite / madrugada
//
26> noun[hora=+,time=+] @= num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+];?[lemma:"%d+[%,]5",num=+,hour=+,hora_complex=+], ?[lemma:hora];?[lemma:h], (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (conj[lemma:e];?[lemma:menos], (?[lemma:o]), num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto];?[lemma:quarto];?[lemma:quartos])), (?[surface:AM]),
                            ?[lemma:"depois de"], (?[lemma:o]), num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto];?[lemma:meio];?[lemma:meia];?[lemma:quarto];?[lemma:quartos]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp,timeval:~]).

26> noun[hora=+,time=+] @= num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+];?[lemma:"%d+[%,]5",num=+,hour=+,hora_complex=+], ?[lemma:hora];?[lemma:h], (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            conj[lemma:e];?[lemma:menos],  ?[lemma:meio];?[lemma:meia], (?[surface:AM]),
                            ?[lemma:"depois de"], (?[lemma:o]), num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto];?[lemma:meio];?[lemma:meia];?[lemma:quarto];?[lemma:quartos]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp,timeval:~]).

27> noun[hora=+,time=+] @= num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], ?[lemma:hora];?[lemma:h], (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            ?[lemma:menos], ?[lemma:um], ?[lemma:quarto];?[lemma:meio], ((?[lemma:de]), ?[lemma:hora]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp,timeval:~]).

27> noun[hora=+,time=+] @= num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], ?[lemma:hora];?[lemma:h], (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            ?[lemma:menos], num[lemma:"um quarto"];?[lemma:meio], ((?[lemma:de]), ?[lemma:hora]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp,timeval:~]).

27> noun[hora=+,time=+] @= num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            conj[lemma:e];?[lemma:menos];?[lemma:"depois de"], (?[lemma:o]), ?[lemma:meio,num=+,hour=+];?[surface:picos], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp,timeval:~]).

28> noun[hora=+,time=+] @= | ?[lemma:~durante]| num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+];?[lemma:"%d+[%,]5",num=+,hour=+,hora_complex=+], ?[lemma:hora];?[lemma:h], (?[surface:AM]),
                            prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada],
                            (conj[lemma:e];?[lemma:menos];?[lemma:"depois de"], (?[lemma:o]), num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto];?[lemma:quarto];?[lemma:quartos])), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp,timeval:~]).

28> noun[hora=+,time=+] @= | ?[lemma:~durante]| num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+];?[lemma:"%d+[%,]5",num=+,hour=+,hora_complex=+], ?[lemma:hora];?[lemma:h], (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (conj[lemma:e];?[lemma:menos];?[lemma:"depois de"], (?[lemma:o]), num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto];?[lemma:quarto];?[lemma:quartos])), (?[surface:AM]),
                            prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada],
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp,timeval:~]).



// 2 horas, 20  minutos e 30 segundos da manhã / tarde / noite / madrugada
//
29> noun[hora=+,time=+] @= num[frac:~,ord:~];?[lemma:um];?[lemma:um],  (?[lemma:hora];?[lemma:minuto];?[lemma:segundo]), (?[surface:AM]),
                            (punct[comma], num[frac:~, ord:~];?[lemma:um], (?[time, meas]))*,
                            conj[lemma:e];?[lemma:"depois de"], (?[lemma:o]), num[frac:~, ord:~];?[lemma:um], (?[time, meas]),  (?[surface:AM]),
                            prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada].

// 2 horas, 20  minutos e 30 segundos
29> noun[hora=+,time=+] @= num[frac:~,ord:~];?[lemma:um], ?[lemma:hora];?[lemma:minuto];?[lemma:segundo], (?[surface:AM]),
                            (punct[comma], num[frac:~, ord:~];?[lemma:um], ?[time, meas])*,
                            conj[lemma:e], num[frac:~, ord:~];?[lemma:um], ?[time, meas], ?[surface:AM].
29> noun[hora=+,time=+] @= num[frac:~,ord:~];?[lemma:um], ?[lemma:hora];?[lemma:minuto];?[lemma:segundo], ?[surface:AM],
                            (punct[comma], num[frac:~, ord:~];?[lemma:um], ?[time, meas])*,
                            conj[lemma:e], num[frac:~, ord:~];?[lemma:um], ?[time, meas], (?[surface:AM]).

// primeiras horas da manhã
29> noun[hora=+,time=+,no_normalization=+] @= ?[lemma:primeiro],  ?[lemma:hora];?[lemma:minuto];?[lemma:segundo], (?[surface:AM]),
                            prep[lemma:de], ?[surface:a], ?[part_of_day];?[season].




//////////////////////////////////////////////////////
// 2-Hours (às | antes das | depois das | cerca das | por volta das) //FALTA
//////////////////////////////////////////////////////

/////////////////////////////
// ao meio-dia e 10 minutos
// ao meio-dia menos 10 minutos
// à meia-noite e meia hora
// à meia-noite e um quarto
// à meia-noite
// ao meio-dia
//
// Xerox lemmatization
// CH has to duplicate rules for "um quarto"
31> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"];?[lemma:"cerca_de"];?[lemma:"após"];?[lemma:"até a"];prep[lemma:a], art[lemma:o]| 
                            ?[lemma:meia-noite]; ?[lemma:meio-dia],
                            conj[lemma:e], ?[lemma:um], ?[lemma:quarto] .
31> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"];?[lemma:"cerca_de"];?[lemma:"após"];?[lemma:"até a"];prep[lemma:a], art[lemma:o]| 
                            ?[lemma:meia-noite]; ?[lemma:meio-dia],
                            conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+];num[frac];?[lemma:meio], (?[lemma:minuto];?[lemma:hora]).
31> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"];?[lemma:"cerca_de"];?[lemma:"após"];?[lemma:"até a"];prep[lemma:a], art[lemma:o]| 
                            ?[lemma:meio], ?[lemma:noite];?[lemma:dia],
			    conj[lemma:e], ?[lemma:um], ?[lemma:quarto] .
31> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"];?[lemma:"cerca_de"];?[lemma:"após"];?[lemma:"até a"];prep[lemma:a], art[lemma:o]| 
                            ?[lemma:meio], ?[lemma:noite];?[lemma:dia],
                            conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+];num[frac];?[lemma:meio], (?[lemma:minuto];?[lemma:hora]).

// às 20 para o meio dia
// às 20 para a meia noite 
// 
33> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"]; ?[lemma:"a_partir_de"]; ?[lemma:"cerca de"];?[lemma:"após"];?[lemma:"até a"];prep[lemma:a], (art[lemma:o])| 
                            num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto]), ?[lemma:para], ?[lemma:o], ?[lemma:meio-dia];?[lemma:meia-noite].
33> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"]; ?[lemma:"a_partir_de"]; ?[lemma:"cerca de"];?[lemma:"após"];?[lemma:"até a"];prep[lemma:a], (art[lemma:o])| 
                            num[lemma:"um quarto"], (?[lemma:de],?[lemma:hora]), ?[lemma:para], ?[lemma:o], ?[lemma:meio-dia];?[lemma:meia-noite].
33> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"]; ?[lemma:"a_partir_de"]; ?[lemma:"cerca de"];?[lemma:"após"];?[lemma:"até a"];prep[lemma:a], (art[lemma:o])| 
                            ?[lemma:meia], (?[lemma:hora]), ?[lemma:para], ?[lemma:o], ?[lemma:meio-dia];?[lemma:meia-noite].
33> ? @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"]; ?[lemma:"a_partir_de"]; ?[lemma:"cerca de"];?[lemma:"após"];?[lemma:"até a"];prep[lemma:a], (art[lemma:o])| 
         ?[lemma:meio-dia,hora=+,time=+];?[lemma:meia-noite,hora=+,time=+].


/////////////////////////////
// às 20 para as 5
// às 20 para as 5 da manhã / tarde / noite / madrugada 
//
35> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"];?[lemma:"a_partir_de"] ;?[lemma:"cerca_de"];?[lemma:"após"];?[lemma:"até a"];prep[lemma:a], art[lemma:o]| 
                            num[frac:~,ord:~,minute=+], (?[lemma:minuto]), 
                            ?[lemma:para], (?[lemma:o]), num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
			     (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).
35> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"]; ?[lemma:"a_partir_de"]; ?[lemma:"cerca de"];?[lemma:"após"];?[lemma:"até a"];prep[lemma:a], art[lemma:o]| 
                            ?[lemma:"um"],?[lemma: "quarto"], (?[lemma:minuto]), 
                            ?[lemma:para], (?[lemma:o]), num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora]), 
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
			    (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).



/////////////////////////////
// às 20 menos 5
// às 20 menos 5 da manhã / tarde / noite / madrugada 
//
// CH March 2008. Add restriction on article gender in order to avoid to take as hour expressions like
// "Aos 61 (anos), ele reformou-se
37> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"]; ?[lemma:"a_partir_de"]; ?[lemma:"cerca_de"];?[lemma:"após"];?[lemma:"desde"];?[lemma:"até a"];prep[lemma:a], art[lemma:o,fem:+]|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            ?[lemma:menos], ?[lemma:um], ?[lemma:quarto];?[lemma:meio], ((?[lemma:de]), ?[lemma:hora]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
			    (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).

37> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"];?[lemma:"a_partir_de"]; ?[lemma:"cerca_de"];?[lemma:"após"];?[lemma:desde]; ?[lemma:"até a"];prep[lemma:a], art[lemma:o,fem:+]|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]), 
                            ?[lemma:menos], (?[lemma:"um"], ?[lemma:" quarto"];?[lemma:meio]), ((?[lemma:de]), ?[lemma:hora]),(?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
			    (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).	

28> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"];?[lemma:"a_partir_de"]; ?[lemma:"cerca_de"];?[lemma:"após"];?[lemma:desde]; ?[lemma:"até a"];prep[lemma:a], art[lemma:o,fem:+]|
                          num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora]), 
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto])),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]).

38> noun[hora=+,time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"];?[lemma:"a_partir_de"];?[lemma:"cerca de"];?[lemma:"perto_de"];?[lemma:"após"];?[lemma:"desde"];?[lemma:"por"];?[lemma:"até a"];prep[lemma:a], art[lemma:o,fem:+]|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+];?[lemma:"%d+[%,]5",num=+,hour=+,hora_complex=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (conj[lemma:e];?[lemma:menos];?[lemma:"depois_de"], (?[lemma:o]), num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto];?[surface:quarto];?[surface:quartos])), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp,timeval:~]).

// às 2 horas, 20  minutos e 30 segundos da manhã / tarde / noite / madrugada
//
29> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"];?[lemma:"a_partir_de"]; ?[lemma:"cerca_de"];?[lemma:"após"];?[lemma:"desde"]; ?[lemma:"até a"];prep[lemma:a], art[lemma:o,fem:+]| 
                            num[frac:~,ord:~];?[lemma:um],  (?[lemma:hora];?[lemma:minuto];?[lemma:segundo]), (?[surface:AM]),
                            (punct[comma], num[frac:~, ord:~];?[lemma:um], (?[time, meas]))*, 
                            conj[lemma:e], num[frac:~, ord:~];?[lemma:um], (?[time, meas]), (?[surface:AM]),
                            prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada].

// às 2 horas, 20  minutos e 30 segundos
39> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"];?[lemma:"a_partir_de"]; ?[lemma:"cerca_de"];?[lemma:"após"];?[lemma:"até a"];prep[lemma:a], art[lemma:o,fem:+]| 
                            num[frac:~,ord:~];?[lemma:um], ?[lemma:hora];?[lemma:minuto];?[lemma:segundo],(?[surface:AM]),
                            (punct[comma], num[frac:~, ord:~];?[lemma:um], ?[time, meas])*, 
                            conj[lemma:e], num[frac:~, ord:~];?[lemma:um], ?[time, meas], (?[surface:AM]). 

// às primeiras horas da manhã
39> noun[hora=+, time=+] @= |?[lemma:"antes_de"];?[lemma:"depois_de"];?[lemma:"por volta de"];?[lemma:"a_partir_de"];?[lemma:"cerca_de"];?[lemma:"perto de"];?[lemma:"após"];?[lemma:"desde"];?[lemma:"por"];?[lemma:"até a"];prep[lemma:a], art[lemma:o,fem:+]|
                            ?[lemma:primeiro],  (?[lemma:hora];?[lemma:minuto];?[lemma:segundo]), (?[surface:AM]),
                            prep[lemma:de], ?[surface:a], ?[part_of_day].




//////////////////////////////////////////////////////
// 2-Hours (verbo ser)                              //
//////////////////////////////////////////////////////

/////////////////////////////
// é meio-dia e 10 minutos
// é meio-dia menos 10 minutos
// é meia-noite e meia hora
// é meia-noite e um quarto
// é meia-noite
// é meio-dia
//
41> noun[hora=+, time=+] @= |?[lemma:ser]| 
                            ?[lemma:meia-noite]; ?[lemma:meio-dia],
                            conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+];num[frac];?[lemma:meio], (?[lemma:minuto];?[lemma:hora]).
41> noun[hora=+, time=+] @= |?[lemma:ser]| 
                            ?[lemma:meio], ?[lemma:noite];?[lemma:dia],
                            conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+];num[frac];?[lemma:meio], (?[lemma:minuto];?[lemma:hora]).

// é 20 para o meio dia
// é 20 para a meia noite 
//
43> noun[hora=+, time=+] @= |?[lemma:ser], (adv)| 
                            num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto]), ?[lemma:para], ?[lemma:o], ?[lemma:meio-dia];?[lemma:meia-noite].
43> noun[hora=+, time=+] @= |?[lemma:ser], (adv)| 
                            num[lemma:"um quarto"], (?[lemma:de],?[lemma:hora]), ?[lemma:para], ?[lemma:o], ?[lemma:meio-dia];?[lemma:meia-noite].
43> noun[hora=+, time=+] @= |?[lemma:ser], (adv)| 
                            ?[lemma:meia], (?[lemma:hora];?[lemma:h]), ?[lemma:para], ?[lemma:o], ?[lemma:meio-dia];?[lemma:meia-noite].
43> ? @= |?[lemma:ser], (adv)| 
         ?[lemma:meio-dia,hora=+,time=+];?[lemma:meia-noite,hora=+,time=+].


/////////////////////////////
// é 20 para as 5
// é 20 para as 5 da manhã / tarde / noite / madrugada 
//
45> noun[hora=+, time=+] @= |?[lemma:ser], (adv)| 
                            num[frac:~,ord:~,minute=+], (?[lemma:minuto]), 
                            ?[lemma:para], (?[lemma:o]), num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
	                    (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).
45> noun[hora=+, time=+] @= |?[lemma:ser], (adv)| 
                            num[lemma:"um quarto"], (?[lemma:minuto]), 
                            ?[lemma:para], (?[lemma:o]), num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]),  (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
	                    (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).


/////////////////////////////
// é 20 menos 5
// é 20 menos 5 da manhã / tarde / noite / madrugada 
//
// CH March 2008. Add restriction on article gender in order to avoid to take as hour expressions like
// "Aos 61 (anos), ele reformou-se
47> noun[hora=+, time=+] @= |?[lemma:ser], (adv)|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            ?[lemma:menos], ?[lemma:um], ?[lemma:quarto];?[lemma:meio], (?[lemma:de]), ?[lemma:hora], (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
		            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).


47> noun[hora=+, time=+] @= |?[lemma:ser], (adv)|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]), 
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]), 
			    conj[lemma:e];?[lemma:menos], ?[lemma:meio,num=+,hour=+];?[surface:picos];?[lemma:"%d+[%,]5",num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).

47> noun[hora=+, time=+] @= |?[lemma:ser], (adv)| 
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], ?[lemma:hora], (?[surface:AM]), 
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto];?[lemma:quarto])), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]).
47> noun[hora=+, time=+] @= |?[lemma:ser], (adv)| 
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora]), 
                            prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada],
                            (conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto])),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]).

48> noun[hora=+, time=+] @= |?[lemma:ser], (adv)|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], ?[lemma:hora], (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto];?[lemma:quarto])), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).
48> noun[hora=+, time=+] @= |?[lemma:ser], (adv)|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]),  (?[surface:AM]),
                            prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada],
                            (conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto];?[lemma:quarto])), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).
48> noun[hora=+, time=+] @= |?[lemma:ser], (adv)|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto];?[lemma:quarto]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).


// é 2 horas, 20  minutos e 30 segundos da manhã / tarde / noite / madrugada
//
49> noun[hora=+, time=+] @= |?[lemma:ser], (adv)|
                            num[frac:~,ord:~];?[lemma:um],  (?[lemma:hora];?[lemma:minuto];?[lemma:segundo]), (?[surface:AM]),
                            (punct[comma], num[frac:~, ord:~];?[lemma:um], (?[time, meas]))*,
                            conj[lemma:e], num[frac:~, ord:~];?[lemma:um], (?[time, meas]), (?[surface:AM]),
                            prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada].

// é 2 horas, 20  minutos e 30 segundos
49> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])|
                            num[frac:~,ord:~];?[lemma:um], ?[lemma:hora];?[lemma:minuto];?[lemma:segundo], (?[surface:AM]),
                            (punct[comma], num[frac:~, ord:~];?[lemma:um], ?[time, meas])*,
                            conj[lemma:e], num[frac:~, ord:~];?[lemma:um], ?[time, meas], (?[surface:AM]).




//////////////////////////////////////////////////////
// 3-Hours (por volta das)                          //
//////////////////////////////////////////////////////

/////////////////////////////
// por volta do meio-dia e 10 minutos
// por volta do meio-dia menos 10 minutos
// por volta do meia-noite e meia hora
// por volta da meia-noite e um quarto
// por volta da meia-noite
// por volta do meio-dia
//
// this rule does not work with Xerox tokenization for "por volta da meia-noite e um quarto"
// has to duplicate the rule to take care of "um quarto"
51> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])| 
                            ?[lemma:meia-noite]; ?[lemma:meio-dia],
                            conj[lemma:e], ?[lemma:um], ?[lemma:quarto] .

51> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])| 
                            ?[lemma:meia-noite]; ?[lemma:meio-dia],
                            conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+];num[frac];?[lemma:meio], (?[lemma:minuto];?[lemma:hora]).

51> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])| 
                            ?[lemma:meio], ?[lemma:noite];?[lemma:dia],
                            conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+];num[frac];?[lemma:meio], (?[lemma:minuto];?[lemma:hora]).

// por volta das 20 para o meio dia
// por volta das 20 para a meia noite 
//
53> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])| 
                            num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto]), ?[lemma:para], ?[lemma:o], ?[lemma:meio-dia];?[lemma:meia-noite].
53> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])| 
                            num[lemma:"um quarto"], (?[lemma:de],?[lemma:hora]), ?[lemma:para], ?[lemma:o], ?[lemma:meio-dia];?[lemma:meia-noite].
53> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])|
                            ?[lemma:meia], (?[lemma:hora];?[lemma:h]), ?[lemma:para], ?[lemma:o], ?[lemma:meio-dia];?[lemma:meia-noite].
53> ? @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])|
         ?[lemma:meio-dia,hora=+,time=+];?[lemma:meia-noite,hora=+,time=+].



/////////////////////////////
// por volta das 20 para as 5
// por volta das 20 para as 5 da manhã / tarde / noite / madrugada 
//
55> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])|
                            num[frac:~,ord:~,minute=+], (?[lemma:minuto]),
                            ?[lemma:para], (?[lemma:o]), num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).
55> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])|
                            num[lemma:"um quarto"], (?[lemma:minuto]),
                            ?[lemma:para], (?[lemma:o]), num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]),  (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).






/////////////////////////////
// por volta das 20 menos 5
// por volta das 20 menos 5 da manhã / tarde / noite / madrugada 
//
// CH March 2008. Add restriction on article gender in order to avoid to take as hour expressions like
// "Aos 61 (anos), ele reformou-se
57> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            ?[lemma:menos], ?[lemma:um], ?[lemma:quarto];?[lemma:meio], ((?[lemma:de]), ?[lemma:hora]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).

57> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            ?[lemma:menos], num[lemma:"um quarto"];?[lemma:meio], ((?[lemma:de]), ?[lemma:hora]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).

57> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            conj[lemma:e];?[lemma:menos], ?[lemma:meio,num=+,hour=+];?[surface:picos];?[lemma:"%d+[%,]5",num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).

58> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto];?[lemma:quarto])), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).




// por volta das 2 horas, 20  minutos e 30 segundos da manhã / tarde / noite / madrugada
//
59> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])|
                            num[frac:~,ord:~];?[lemma:um],  (?[lemma:hora];?[lemma:minuto];?[lemma:segundo]), (?[surface:AM]),
                            (punct[comma], num[frac:~, ord:~];?[lemma:um], (?[time, meas]))*,
                            conj[lemma:e], num[frac:~, ord:~];?[lemma:um], (?[time, meas]), (?[surface:AM]),
                            prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada].


// por volta das 2 horas, 20  minutos e 30 segundos
59> noun[hora=+, time=+] @= |prep[lemma:por], ?[lemma:volta], prep[lemma:de], (?[lemma:o])| 
                            num[frac:~,ord:~];?[lemma:um], ?[lemma:hora];?[lemma:minuto];?[lemma:segundo],
                            (punct[comma], num[frac:~, ord:~];?[lemma:um], ?[time, meas])*, 
                            conj[lemma:e], num[frac:~, ord:~];?[lemma:um], ?[time, meas].


//////////////////////////////////////////////////////
// 4-Hours (cerca|perto|antes|depois|por_volta das) //
//////////////////////////////////////////////////////

/////////////////////////////
// antes das meio-dia e 10 minutos
// antes das meio-dia menos 10 minutos
// antes das meia-noite e meia hora
// antes das meia-noite e um quarto
// antes das meia-noite
// antes das meio-dia
//
// CH has to duplicate rule in order to have "um quarto"
// IMPORTANT - THESE RULES ARE REDUNDANT WITH RULES OF LAYER 31> They never apply
61> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])|
                            ?[lemma:meia-noite]; ?[lemma:meio-dia],
		            conj[lemma:e], ?[lemma:um], ?[lemma:quarto].
61> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])| 
                            ?[lemma:meia-noite]; ?[lemma:meio-dia],
                            conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+];num[frac];?[lemma:meio], (?[lemma:minuto];?[lemma:hora]).
// CH has to duplicate rule in order to have "um quarto"
61> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])|
			    ?[lemma:meio], ?[lemma:noite];?[lemma:dia],                            
		            conj[lemma:e], ?[lemma:um], ?[lemma:quarto].
61> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])| 
                            ?[lemma:meio], ?[lemma:noite];?[lemma:dia],
                            conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+];num[frac];?[lemma:meio], (?[lemma:minuto];?[lemma:hora]).

// antes das 20 para o meio dia
// antes das 20 para a meia noite 
//
63> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])| 
                            num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto]), ?[lemma:para], ?[lemma:o], ?[lemma:meio-dia];?[lemma:meia-noite].
63> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])| 
                            num[lemma:"um quarto"], (?[lemma:de],?[lemma:hora]), ?[lemma:para], ?[lemma:o], ?[lemma:meio-dia];?[lemma:meia-noite].
63> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])| 
                            ?[lemma:meia], (?[lemma:hora]), ?[lemma:para], ?[lemma:o], ?[lemma:meio-dia];?[lemma:meia-noite].
63> ? @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])| 
         ?[lemma:meio-dia,hora=+,time=+];?[lemma:meia-noite,hora=+,time=+].


/////////////////////////////
// antes das 20 para as 5
// antes das 20 para as 5 da manhã / tarde / noite / madrugada 
//
65> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])| 
                            num[frac:~,ord:~,minute=+], (?[lemma:minuto]), 
                            ?[lemma:para], (?[lemma:o]), num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]).
65> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])| 
                            num[lemma:"um quarto"], (?[lemma:minuto]), 
                            ?[lemma:para], (?[lemma:o]), num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora]), 
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]).



/////////////////////////////
// antes das 20 menos 5
// antes das 20 menos 5 da manhã / tarde / noite / madrugada 
//
// CH March 2008. Add restriction on article gender in order to avoid to take as hour expressions like
// "Aos 61 (anos), ele reformou-se
67> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            ?[lemma:menos], ?[lemma:um], ?[lemma:quarto];?[lemma:meio], ((?[lemma:de]), ?[lemma:hora]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).

67> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])|
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]), 
                            ?[lemma:menos], num[lemma:"um quarto"];?[lemma:meio], ((?[lemma:de]), ?[lemma:hora]), (?[surface:AM]),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).

67> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])| 
                            num[frac:~,ord:~,hour=+];?[lemma:um,num=+,hour=+], (?[lemma:hora];?[lemma:h]), (?[surface:AM]), 
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (conj[lemma:e];?[lemma:menos], num[frac:~,ord:~,minute=+];?[lemma:um,num=+,minute=+], (?[lemma:minuto])),
                            (prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada]),
                            (prep[lemma:de], (?[lemma:o]), ?[advtimetemp]).



// antes das 2 horas, 20 minutos e 30 segundos da manhã / tarde / noite / madrugada
//
69> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])| 
                            num[frac:~,ord:~];?[lemma:um],  (?[lemma:hora];?[lemma:minuto];?[lemma:segundo]), (?[surface:AM]),
                            (punct[comma], num[frac:~, ord:~];?[lemma:um], (?[time, meas]))*, 
                            conj[lemma:e], num[frac:~, ord:~];?[lemma:um], (?[time, meas]), (?[surface:AM]),
                            prep[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite];?[lemma:madrugada].

// antes das 2 horas, 20 minutos e 30 segundos
69> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])| 
                            num[frac:~,ord:~];?[lemma:um], ?[lemma:hora];?[lemma:minuto];?[lemma:segundo], (?[surface:AM]),
                            (punct[comma], num[frac:~, ord:~];?[lemma:um], ?[time, meas])*, 
                            conj[lemma:e], num[frac:~, ord:~];?[lemma:um], ?[time, meas], (?[surface:AM]).

// antes das primeiras horas da manhã
69> noun[hora=+, time=+] @= |prep[lemma:cerca_de];?[lemma:perto_de];?[lemma:antes_de];?[lemma:depois_de];?[lemma:"por volta"], prep[lemma:de], (?[lemma:o])|
                            ?[lemma:primeiro],  (?[lemma:hora];?[lemma:minuto];?[lemma:segundo]),
                            prep[lemma:de], ?[surface:a], ?[part_of_day].


//////////////////////////////////////////////////////
// 6-Hours (meio dia / meia noite)                  //
//////////////////////////////////////////////////////

// If meio dia and meia noite are not included in a bigger expression.
// CAREFUL WITH AMBIGUITY
70> noun[hour=+] @= ?[lemma:meio], ?[lemma:noite];?[lemma:dia] .

//////////////////////////////////////////////////////
// 7-Hours (outras)                                 //
//////////////////////////////////////////////////////

// Example: dentro de 5 minutos
75> noun[hora=+,time=+] @= |prep[lemma:"dentro de"]|  (adv[advnum]), num, ?[lemma:hora,pl];?[lemma:minuto,pl];?[lemma:segundo,pl].


//////////////////////////////////////////////////////
// quantidades de tempo                             //
//////////////////////////////////////////////////////

// Example: (aproximadamente) dois quintos de hora
// Example: (aproximadamente) um quinto de uma hora
//
90> noun[hora=+,time=+] = (adv[advnum]), (art[lemma:um]), num[frac], prep[lemma:de], (art[lemma:um];art[lemma:o]),  ?[meas,time_meas].

// Example: dois minutos e meio
// Example: um minuto e meio
// 
90> noun[hora=+,time=+] @= (adv[advnum]), (art;?[dem]), num, ?#1[meas,time_meas], ((conj[lemma:e]), num, ?#2[meas,time_meas])*, where(#1[meastype]::#2[meastype]).
90> noun[hora=+,time=+] @= (adv[advnum]), art[lemma:um],     ?#1[meas,time_meas], ((conj[lemma:e]), num, ?#2[meas,time_meas])*, where(#1[meastype]::#2[meastype]).

91> noun[hora=+,time=+] @= (adv[advnum]), (art;?[dem]), num, ?[meas,time_meas,lemma:minuto];?[meas,time_meas,lemma:hora], conj[lemma:e], num[frac];?[lemma:meio], ((prep[lemma:de]), (art[lemma:um];art[lemma:o]), ?[meas,time_meas]).
91> noun[hora=+,time=+] @= (adv[advnum]), art[lemma:um],     ?[meas,time_meas,lemma:minuto];?[meas,time_meas,lemma:hora], conj[lemma:e], num[frac];?[lemma:meio], ((prep[lemma:de]), (art[lemma:um];art[lemma:o]), ?[meas,time_meas]).

92> noun[hora=+,time=+] @= (adv[advnum]), (art;?[dem]), num, ?[meas,time_meas,lemma:minuto]; ?[meas,time_meas,lemma:hora], (conj[lemma:e], num, (prep[lemma:de]), (art[lemma:um];art[lemma:o]), ?[meas,frac]).
92> noun[hora=+,time=+] @= (adv[advnum]), art[lemma:um],     ?[meas,time_meas,lemma:minuto]; ?[meas,time_meas,lemma:hora], (conj[lemma:e], num, (prep[lemma:de]), (art[lemma:um];art[lemma:o]), ?[meas,frac]).


// Example: semana e meia
// Example: próxima semana
// Example: semana anterior
// Example: primeira semana
//
94> noun[time=+,date=+,uncertain=+] @= (adv[advnum]), ?[time_meas],  conj[lemma:e], num[frac];?[lemma:meio], ((prep[lemma:de]), (art[lemma:um];art[lemma:o]), ?[meas,time_meas]).
94> noun[time=+,date=+,uncertain=+] @= (adv[advnum]), ?[time_meas],  conj[lemma:e], num, (prep[lemma:de], (art[lemma:um];art[lemma:o]), ?[meas,frac]).
94> noun[time=+,date=+,uncertain=+] @= (adv[advnum]), ?[time_meas],  (conj[lemma:e], num), prep[lemma:de], (art[lemma:um];art[lemma:o]), ?[meas,frac].



// RUNHAREM
94> noun[time=+,time_meas=+] @= (adv[advnum]), (art;?[dem]), num, ((prep[lemma:de]), (art[lemma:um];art[lemma:o])), ?[meas,time_meas].
94> noun[time=+,time_meas=+] @= (adv[advnum]), art[lemma:um],     ((prep[lemma:de]), (art[lemma:um];art[lemma:o])), ?[meas,time_meas].


// MOVED rules from previous files
// Add new features
75> noun[t-relativ=+,date=+,tipo_tempref=enun] = ?[!timeref,!timeval, tipo_tempref:enun,timeval:~], ?[date];?[time,timeval:~] .

// CH
// This rule does not apply to the example ??? HAS TO DO WITH THE SPLIT OF PREP+DET
// adding num but still does not work
// cerca das 2 horas
//79>  prep = ?[lemma:cerca], prep[lemma:de] .
79>  prep = ?[lemma:cerca], prep[lemma:de] |(?[lemma:o]), num, noun[hora, time] |.


////////////////////////////////////////////////////////////////////////////////
// Duration                                                                   //
////////////////////////////////////////////////////////////////////////////////

// as outras durações são identificadas pelos traços quant e time

// CH - commented this rule. Already treated in LGAdvTime.xip
// Example: anos e anos
// Example: dias e dias
//80> pp[time=+,duration=+] @= ?[time_meas,pl], conj[lemma:e], ?[time_meas,pl].


// CH : durante+time_meas (e.g. durante decadas, durante tres longos meses
80> pp[time=+,duration=+] @= prep[lemma:durante], (num;art[indef]), (adj), (num[frac];?[time_meas];?[lemma:meio];conj[lemma:e];art[indef];num[frac:~])*, num[frac:+];?[lemma:meio]. 
80> pp[time=+,duration=+] @= prep[lemma:durante], (num;art[indef]), (adj), (num[frac];?[time_meas];?[lemma:meio];conj[lemma:e];art[indef];num[frac:~])*, ?[time_meas:+]. 

// Example: Em meio ano
80> pp[time=+, duration=+] = ?[lemma:em], num;art[indef];?[lemma:meio], ?[time,hora:~].

// Example: Ao longo de um ano
// Example: Ao longo de meio ano
80> pp[time=+, duration=+] = ?[lemma:"ao_longo_de"], (art[def];num;?[time_meas];?[lemma:meio];conj[lemma:e];art[indef];num), ?[part_of_day];?[time,meas].

// CH
// durante + part_of_day gives a duration
82> pp[time=+, duration=+, part_of_day=+] @= prep[lemma:durante], art[def];?[dem], ?[part_of_day].


////////////////////////////////////////////////////////////////////////////////
// Intervalos                                                                   //
////////////////////////////////////////////////////////////////////////////////

// Sao intervalos na terminologia do HAREM
100> pp[time=+,interval=+] = ?[lemma:entre], (art[def]), ?[time, date];?[century], conj[lemma:e],  (art[def]), ?[time, date];?[century].
100> pp[time=+,interval=+] = prep[lemma:de], ?[time, date], ?[surface:a],   ?[time, date].
100> pp[time=+,interval=+] = prep[lemma:de], ?[time, date], ?[surface:até], ?[time, date].
100> pp[time=+,interval=+] = ?[lemma:desde], ?[time, date], ?[lemma:até],   ?[time, date].

100> pp[time=+,interval=+] = ?[lemma:em], art[def], ?[lemma:século], ?[lemma:"%c+\-%c+"];?[lemma:"%C+\-%C+"].
100> pp[time=+,interval=+] = ?[lemma:em], art[def], ?[century], ?[surface:a], ?[rom].

100> pp[time=+,interval=+] = ?[lemma:em], art[def], ?[time, date], ?[surface:a], ?[time, date];?[num].

100> pp[time=+,interval=+] = ?[lemma:de],    num, ?[surface:a],   ?[num,date], ?[time_meas].
100> pp[time=+,interval=+] = ?[lemma:de],    num, ?[surface:até], ?[num,date], ?[time_meas].
100> pp[time=+,interval=+] = ?[lemma:desde], num, ?[surface:até], ?[num,date], ?[time_meas].
100> pp[time=+,interval=+] = ?[lemma:entre], num, ?[surface:e],   ?[num,date], ?[time_meas].

/////////////////////////////////////////////////////////////////////////////
// COMPLEX EXPRESSIONS (addition CH for HAREM 2)
/////////////////////////////////////////////////////////////////////////////
// Example: (aproximadamente) dois dias
105> noun[time=+] @= (adv[advnum]), (art;?[dem]), num, ((prep[lemma:de]), (art[lemma:um];art[lemma:o])), ?[time_meas].
105> noun[time=+] @= (adv[advnum]), art[lemma:um],     ((prep[lemma:de]), (art[lemma:um];art[lemma:o])), ?[time_meas].

// Example: 2 horas mais tarde
106> adv[advtimetemp=+,ref-after=+] = noun[time], ?[lemma:mais], ?[lemma:tarde].

// Example: 2 dias mais cedo
106> adv[advtimetemp=+,ref-before=+] = noun[time], ?[lemma:mais], ?[lemma:cedo].


// Example : dentro de noun[time]
106> adv[advtimetemp=+,ref-after=+] = prep[lemma:"dentro_de"], noun[time].


// Combination Feast_event + year
// o Natal de 2004
110> noun[time=+,one_day=+,proper=+,date=+]      @= noun[one_day], prep, num[dig:+,year=+] .
110> noun[time=+,several_days=+,proper=+,date=+] @= noun[several_days], prep, num[dig:+,year=+] .

// Dois dias antes/depois do Natal
115>  noun[time=+,tipo_tempref=disc, ref-before=+] @= (num), ?[time],  prep[ref-before], (art), (adj), noun[proper,feast_event] .
115>  noun[time=+,tipo_tempref=disc, ref-after=+] @= (num), ?[time],  prep[ref-after], (art), (adj), noun[proper,feast_event] .


// Dois dias antes/depois
95>  noun[time=+,tipo_tempref=enun, ref-before=+] @= (num), ?[time_meas],  adv[ref-before]. 
95>  noun[time=+,tipo_tempref=enun, ref-after=+] @= (num), ?[time_meas],  adv[ref-after]. 

// Na altura do Natal, neste periodo do Natal.
95> noun[time=+,tipo_tempref=disc, ref-simult=+] @= prep[lemma:em], (art[def];?[dem]), (adj), ?[denot_time], prep[lemma:de], (art), ?[feast_event], (prep[lemma:de], num[dig]) .
95> noun[time=+,tipo_tempref=disc, ref-simult=+] @= prep[lemma:por], ?[lemma:altura], ?[lemma:de], (art[def];?[dem]), (adj), (?[denot_time], prep[lemma:de]), (art), ?[feast_event], (prep[lemma:de], num[dig]) .

// CH - Already present in chunktime.xip - commented
//NJM   ?????? TRANSFERIR PARA CHUNKTIME.XIP ????????
// Duração mais data absoluta.
// Example: durante a manhã de 25 de Abril de 2008
//100> noun[time=+,date=+,tipo_tempref=absolut] = adv[time,duration], prep, num[dig,year];?[date,tipo_tempref:absolut].
//100> noun[time=+,duration=+] = adv[time,duration], prep, ?[date,tipo_tempref:~absolut].

//NJM    ?????? TRANSFERIR PARA CHUNKTIME.XIP ????????
// CH - NAO, nao se pode transferir porque senao passamos pelo chunker primeiro
// que vai construir um advp em cima do adv 
// CH Transferred and also extracted in entit_dependency.xip
// advtimetemp mais data absoluta.
// Example: ao início da manhã de sexta-feira, 25 de Abril de 2008
120> adv[time=+,date=+,tipo_tempref=absolut] = adv[advtimetemp], (?[comma];prep), num[dig,year];?[date,tipo_tempref:absolut].

// dia de Santo António
// Example: dia de Santo António
120> noun[time=+,date=+,tipo_tempref=enun] = noun[lemma:dia], prep[lemma:de], (art[def]), ?[lemma:santo];?[surface:São];?[lemma:S];?[lemma:"S."];?[lemma:sant], (?[lemma:"\'"]), (noun[individual,proper,maj])+.
120> noun[time=+,date=+,tipo_tempref=enun] = noun[lemma:dia], prep[lemma:de], (art[def]), ?[lemma:"Santo %+"];?[lemma:"São %+"];?[lemma:"Santa %+"].
120> noun[time=+,date=+,tipo_tempref=enun] = noun[lemma:dia], prep[lemma:de], (art[def]), ?[lemma:"Nossa Senhora"], prep[lemma:de], (art[def]), (?[lemma:imaculado]), (noun[individual,proper,maj])+.

// dia dos namorados
// dia da árvore
120> noun[time=+,date=+,tipo_tempref=enun] = |art[def];?[proxdem]| noun[lemma:dia], prep[lemma:de], (art[def]), noun.
120> noun[time=+,date=+,tipo_tempref=enun] = |art[def];?[proxdem]| noun[lemma:dia], prep[lemma:de], (art[def]), ?[lemma:não], noun.


// Combination adv[tempref] + prep + part_of_day
// ontem a tarde
110> adv = | ?[prep:~] | adv[!timeref,!tipo_tempref], ?[advtimetemp] .
 



/////////////////////////////////////////////////////////////////////////////
// (Must remain at the end of the file)                                    //
/////////////////////////////////////////////////////////////////////////////

// Example: no dia
125> adv[advtimetemp=+] @= prep[lemma:"até a"], ?[def], (?[lemma:último];?[lemma:presente]), ?[time_meas], prep[lemma:de], ?[time].
125> adv[advtimetemp=+] @= prep[lemma:"em"],    ?[def], (?[lemma:último];?[lemma:presente]), ?[time_meas], prep[lemma:de], ?[time].


// THINGS TO ADD - PLEASE KEEP IT HERE FOR REMEMBERING
// CH 15/2/2008
//DependencyRules:

// TO DO - Only possible with scripting because we cannot have the values of years of the second expression
// Coordination of dates
// Nos dias 17 e 18 de Novembro
// Two structures have been constructed, we cannot apply sequence rules
//| noun#1[time,tipo_tempref:absolut,monthday], conj[coord,lemma:e], noun#2[tipo_tempref:absolut]{?*,#3[month:1]} |

// Nos dias 16, 17 e 18 de Novembro
// two structures also but in the first one the coordination is present
// each member of the first complex noun must have new features added on them
//| noun#1[time,tipo_tempref:absolut,monthday]{?*, num[dig,date], ?*, num[dig:date]}, conj[coord,lemma:e], noun#2[tipo_tempref:absolut]{?*,#3[month:1]} |
