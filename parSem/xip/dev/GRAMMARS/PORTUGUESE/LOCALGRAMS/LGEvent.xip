//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! L2F - Spoken Language Systems Laboratory / INESC-ID Lisboa                 !
//!                                                                            !
//! Copyright (c) 2006 INESC-ID Lisboa.  All Rights Reserved.                  !
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : LGEvent.xip                                                 !
//!   Date       : 2007/March                                                  !
//!   Author     : Luís Romão                                                  !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!  Event Name Rules                                                          !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

// $Id: LGEvent.xip,v 1.2 2008/10/21 15:20:03 chagege Exp $


Sequence:

// Batalha de...
1> NOUN[event=+, ephem=+] @= ?[lemma:batalha, maj], prep[lemma:de], (?[lemma:o]), ?+[maj], (prep, (art), ?+[maj])*.

// Revolução de...
1> NOUN[event=+, ephem=+] @= ?[lemma:revolução, maj], prep[lemma:de], (?[lemma:o]), ?+[maj];num, (prep, (art), ?+[maj])*.
1> NOUN[event=+, ephem=+] @= ?[lemma:revolução, maj], (adj[maj]).

// CH added
// O Caso Whitaker
1> NOUN[event=+, ephem=+] @= ?[lemma:caso, maj], ?[maj:+], (prep[lemma:de]; art; ?[maj])* .






// Declaração dos Direitos Humanos
1> noun[event=+,ephem=+] @= ?[lemma:declaração, maj], prep[lemma:de], (art), ?+[maj:+] .

// Conferência/Cimeira/Festival/Torneio/Taça/Copa XXX
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[lemma:cimeira,maj];?[lemma:festival,maj];?[lemma:torneio,maj];?[lemma:taça,maj];?[lemma:copa, maj], (prep), (art), ?+[maj];?+[people];?[org], (?[org]), (prep, (art), ?+[maj])*.
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[lemma:congresso,maj], (prep), (art), ?+[maj, location:~];?+[people], (prep, (art), ?+[maj])*.
1> NOUN[event=+, organized=+] @= ?[lemma:taça, maj].


// CH
// Grande Prémio de
// GP de 
1> noun[event=+,organized=+] @= ?[surface:"GP"], prep[lemma:de], (art), ?[location] .
1> noun[event=+,organized=+] @= ?[surface:"Grande"], ?[surface:"Prémio"], prep[lemma:de], (art), ?[location] .


//a Copa, o Mundial, o Europeu (de futebol)
// CH added restriction
// Instituto Europeu de Design we do not want to mark Europeu de Design as an event
1> ? @= | art[def];pron[dem] | (num[rom,start_event=+,event=+,organized=+]), ?[lemma:copa, maj,start_event=+,organized=+,event=+];?[lemma:mundial,maj,start_event=+,organized=+,event=+];?[lemma:europeu,maj,start_event=+,organized=+,event=+], (prep[lemma:de], ?+[maj,end_event=+], (num)).

// Volta a Portugal, Volta a França, Tour de France
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[lemma:volta,maj], ?[lemma:a], (?[lemma:o]),  noun[location, country], (prep[lemma:em], noun).
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[lemma:tour, maj], ?[lemma:de], ?[lemma:France, maj].

//Campeonato Distrital de...
1> NOUN[event=+, organized=+] @= ?[lemma:campeonato, maj], (adj), prep[lemma:de], ?+[maj], (prep, (art), ?+[maj])* .

// Campeonato do Mundo (Europeu) (de Futebol) (de ANOXXXX)
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[lemma:campeonato,maj], prep, (art), (adj;noun), (prep[lemma:de], ?[lemma:futebol];?[lemma:voleibol];
?[lemma:vólei];?[lemma:andebol];?[lemma:natação];?[lemma:equitação];?[lemma:rugby]), (prep[lemma:de]), (art), (?[location]), (prep),(num).
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[lemma:campeonato,maj], adj;noun, (prep[lemma:de], ?[lemma:futebol];?[lemma:voleibol];
?[lemma:vólei];?[lemma:andebol];?[lemma:natação];?[lemma:equitação];?[lemma:rugby]), (prep[lemma:de]), (art), (?[location]), (prep),(num).

//Euro 2004
1> NOUN[event=+, organized=+] @= ?[lemma:Euro, maj], num.

// Grande Prémio de X...
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[lemma:grande, maj], ?[lemma:prémio, maj], (prep[lemma:de], noun, (num)), (prep[lemma:de], (?[lemma:o]), noun[location]).

// Jogos Olímpicos (de Inverno) (de 1994)
1> NOUN[event=+, organized=+] @= ?[lemma:jogo, maj], ?[lemma:olímpico, maj], (prep[lemma:de], ?[lemma:inverno];?[lemma:verão]), (prep[lemma:de], noun[location]), (prep[lemma:de], ?[num]).

// Madeira Cup, X Cup
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[maj], ?[lemma:cup].

// 1ª/2ª Guerra Mundial (Grande Guerra) - também II Guerra Mundial
1> NOUN[event=+, ephem=+] @= ?[lemma:2ª];?[lemma:1ª];?[lemma:primeiro];?[lemma:segundo];?[rom], ?[lemma:guerra,maj], ?[lemma:mundial, maj].
1> NOUN[event=+, ephem=+] @= ?[lemma:2ª];?[lemma:1ª];?[lemma:primeiro];?[lemma:segundo];?[rom], ?[lemma:grande,maj], ?[lemma:guerra,maj].

//Guerra do Vietname, Guerra do Yom Kippur, Guerra dos 100 Anos/6 Dias, Guerra Colonial
1> NOUN[event=+, ephem=+] @= (num[ord,maj];num[ord,dig]), ?[lemma:guerra, maj], prep[lemma:de], (art), (?[maj];num)+, (prep, (art), ?+[maj])*.
1> NOUN[event=+, ephem=+] @= ?[lemma:guerra, maj], adj+.

// o 25 de Abril, o 11 de Setembro
1> ? @= |art[surface:o]| noun[date, event=+, ephem=+, uncertain:~]. 

//Festa da Cerveja, Festa da Música
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[lemma:festa,maj], prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*.

//Feira Internacional de Lisboa, Feira do Móvel (foi para ORG)
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[lemma:feira,maj], (adj), prep[lemma:de], (art), ?[maj], (punct[comma]), ?*[maj], ((punct[comma]), (?[lemma:e]), prep*, (art), ?+[maj])*.
1> NOUN[event=+, organized=+] @= ?[lemma:feira, maj].

//Mostra de Vinhos do Cadaval
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[surface:"Mostra"], (adj), prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*.

//Semana da Música, Semana da Informática, Mês da Juventude
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[lemma:semana,maj];?[lemma:mês, maj], (adj), (prep[lemma:de]), (art), ?+[maj], (prep, (art), ?+[maj])*.

//Queima das Fitas, Bênção das Feitas
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[lemma:queima,maj];?[lemma:bênção], prep[lemma:de], art, ?[lemma:fita].

//Recepção ao Caloiro
1> NOUN[event=+, organized=+] @= (num[rom];num[ord]), ?[lemma:recepção,maj], prep[lemma:a], art, ?[lemma:caloiro].

// CH added "Concilio de XXX"
1> noun[event=+, eventac=+] @= (num[rom];num[ord]), ?[lemma:concílio,maj], prep[lemma:de], ?[maj], (prep[lemma:de];art;?[maj])* .

//Cerimónia/Sessão de abertura/inauguração/encerramento
1> NOUN[event=+, eventac=+] @= ?[lemma:cerimónia,maj];?[lemma:cerimônia,maj];?[lemma:sessão, maj], prep[lemma:de], 
?[lemma:abertura];?[lemma:inauguração];?[lemma:encerramento], prep[lemma:de], (art), ?+[maj],num*, (prep, (art), ?+[maj])*. 

//Coroação da Rainha Isabel II, tomada de posse de Cavaco Silva
1> NOUN[event=+, eventac=+] @= ?[lemma:coroação,maj], prep[lemma:de], (art), ?[people]. 
1> NOUN[event=+, eventac=+] @= ?[lemma:tomada,maj], prep[lemma:de], ?[lemma:posse], prep[lemma:de], (art), (noun), ?+[maj], (prep, (art), ?+[maj])*. 

//Exposição de Pintura Berardo
1> NOUN[event=+, organized=+] @= (num[ord];num[rom]), ?[lemma:exposição,maj], (prep[lemma:de], noun+), (prep[lemma:de]), ?+[maj], (prep, (art), ?+[maj])*. 
1> NOUN[event=+, organized=+] @= |?[lemma:exposição]| ?+[maj];num, ((?[lemma:e]),prep*, (art), ?+[maj])*. 

//Jornadas da Juventude, da Inovação
1> NOUN[event=+, organized=+] @= ?[lemma:jornada,maj], prep[lemma:de], art, ?+[maj], (prep, (art), ?+[maj])*. 

//Semana Santa
1> NOUN[event=+, ephem=+] @= ?[lemma:semana,maj], ?[lemma:santo, maj].

//Cimeira/Conferencia de Lisboa
1> NOUN[event=+, organized=+] @= ?[lemma:cimeira,maj];?[lemma:conferência,maj], prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*. 


//Dia Mundial do Idoso, Dia da Mãe
1> NOUN[event=+, ephem=+] @= ?[lemma:dia,maj], (adj), prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*. 

//Simpósio de...
1> NOUN[event=+, organized=+] @= (num[ord]), ?[lemma:simpósio,maj], (adj[maj]), prep[lemma:de], (art;prep[lemma:de];?[maj])*.
1> ? = ?[lemma:simpósio,maj,event=+,organized=+]. 

//decorre o.... X decorre...
1> NOUN[event=+, organized=+] @= |?[lemma:decorrer], (art)| ?+[maj], (prep, (art), ?+[maj])*. 

//Honra (Liga de Honra), Super Liga
1> NOUN[event=+, organized=+] @= |?[lemma:em], art| (?[lemma:liga], ?[lemma:de]), ?[lemma:honra, maj]. 
1> NOUN[event=+, organized=+] @= ?[lemma:super, maj], ?[lemma:liga, maj].

//22ª jornada (da II Divisão de Honra)
1> NOUN[event=+, organized=+] @= noun, ?[lemma:jornada], prep[lemma:de], art, (num), ?+[maj], (prep, (art), ?+[maj])*. 

//Jogos de futebol
1> ? @= ?[surface:"Sporting[%-]Benfica",event=+,eventac=+];?[surface:"Benfica[%-]Sporting",event=+,eventac=+].

// Any complex expression containing FC and hifen is football play
// o FC Porto-Boavista
1> ? @= ?[surface:"FC %+-%+",event=+, eventac=+] .


// Maratona
1> NOUN[event=+, eventac=+] @= (num[ord];num[rom]), (?[maj], prep[lemma:de], art[def]), (?[surface:"Meia"];?[surface:"Mini"]), ?[surface:"Maratona"], (adj[maj]), (prep[lemma:de];art;?[maj])* .


//Expo 98
1> NOUN[event=+, organized=+] @= ?[maj,lemma:Expo];?[lemma:EXPO], num[rom:~,lemma:"%d%d"]. 

1> ? = noun[lemma:"EXPO'%d%d",event=+,organized=+];noun[lemma:"Expo'%d%d",event=+,organized=+] .

//Jogos Municipais
1> NOUN[event=+, organized=+] @= ?[lemma:jogo, maj], adj*, (prep[lemma:de]), (art), (?+[maj]), (prep, (art), ?+[maj])*. 
1> ? @= ?[lemma:jogo, maj,event=+,organized=+]. 

//Encontro
1> NOUN[event=+, organized=+] @= (noun;num), ?[lemma:encontro, maj], adj*, (prep[lemma:de]), (art), (?+[maj]), (prep, (art), ?+[maj])*. 

//Show
1> NOUN[event=+, eventac=+] @= ?[lemma:show, maj], ?[maj], (prep; art; ?[maj])*. 
1> NOUN[event=+, eventac=+] @= noun[maj], (prep; art; ?[maj])*, ?[lemma:show, maj] . 
1> ? @= ?[lemma:show,maj:~], ?[maj,event=+,eventac=+,start_event=+], (prep;art;?[maj,end_event=+])* .
1> ? @= ?[lemma:show,maj,event=+,eventac=+] .


//Party
1> NOUN[event=+, eventac=+] @= noun[maj], (prep; art; ?[maj])*, ?[lemma:party, maj], (num) . 
1> ? @= ?[lemma:party,maj,event=+,eventac=+] .

//Santos Populares
//1> NOUN[event=+, eventac=+] @= |prep[lemma:em], (art)| ?[people];?[lemma:popular].
//1> NOUN[event=+, eventac=+] @= |?[lemma:noite], prep[lemma:de]| ?[people];?[lemma:popular] .

//Excursão
//1> NOUN[event=+, organized=+] @= ?[lemma:excursão, maj], (?+[maj]), (prep;art;?[maj])*. 


//Seminário de Lógica Matemática
//Seminário Matemático
1> NOUN[event=+, organized=+] @= ?[lemma:seminário,maj], (prep[lemma:de]),  ?[maj:+,location:~], (prep;art;?[maj])*.


// seminário XXX
1> ? @= ?[lemma:seminário,maj:~], ?[maj:+,start_event=+,event=+,organized=+], (prep; art; ?[maj,end_event=+])*. 


// event (sigla) -> sigla = event tb
//3> NOUN[org=+, organized=+] @= |?[event], punct[paren]| noun[toutmaj] |punct[paren]|.
3> ? @= ?[event], punct[paren], noun[toutmaj,event=+,organized=+], punct[paren] .
