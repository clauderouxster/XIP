//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//! --- CONTENTS ---
//!   Project    : SemEval07
//!   filename   : triplets.xip
//!   Date       : 2007
//!   Author	 : 
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!



//////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////
// 
// *** LEVEL 0: BASIC
//	- NAMED ENTITIES: recuperer l'info d'une dep au niveau des noeuds 
//			qu'elle contient.
//	- TRIPLETS LONGS: pour faire de l'analyse distributionnelle sur des
//			"syntagmes" complexes
//	- LEXICALISATION de PREPD
//	- CONSTRUCTIONS POSSESSIVES
// 	- COORD ITEMS
// 
// *** LEVEL 1: SEMEVAL TAGGING
//	     --> same file
// 
// *** LEVEL 2: METONYMY Interpretation for ORGANISATION class
//	     -> dependencyOrg.xip
// 
// *** LEVEL 3: METONYMY Interpretation for COUNTRY  class
//	     -> dependencyCountry.xip
// 
//////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////



//**********
// LEVEL 0 :
//**********


// NAMED ENTITIES
// Pour faire affecter le trait de la dep au noeud concerné

Script: 

1> if (PERSON(#1)) {#1[entitnom=+,person=+];}
1> if (ORGANISATION(#1)) {#1[entitnom=+,comp=+];}// trait /org/=feature,/comp/=dependance
1> if (EVENT(#1)) {#1[entitnom=+,event=+];}
1> if (LOCORG(#1[!country:+!, !city:+!, !contin:+!])) {#1[entitnom=+,locorg=+];}


// Pour faire "descendre" le trait a ts les noeuds fils
1> |Noun[entitnom]{noun#1}| {#1[entitnom=+];}
1> |Noun[entitnom]{?*#1}| {#1[entitnom_part=+];}
1> |Noun[person:+]{?*,Noun#1}|{#1[person=+];}
1> |Noun[evt:+]{?*,Noun#1}|{#1[evt=+];}
1> |Noun[comp:+]{?*,Noun#1}|{#1[comp=+];}
1> |Noun[locorg:+]{?*,Noun#1}|{#1[locorg=+];}


DependencyRules:

// ******
// Pronpers --> PERSON 
// dep avec pronpers effacee.
if (^$1(#1,#2[pron_trip]))
        $1(#1,##fict[lemme=PERSON]).

if (^$1(#1[pron_trip],#2))
        $1(##fict[lemme=PERSON],#2).

// ******
// POUR TRIPLETS LONGS
// (The little cat is very nice.)
// attention, c'est recursif.
// d'un cote
if ($1[!post:+!,!pre:+!,!appos:+!,!proper:+!](#1,#2) & HEAD[long:~](#2,#3) & (#3[lemme]~:#2[lemme]))
	   $1[long=+](#1,#3).
//// de l'autre 
if ($1[!post:+!,!pre:+!,!appos:+!,!proper:+!](#1,#2) & HEAD[long:~](#1,#3) & (#3[lemme]~:#1[lemme]))
	   $1[long=+](#3,#2).


//// Pour gerer MOD_LONG(The little cat,little) 
//// tous les nds au meme niveau (le mot cible est le dernier)
|?#3{?*,?+#2,?#1[last]}|
	if($1[long:~](#1,#2) & ^$1[long:+](#3,#2) & (#2[lemme]::#2[lemme]))
		~.

//// on descend plus bas
|?#3{?*,?{?+#2},?#1[last]}|
	if($1[long:~](#1,#2) & ^$1[long:+](#3,#2) & (#2[lemme]::#2[lemme]))
		~.

//// avec chunk plus compliques
|?#3{?*,?+#2,?*,?#1[last]}|
	if($1[long:~](#1,#2) & ^$1[long:+](#3,#2) & (#2[lemme]::#2[lemme]))
		~.

|?#3{?*,?{?*,?+#2,?*},?*,?#1[last]}|
	if($1[long:~](#1,#2) & ^$1[long:+](#3,#2) & (#2[lemme]::#2[lemme]))
		~.

// ******
// PREP (+pron) (=PP de SemEval)
// Ex: "The committee has taken over the organisation of the XRCE seminars."
// On a :
// MOD-T(NOUN:organisation,NOUN:seminar)
// PREPD(NOUN:seminar,PREP:of)
// On construit:
// PREP_OF(NOUN:organisation,NOUN:seminar)
// Pour les pron : creation d'un noeud fictif.

// deps sans pronoms
if((MOD(#1,#2[pronpers:~])| LOC (#1,#2)) & PREPD(#2,#3[!form_prep:!]) & ~PREP(#1,#2))
      	PREP(#1,#2).
// deps avec pronoms (que personnels)
if(MOD(#1,#2[pron_trip]) & PREPD(#2,#3[!form_prep:!]) )
	PREP(#1,##Pron[lemme=PERSON]).

// ******
// Verb particle + pronoms
// Ex : "The committee has taken over the organisation of the XRCE seminars."
// On a :
// NUCL(VERB:take,ADV:over)
// OBJ-N(VERB:take,NOUN:organisation)
// On construit :
// PREP_P_OVER(VERB:take,NOUN:organisation)

// Config normale:
if ( (NUCL(#1[verb:+],#2[adv:+,!partic:!]) | NUCL(#1[verb:+],#2[prep:+,!partic:!]))  & (OBJ-N(#1,#3[pronpers:~])))
		PREP(#1,#3).


// Avec un Pronom:
if ( (NUCL(#1[verb:+],#2[adv:+,!partic:!]) | NUCL(#1[verb:+],#2[prep:+,!partic:!]))  & (OBJ-N(#1,#3[pronpers:+])))
		PREP(#1,#3),
	        PREP(#1,##fict[lemme=PERSON]).

// ******
// Constructions possessives (=GEN ou HASGEN de SemSeval)

| NP#5{?*, noun#1[last:~],?*, part#2[last:~],?*,noun#4[verb:~]}|
	if (PARTICLE (#1,#2) & HEAD(#4,#5))
	POSSES(#1,#4).

// attention peut-etre trop laxiste
| NOUN#5{?*, noun#1[last:~], part#2[last:~],?*,noun#4[verb:~]}|
	if (~POSSES(#1,#4))
	POSSES(#1,#4).

//de caroline
| ?{#1[last]};#1, part, #2 |
	if ( MOD[pre](#2[noun],#1) & ~POSSES(#1,#2))
	POSSES(#1,#2)

// ******
// COORDITEMS
|NP{?*,#1[last]},CONJ,NP{?*,#2[last]}|
	if(~NCOORD(#1,#2))
	NCOORD(#1,#2)

|PP{PREP,NP{?*,#1[last]}},CONJ,NP{?*,#2[last]}|
	if(~NCOORD(#1,#2))
	NCOORD(#1,#2)

|?{?*,NP{?*,#1[last]}},CONJ,NP{?*,#2[last]}|
	if(~NCOORD(#1,#2))
	NCOORD(#1,#2)


// ******
// Effacements:

if  (^MOD(#1,#2) & ATTRIB(#1,#2))
     ~.
if  (^MOD(#1,#2) & PREP(#1,#2))  
     ~.
if  (^MOD(#1,#2) & PREP(#1,#2))	 
     ~.
if  (^MOD(#1,#2[pronpers]))	
     ~.
if  (^MOD(#1[pronpers],#2))
    ~.
if  (^MOD(#1,#2) & POSSES(#2,#1))
    ~.

// PREP_OF(NP:the president,PP:of France_LOCORG:+_COUNTRY:+)

|PP#2|
  if (^PREP(#1,#2)) 
     ~.
	   	
|PP#2|
  if (^MOD(#1,#2))
      ~.
	   	
|PP#2|
  if (^MOD(#2,#1))
      ~.
	  	

//********					 
// LEVEL 1:           
//********					  

// Marquage unites metonymiques (SemEval)

// Pour avoir les deps concernant cette unite.

if ($1[long:~](#1,#2[annotsupOrg]) )
	$1[metoregOrg=+](#1,#2).

if ($1[long:~](#1[annotsupOrg],#2))
	$1[metorectOrg=+](#1,#2).

if ($1[long:~](#1,#2[annotsupLoc]) )
	$1[metoregLoc=+](#1,#2).

if ($1[long:~](#1[annotsupLoc],#2))
	$1[metorectLoc=+](#1,#2).

// effacements 

//if  (^SUBJ[metoregOrg](#1,#2))
//	~.
//if  (^SUBJ[metoregLoc](#1,#2))
//	~.
//if  (^HEAD[metosem](#1,#2))
//	~.
//if  (^HEAD[metosem](#1,#2))
//	~.
//if  (^PREPD[metosem](#1,#2))
//	~.
//if  (^PREPD[metosem](#1,#2))
//	~.

