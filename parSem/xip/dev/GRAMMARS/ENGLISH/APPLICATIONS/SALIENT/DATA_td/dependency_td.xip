DependencyRules:

//1. Professional Knowledge.  This might include,
//(i) knowing students and -> know-student
//how they learn, and -> student-learn
//(ii) knowing the content and -> know-content
//how to teach it. -> do-teaching


if ( SENT(#1) )
   SENTENCE(#1).

if ( REFLCONC(#1) )
   SENTENCE(#1).

/////////////////////// ENTITY CONCEPTS \\\\\\\\\\\\\\\\\\\\\

//SUBJECT
//OTHERSUBJECT
//SCHOOL

////////////////////// RELEVANT DEPENDENCIES \\\\\\\\\\\\\\\\

/////////// X DO

//Over the six days that I was placed in room 11 , I was able to get to know each student and see what type of learner they were and what level they were at .
//SUBJ-N_PRE(get,I)
//PREPD(get,to)


if ( ^SUBJ-N(#1,#2) )
   SUBJ-N[X-do=+](#1,#2).


//keep only the most embedded verb

//OBJ_POST_INFINIT(#1get,#3know)
//SUBJ-N_X-DO_PRE(#1get,#2I)
//SUBJ-N_X-DO(#3know,#2I)

if ( ^SUBJ-N[X-do](#1,#2) & OBJ[infinit](#1,#3) & SUBJ-N(#3,#2) & #1 < #3 & #2::#2)
   SUBJ-N[X-do=~](#1,#2).

/////////// X CHARACTERISTICS

//BE is characteristics

if ( ^SUBJ-N[X-do](#1[lemme:be],#2) )
   SUBJ-N[X-do=~,X-char=+](#1,#2).

//BECOME is characteristics

if ( ^SUBJ-N[X-do](#1[lemme:become],#2) )
   SUBJ-N[X-do=~,X-char=+](#1,#2).

// STANCE is characteristics

if ( ^SUBJ-N[X-do](#1[stance],#2) )
   SUBJ-N[X-do=~,X-char=+](#1,#2).

//I believe on placement I was able to use the weeks prior to the week block to learn about my students.
//SUBJ-N_I-DO_GRPR(#1use,#2I)
//SUBJ_PRE(was,I)
//QUALIF(#2I,#3able)
//EMBED_INFINIT(use,was)
//NUCL_SUBJCOMPL(was,able)
//SUBJATTR(I,able)
//MOD_SUBCAT_POST_INFINIT(able,use)


if ( SUBJ-N[X-do](#1,#2) & ^$1(#3,#2) & #1~:#3 )
   $1[X-char=+](#3,#2).

if ( $1[X-char](#3,#2) & ^NUCL(#3,#4) )
   NUCL[X-char=+](#3,#4).

// eliminate CHAR that is DO:

if ( ^ARGUMENT[X-char](#1,#2) & SUBJ-N[X-do](#1,#2) )
   ARGUMENT[X-char=~](#1,#2)

if ( ^SUBJ[X-char](#1,#2) & SUBJ-N[X-do](#1,#2) )
   SUBJ[X-char=~](#1,#2)

if ( ^SUBJ-N[X-char,X-do](#1,#2) )
   SUBJ-N[X-char=~](#1,#2)

//From here I was able to construct lesson plans that were suited to the level that my students were at.
//MOD_POST_SENTENCE_RELATIV(plans,suited)

if ( ^MOD[sentence,relativ](#1,#2) )
   MOD[X-char=+](#1,#2)

if ( ^QUALIF(#3,#4) )
   QUALIF[X-char=+](#3,#4).

if ( ^QUANTD(#3,#4) )
   QUANTD[X-char=+](#3,#4).

if ( ^DETD(#3,#4) )
   DETD[X-char=+](#3,#4).


// I

if ( ^$1[X-do](#1,#2[subject]) )
   $1[X-do=~,I-do=+](#1,#2)

if ( ^$1[X-char](#1,#2[subject]) )
   $1[X-char=~,I-char=+](#1,#2)

//SUBJ_I-CHAR_PRE(was,I)
//NUCL_X-CHAR_VLINK(was,able)

if ( $1[I-char](#1,#2) & ^NUCL[X-char](#1,#3) )
   NUCL[X-char=~,I-char=+](#1,#3)

// OTHER

if ( ^$1[X-do](#1,#2[othersubject]) )
   $1[X-do=~,o-do=+](#1,#2)

if ( ^$1[X-do](#1[othersubject],#2) )
   $1[X-do=~,o-do=+](#1,#2)

if ( ^$1[X-char](#1,#2[othersubject]) )
   $1[X-char=~,o-char=+](#1,#2)

if ( ^$1[X-char](#1[othersubject],#2) )
   $1[X-char=~,o-char=+](#1,#2)

if ( $1[o-char](#1,#2) & ^NUCL[X-char](#1,#3) )
   NUCL[X-char=~,o-char=+](#1,#3)

//SCHOOL

if ( ^$1[X-char](#1[school],#2) )
   MOD[X-char=~,school-char=+](#1,#2).

if ( ^$1[X-char](#1,#2[school]) )
   MOD[X-char=~,school-char=+](#1,#2).




// marking words

| #1[I-do=+] |
if ( $1[I-do](#1,#2) )
   ~.

| #2[I-do=+] |
if ( $1[I-do](#1,#2) )
   ~.

| #1[o-do=+] |
if ( $1[o-do](#1,#2) )
   ~.

| #2[o-do=+] |
if ( $1[o-do](#1,#2) )
   ~.

| #1[I-char=+] |
if ( $1[I-char](#1,#2) )
   ~.

| #2[I-char=+] |
if ( $1[I-char](#1,#2) )
   ~.

| #1[o-char=+] |
if ( $1[o-char](#1,#2) )
   ~.

| #2[o-char=+] |
if ( $1[o-char](#1,#2) )
   ~.

| #1[school-char=+] |
if ( $1[school-char](#1,#2) )
   ~.

| #2[school-char=+] |
if ( $1[school-char](#1,#2) )
   ~.


//As mentioned above my proffessionalism was outstanding .
//NUCL_VLINK_PROGRESS(was,outstanding)

