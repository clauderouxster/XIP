DependencyRules:


///////////////////// NOT IMPSENT IN REFLECETIVE SENTENCES \\\\\\\\\\\\\\\\

// sentences that don't have any other kind of KSWDEP but CONTR+DEICT


if ( ^KSWDEP(#1[contr],#2[deict]) )
   KSWDEP[contr=+,deict=+](#1,#2).   

if ( ^KSWDEP(#1[deict],#2[contr]) )
   KSWDEP[contr=+,deict=+](#1,#2).

if ( ^KSWDEP[contr,deict](#2,#3) )
   ~.   

if ( IMPSENT(#1) & ~KSWDEP(#3,#4) )
   SENT(#1).

if ( IMPSUMMARY(#1) & ~KSWDEP(#3,#4) )
   SENT(#1).

if ( ^IMPSENT(#1) & SENT(#1) )
   ~.

if ( ^IMPSUMMARY(#1) & SENT(#1) )
   ~.



///////////////////////// final for NOT IMPSENT IN REFLECETIVE SENTENCES


if ( SENT(#1) & ^CONCEPT[impsent](#2) )
   CONCEPT[impsent=~](#2).

if ( SENT(#1) & ^CONCEPT[impsent](#2,#3) )
   CONCEPT[impsent=~](#2,#3).

if ( SENT(#1) & ^CONCEPT[impsent](#2,#3,#4) )
   CONCEPT[impsent=~](#2,#3,#4).

if ( SENT(#1) & ^CONCEPT[impsent](#2,#3,#4,#5) )
   CONCEPT[impsent=~](#2,#3,#4,#5).


| #1[md=~] |
if ( SENT(#2) )
   ~.


///////////////////// IMPSENT \\\\\\\\\\\\\\\\\\\\

// SUMMARY sentences are not relevant in reflective writing

| #1[md=~] |
if ( SUMMARY(#2) )
    ~.

if ( ^AUTHOR(#1) )
    PERSON(#1).

if ( SUMMARY(#1) )
    SENT(#1).

if ( ^SUMMARY(#1) )
    ~.

if ( ^IMPSUMMARY(#1) )
    IMPSENT[contribution=~](#1).

if ( ^IMPSENT(#1) )
    REFLSENT[analytic=+](#1).

/
if ( ^IMPSENT[reference](#1) )
    IMPSENT[reference=~](#1).

if ( IMPSENT[novstat](#1) & ^REFLSENT(#1) )
    REFLSENT[novstat=+,analytic=+](#1).


if ( IMPSENT[vis](#1) & ^REFLSENT(#1) )
    REFLSENT[vis=+,analytic=+](#1).

if ( IMPSENT[attitude](#1) & ^REFLSENT(#1) )
    REFLSENT[attitude=+,analytic=+](#1).

if ( IMPSENT[emph](#1) & ^REFLSENT(#1) )
    REFLSENT[emph=+,analytic=+](#1).


if ( IMPSENT(#1) & ~REFLSENT(#1) )
    REFLSENT[analytic=+](#1).

if ( ^IMPSENT(#1) & REFLSENT(#1) )
    ~.
\



//////////////////////////////  AUTHOR DEPENDENCIES \\\\\\\\\\\\\\\\\\\\\\

// *** dependencies involving the author *** \\

if ( ^$1(#1,#2[lemme:I]) )
	$1[authdep=+](#1,#2).

if ( ^$1(#1[lemme:I],#2) )
	$1[authdep=+](#1,#2).

if ( ^$1(#1,#2[lemme:i]) )
	$1[authdep=+](#1,#2).

if ( ^$1(#1[lemme:i],#2) )
	$1[authdep=+](#1,#2).

if ( ^$1(#1,#2[lemme:we]) )
	$1[authdep=+](#1,#2).

if ( ^$1(#1[lemme:we],#2) )
	$1[authdep=+](#1,#2).

// *** dependencies involving the author and a reflective word *** \\

| #1[authrefl=+] |
if ( ^$1[authdep](#1[reflection],#2) )
   $1[refldep=+](#1,#2).

| #2[authrefl=+] |
if ( ^$1[authdep](#1,#2[reflection]) )
   $1[refldep=+](#1,#2).



//During the subject progress , I have developed a better understanding of professional strengths and attributes as a graduate , as well as knowledge including rules and regulations related to Australia workplace such as safety and ethics issues .
//OBJ_POST(developed,understanding)
//SUBJ-N_PRE(developed,I)


| #3[authrefl=+] |
if ( SUBJ-N[pre,authdep](#1[shift],#2) & ^OBJ[post,sentence:~](#1,#3[reflection]) )
   OBJ[post,authdep=+,refldep=+](#1,#3).


| #3[authrefl=+] |
if ( ^SUBJ-N[pre,authdep](#1[shift],#2) & OBJ[post,sentence:~,authdep,refldep](#1,#3[reflection]) )
   SUBJ-N[refldep=+](#1,#2).


//I hope to be able to incorporate this into my future projects as I now have a deeper understanding of this broad concept of sustainability.
//OBJ-N(have,understanding)
//SUBJ_PRE_AUTHDEP(have,I)

| #3[authrefl=+] |
if ( $1[authdep](#1[lemme:have],#2) & $2(#1,#3[reflection,myrefl]) & ~$1(#2,#3) & ~$2(#3,#2) )
   DEP[authdep=+,refldep=+](#2,#3).


// no subject of non-finite verb that has a modifier having a determiner "my"
//That readiness form was actually an attempt to think in an organized way about my career .
//MOD_SUBCAT_POST(think,career)
//DETD(career,my)

| #1[authrefl=+] |
if ( ^MOD[post](#1[reflection,authrefl:~],#2) & DETD(#2,#3[lemme:I]) )
   MOD[post,authdep=+,refldep=+](#1,#2).


// impersonal subject sentences with reflection words are also authrefl 

//Being irresponsible for day cost me marks that could have added to my final assessment but I was happy as it was a different learning experience.
//NUCL_SUBJCOMPL(was,learning experience)
//ATTRIB(it,learning experience)

| #2[authrefl=+] |
if ( ^NUCL[subjcompl](#1,#2[reflection,authrefl:~]) & ATTRIB(#3[lemme:it],#2) )
   NUCL[authdep=+,refldep=+,impersonal=+](#1,#2).



// *** dependencies involving the author and a capability word *** \\

| #1[authcapa=+] |
if ( $1[authdep](#1[capability],#2) )
   ~.

| #2[authcapa=+] |
if ( $1[authdep](#1,#2[capability]) )
   ~.

// marking the dependencies that involve the capability

//SUBJ_AUTHDEP_PRE(can_AUTHCAPA:+,we)
//SUBJ-N_AUTHDEP_PRE(help,we)
//ARGUMENT_AUTHDEP(help,we)

| #2[authcapa=+] |
if ( $1[authdep](#1[authcapa],#2) )
    ~.

| #1[authcapa=+] |
if ( $1[authdep](#1,#2[authcapa]) )
    ~.

if ( ^$1(#1[authcapa],#2) )
    $1[authcapadep=+](#1,#2)

if ( ^$1(#1,#2[authcapa]) )
    $1[authcapadep=+](#1,#2)

///////////////////// CONTEXT OF THE REFLECTION \\\\\\\\\\\\\\\\\\\\

//contextmark

//TIMEX3


//at that time 
//PREPD(time,at)
//PP{at NP{that time}} 

| pp#1{?, np{?*, #2[lemme:time]}} |
if ( ~TIMEX3(#1) )
  TIMEX3[grrefl=+](#1).

//exception: During one of the field work I was a part of group conducting surveys by contacting stakeholders related to UTS-2020 master plan, I didn't perform well when it came to communicating with people, asking them questions and describing them about the scenario.
//QUANTD(work,one)




| pp#1{#2[lemme:during], np{?*,#3}} |
if ( ~TIMEX3(#1) & ~QUANTD(?,#3) )
  TIMEX3[grrefl=+](#1).



//At the beginning of the semester I had to go through the career readiness form and at that time I actually realized I have to think deeply and research seriously about the job market in Australia.
//TIMEX3(At the beginning of the semester)


| #1[contextmark=+]{?[lemme:~when]} |
if ( ^TIMEX3[t_relat:~,t_freq:~](#1) )
   TIMEX3[contextmark=+](#1). 



//•	Context of the event that triggers the reflection (why, when, where, who, how much, what): the more detail the better, as long as the event is non-trivial

// *** reflective author + context *** \\
//exception: When we look at other peoples' arcs, we don't see all of the deviations (both high and low) from this upward and forward trajectory, but they are there.


if ( $1[refldep](#1,#3) & ^TIMEX3[t_relat:~,t_freq:~,contextmark](#2) )
   TIMEX3[contextdep=+](#2).
   

/
| #4[contextmark] |
//if ( ^$1[refldep](#1,#2) & $2(?[past],?) )
//if ( ^$1[refldep](#1,#2) & PAST(?) )

if ( $1[refldep](#1,#3) & SUBJ(#1[past],#2) )
	SUBJ[contextdep=+](#1,#2).

| #4[contextmark] |
if ( $1[refldep](#3,#1) & SUBJ(#1[past],#2) )
	SUBJ[contextdep=+](#1,#2).


| #4[contextmark] |
if ( $1[refldep](#2,#3) & SUBJ(#1[past],#2) )
	SUBJ[contextdep=+](#1,#2).

| #4[contextmark] |
if ( $1[refldep](#3,#2) & $2(#1[past],#2) )
	SUBJ[contextdep=+](#1,#2).
\

//if 

// In refldeps with impersonal verbs: if there are at least two clauses, i.e. two finite CORE-VERBs
//Furthermore I came across couple of subjects which were manufacturing engineering and solid works , it was a good learning experience for me .
//NUCL_REFLDEP_SUBJCOMPL(was,learning experience)
//CORE-VERB(came)
//CORE-VERB(was)
/
| fv{?*,#3} |
if ( ^NUCL[refldep,subjcompl](#1,#2) & CORE-VERB(#3) & #1~:#3 )
   NUCL[contextdep=+](#1,#2).
\
//Also during some professional interaction I felt a bit behind when it came to describing something as compared to our team members.

//PREPD(interaction,during)

if ( ^PREPD(#1,#2[contextmark] ) & $1[refldep](?,?) )
   PREPD[contextdep=+](#1,#2).

//After participating in a major group presentation, I realised that nerve management is an area of improvement that I will have to prioritise on.
//CONNECTOR_SUBORD(participating,After_CONTEXTMARK:+)

if ( ^CONNECTOR[subord](#1,#2[contextmark] ) & $1[refldep](?,?) )
   CONNECTOR[contextdep=+](#1,#2).

//What I found was when conducting the individual interview I was very nervous and when asked about my attributes and my personal skills .

//I found that when the clients don't return the device to its original state in this case the vertical stance, the Mini-pacc system does not recognize that the carrier has been rescued and stood back up.
//TEMP_COMPLEX_CONTEXTDEP_PRE_RELATIV(asked,when)

if ( ^TEMP(#1,#2) & & $1[refldep](?,?) )
    TEMP[contextdep=+](#1,#2).


//When I was doing my bachelor on electrical engineering at UTS I thought doing only electrical subjects are not enough for my future career.
//MOD_PRE(doing,When)
//NUCL_VLINK_PROGRESS(was,doing)

//if ( ^MOD[pre](#1,#2[contextmark]) & NUCL(#3[past],#1) & $1[refldep](?,?) )
if ( ^MOD[pre](#1,#2[contextmark]) & $1[refldep](?,?) )
    MOD[contextdep=+](#1,#2).

//Also during some professional interaction I felt a bit behind when it came to describing something as compared to our team members.
//TEMP_COMPLEX_PRE_RELATIV(came,when)

if ( ^TEMP(#1[past],#2[contextmark]) & $1[refldep](?,?) )
    TEMP[contextdep=+](#1,#2).


//// INDICATOR_beg \\\\
/
| #3[contextmark,contextindicator=+] |
if ( $1[contextdep](#1,#2) & $2(#3[past],#4) )
    INDICATOR(#3), INDICATOR(#1), INDICATOR(#2), INDICATOR(#4).
\
/
| #3[contextmark,contextindicator=+] |
if ( $1[contextdep](#1,#2) & $2(?[past],?) )
    INDICATOR(#1), INDICATOR(#2).

| #3[contextmark,contextindicator=+] |
if ( $1[contextdep](?,?) & $2(#1[past],#2) )
    INDICATOR(#1), INDICATOR(#2).
\
//// INDICATOR_end \\\\

if ( ^SENT(#3) & $1[contextdep](#1,#2) )
   REFLSENT[context=+](#3), INDICATOR(#1), INDICATOR(#2).

if ( ^REFLSENT(#3) & $1[contextdep](#1,#2) )
   REFLSENT[context=+](#3), INDICATOR(#1), INDICATOR(#2).

if ( ^SENT(#3) & $1[contextdep](#1) )
   REFLSENT[context=+](#3), INDICATOR(#1).

if ( ^REFLSENT(#3) & $1[contextdep](#1) )
   REFLSENT[context=+](#3), INDICATOR(#1).



///////////////////// SPECIFICATION OF THE REFLECTION \\\\\\\\\\\\\\\\\\\\


//•	Specific information about the event; specific examples of actions , ideas, learning experiences

//Expressions about learning SOMETHING SPECIFIC (I learned that)*

// *** reflective author + embedded clause, which is not shift *** \\

if ( ^$1[infinit](#1[authrefl,bf:~],#2[lemme:~be,shift:~,sentiment:~]) )
   $1[specificdep=+](#1,#2).

if ( ^$1[infinit](#1[lemme:~be,shift:~,sentiment:~],#2[authrefl,bf:~]) )
   $1[specificdep=+](#1,#2).

//if ( ^EMBED[complthat](#1[shift:~,sentiment:~],#2[authrefl]) )
if ( ^EMBED(#1[shift:~,sentiment:~],#2[authrefl]) & #2 < #1 & ~TEMP(#1,?) )
   EMBED[specificdep=+](#1,#2).

//What really struck me about the class was the caring environment.
//SUBJ_PRE_SENTENCE(was,struck)

if ( ^SUBJ[pre,sentence](#1[shift:~,sentiment:~],#2[authrefl]) & #2 < #1 )
   SUBJ[specificdep=+](#1,#2).

if ( ^OBJ[post,sentence](#1[authrefl],#2[shift:~,sentiment:~]) )
   OBJ[specificdep=+](#1,#2).

//I learned the importance of continually developing a relationship between my patient and I, no matter what barriers come along the way.
//OBJ-N(learned,importance)

if ( ^OBJ(#1[authrefl],#2[ksw]) )
    OBJ[specificdep=+](#1,#2).

///I hope to be able to incorporate this into my future projects as I now have a deeper understanding of this broad concept of sustainability.
//MOD_POST(understanding[authrefl],concept)
//PREPD(concept,of)

if ( ^MOD[post](#1[authrefl],#2) & PREPD(#2,#3[lemme:of]) )
   MOD[specificdep=+](#1,#2).

//Also in that visit I learned a lot about workplace health and safety requirements .
//MANNER_POST(learned,a lot)

if ( ^MANNER[post](#1[authrefl],#2) )
   MANNER[specificdep=+](#1,#2).

//That readiness form was actually an attempt to think in an organized way about my career .
//MOD_REFLDEP_SUBCAT_POST(think,career)

if ( ^MOD[refldep,subcat](#1,#2) )
   MOD[specificdep=+](#1,#2).

//A lot of which were done routinely , meaning I found it difficult to find the true purpose of studying these subjects .
//OBJCOMPL_POST(found,difficult)

if ( ^OBJCOMPL[post](#1[authrefl],#2) )
   OBJCOMPL[specificdep=+](#1,#2).

//However, I found it confusing and at time frustrating as the study materials where based on various separate topics.
//POTMOD(found,confusing)


if ( ^POTMOD(#1[authrefl],#2) )
   POTMOD[specificdep=+](#1,#2).

//This had reminded me that there is meaning behind the various formulas and information that I learn in class.
//CONNECTOR_COMPLEX_COMPLTHAT(learn,that)

if ( ^CONNECTOR[complthat](#1[authrefl],#2) )
   CONNECTOR[specificdep=+](#1,#2).
  

// With impersonal subject: The reflection is in a subordinate clause

//Being irresponsible for day cost me marks that could have added to my final assessment but I was happy as it was a different learning experience.
//CONNECTOR_COMPLEX_SUBORD(was,as)
//NUCL_AUTHDEP_SUBJCOMPL(was,learning experience)

if ( CONNECTOR[subord](#1,#2) & ^NUCL[authdep,subjcompl](#1,#3) )
   NUCL[specificdep=+,contextdep:~](#1,#3).

//When I was doing my bachelor on electrical engineering at UTS I thought doing only electrical subjects are not enough for my future career .
//MOD_POST_GERUND(thought,doing)

if ( ^$1[post,gerund](#1[authrefl],#2) )
   $1[specificdep=+](#1,#2).

//I was wondering why there were no compression tabs underneath the base to minimize material distortion and buckling.
//MOD_PRE(were,why)
//SC{BG{why} there FV{were}}


//exception:
//ANTECEDENT(person,who_SPECIFICINDICATOR:+)


| #2[authrefl]  |
if ( $1(?,#1[wh,contextmark:~]) & #2 < #1 & ~ANTECEDENT(?,#1) & ~$1(#2,#1) & ~$2(#1,#2) & ~MOD[post](?,#1) )
    DEP[grrefl=+,specificdep=+](#2,#1).
 

//Reflecting on planning my learning, I suppose I planned my learning by taking a systematic approach to the task at hand.
//EMBED_POST_GERUND(planned,taking)
//OBJ_POST(planned,learning)

if ( OBJ[post](#1,#2[authrefl]) & ^EMBED(#1,#3) )
    EMBED[specificdep=+](#1,#3).

//I would also inspect other motor tensioning systems that were used throughout the production process and try to think in my mind if they were applicable in this instance.
//MOD_SUBCAT_POST_SENTENCE(think,were)

if ( ^MOD[subcat,sentence](#1[authrefl],#2) )
    MOD[specificdep=+](#1,#2).

//Thus I learned some of the key cultural differences such as language, non-verbal communication, time and space (Dwyer 2012).
//OBJ_POST(learned_INDICATOR:+_MD:+,differences)
//MOD_POST(learned_INDICATOR:+_MD:+,language)

if ( OBJ(#1[authrefl],#2) & ^MOD[post](#1,#3) )
   MOD[specificdep=+](#1,#2).




//exception

//VCOORD_SPECIFICDEP_REFLDEP_INFINIT(construct,visualise)

if ( ^VCOORD[specificdep,refldep](#1,#2) )
   VCOORD[specificdep=~,refldep=~](#1,#2).

// REFLSENT_SPECIFIC

if ( ^SENT(#3) & $1[specificdep](#1,#2) )
   REFLSENT[specific=+](#3), INDICATOR(#1), INDICATOR(#2).


//The important lesson I learn here is that data sheets, current models and guidelines are very important.
//REFLSENT_COMPLEX_ANALYTIC(

| #1[authrefl] |
if ( ^REFLSENT[analytic](#2) )
   REFLSENT[specific=+](#2), INDICATOR(#1).



if ( ^REFLSENT(#3) & $1[specificdep](#1,#2) )
   REFLSENT[specific=+](#3), INDICATOR(#1),  INDICATOR(#2).


///////////////////// SHIFT IN REFLECTION \\\\\\\\\\\\\\\\\\\\

//deeper understanding
//QUALIF(understanding,deeper)

| #1[shift=+] |
if ( ^QUALIF(#1[reflection,authrefl],#2[compar]) )
   QUALIF[shiftdep=+](#1,#2).

//•	Change between assumptions of learning and/or prior knowledge and actual performance

// *** IMPSENT_CONTRAST + reflective author
/
| #1[authrefl] |
if ( $1[analytic](#2) )
   REFLSENT[shift=+](#2).
\
//// INDICATOR_beg \\\\
/
| #1[authrefl] |
if ( $1[analytic](#2) &  REFLSENT[shift](#2) )
    INDICATOR(#1).
\
//// INDICATOR_end \\\\


// *** reflective author - reflection word is also shift word *** \\\

| #1[shift,authrefl] |
if ( ^SENT(#2) )
   REFLSENT[shift=+](#2).

| #1[shift,authrefl] |
if ( ^REFLSENT(#2) )
   REFLSENT[shift=+](#2).

| #1[shift,authrefl] |
if ( REFLSENT(#3) & ^$1[refldep](#1,#2) )
   $1[shiftdep=+](#1,#2), INDICATOR(#1), INDICATOR(#2).
   
| #1[shift,authrefl] |
if ( REFLSENT(#3) & ^$1[refldep](#2,#1) )
   $1[shiftdep=+](#1,#2), INDICATOR(#1), INDICATOR(#2).
   

// *** reflective author + embedded clause, which is shift *** \\


//I can already see that my attitude towards University has changed.
//EMBED_SPECIFICDEP_REFLDEP_COMPLTHAT(changed,see)
/

if ( ^$1[infinit](#1[authrefl,bf:~],#2[lemme:~be,shift]) )
   $1[shiftdep=+,refldep=+](#1,#2).

if ( ^$1[infinit](#1[lemme:~be,shift],#2[authrefl,bf:~]) )
   $1[shiftdep=+,refldep=+](#1,#2).

//if ( ^EMBED[complthat](#1[shift],#2[authrefl]) )
if ( ^EMBED(#1[shift],#2[authrefl]) )
   EMBED[shiftdep=+,refldep=+](#1,#2).

if ( ^SUBJ[pre,sentence](#1[shift],#2[authrefl]) )
   SUBJ[shiftdep=+,refldep=+](#1,#2).

if ( ^OBJ[post,sentence](#1[authrefl],#2[shift]) )
   OBJ[shiftdep=+,refldep=+](#1,#2).
\

//After this discussion though, I started to see how deviation from social norms can be much less extreme, even going as far to say some deviations could not be problematic and may not be extremely harmful at all.
//OBJ_POST_INFINIT(started,see)

if ( ^$1(#1[shift],#2[authrefl]) )
    $1[shiftdep=+](#1,#2).

//During the subject progress , I have developed a better understanding of professional strengths and attributes as a graduate , as well as knowledge including rules and regulations related to Australia workplace such as safety and ethics issues .
//OBJ_POST(developed,understanding)
//SUBJ-N_PRE(developed,I)


| #3[authrefl=+] |
if ( SUBJ-N[pre,authdep](#1[shift],#2) & ^OBJ[post,sentence:~](#1,#3[reflection]) )
   OBJ[post,shiftdep=+](#1,#3).

| #3[authrefl=+] |
if ( ^SUBJ-N[pre,authdep](#1[shift],#2) & OBJ[post,shiftdep,sentence:~](#1,#3[reflection]) )
   SUBJ-N[shiftdep=+](#1,#3).



// reflective author + authchange


if ( $1(#1[shift],#2[lemme:I]) & $2(#3[authrefl],#1) )
   $1[shiftdep=+](#1,#2).

if ( $1(#1[shift],#2[lemme:I]) & $2(#1,#3[authrefl]) )
   $1[shiftdep=+](#1,#2).

if ( $1(#1[lemme:I],#2[shift]) & $2(#3[authrefl],#2) )
   $1[shiftdep=+](#1,#2).

if ( $1(#1[lemme:I],#2[shift]) & $2(#2,#3[authrefl]) )
   $1[shiftdep=+](#1,#2).


//analytical when a mentsco that is also contr has a dependency with scope
//Again showing that I was attempting IV NP granted others opportunities to help me and clarify concepts.
//KSWDEP(#1clarify_MD:+,#2concepts_MD:+)
//VCOORD_INFINIT(#3help,#1clarify_MD:+)
//OBJ-N(#3help,#4me)

if ( ^KSWDEP(#1[shift],#2[mentsco]) & $1(#3,#1) & $2[authdep](#3,?) )
   KSWDEP[shiftdep=+](#1,#2).



if ( ^SENT(#1) & $1[shiftdep](#2,#3) )
    REFLSENT[shift=+](#1), INDICATOR(#2), INDICATOR(#3).

if ( ^REFLSENT(#1) & $1[shiftdep](#2,#3) )
    REFLSENT[shift=+](#1), INDICATOR(#2), INDICATOR(#3).

// analytic sentence + authorshift
/
if ( ^REFLSENT[analytic](#1) & $1(#2[shift],#3[lemme:I]) )
    REFLSENT[shift=+](#1),INDICATOR(#2), INDICATOR(#3).

if ( ^REFLSENT[analytic](#1) & $1(#2[lemme:I],#3[shift]) )
    REFLSENT[shift=+](#1), INDICATOR(#2), INDICATOR(#3).
\


//Reference to the present and future in the context of reflecting on changed behaviour, expectations or beliefs: since; now; when; as

///////////////////// EXPERIMENTATION \\\\\\\\\\\\\\ 

//Expressions of experimentation and ability (I tried, I tested, I experimented, I developed my capability to, I was/am able to, I was/am unable to, I practised, I asked, I sought advice, I overcame, I couldn’t overcome)

// *** author capability
/
| #1[authcapa] |
if ( ^SENT(#2) & $1[authdep,authcapadep:~](#3[authcapa:~],#4[authcapa:~]) )
   REFLSENT[capability=+](#2).

| #1[authcapa] |
if ( ^REFLSENT(#2) & $1[authdep,authcapadep:~](#3[authcapa:~],#4[authcapa:~]) )
   REFLSENT[capability=+](#2).
\

///////////////////// OTHER'S POINT OF VIEW \\\\\\\\\\\\\\\\\\\


//For some , it was described as less pressuring and time constrained than high school , while others felt like college made them give up some free time they may have had in the past .

///////////////////// other reflection \\\\\\\\\\\\\\\


if ( ^SUBJ-N[authdep:~](#1[reflection,vprog:~],#2) )
   SUBJ-N[otherrefldep=+](#1,#2).
/
if ( ^SUBJ-N[authdep:~](#1[mentsco,vprog:~],#2) )
   SUBJ-N[otherrefldep=+](#1,#2).
\
//exceptions

if ( ^SUBJ-N[otherrefldep](#1,#2[lemme:we]) |  ^SUBJ-N[otherrefldep](#1,#2[lemme:it]) | ^SUBJ-N[otherrefldep](#1,#2[lemme:you]) )
    SUBJ-N[otherrefldep=~](#1,#2).

| #4[argumentation,contrast] |
if ( ^SENT(#1) & $1[otherrefldep](#2,#3) )
    REFLSENT[otherrefl=+](#1), INDICATOR(#2), INDICATOR(#3).

| #4[argumentation,contrast] |
if ( ^REFLSENT(#1) & $1[otherrefldep](#2,#3) )
    REFLSENT[otherrefl=+](#1), INDICATOR(#2), INDICATOR(#3).


//it turned/turns out; it became clear


///////////////////// YELLOW sentences \\\\\\\\\\\\\\\\\\\

//extending beyond self to domain/world 

//I didn't find indicators of this meaning, but found the following

//will have to
/
//Even throughout a single shift, we will have to encounter many different problems, such as a patient dying or receiving a devastating diagnosis, and still have to care for our many other patients.

//NUCL_VLINK_MODAL(will,have to)

if ( NUCL(#1[lemme:will],#2) & ^SENT(#3) )
    REFLSENT[lesson=+](#3).
    
if ( NUCL(#1[lemme:will],#2) & ^REFLSENT(#3) )
    REFLSENT[lesson=+](#3).
 \   

///////////////////// SURFACEREFL sentences \\\\\\\\\\\\\\\\\\\


//////////////// Complex structures \\\\\\\\\\\\\\\\\


if ( ^EMBED[temp](#1,#2) )
    EMBED[complex=+](#1,#2).

if ( ^$1[gerund](#1,#2) )
    $1[complex=+](#1,#2).

if ( ^CONNECTOR(#1,#2) )
    CONNECTOR[complex=+](#1,#2).

if ( ^MOD[pre,sentence](#1,#2) )
    MOD[complex=+](#1,#2).

if ( ^TEMP(#1,#2) )
    TEMP[complex=+](#1,#2).

if (^MAIN[ksw](#1) )
    MAIN[complex=+](#1).

if ( ^KSWDEP(#1) )
   KSWDEP[complex=+](#1)


//I chose this subject and I would like to reflect back on some of key experiences that have contributed to my  individual professional growth.
//SUBJ_PRE_RELATIV(have,that)

if ( ^SUBJ[pre,relativ](#1,#2) )
   SUBJ[complex=+](#1,#2).

//On the other hand, it makes me conscious of the importance of a positive internal dialogue within the company, because of the major impact in can have on the performance and outcomes.

if ( PREPD(#1,#2[lemme:because_of]) )
   PREPD[complex=+](#1,#2).


//I found a efficient was for myself to manage time , both using a physical white board and also the application on my phone to plan and remind me of different deadlines and progress .

| sc, ?*, sc |
if ( ^REFLSENT(#1) )
   REFLSENT[complex=+](#1)

//I tend to look at the problem in depth and consider the problem in an engineer's perception.

| iv , ?*, iv |
if ( ^REFLSENT(#1) )
   REFLSENT[complex=+](#1)

| gv , ?*, iv |
if ( ^REFLSENT(#1) )
   REFLSENT[complex=+](#1)

| iv , ?*, gv |
if ( ^REFLSENT(#1) )
   REFLSENT[complex=+](#1)



if ( ^REFLSENT(#3) & ( $1[complex](#1,#2) | $1[complex](#1) ) )
   REFLSENT[complex=+](#3).




/////////////// REFLSENT[specific] and no other label \\\\\\\\\\\\\
/
if ( ^REFLSENT[specific,capability:~,context:~,course:~,grow:~,emph:~,otherrefl:~,shift:~,contrast:~,vis:~](#1) )
    REFLSENT[specificonly=+,1label=+](#1).
\
//////////////// REFLSENT[context] and no other label \\\\\\\\\\\\\\
/
if ( ^REFLSENT[specific:~,capability:~,context,course:~,grow:~,emph:~,otherrefl:~,shift:~,contrast:~,vis:~](#1) )
    REFLSENT[contextonly=+,1label=+](#1).
\


//////////////////////////////////////////////////////////////////////////////
// SURFACEREFL SENTENCES: NOMODAL
// the reflective verb does not have any modality: (SUBJ=SUBJ-N)
// +
// the sentence's label is only SPECIFIC or only CONTEXT
// +
// no complex structures
/////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////
// SIMPLE EVALUATION/1
// a REFLSENT contains a NUCL[subjcompl]
// +  no complex structures
/////////////////////////////////////////////////////////////////////////////


//sentences ending in a colon are not surface, because their content is continued

| #1[form:fcolon] |
if ( ^REFLSENT(#2) )
    REFLSENT[continued=+](#2).

| #1[form:fcolon] |
if ( ^SENT(#2) )
    SENT[continued=+](#2).

//It is important for me to know what the company is looking for and change accordingly .
//NUCL_SUBJCOMPL(is,important)

if ( ^NUCL[subjcompl](#1[lemme:be],#2[adj]) & REFLSENT[surfacerefl:~,continued:~](#3) )
    NUCL[subjcompl,surfacerefl=+](#1,#2).

// NUCL[subjcompl] is not surfacerefl if the sentence contains complex structures

if ( $1[complex](#1,#2) & REFLSENT[surfacerefl:~,continued:~](#3) &  ^NUCL[subjcompl,surfacerefl](#4,#5) )
    NUCL[subjcompl,surfacerefl=~](#4,#5).

if ( $1[complex](#1) & REFLSENT[surfacerefl:~,continued:~](#3) &  ^NUCL[subjcompl,surfacerefl](#4,#5) )
    NUCL[subjcompl,surfacerefl=~](#4,#5).

// REFLSENT containing NUCL[surfacerefl] is SURFACEREFL

if ( NUCL[subjcompl,surfacerefl](#1,#2) & ^REFLSENT[surfacerefl:~,continued:~](#3) )
    REFLSENT[surfacerefl=+,simpleval=+](#3).


// further complex structures

//I learnt how to equate petrol motor and electric motor specifications such that the two motors would have the same output as well as their respective pulley sizes needed .
//ORDER_TEMP_REL(learnt_INDICATOR:+,equate_INDICATOR:+)


if ( ^REFLSENT[surfacerefl](#1) & ORDER(#2[reflection],#3) )
    REFLSENT[surfacerefl=~,simpleval=~](#1)

///////////////////////////////////////////////////////////////
// SIMPLE EVALUATION/2
// "it is/was" is surfacerefl in not complex sentences
// SUBJ_PRE(was,It)
///////////////////////////////////////////////////////////////

//It is important for me to know what the company is looking for and change accordingly .

if ( ^SUBJ[pre](#1[lemme:be],#2[lemme:it]) & REFLSENT[surfacerefl:~,continued:~](#3) & ~$1[complex](#4,#5) & ~$2[complex](#6) )
    SUBJ[surfacerefl=+](#1,#2).

if ( SUBJ[pre,surfacerefl](#1[lemme:be],#2[lemme:it]) & ^REFLSENT[surfacerefl:~,continued:~](#3) )
    REFLSENT[surfacerefl=+,simpleval=+](#3).


///////////////////////////////////////////////////////////////
// SIMPLE SENTEENCE
// sentences without complex structure, contr words and COURSE
///////////////////////////////////////////////////////////////

if ( ^REFLSENT[surfacerefl:~,continued:~](#1) & ( $1[complex](#2,#3) | $2[complex](#4) ) )
    REFLSENT[proba=+](#1).

if ( ^REFLSENT[proba:~,surfacerefl:~,continued:~](#1) )
    REFLSENT[surfacerefl=+,proba=+,simplsent=+](#1).

| #1[contr] |
if ( ^REFLSENT[surfacerefl,proba](#2) )
    REFLSENT[surfacerefl=~,proba=~,simplsent=~](#2).

if ( ^REFLSENT[surfacerefl,proba,course](#2) )
    REFLSENT[surfacerefl=~,proba=~,simplsent=~](#2).

if (^REFLSENT[surfacerefl,simplsent](#3) & PRESENT(#1) & PAST(#2) )
   REFLSENT[surfacerefl=~,simplsent=~](#3)

if ( ^REFLSENT[proba](#1) )
    REFLSENT[proba=~](#1).

///////////////////////////////////////////////////////////////
// REDUCED PERSPECTIVE
//sentences that have no SUBJ that are not pronouns
// + no contr connector (KSWDEP)
///////////////////////////////////////////////////////////////

if ( ^REFLSENT[surfacerefl:~,course:~,continued:~,analytic:~](#3) & ~SUBJ[sentence:~](#1,#2[pron:~]) & ~KSWDEP(#4) & ~MAIN[ksw](#5) )
    REFLSENT[surfacerefl=+,reduced=+](#3).

// not surfacerefl if there are several tenses

if (^REFLSENT[surfacerefl,reduced](#3) & PRESENT(#1) & PAST(#2) )
   REFLSENT[surfacerefl=~,reduced=~](#3)

//I think they are fantastic opportunities , and if there is anything about the course that I think has changed me as a person , it would be the community volunteer placements .
//VASPECT_A_PERFECT(changed)
//PRESENT(think)

if (^REFLSENT[surfacerefl,reduced](#3) & PRESENT(#1) & NUCL[perfect](#2,#4) )
   REFLSENT[surfacerefl=~,reduced=~](#3).

// exception: there are at least two pronoun subjects, but they are different
//Also during some professional interaction I felt a bit behind when it came to describing something as compared to our team members.
//SUBJ_PRE(came,it)
//SUBJ(felt,I)

if ( SUBJ(?,#1[pron]) & SUBJ(?,#2[pron]) & ^REFLSENT[surfacerefl,reduced](#3) & #1 ~: #2 )
    REFLSENT[surfacerefl=~,reduced=~](#3).

///////////////////////

if ( ^REFLSENT[surfacerefl,complex](#1) )
   REFLSENT[surfacerefl=~,simplsent=~,simpleval=~,reduced=~](#1)


if ( ^REFLSENT[specificonly,complex:~,continued:~](#1) )
   REFLSENT[surfacerefl=+](#1).
 



////////////// eliminate 1label, specificonly, contextonly in not surfacerels sentences
/
if ( ^REFLSENT[surfacerefl:~](#1) )
   REFLSENT[1label=~,specificonly=~,contextonly=~](#1).
\

if ( ^REFLSENT[complex:~,analytic:~,continued:~](#1) )
   REFLSENT[surfacerefl=+](#1)

if ( ^REFLSENT[surfacerefl,simplsent:~,simpleval:~,reduced:~,continued:~](#1) )
   REFLSENT[vague=+](#1).


//if SENT with REFLDEP -> vague
//However , I have learnt a lesson from the deficiency of this practice presentation and committed to contribute further .

if ( ^SENT[continued:~](#1) & $1[refldep](?,?) & ~$2[complex](?,?) )
   REFLSENT[surfacerefl=+,vague=+](#1).

if ( REFLSENT[vague](#1) & $1[refldep](#2,#3) & ~INDICATOR(#2) & ~INDICATOR(#3) )
   INDICATOR(#2), INDICATOR(#3).


///////////////////// BLUE sentences \\\\\\\\\\\\\\\\\\\

//course-specific

| #2[course] |
if ( ^REFLSENT(#1) )
    REFLSENT[course=+](#1).





/////////////////////////////////////////////////////////////////////////////
///////////////////////  MARK TRIGGER WORDS \\\\\\\\\\\\\\\\\\\\\\\\\\\\\
/////////////////////////////////////////////////////////////////////////////

/
if ( INDICATOR(#1[authrefl]) & $1[authdep](#1,#2) )
    INDICATOR(#2).

if ( INDICATOR(#2[authrefl]) & $1[authdep](#1,#2) )
    INDICATOR(#1).
\



if ( INDICATOR(?) & $1[authrefl](#1,#2) )
    INDICATOR(#1), INDICATOR(#2).

if ( INDICATOR(?) & $1[refldep](#1,#2) )
    INDICATOR(#1), INDICATOR(#2).

if ( ^INDICATOR(#1) & INDICATOR(#1) )
   ~.

| #1[indicator=+] |
if ( INDICATOR(#1) )
    ~.

| #1[contextindicator=+] |
if ( $1[contextdep](#1) )
   ~.
   
| #1[contextindicator=+] |
if ( $1[contextdep](#1,#2) )
   ~.
   
| #2[contextindicator=+] |
if ( $1[contextdep](#1,#2) )
   ~.
   
| #1[specificindicator=+] |
if ( $1[specificdep](#1,#2) )
   ~.
   
| #2[specificindicator=+] |
if ( $1[specificdep](#1,#2) )
   ~.
   
| #1[shiftindicator=+] |
if ( $1[shiftdep](#1,#2) )
   ~.
   
| #2[shiftindicator=+] |
if ( $1[shiftdep](#1,#2) )
   ~.
   

///////////////////////// CONTEXTS \\\\\\\\\\\\\\\\\\\


| pp#1{?[contextindicator],?+} |
if ( REFLSENT(#2) & ~CONTEXT(#1) )
   CONTEXT(#1).

| pp#1[contextindicator] |
if ( REFLSENT(#2) & ~CONTEXT(#1) )
   CONTEXT(#1).

if ( REFLSENT(#2) & ~CONTEXT(#1) & TIMEX3[contextdep](#1))
   CONTEXT(#1).

//exceptions

//From that time I felt really passionate about electrical engineering.
//TIMEX3_CONTEXTDEP_GRREFL(From that time)

| #1{?*, ?{?[form:fthat], ?*}} |
if ( ^CONTEXT(#1) )
   ~.

| #1{?*, ?{?[form:fthis], ?*}} |
if ( ^CONTEXT(#1) )
   ~.

//at times
//CONTEXT(at times)
//PP{at NP{times}}

| pp#1{#2[lemme:at], np{#3[first,last,pl]}} |
if ( ^CONTEXT(#1) )
    ~.

//////////// CONTEXTWORD \\\\\\\\\\\\\\\\\

//PP{during NP{some professional interaction}} 
//CONTEXT(during some professional interaction)


| #1{?+, ?{?[contextword=+], ?[contextword=+], ?[contextword=+] }} |
if ( CONTEXT(#1) )
    ~.

| #1{?+, ?{?[contextword=+], ?[contextword=+] }} |
if ( CONTEXT(#1) )
    ~.

| #1{?+, ?{?[contextword=+] }} |
if ( CONTEXT(#1) )
    ~.

/////////// SURFACE + SPECIFIC  \\\\\\\\\\\\\

if ( ^REFLSENT[surfacerefl,specific](#1) )
    REFLSENT[specific=~](#1).


