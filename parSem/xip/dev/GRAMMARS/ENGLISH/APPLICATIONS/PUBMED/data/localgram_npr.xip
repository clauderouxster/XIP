//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2003 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : English XIP grammar
//!   filename   : localgram_npr.xip
//!   Date       : 10/02/2003
//!   Author     : Caroline Brun
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!



Sequence:


//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!



// Quoted relation for elements between quotes, which can be on interest ???
// for example "Andy Warhol" is a Song, some context may help later 
// to determine "real"  tags ...


//1> noun[quoted:+,proper:+] @=    |?[guille:~]| ?[guille:+,proper:+];?[guille:+,punct:+],  ?*[guille], ?[guille:+,proper:+];?[guille:+,punct:+] |?[guille:~]| .

//1> noun[quoted:+,proper:+] @=    ?[guille:+,proper:+,start];?[guille:+,punct:+], ?*[guille], ?[guille:+,proper:+];?[guille:+,punct:+] |?[guille:~]| .

//1> noun[quoted:+,maj:+] @=    |?[guille:~]| ?[guille:+,maj:+];?[guille:+,punct:+],  ?*[guille], ?[guille:+,maj:+];?[guille:+,punct:+] |?[guille:~]| .

//1> noun[quoted:+,maj:+] @=    ?[guille:+,maj:+,start];?[guille:+,punct:+], ?*[guille], ?[guille:+,maj:+];?[guille:+,punct:+] |?[guille:~]| .

1> noun[quoted:+,proper:+] @=    |?[guille:~]| ?[guille:+,proper:+] |?[guille:~]| .

1> noun[quoted:+,maj:+] @=    |?[guille:~]| ?[guille:+,maj:+] |?[guille:~]| .

// Non-Ambiguous person names 
// not so sure :

1> noun[proper:+,mwe:+,maj:+] @= adv[lemme:"al."];noun[lemme:"al",particlename], punct[form:fhyph,left,right], noun[maj].


2> noun[proper:+,person:+,mwe:+] @= noun+[firstname:+], (dig[rom];pron[maj,lemme:I]),  noun+[particlename];det[lemme:an;maj], (punct[form:fhyph,left,right]), noun[proper,particlename:~,orghead:~,orgend:~,locpost:~,locpre:~];noun[maj,particlename:~,orghead:~,orgend:~,locpost:~,locpre:~,eventmod:~].

//WARNING
2> noun[proper:+,person:+,mwe:+] @= noun+[firstname:+], nadj[maj,guess].

// DANGEROUS RULE :

//2> noun[proper:+,person:+,mwe:+] @=  |?[guess:~,proper:~,punct:~,prep:~]|  noun[proper,place:~],  noun+[particlename];det[lemme:an;maj], (punct[form:fhyph,left,right]), noun[proper,particlename:~,orghead:~,orgend:~,locpost:~,locpre:~];noun[maj,particlename:~,orghead:~,orgend:~,locpost:~,locpre:~].

2> noun[proper:+,person:+,mwe:+] @=   noun[proper,place:~,orghead:~,orgend:~],  noun+[particlename];det[lemme:an;maj], (punct[form:fhyph,left,right]), noun[proper,particlename:~,orghead:~,orgend:~,locpost:~,locpre:~];noun[maj,particlename:~,orghead:~,orgend:~,locpost:~,locpre:~].

2> noun[proper:+,person:+,mwe:+] @= |?[guess:~,proper:~]| noun+[particlename];det[lemme:an;maj], (punct[form:fhyph,left,right]), noun[proper,guess:~,particlename:~,orghead:~,orgend:~,orgmod:~,locpre:~,locpost:~];noun[maj,particlename:~,orghead:~,orgend:~,orgmod:~,locpre:~,locpost:~].


// WITH FIRSTNAME


2>noun[person:+,mwe:+] @= noun+[firstname,pl:~,quoted:~,place:~], (noun[maj,initial];adj[maj];nadj[maj]), noun*[locpost:~,particlename:~,proper,orghead:~,orgend:~,orgmod:~,locpre:~,onlyplace:~,eventmod:~,cr2:~,cr:~],  noun[locpost:~,particlename:~,proper,orghead:~,orgend:~,orgmod:~,locpre:~,onlyplace:~,eventmod:~,country:~,city:~,cr:~,cr2:~] .

2> noun[proper:+,person:+,mwe:+] @= noun+[firstname:+,place:~], (dig[rom];pron[maj,lemme:I]),  prep[form:fof] , noun[place].

2> noun[proper:+,person:+,mwe:+] @= noun+[firstname:+,place:~], dig[rom];pron[maj,lemme:I], (noun[proper,particlename:~,orghead:~,orgend:~,orgmod:~,locpre:~,locpost:~]).

2> noun[proper:+,person:+,mwe:+] @= noun[guess,initial], noun*[proper,orgend:~,orgmod:~,orghead:~,cr:~,cr2:~],  noun[proper,orgend:~,orgmod:~,orghead:~,cr:~,cr2:~].
 
//WARNING 
2> noun[proper:+,person:+,mwe:+] @=  noun+[firstname:+,place:~,ambig:~], (noun[maj,initial];noun[maj,let]) ,  noun[maj,toutmaj:~,orghead:~,orgend:~,orgmod:~,locpre:~,locpost:~,eventmod:~].

2> noun[proper:+,person:+,mwe:+] @= noun+[firstname:+,place:~], noun[proper,toutmaj:~,orghead:~,orgend:~,orgmod:~,locpre:~,locpost:~,eventmod:~].

2> noun[proper:+,person:+,mwe:+] @=  noun+[firstname:+,place:~], noun[maj,initial].

2> noun[proper:+,person:+,mwe:+] @=  noun+[firstname:+], dig[rom];pron[maj,lemme:I].

//Henry of Guise
2> noun[proper:+,person:+,mwe:+] @= noun+[firstname:+,place:~],  prep[form:fof] , noun[maj,orghead:~,orgend:~,orgmod:~].

2>noun[person:+,mwe:+] @= noun+[initial,maj], noun[particlename:~,maj,guess];noun[particlename:~,maj,proper,locpost:~,orghead:~,locpre:~,orgend:~,orgmod:~] , noun[particlename:~,maj,guess];noun[particlename:~,maj,proper,locpost:~,orghead:~,locpre:~,orgend:~,orgmod:~].

2>noun[person:+,mwe:+] @= noun+[initial,maj] , noun[cr:~,cr2:~,particlename:~,maj,guess];noun[cr:~,cr2:~,particlename:~,maj,proper,locpost:~,orghead:~,locpre:~,orgend:~,orgmod:~].

// to prevent ... Commonwealth of the Virginia Home page : PERSON(Virginia Home Page)
2>noun[person:+,mwe:+] @= | ?[orgcountry:~], ? | noun+[firstname,pl:~,quoted:~,place,toutmaj:~], (noun[maj,initial,orghead:~,toutmaj:~]), noun+[locpost:~,particlename:~,proper,orghead:~,orgend:~,orgmod:~,locpre:~,toutmaj:~] .

2>noun[person:+,mwe:+] @= noun+[firstname,pl:~,quoted:~,place,toutmaj:~], (noun[maj,initial]), noun*[locpost:~,particlename:~,proper,orghead:~,orgend:~,orgmod:~,locpre:~,place:~,toutmaj:~],  noun[locpost:~,particlename:~,proper,orghead:~,orgend:~,orgmod:~,locpre:~,place:~,toutmaj:~].


//2> noun[person:+,mwe:+] @= noun+[firstname,pl:~,quoted:~,place:~], noun[quoted], noun+[orghead:~,orgend:~,orgmod:~,maj,locpost:~,particlename:~,locpre:~].

//For Georges pompidou and the pb of plural !
2>noun[person:+,mwe:+] @= noun+[firstname,pl,sg,quoted:~,place:~], (noun[firstname,pl,sg,place:~]), (noun[maj,initial]),   noun+[maj,locpost:~,particlename:~,orghead:~,orgend:~,orgmod:~,locpre:~].


// A test using professions and family links

2> noun[person:+,mwe:+] @= | noun[prof:+,maj:~] |  noun[proper,toutmaj:~] , noun[proper,toutmaj:~,celeb:~,orghead:~,orgend:~,orgmod:~,maj,locpost:~,particlename:~,locpre:~] .


2> noun[person:+,mwe:+] @= | noun[famlink:+,maj:~] |  noun[proper,toutmaj:~] , noun[proper,toutmaj:~,celeb:~,orghead:~,orgend:~,orgmod:~,maj,locpost:~,particlename:~,locpre:~] .

2> noun[person:+,mwe:+,famlink:+] @= noun[proper,toutmaj:~] , noun[proper,toutmaj:~,celeb:~] | noun[famlink:+] |.




// Locations

// Adresses 
// 111, Wisconsin Avenue NE , Washington DC  10001

2> noun[mwe:+,place:+,address:+] @= dig,  (punct[comma:+]),  noun+[proper,address:~], (dig[ord]), noun[locpost:+,address:+], (?[lemme:NE];?[lemme:SE];?[lemme:NW];?[lemme:SW]), (punct[comma:+]), noun[city:+], punct[comma:+], noun[usastate:+], (dig)  .

2> noun[mwe:+,place:+,address:+] @= dig,  (punct[comma:+]),  noun+[proper,address:~], (dig[ord]), noun[locpost:+,address:+], (?[lemme:NE];?[lemme:SE];?[lemme:NW];?[lemme:SW]), (punct[comma:+]), noun[city:+], (dig)  .

2> noun[mwe:+,place:+,address:+] @=   noun+[proper,address:~], (dig[ord]), noun[locpost:+,address:+], (?[lemme:NE];?[lemme:SE];?[lemme:NW];?[lemme:SW]), (punct[comma:+]), noun[city:+], punct[comma:+], noun[usastate:+], (dig)  .

2> noun[mwe:+,place:+,address:+] @=   noun+[proper,address:~], (dig[ord]), noun[locpost:+,address:+], (?[lemme:NE];?[lemme:SE];?[lemme:NW];?[lemme:SW]), (punct[comma:+]), noun[city:+], (dig).

2> noun[mwe:+,place:+,address:+] @= dig,  (punct[comma:+]),  noun+[proper,address:~], noun[locpost:+,address:+].

// The Russian village of Barenstburg

//2>noun[proper:+,place:+,mwe:+]@= |det[def], (?[adj:+,country:~]) | (adj[country];nadj[country]), noun[locpost], prep[form:fof],  noun[place:+];noun[guess:+,propag=+].

2>noun[proper:+,place:+,mwe:+]@=  (adj[country];nadj[country]), noun[locpost], prep[form:fof],  noun[place:+];noun[guess:+,propag=+].

//2>noun[proper:+,place:+,mwe:+] @= |det[def], (?[adj:+,country:~]) | (adj[country];nadj[country]) , noun[lemme:city];noun[lemme:town];noun[lemme:island];noun[lemme:village],  prep[form:fof] , noun[place:+];noun[guess,propag=+].
2>noun[proper:+,place:+,mwe:+] @= (adj[country];nadj[country]) , noun[lemme:city];noun[lemme:town];noun[lemme:island];noun[lemme:village],  prep[form:fof] , noun[place:+];noun[guess,propag=+].

// Federal Republic of Germany

//German Republic

2>  noun[proper:+,place:+,mwe:+,country:+]  @= nadj[country];adj[country], (nadj[maj];adj[maj]), noun[orgcountry].

2> noun[proper:+,country:+,place:+,mwe:+]  @= (nadj[maj,guess:~];adj[maj,guess:~]),  noun[orgcountry], prep[form:fof], noun[proper,country].

2> noun[proper:+,place:+,mwe:+]  @=  (nadj[maj,guess:~];adj[maj,guess:~]),  noun[orgcountry], prep[form:fof], noun[proper,place].

// Some modifiers like Greater London

2> noun [proper:+,city:+,place:+,mwe:+] @= adj[maj], noun[city].
                   
//Lower Saxony

2> noun[proper:+,place:+,mwe:+,features:!] @= ?[lemme:Lower];?[lemme:lower];?[lemme:upper];?[lemme:middle], noun[place:+,!features:!].

// Southern Africa, South of Andalusia
2> noun[proper:+,place:+,features:!,mwe:+] @=  ?[modloc:+],  noun[place:+,!features:!].

2> noun[proper:+,place:+,features:!,mwe:+] @=  ?[modloc:+], prep[form:fof], noun[place:+,!features:!].

3> noun[proper:+,place:+,city:+] @= noun[city:+], (punct[comma:+]), noun[usastate:+,city:~].

//other cases : (?)
3> noun[proper:+,mwe:+,place:+,features:!] @= noun[place:+,!features:!,usastate:~,city:~,country:~], punct[comma:+], noun[usastate:+].

// Canari Islands; Grimsburry Castle

3> noun[place:+,mwe:+] @=  adj[maj] , noun[proper,firstname:~], noun[locpost:+].

3> noun[place:+,mwe:+] @=  nadj[maj] , noun[proper,firstname:~], noun[locpost:+].

3> noun[place:+,mwe:+] @=  adj[maj] , noun[maj,firstname:~], noun[locpost:+].

3> noun[place:+,mwe:+] @= nadj[maj] , noun[maj,firstname:~], noun[locpost:+].

3> noun[place:+,mwe:+] @=   noun[proper,firstname:~], noun[locpost:+].

3> noun[place:+,mwe:+] @=   noun[maj,firstname:~], noun[locpost:+].

3> noun[place:+,mwe:+] @=   nadj[maj] , noun[locpost:+].

// Victoria Land ????
3> noun[place:+,mwe:+] @=   noun[maj,firstname,ambig], noun[locpost:+].


// Iberian Peninsula 
3> noun[place:+]  @= (nadj[modloc]), nadj[country:+];adj[country:+], noun[locpost:+].

//Hui Sui Road : Several (2 for the moment ?) nouns/adj allowed when they are NOT places : previous rule for places



// Camp David ; Rio Grande
3>  noun[place:+] @=  noun[locpre:+];noun[lemme:River], noun[proper,orghead:~,orgend:~,orgmod:~] .

// Territory of the US.
3> noun[proper:+,place:+,mwe:+] @= noun[locpost:+], prep[form:fof], (det[def]),  noun[place];noun[place].

// TEST

4> noun[proper:+,place:+,mwe:+]  @= | prep[form:fbetween], (det[def]), noun[proper,place],  conj[coord], (det[def]) |  noun[maj,org:~].

4> noun[proper:+,place:+,mwe:+]  @= | prep[form:fbetween], (det[def])  | noun*[maj], noun[maj,org:~] | conj[coord], (det[def]), noun[proper,place] |.

4> noun[proper:+,place:+,mwe:+]  @= noun[city] , punct[comma:+],  noun[country].


//London's Victoria Station 


//5> noun[place:+,mwe:+] @= noun[country];noun[city], part, noun[proper,let:~,pl:~,firstname:~];adj[maj];nadj[maj], (noun[proper,let:~,pl:~];adj[maj];nadj[maj]),(noun[proper,let:~,pl:~,firstname:~];adj[maj];nadj[maj]),   noun[locpost:+].
5>  noun[proper:+,place:+,mwe:+] @= noun[country];noun[city],  part, noun[place:+].




8>  noun[proper:+,title:+,person:+] @= noun[place],  ?[title:+].

9> noun[proper:+,person:+,mwe:+,title:+] @= (nadj[country]), (adj[maj];nadj[maj]), ?+[title:+,toutmaj:~];?+[hmm_tit:+,toutmaj:~],  noun[person].

// Context to avoid Mr James Molyneux, Mr Trimble  --> PERS(Mr James Molyneux, Mr)
9>  noun[proper:+,person:+,mwe:+,title:+] @= noun[person],  (punct[comma:+]), noun[abr,title];noun[abr,hmm_tit] |?[noun:+,maj:~];?[noun:~]| .

10>  noun[proper:+,person:+,mwe:+,title:+] @= (nadj[country]), (adj[maj]), ?[title:+,toutmaj:~];?[hmm_tit:+,toutmaj:~],  noun[person];noun[celeb].

10> noun[proper:+,person:+,mwe:+,title:+] @=  (nadj[country]), (adj[maj];nadj[maj]), ?+[toutmaj:~,title:+];?[hmm_tit:+], noun*[lawmod:~,locpost:~,particlename:~,proper,orghead:~,orgend:~,orgmod:~,locpre:~,place:~,org:~], (noun[maj]), noun[lawmod:~,proper,locpost:~,particlename:~,orghead:~,orgend:~,orgmod:~,locpre:~,org:~,ambig:~,countable:~,wordpart:~].


//In case the person has'nt be detected before ;

10>  noun[proper:+,person:+,mwe:+,title:+] @= (adj[maj]),  ?[title:+,toutmaj:~];?[hmm_tit:+,toutmaj:~], (noun*[maj,place:~]), noun[fam,maj,org:~,countable:~];noun[firstname,maj].

10>  noun[proper:+,person:+,mwe:+,title:+] @= (adj[maj]), ?[title:+];?[hmm_tit:+,toutmaj:~], prep[form:fof],  noun[place].

10>  noun[proper:+,person:+,mwe:+,title:+] @= (adj[maj]), ?[title:+];?[hmm_tit:+,toutmaj:~], prep[form:fof],  noun[maj].


10>  noun[proper:+,person:+,mwe:+] @= noun[proper,orgcountry:~,locpost:~,particlename:~,proper,orghead:~,orgend:~,orgmod:~,locpre:~,place:~], (noun[proper,orgcountry:~,locpost:~,particlename:~,proper,orghead:~,orgend:~,orgmod:~,locpre:~,place:~]) , noun[celeb].


// Mr. A. G. Smith

// Normally Street should be St: need to do it in desamb 
// could be a person or a location, like St Louis

//10> noun[proper:+,person:+,mwe:+] = (nadj[country]), (adj[maj];nadj[maj]), noun[title,toutmaj:~], noun[proper,locpost:~,particlename:~,proper,orghead:~,orgend:~,orgmod:~,locpre:~,place:~];noun[maj,locpost:~,particlename:~,proper,orghead:~,orgend:~,orgmod:~,locpre:~,place:~].

// St Patrick the Divine, Charles the Great

11> noun[proper:+,person:+,mwe:+] = noun[firstname], det[def], adj[maj];nadj[maj];noun[maj].

// Ambassador Abdallah Baali of Algeria

11> noun[proper:+,person:+,mwe:+,title:+] @= noun[proper:+,person:+,mwe:+,title:+],  prep[form:fof],  noun[place].

//12> noun[proper:+,person:+] @=  (adj[maj]), ?[title:+];?[hmm_tit:+], prep[form:fof],  noun[place].

//====================== Proper Nouns Grouping ========================================

IDrules:



// One or more proper nouns, followed by a comma, followed by a usastate:+
// eg Atlantic City, N.J.

//15> noun[proper:+,city:+] -> noun[proper:+], (noun[proper:+])*, punct[comma:+], noun[usastate:+].


// At least 2 proper nouns, maybe more at the end of a clause.
// CAUTION WITH THE FOLLOWING EXAMPLE "He told Mary John was sick
// or "He told Mary John Doe was sick
//30> noun[proper:+] -> noun[proper:+], noun+[proper:+] .


// A title followed by one or more proper nouns
//20> noun[proper:+] -> noun[title], noun[proper:+], (noun[proper:+])*.


// A city name a particle and a proper noun makes a proper noun
// e.g. London's Docklands Light Railways
//20> noun[proper:+] -> noun[proper:+,city:+], part, adj*, noun[proper] .



// A Proper noun followed by a digit is a proper noun
// eg. Skynet 4 CB ATTENTION A L'ORDRE !!!
//20> noun[proper:+] -> noun[proper:+,period:~,place:~,firstname:~], dig .


//CB : A test with tagging rules 
Tagging:

//when using context and in case of only one name to avoid a double noun tree

21> noun[maj:+,firstname:~,fam:~,person:~,guess:+]  %= |noun[lemme:town];noun[lemme:city], prep[form:fof] |  noun[maj:+,city:+,place:+,propag:+] .

21> noun[proper:+,firstname:~,fam:~,person:~,guess:+] %= |noun[lemme:town];noun[lemme:city], prep[form:fof] |  noun[maj:+,city:+,place:+,propag:+] .

21>  noun[proper:+,guess:+] %= | noun[lemme:resident], prep[form:fof] |  noun[proper:+,place:+,propag:+] .

21>  noun[proper,toutmaj:~,celeb:~] %= | noun[prof:+,maj:~] | noun[person:+,mwe:+,propag:+].

21>  noun[proper,toutmaj:~,celeb:~] %= | noun[famlink:+,maj:~] | noun[person:+,mwe:+,propag:+].

21>  noun[proper,toutmaj:~,celeb:~] %= noun[person:+,mwe:+,propag:+]  | noun[famlink:+,maj:~] |.


21>  noun[proper,toutmaj:~,celeb:~] %= noun[person:+,mwe:+,propag:+]  |  (punct[comma:+]) , noun[lemme:"et_al."] |.
