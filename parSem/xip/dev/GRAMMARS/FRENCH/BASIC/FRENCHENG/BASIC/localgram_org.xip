//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Sequence:
/la radio d'Etat\
2> noun[mwe=+, preorg=+] = noun[preorg, !sg:!, !masc:!, !fem:!], prep[form:fde], noun[lemme:état, maj].

/la chaîne de télévision\
2> noun[mwe=+, preorg=+, fem=+] = noun[lemme:chaîne, !sg:!], prep[form:fde], noun[lemme:télévision];noun[lemme:TV].

/les Brigades Rouges\
2> noun[mwe=+, org=+, fem=+, pl=+] = noun[lemme:brigade, pl, !maj:!], ?[lemme:rouge, pl, CR:~].

/Comité de soutien à François Kolber\
2> noun[mwe=+, org=+, masc=+, sg=+] = noun[lemme:comité, sg, !maj:!], prep[form:fde, CR:~], noun[lemme:soutien, CR:~], (prep[form:fde], adj*, noun[CR:~], (adj)), prep[form:fa], noun+[maj, CR:~].

/les Forces armées\
2> noun[mwe=+, org1pl=+, orgadj=+, anton=+, maj=+] = noun[lemme:force, pl, maj], adj[lemme:armé, pl, fem];noun[lemme:armée, pl].

/le département d'Etat\
2> noun[mwe=+, org=+, masc=+, sg=+] = noun[lemme:département, sg, !maj:!], prep[form:fde, CR:~], noun[lemme:état, maj, CR:~].


Marker:
/marquage d'éléments qui peuvent faire partie de noms d'organisations\
4> noun[preorg, maj, toutmaj:~, org1=+].

4> noun[org2maj, maj, phrasemaj:~, org2=+].
4> noun[firme2maj, maj, phrasemaj:~, firme2=+].

4> noun[lemme:force, pl, maj, org1pl=+, anton=+, orgadj=+].

4> noun[lemme:fabricant, prefirme=~], (adj), (prep[form:fde], noun[maj:~], (adj)), noun[person].
4> noun[person], punct[form:fcm], (det), noun[lemme:fabricant, prefirme=~].

/comité de soutien\
4> noun[lemme:comité, org1=~], prep[form:fde], noun[lemme:soutien].

/Générale\
4> noun[lemme:général, maj, sg, fem, firme1=+].

/Assurances\
4> noun[lemme:assurance, maj, pl, firme2=+].

/Le Boeing\
4> det, noun[nondef, firme=~, org=~, CR:~].
4> prep[def], noun[nondef, firme=~, org=~, CR:~].

/un Zodiac\
4> det[indef], noun[firme:+, firme=~, org=~, CR:~].

/une 205 Peugeot\
4> det[indef], num[CR:~], noun[firme=~, org=~, CR:~].

/le Sénat\
4> ?[def], noun[majorg, maj, org=+].

/du Monde\
4> ?[def], noun[orgdef, maj, org=+].

/Solidarité\
4> ?[def:~], noun[lemme:solidarité, maj, org=+], ?[adj:~, maj:~].


/le président de Baudoin\
//5> noun[preorgde, persorg:~], prep[form:fde], (det[CR:~]), (adj[maj:~, CR:~]), noun[maj, phrasemaj:~, CR:~, org=+], ?[adj:~, maj:~, ang:~].
5> noun[preorgde], prep[form:fde], (det[CR:~]), (adj[maj:~]), noun[maj, phrasemaj:~, CR:~, nonorg:~, person:~, org1:~, lieu:~, firme1pl:~, org=+], ?[adj:~, maj:~, ang:~].
5> noun[preorgde], prep[form:fde], (det[CR:~]), (adj[maj:~]), noun[maj, phrasemaj:~, CR:~, person:~, org1:~, firme1pl:~, org=~], prep[form:fde, CR:~], ?[maj, CR:~].

/le Parti fédéraliste flamand Volksunie\
5> noun[orgadj, maj:~], (adj)+, noun[maj, phrasemaj:~, person:~, lieu:~, org=+, nonorg2=+].

/le parti Socialiste juif Bund\
5> noun[parti, nonorg=+], (adj)*, (adj[maj];adj[postprof];adj[nat])+, (adj)*, noun[maj, phrasemaj:~, person:~, lieu:~, org=+, nonorg2=+], ?[maj:~];?[person].
5> noun[parti, nonorg=+], (adj)*, (adj[maj];adj[postprof];adj[nat])+, (adj)*, noun[maj, phrasemaj:~, person:~, lieu:~], ?[maj, person:~].


/le PDG de Baudoin ; une filiale britannique de Baudoin\
5> noun[prefirmede], (adj[CR:~]), prep[form:fde], (det[def], (adj[maj:~])), noun[maj, phrasemaj:~, CR:~, preorg:~, prefirme:~, batimentorg1:~, batimentorg:~, org:~, nonorg:~, firme=+].

/l'association X\
3> ?[def], (adj[maj:~]), noun[preorgpers, preorg=+, sansde=+], (adj[nat, CR:~]), noun[maj, phrasemaj:~, CR:~, person:~, lieu:~].
4> ?[def], (adj[maj:~]), noun[preorg, maj:~];noun[preorg, toutmaj], (adj[CR:~]), noun[maj, phrasemaj:~, CR:~, person:+, persorg=+], ?[maj:~].
5> ?[def], (adj[maj:~]), noun[preorg, maj:~];noun[preorg, toutmaj], (adj[CR:~]), noun[maj, phrasemaj:~, CR:~, lieu:~, org=+], ?[maj:~, adj:~].
/l'association Clair de Terre\
6> ?[def], (adj[maj:~]), noun[preorg, maj:~];noun[preorg, toutmaj], (adj[CR:~]), noun[maj, phrasemaj:~, CR:~, persorg:~, org=~], prep[form:fde, CR:~], noun[maj, CR:~].

/le magazine britannique, Tatler\
5> ?[def], (adj[maj:~]), noun[preorg, maj:~];noun[preorg, toutmaj], (adj[nat, CR:~]), punct[form:fcm], (punct[form:fquotes, CR:~]), noun[maj, phrasemaj:~, CR:~, persorg:~, person:~, org=+], (punct[form:fquotes, CR:~]), punct.


/les associations X, Y et Z\
5> ?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), noun[maj, phrasemaj:~, CR:~, org=+], punct[form:fcm]; coord.

6> ?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~, CR:~], (noun[maj, CR:~])*, (adj[CR:~])*, punct[form:fcm], noun[maj, phrasemaj:~, org=+, CR:~], punct[form:fcm];coord.

7> ?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~, CR:~], (noun[maj, CR:~])*, (adj[CR:~])*, punct[form:fcm], noun[maj, CR:~, org=+], punct[form:fcm];coord.

8> ?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~, CR:~], (noun[maj, CR:~]), (punct[form:fcm], (det), noun[maj, phrasemaj:~, CR:~, org=+], (noun[maj, CR:~]))*, coord, noun[maj, phrasemaj:~, CR:~, org=+], ?[maj:~].


/le président de leur compagnie Pierre Eelsen \
5> noun[preorgde, prof], prep[form:fde], ?[def];det[poss], (adj[maj:~]), noun[prefirme, sg], (adj), (prep[form:fde], noun[maj:~]), (adj[nat]), (prep[form:fde], noun[maj:~]), noun[maj, phrasemaj:~, CR:~, person, persorg=+], ?[maj:~].

/l'entreprise X\
5> ?[def], (adj[maj:~]), noun[prefirme, sg], (adj), (prep[form:fde], noun[maj:~]), (adj[nat]), (prep[form:fde], noun[maj:~]), noun[maj, phrasemaj:~, CR:~, org:~, persorg:~, firme=+, orgpers=+, orglieu=+,nat:~], ?[maj:~].
5> det[poss], (adj[maj:~]), noun[prefirme, sg], (adj), (prep[form:fde], noun[maj:~]), (adj[nat]), (prep[form:fde], noun[maj:~]), noun[maj, phrasemaj:~, CR:~, org:~, persorg:~, firme=+, orgpers=+, orglieu=+], ?[maj:~].
6> ?[def], (adj[maj:~]), noun[prefirme, sg], (adj), (prep[form:fde], noun[maj:~]), (adj[nat]), (prep[form:fde], noun[maj:~]), noun[maj, phrasemaj:~, CR:~, org:~, persorg:~, firme=~, orgpers=~, orglieu=~], coord;?[ang], ?[maj, CR:~].

/un représentant de la banque, David Paulombi.\
4> noun[prof, sg], prep[form:fde], det[def], (adj[maj:~]), noun[prefirme, maj:~], (adj[nat, CR:~]);adj[CR:~], punct[form:fcm], (punct[form:fquotes, CR:~]), noun[maj, phrasemaj:~, CR:~, person, persorg=+], (punct[form:fquotes, CR:~]), punct.
4> noun[prof, sg], prep[form:fde, def], (adj[maj:~]), noun[prefirme, maj:~], (adj[nat, CR:~]);adj[CR:~], punct[form:fcm], (punct[form:fquotes, CR:~]), noun[maj, phrasemaj:~, CR:~, person, persorg=+], (punct[form:fquotes, CR:~]), punct.

/le fabricant britannique, Tatler\
5> ?[def], (adj[maj:~]), noun[prefirme, sansvirg:~, maj:~], (adj[nat, CR:~]);adj[CR:~], punct[form:fcm], (punct[form:fquotes, CR:~]), noun[maj, phrasemaj:~, CR:~, persorg:~, firme=+], (punct[form:fquotes, CR:~]), punct.


/les compagnies X, Y et Z\
//5> ?[def, pl], (adj[maj:~]), noun[prefirme, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~, CR:~, org:~, firme=+], ((det), noun[maj, CR:~]), (punct[form:fcm], (det), noun[maj, phrasemaj:~, CR:~, org:~, firme=+], (noun[maj, CR:~]))+, coord, (det), noun[maj, phrasemaj:~, CR:~, org:~, firme=+].

5> ?[def, pl], (adj[maj:~]), noun[prefirme, pl], (adj), (adj[nat]), noun[maj, phrasemaj:~, CR:~, org:~, firme=+], punct[form:fcm]; coord.

6> ?[def, pl], (adj[maj:~]), noun[prefirme, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~, CR:~], (noun[maj, CR:~])*, (adj[CR:~])*, punct[form:fcm], noun[maj, phrasemaj:~, org:~, firme=+, CR:~], punct[form:fcm];coord.

7> ?[def, pl], (adj[maj:~]), noun[prefirme, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~, CR:~], (noun[maj, CR:~])*, (adj[CR:~])*, punct[form:fcm], noun[maj, CR:~, org:~, firme=+], punct[form:fcm];coord.

8> ?[def, pl], (adj[maj:~]), noun[prefirme, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~, CR:~], (noun[maj, CR:~]), (punct[form:fcm], (det), noun[maj, phrasemaj:~, CR:~, org:~, firme=+], (noun[maj, CR:~]))*, coord, noun[maj, phrasemaj:~, CR:~, firme=+], ?[maj:~].



/les actions Darty\
5> noun[prefirmepl, pl], (adj[CR:~]),  noun[maj, phrasemaj:~, CR:~, firme=+].
5> prep[form:fpar], noun[prefirmeindef, sg], (adj[CR:~]),  noun[maj, phrasemaj:~, CR:~, firme=+].
5> det[indef], noun[prefirmeindef, sg], (adj[CR:~]),  noun[maj, phrasemaj:~, CR:~, firme=+].



Sequence:
/le Monde (de l'Education): déjà fait quand guillemets dans localgram_entit.xip\
10> noun[org=+, mwe=+] @= det[def], (punct[form:fquotes]), noun[orgdef, maj], (?[lemonde]), (prep[form:fde], (det[def]), noun[lemonde]), (punct[form:fquotes]).
10> noun[org=+, firme=+, mwe=+] @= |?[def], (punct[form:fquotes])| noun[orgdef, maj], prep[form:fde], (det[def]), noun[lemonde].
10> noun[org=+, firme=+, mwe=+] @= |?[def], (punct[form:fquotes])| noun[orgdef, maj], ?[lemonde].

/l'Internationale socialiste, la IVe Internationale ouvrière\
10> noun[org=+, mwe=+, fem=+, sg=+] = ?[def], (num[CR:~];adj[ord, CR:~]), noun[maj, lemme:international, fem, sg], ?[lemme:ouvrier, fem, sg];?[lemme:socialiste, fem, sg].

/la Croix-Rouge Internationale\
10> noun[org=+, mwe=+, fem=+] = noun[lemme:"Croix-Rouge", !maj:!, !sg:!], ?[nat];?[lemme:international].

/la Communauté européenne\
10> noun[org=+, mwe=+, fem=+] = noun[lemme:"communauté", !maj:!, !sg:!], ?[lemme:européen, fem] |?[form:~fde]|.

/le Jihad islamique plaestinien\
10> noun[org=+, mwe=+, fem=+, sg=+] @= det[fem, sg], noun[org, !maj:!, fem, sg], (adj[nat, fem, sg, CR:~];adj[postprof, fem, sg, CR:~])+.
10> noun[org=+, mwe=+, masc=+, sg=+] @= det[masc, sg], noun[org, !maj:!, masc, sg], (adj[nat, masc, sg, CR:~];adj[postprof, masc, sg, CR:~])+.


/l'Eymet Cricket Club\
//10> noun[org=+, maj=+, mwe=+] = |?[def], (adj[maj:~])| (?[maj, phrasemaj:~, CR:~])+, noun[org2, CR:~], (prep[form:fde];?[of], (noun[maj, CR:~])+, (?[ang]), noun[maj, CR:~]).
//10> noun[org=+, maj=+, mwe=+] = ?[maj, phrasemaj:~, start, CR:~], (?[maj, phrasemaj:~, CR:~])*, noun[org2, CR:~], (prep[form:fde];?[of], (noun[maj, CR:~])+, (?[ang]), noun[maj, CR:~]).
10> noun[org=+, maj=+, mwe=+] = ?[maj, phrasemaj:~, CR:~], (?[maj, phrasemaj:~, CR:~])*, noun[org2, CR:~], (prep[form:fde];?[of], (noun[maj, CR:~])+, (?[ang]), noun[maj, CR:~]).


/la National Westminster Bank\
10> noun[firme=+, maj=+] @= noun[maj, phrasemaj:~, start:~, CR:~];noun[toutmaj, phrasemaj:~, CR:~];noun[maj, phrasemaj:~, inconnu, CR:~] , (?[maj, phrasemaj:~, CR:~])*, (noun[firme2, CR:~];adj[firme2, CR:~])+.

/l'Institute of National Finance\
10> noun[org=+, maj=+, mwe=+] @= noun[org1, ang, CR:~, maj], (noun[maj, CR:~]), (?[ang]), (noun[maj, CR:~])+.


/Pacific FM\
10> noun[org=+, maj=+, mwe=+] @= (?[maj, phrasemaj:~,  CR:~])+, noun[org2, sansdef, CR:~].

/le Special Air Service\
10> noun[org=+, maj=+, mwe=+] = |?[def], (adj[maj:~])| (?[maj, phrasemaj:~, CR:~])+, noun[org2maj, maj, CR:~].

/le secrétaire de l'Union locale CGT\
10> noun[org=+, maj=+, mwe=+] @= |noun[preorgde], prep[form:fde, CR:~], (det[def, CR:~])| (adj), (noun[maj, phrasemaj:~, CR:~, org:~, org1:~, firme1pl:~, person:~, nonorg:~, lieu:~])+, (adj[CR:~])*, (?[ang, CR:~]), (noun[maj, CR:~, person:~])*, (?[ang]), (noun[maj, CR:~, person:~])*, (prep[CR:~], (det[CR:~]), (adj[CR:~]),  noun[maj, CR:~, person:~]), (adj[CR:~])*, (noun[maj, CR:~, person:~])*.
/le directeur de Paris VI \
10> noun[org=+, maj=+, mwe=+] @= |noun[preorgde], prep[form:fde, CR:~], (det[def, CR:~])| (adj), (noun[maj, phrasemaj:~, CR:~, ville])+, num[rom, CR:~].
/le secrétaire de France Plus\
10> noun[org=+, maj=+, mwe=+] @= |noun[preorgde], prep[form:fde, CR:~]| (adj), (noun[maj, phrasemaj:~, CR:~, org:~, org1:~, firme1pl:~, person:~, nonorg:~])+, (adj[CR:~])*, (?[ang, CR:~]), (noun[maj, CR:~])+, (?[ang]), (noun[maj, CR:~])*, (prep[CR:~], (det[CR:~]), (adj[CR:~]),  noun[maj, CR:~]), (adj[CR:~])*, (noun[maj, CR:~])*.

/Paris X \
10> noun[batimentorg=+, sg=+] @= noun[ville, maj, CR:~], (punct[form:fhyph]), num[rom, CR:~] |?[noun:~, adj:~]|.

/du Comité central\
//10> noun[org=+, maj=+, mwe=+] @= |noun[preorgde], prep[form:fde, CR:~], (det[def, CR:~])| noun[maj, phrasemaj:~, CR:~], adj[CR:~], (prep[CR:~], (det[CR:~]), (adj[CR:~]),  noun[maj, CR:~]).

/le responsable de la Nouvelle action royaliste\
10> noun[org=+, maj=+, mwe=+] @= |noun[preorgde], prep[form:fde, CR:~], (det[def, CR:~])| adj[maj, phrasemaj:~, CR:~], (noun[CR:~])+, (adj[CR:~])*, (prep[CR:~], (det[CR:~]), (adj[CR:~]),  noun[maj, CR:~]).

/le PDG de X Y\
10> noun[firme=+, maj=+, mwe=+] @= |noun[prefirmede], prep[form:fde], (det[def])| noun[maj, phrasemaj:~, CR:~], noun[maj, CR:~, person:~], (noun[maj, CR:~, person:~])*.

/filiale commune de Ford et de V W\
40> noun[firme=+, maj=+, mwe=+] @= |noun[prefirmede], (adj[CR:~]), prep[form:fde], (det[def], (adj[maj:~])), noun[maj, phrasemaj:~, CR:~, firme:+], coord[CR:~], prep[form:fde, CR:~]| (det[CR:~]), (adj)*,  (noun[maj, CR:~, preorg:~, prefirme:~, batimentorg1:~])+.


Sequence:
/***** construction directe avec un nom *****\

/les éditions Londreys\
10> noun[firme=+, maj=+, mwe=+] @= noun[firme1pl, pl, !maj:!, !masc:!, !fem:!], (prep[form:fde, CR:~]), (det[def, CR:~]), (adj[CR:~])*, (noun[maj, person:~, CR:~])+, (adj[CR:~])*.

/sa société, la Cefaldi,\
10> noun[firme=+, maj=+, mwe=+] @= |det[poss];det[indef], (adj[maj:~]), noun[prefirme, maj:~], (adj[nat]), punct[form:fcm]| (det[CR:~]), noun[maj, phrasemaj:~, CR:~, org:~, firme:~, person:~], (noun[maj, CR:~])*.


/******* construction en 'de' *****\
/l'Union de défense des commerçants et artisans\
20> noun[org=+, maj=+, sg=+, mwe=+] @= |?[def];punct| noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (noun[maj, CR:~, person:~])*, (adj[sg, CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~, sg]);(coord[CR:~], noun[postprof]),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, CR:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof, CR:~]))*,
	prep[form:fde, pl, CR:~], (adj[CR:~];noun[postprof])*, (noun[person:~, CR:~])+, coord[CR:~], noun[CR:~], (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof, CR:~]),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, CR:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]))*.

/la même en début de phrase\
20> noun[org=+, maj=+, sg=+, mwe=+] @= noun[org1, start, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (noun[maj, CR:~, person:~])*, (adj[sg, CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~, sg]);(coord[CR:~], noun[postprof]),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, CR:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof, CR:~]))+,
	(coord[CR:~], (prep[form:fde, CR:~], (det[def, CR:~])), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), noun+[person:~, prof:~, CR:~], (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof])),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, CR:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]))*.

/règle-test\
/20> noun[org=+, maj=+, sg=+, mwe=+] @= |?[def];punct| noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (noun[maj, CR:~, person:~])*, (adj[sg, CR:~];noun[postprof])*,
	(prep[form:fde, CR:~], (noun[person:~, CR:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof, CR:~])).
\

/l'Association des Aveugles de France\
20> noun[org=+, maj=+, sg=+, mwe=+] @= |?[def];punct| noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (noun[maj, CR:~, person:~])*, (adj[sg, CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~, sg]);(coord[CR:~], noun[postprof]),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, CR:~, date:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof, CR:~]))+,
	(coord[CR:~], (prep[form:fde, CR:~], (det[def, CR:~])), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), noun+[person:~, prof:~, CR:~, date:~], (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof])),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, CR:~, date:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]))*.
/la même en début de phrase\
20> noun[org=+, maj=+, sg=+, mwe=+] @= noun[org1, start, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (noun[maj, CR:~, person:~])*, (adj[sg, CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~, sg]);(coord[CR:~], noun[postprof]),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, CR:~, date:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof, CR:~]))+,
	(coord[CR:~], (prep[form:fde, CR:~], (det[def, CR:~])), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), noun+[person:~, date:~, prof:~, CR:~], (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof])),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, date:~, CR:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]))*.

/les Forces de défense israéliennes\
20> noun[org=+, maj=+, pl=+, mwe=+] @= |?[def];punct| noun[org1pl, sansde:~, maj, phrasemaj:~, pl, !masc:!, !fem:!], (noun[maj, CR:~, person:~])*, (adj[pl, CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~, pl]);(coord[CR:~], noun[postprof]),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, CR:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof, CR:~]))+,
	(coord[CR:~], (prep[form:fde, CR:~], (det[def, CR:~])), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), noun+[person:~, prof:~, CR:~], (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof])),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, CR:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]))*.


/la "Fédération française d'athlétisme"\	
20> noun[org=+, maj=+, sg=+, mwe=+] @= |?[def], punct[form:fquotes, CR:~]| noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!, CR:~], (adj[sg, CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~, sg];noun[postprof]),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~];noun[postprof]), (noun[person:~, CR:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~];noun[postprof]))+,
	(coord[CR:~], (prep[form:fde, CR:~], (det[def, CR:~])), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~];noun[postprof]), noun+[person:~, prof:~, CR:~], (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~];noun[postprof])),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~];noun[postprof]), (noun[person:~, CR:~])+, (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~];noun[postprof]))* |punct[form:fquotes, CR:~]|.

/un Office des Cartels pointilleux\
20> noun[org=+, maj=+, sg=+, mwe=+] @= noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (adj[sg, CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~, sg]);(coord[CR:~], noun[postprof]),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, CR:~])+, (adj[CR:~];noun[postprof]), (adj[CR:~];noun[prof:~, CR:~]))+,
	(coord[CR:~], (prep[form:fde, CR:~], (det[def, CR:~])), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), noun+[person:~, prof:~, CR:~], (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof])),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, CR:~])+, (adj[nat, CR:~];adj[postprof, CR:~];noun[postprof])*, (coord, adj[nat, CR:~]);(coord[CR:~], adj[postprof, CR:~]);(coord[CR:~], noun[prof:~, CR:~]))+.

/
20> noun[org=+, maj=+, sg=+, mwe=+] @= noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (adj[sg, CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~, sg];noun[postprof]),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~];noun[postprof]), (noun[person:~, CR:~])+, (adj[CR:~];noun[postprof]), (adj[CR:~];noun[prof:~, CR:~]))+,
	(coord[CR:~], (prep[form:fde, CR:~]), (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~];noun[postprof]), noun+[person:~, prof:~, CR:~], (adj[nat, CR:~];adj[postprof, CR:~];noun[postprof]), (coord[CR:~], adj[nat, CR:~];adj[postprof, CR:~];noun[postprof]))+.
\
/Confédération africaine d'athlétisme amateur\
20> noun[org=+, maj=+, sg=+, mwe=+] @= noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (adj[sg, CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~, sg]);(coord[CR:~], noun[postprof]),
	(prep[form:fde, CR:~], (det[def, CR:~]), (adj[CR:~];noun[postprof])*, (coord[CR:~], adj[CR:~]);(coord[CR:~], noun[postprof]), (noun[person:~, CR:~])+, (adj[postprof, CR:~];noun[postprof]), (adj[nat, CR:~];adj[postprof, CR:~];noun[prof:~, CR:~]), (coord, adj[postprof, CR:~]);(coord[CR:~], noun[postprof, CR:~]);(coord[CR:~], adj[nat, CR:~]))+.

/L'OFFICE NATIONAL DE L'IMMIGRATION\
20> noun[org=+, maj=+, sg=+, mwe=+] @= noun[org1, sansde:~, phrasemaj:+, sg, !masc:!, !fem:!], (adj[sg, nat, CR:~];adj[postprof, sg];noun[postprof, sg])*, (coord[CR:~], adj[CR:~, postprof, sg];noun[postprof]),
	(prep[form:fde, CR:~], (det[def, CR:~]), (noun[person:~, CR:~, org1:~])+, (adj[postprof, CR:~];noun[postprof, CR:~];adj[nat, CR:~])*, (adj[postprof, CR:~];noun[postprof, CR:~];adj[nat, CR:~]))+,
	(coord[CR:~], (prep[form:fde, CR:~]), (det[def, CR:~]), noun+[person:~, prof:~, CR:~, org1:~], (adj[nat, CR:~];adj[postprof, CR:~];noun[postprof]), (coord[CR:~], adj[nat, CR:~];adj[postprof, CR:~];noun[postprof]))*.

/le ministère de l'Education nationale\
20> noun[org=+, mwe=+, sg=+] @= noun[org1, sansde:~, sansmaj, sg, !masc:!, !fem:!], (adj[sg, nat, CR:~]), prep[form:fde], (det[def]), (noun[person:~, CR:~, lemme:~base])+, (adj[CR:~])*, (adj[CR:~], coord[CR:~], adj[CR:~]), (prep[form:fde], (det[def, CR:~]), (noun[CR:~])+, (adj[CR:~])*, (adj[CR:~])), (coord, prep[form:fde], (det[def, CR:~]), (noun[CR:~])+, (adj[CR:~])*, (adj[CR:~])), (adj[CR:~]).

/les ministères de la Justice et de l'Intérieur\
20> noun[org=+, mwe=+, pl=+] @= noun[org1, sansde:~, sansmaj, pl, !masc:!, !fem:!], prep[form:fde], (det[def]), (noun[CR:~])+, (adj[CR:~])*, coord, prep[form:fde], (det[def, CR:~]), (noun[CR:~])+, (adj[CR:~])*, (adj[CR:~]).

/la Fédération française d'athlétisme\
//20> noun[org=+, maj=+, mwe=+, sg=+] @= noun[org1, sansde:~, maj, sg, !masc:!, !fem:!], (adj[sg, CR:~])+, (coord[CR:~], adj[CR:~, sg]), prep[form:fde, CR:~], (det[CR:~]), (adj[CR:~])*, (coord[CR:~], adj[CR:~])*, noun[person:~, CR:~], (adj[CR:~])*, (adj[CR:~])), (noun[person:~, org:~], (adj[CR:~])*, (adj[CR:~])).


/le parti des Travailleurs\
20> noun[org=+, mwe=+, masc=+, sg=+] @= |?[def], (adj[maj:~])| noun[parti, sg, !maj:!], prep[form:fde], (det[def]), (adj[maj]), noun[CR:~, maj, nat:~, person:~], (adj), (coord, (prep), (det[def]), (adj[maj]), (noun[maj, CR:~])*, noun[maj, CR:~]).

/le mouvement des jeunes Giscardiens\
20> noun[org=+, mwe=+, masc=+, sg=+] @= |?[def], (adj[maj:~])| noun[lemme:mouvement, !maj:!, sg], prep[form:fde, pl], (adj), (noun[CR:~]), noun[CR:~, maj, nat:~, person:~];adj[CR:~, maj, nat:~, person:~], (adj), (coord), (prep, (det[def])), (adj[maj]), (noun[maj, CR:~])*.

/Assurances Générales de France\
20> noun[org=+, mwe=+, masc=+, sg=+] @= noun[firme1, maj, phrasemaj:~], (?[maj])*, prep[form:fde], noun+[maj, person:~].

/la Générale Occidentale\
20> noun[org=+, mwe=+, masc=+, sg=+] @= noun[firme1, maj, phrasemaj:~], (?[maj])*, noun+[maj, person:~].


/********** construction en 'pour' **********\
/le Comité pour la libération des prisonniers politiques arabes et du Proche-Orient\
20> noun[org=+, mwe=+, sg=+, maj=+] @= noun[org1, orgpour, org1de:~, sg, maj, !masc:!, !fem:!], (adj[CR:~];noun[maj, CR:~]), prep[form:fpour];prep[form:fcontre], (det[def]), (noun[CR:~])+, (adj[CR:~])*, (prep[form:fde], (det[def]), (adj), (noun[CR:~])+, (adj[CR:~]),  (adj[CR:~])), (coord, (prep), (det[def, CR:~]), (noun[CR:~])+, (adj[CR:~]), (adj[CR:~])), (prep[form:fen];prep[form:fa, det], noun[lieu]).

/la Fondation pour les Etudes de Défense Nationale\
20> noun[batimentorg=+, mwe=+, sg=+, maj=+] @= noun[batimentorg1, orgpour, org1de:~, sg, maj, !masc:!, !fem:!], (adj[CR:~];noun[maj, CR:~]), prep[form:fpour];prep[form:fcontre], (det[def]), (noun[CR:~])+, (adj[CR:~])*, (prep[form:fde], (det[def]), (adj), (noun[CR:~])+, (adj[CR:~]),  (adj[CR:~])), (coord, (prep), (det[def, CR:~]), (noun[CR:~])+, (adj[CR:~]), (adj[CR:~])).

/************* construction avec des adjectifs uniquement *********\
/le Parti fédéraliste flamand X Y\
20> noun[org=+, mwe=+] @= |noun[orgadj, maj], (adj)+| noun[maj, phrasemaj:~, person:~, lieu:~, CR:~], noun[maj, person:~, lieu:~, CR:~].

/le Parti démocrate français\
21> noun[org=+, maj=+, mwe=+, sg=+] @= noun[orgadj, maj, sg, !masc:!, !fem:!, nonorg:~], (adj[sg, postprof, CR:~];adj[sg, nat, CR:~];noun[postprof, CR:~])+, (coord[CR:~], adj[CR:~, sg, postprof];adj[CR:~, sg, nat]).

/les Forces armées angolaises\
20> noun[org=+, mwe=+] @= noun[org1pl, !maj:!, orgadj, !masc:!, !fem:!], (adj[pl, nat, CR:~];adj[pl, postprof, CR:~];noun[postprof, CR:~])+, (coord[CR:~], adj[CR:~, pl, nat];adj[CR:~, pl, postprof]).

/le parti Socialiste juif Bund\
20> noun[org=+, mwe=+] @= |noun[parti, !maj:!, !sg:!, !masc:!], (adj[masc, sg, CR:~])*, (adj[maj, masc, sg, CR:~])+, (adj[masc, sg, CR:~])*| noun[maj, phrasemaj:~, person:~, lieu:~, CR:~], noun[maj, phrasemaj:~, person:~, lieu:~, CR:~].

/le parti intégriste Hezbe-i-Islami\
20> noun[org=+, mwe=+, nonorg2=+] @= |?[def], (adj[masc, sg, CR:~])*, noun[parti, !maj:!, !sg:!, !masc:!], (adj[masc, sg, CR:~])+| noun[maj, phrasemaj:~, person:~, lieu:~, CR:~], (punct[hyph, CR:~]), noun[maj, phrasemaj:~, person:~, lieu:~, CR:~];noun[partic, CR:~], ((punct[hyph, CR:~]), noun[maj, phrasemaj:~, person:~, lieu:~, CR:~]).


/le parti Socialiste juif\
21> noun[org=+, maj=+, mwe=+] @= |?[indef:~, adj:~]| noun[parti, nonorg:~, sg, !masc:!, !fem:!, orgadj], (adj[CR:~])*, (adj[maj, CR:~];adj[postprof, CR:~])+, (adj[nat, CR:~])*.

/les partis Socialistes flamand et belge\
21> noun[org=+, maj=+, mwe=+, pl] @= noun[parti, pl !masc:!, !fem:!], (adj[pl, CR:~])*, (adj[maj, pl, CR:~];adj[postprof, CR:~])+, adj[nat, sg], coord, adj[nat, sg].

/les Partis socialistes flamand et belge\
21> noun[org=+, maj=+, mwe=+, pl] @= noun[orgadj, maj, pl, !masc:!, !fem:!], (adj[pl, CR:~])+, adj[nat, sg], coord, adj[nat, sg].

/le Karlsruher SC, le Hertha BSC Berlin\
25> noun[org=+, maj=+, mwe=+] @= |?[def]| noun[maj, nat:~, prof:~, tit:~, nonorg:~,toutmaj:~], noun[toutmaj, phrasemaj:~, nonorg:~, CR:~, nonorg2:~], ((num[CR:~]), noun[maj, person:~, CR:~]).

/le FC Cologne, le TSV Bayer 04 Leverkusen\
25> noun[org=+, maj=+, mwe=+] @= |?[def]| noun[toutmaj, phrasemaj:~, org:~, preorg:~, prefirme:~, firme:~, lettre:~], noun[maj, person:~, inconnu];noun[maj, person:~, ville];noun[maj, person:~, region];noun[maj, person:~, proper], ((num), noun[maj, person:~, inconnu];noun[maj, person:~, ville];noun[maj, person:~, region];noun[maj, person:~, org]).

/*********** contexte ***********\
/l'association X Y\
26> noun[org=+, maj=+, mwe=+] @= |?[def], (adj[maj:~]), noun[preorg, maj:~];noun[preorg, toutmaj], (adj[nat];adj[postprof])| (det[CR:~]), (adj[nat:~, postprof: ~, CR:~])*, noun[maj, phrasemaj:~, org:~, persorg:~, CR:~], (adj[guessed:~])*, ((prep[CR:~]), (adj[CR:~]), noun[maj, CR:~])* .
/le magazine britannique, "The Tatler".\
26> noun[org=+, maj=+, mwe=+] @= |?[def], (adj[maj:~]), noun[preorg, maj:~, sg];noun[preorg, toutmaj], (adj[nat]), punct[form:fcm], (punct[form:fquotes])| (det[CR:~]), (adj[nat:~, CR:~])*, noun[maj, phrasemaj:~, org:~, person:~, CR:~], (adj)*, (prep)*, (noun[maj, CR:~])* |(punct[form:fquotes]), punct|.


/l'association Clair de Terre\
26> noun[org=+, maj=+, mwe=+] @= |?[def], (adj[maj:~]), noun[preorg, maj:~], (adj[nat])| noun[maj, phrasemaj:~, CR:~, org:~], ((prep), (adj[CR:~])*, noun[maj, CR:~])* .

/la ligue Vie et Santé\
26> noun[org=+, maj=+, mwe=+] @= |?[def], (adj[maj:~]), noun[preorg, maj:~], (adj[nat])| noun[maj, phrasemaj:~, CR:~], (adj[CR:~])*, coord[CR:~], (adj[CR:~])*, noun[maj, CR:~] .


/le journal "Navarra Hoy"\
26> noun[org=+, orglieu=+, maj=+, mwe=+] @= |?[def], (adj[maj:~]), noun[preorg, maj:~], (adj[nat]), punct[form:fquotes, CR:~]| (det[CR:~]), (adj)*, noun[maj, phrasemaj:~, CR:~, org:~], (adj)*, (prep)*, (noun[maj, CR:~])* |punct[form:fquotes, CR:~]|.


/une association anglaise, la Cefaldi,\
26> noun[org=+, maj=+, mwe=+] @= |det[poss];det[indef], (adj[maj:~]), noun[preorg, maj:~], (adj[nat]), punct[form:fcm]| (det[CR:~]), noun[maj, phrasemaj:~, CR:~, org:~, person:~, lieu:~], (noun[maj, CR:~])*.


/la compagnie maritime britannique P & O\
26> noun[firme=+, maj=+, mwe=+, sg=+, orglieu=+] @= |?[def], (adj[maj:~]), noun[prefirme, sg, !masc:!, !fem:!, CR:~], (adj), (adj[nat, CR:~])| noun[maj, phrasemaj:~, CR:~, firme:~, persorg:~,nat:~], (noun[maj, CR:~])*, (coord;?[lemme:and], noun[maj, CR:~])*, (noun[maj, CR:~])*.

/le groupe Lafarge Coppée\
26> noun[firme=+, maj=+, mwe=+, sg=+] @= |?[def], (adj[maj:~]), noun[prefirme, sg, !masc:!, !fem:!, CR:~], (adj), (adj[nat, CR:~])| noun[maj, phrasemaj:~, CR:~], (coord[lemme:"&"];?[lemme:and]), (noun[maj, CR:~])+.


/le fabricant, "faux Systems",\
26> noun[firme=+, maj=+, mwe=+] @= |?[def], (adj[maj:~]), noun[prefirme, sansvirg:~, maj:~, !masc:!, !fem:!], (adj[nat]), punct[form:fcm], (punct[form:fquotes])| (det[CR:~]), (adj[nat:~, CR:~])*, noun[maj, phrasemaj:~, firme:~, person:~, CR:~];noun[maj, phrasemaj:~, firme:~, persorg:~, CR:~], (adj)*, (prep)*, (noun[maj, CR:~])* |(punct[form:fquotes]), punct|.

/l'OPA de X sur Y\
26> noun[firme=+, orgpers=+, maj=+, mwe=+] @= |noun[prefirmesur], (adj;verb[partpas]), prep[form:fde, CR:~], (det, ?[nat, CR:~])| (det), noun[CR:~, person:~, org:~, maj, nat:~], (noun[CR:~, maj])* |prep[form:fsur, CR:~];coord|.
27> noun[firme=+, orgpers=+, maj=+, mwe=+] @= |noun[prefirmesur], (adj;verb[partpas]), prep[form:fde, CR:~], (det, ?[nat, CR:~]), noun[firme], coord, (prep[form:fde]), (det), (?[nat, CR:~])| (det), noun[maj, CR:~, nat:~, person:~, org:~], (noun[maj, CR:~]) |prep[form:fsur, CR:~]|.
//27> noun[firme=+, maj=+, mwe=+] @= |noun[prefirmesur], (adj;verb[partpas]), prep[form:fde, CR:~], (det, ?[nat, CR:~]), noun[firme], coord, (prep[form:fde]), (det), (?[nat, CR:~])| (det), noun[CR:~, person:~, org:~, maj, nat:~], (noun[CR:~, maj])* |prep[form:fsur, CR:~]|.

27> noun[firme=+, orgpers=+, maj=+, mwe=+] @= |noun[prefirmesur], (adj;verb[partpas]), (prep[form:fde, CR:~], (noun[prefirme]), (det), (?[nat, CR:~]), noun[CR:~], adj*), prep[form:fsur, CR:~], (det, noun[nat])| (det[CR:~]), noun[maj, phrasemaj:~, CR:~, batimentorg:~, batimentorg1:~, lieu:~, nat:~], (coord[CR:~], noun[maj, CR:~]), (noun[maj, CR:~])*.
27> noun[firme=+, orgpers=+, maj=+, mwe=+] @= |noun[prefirmesur], (adj;verb[partpas]), (prep[form:fde, CR:~], (noun[prefirme]), (det), (?[nat, CR:~]), noun[CR:~], adj*), coord, (prep[form:fde]), (noun[prefirme]), (det), (?[nat, CR:~]), noun[CR:~], adj*, prep[form:fsur, CR:~], (det, noun[nat])| (det[CR:~]), noun[maj, phrasemaj:~, CR:~, batimentorg:~, batimentorg1:~, lieu:~, nat:~], (coord[CR:~], noun[maj, CR:~]), (noun[maj, CR:~])*.

/les ONG X, (Y et Z)\
30> noun[org=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat])| (det), (noun[maj, phrasemaj:~, CR:~, org:~])+, (adj)* |coord;punct[form:fcm]|.

/les ONG (X et) Y\
31> noun[org=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), noun[org, CR:~], coord| (det), (noun[maj, phrasemaj:~, CR:~])+, (adj)*.

/les ONG (X,) Y (et Z)\
32> noun[org=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), noun[org, CR:~], punct[form:fcm]| (det), noun[maj, phrasemaj:~, CR:~, org:~], (noun[maj, phrasemaj:~, CR:~])*, (adj)* |coord;punct[form:fcm]|.

/les ONG (X,) Y (et Z)\
33> noun[org=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), noun[org, CR:~], punct[form:fcm], noun[org, CR:~], punct[form:fcm]| (det), (noun[maj, phrasemaj:~, CR:~])+, (adj)* |coord;punct[form:fcm]|.

/les ONG (X, Y et) Z\
34> noun[org=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), noun[org, CR:~], (punct[form:fcm], noun[org, CR:~])+, coord| (det), (noun[maj, phrasemaj:~, CR:~])+, (adj)*.


/les compagnies Lausonia, La Renama, Latina Vita et Lausonia Vita\
/les compagnies X, (Y et Z)\
30> noun[firme=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[prefirme, pl], (adj), (adj[nat])| (det), (noun[maj, phrasemaj:~, org:~, firme:~, CR:~])+, (adj)* |coord;punct[form:fcm]|.

/les compagnies (X et) Y\
31> noun[firme=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[prefirme, pl], (adj), (adj[nat]), noun[firme, CR:~], coord| (det), (noun[maj, phrasemaj:~, CR:~])+, (adj)*.

/les compagnies (X,) Y (et Z)\
32> noun[firme=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[prefirme, pl], (adj), (adj[nat]), noun[firme, CR:~], punct[form:fcm]| (det), (noun[maj, phrasemaj:~, CR:~])+, (adj)* |coord;punct[form:fcm]|.

/les compagnies (X,) Y (et Z)\
33> noun[firme=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[prefirme, pl], (adj), (adj[nat]), noun[firme, CR:~], punct[form:fcm], noun[firme, CR:~], punct[form:fcm]| (det), (noun[maj, phrasemaj:~, CR:~])+, (adj)* |coord;punct[form:fcm]|.

/les compagnies (X, Y et) Z\
34> noun[firme=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[prefirme, pl], (adj), (adj[nat]), noun[firme, CR:~], (punct[form:fcm], noun[firme, CR:~])+, coord| (det), (noun[maj, phrasemaj:~, CR:~])+, (adj)*.


Marker:
/Bleu, le quotidien d'Agen\
45> ?[maj:~, det:~, adj:~], noun[maj, person:~, lieu:~, org=+], punct[form:fcm], (det[CR:~]), noun[preorg, sansvirg:~, CR:~], ?[org:~].
45> punct[form:fquotes], noun[maj, org=+], punct[form:fquotes], punct[form:fcm], (det[CR:~]), noun[preorg, CR:~].

45> ?[maj:~, det:~, adj:~], noun[maj, person:~, lieu:~, firme=+], punct[form:fcm], (det[CR:~]), noun[prefirme, postfirme:~-, CR:~], ?[org:~, firme:~].


/filiale commune de Ford et de VW\
45> noun[prefirmede], (adj[CR:~]), prep[form:fde], (det[def], (adj[maj:~])), noun[maj, phrasemaj:~, CR:~, firme:+], coord[CR:~], prep[form:fde, CR:~], noun[maj, CR:~, preorg:~, prefirme:~, batimentorg1:~, firme=+].

/la filiale de la Socpress, Presse-Infor\
45> noun[prefirmede, prefirme], prep[form:fde], (det), (adj), noun, (noun), (adj), punct [form:fcm, nonorg=+], noun[maj, toutmaj:~, person:~, firme=+].

/************* autres **********\
/le KFLT\
//45> ?[def, sg], (adj[maj:~]), noun[toutmaj, phrasemaj:~, preorg:~, prefirme:~, lettre:~, nonorg:~, pays:~, org=+];noun[toutmajtiret, phrasemaj:~, preorg:~, prefirme:~, lettre:~, nonorg:~, pays:~, org=+].
//45> punct[form:fopar], noun[toutmaj, phrasemaj:~, preorg:~, prefirme:~, lieu:~, nonorg:~, org=+], punct[form:fcpar].
/(AMD-BA)\
//45> punct[form:fopar], noun[toutmajtiret, phrasemaj:~, preorg:~, prefirme:~, lieu:~, nonorg:~, org=+], punct[form:fcpar].


/l'Assemblée\
45> ?[def], noun[org1, maj, sg, phrasemaj:~, CR:~, anton:~, nonorg:~, org=+], ?[maj:~].

45> noun[toutmaj, org], punct[form:fopar], noun[maj, org, nonorg=+], punct[form:fcpar].


Sequence:
/le LTTE (Tigres pour la libération du Tamil Eelam)\
50> noun[org=+, maj=+] @= |noun[toutmaj, org], punct[form:fopar]| noun[maj, nonorg:~], (prep, (det[def])), (adj)*, (noun)*, (adj)*, (prep, (det[def])), (adj)*, (noun)*, (adj)* |punct[form:fcpar]|.

/Radio France\
50> noun[org=+, mwe=+] @= noun[org1, sansmaj, org1de:~, sansdef, !maj:!];noun[org1, org1de:~, sansdef, maj:+, !maj:!], (adj[maj]), noun[maj, phrasemaj:~, person:~, CR:~];adj[maj, phrasemaj:~, person:~, CR:~], (adj[maj]), (noun[maj, CR:~])*.

/Le Club Med\
50> noun[org=+, mwe=+] @= |?[def]| noun[org1, sansmaj, org1de:~, !maj:!];noun[org1, org1de:~, maj:+, !maj:!], (adj[maj]), noun[maj, phrasemaj:~, person:~, CR:~];adj[maj, phrasemaj:~, person:~, CR:~], (adj[maj]), (noun[maj, CR:~])*.

/le petit Bleu, le quotidien d'Agen\
50> noun [org=+, mwe=+] @= (det[def]), (adj)*, (noun[maj, CR:~])*, noun[maj, org:~, person:~, lieu:~, CR:~] |punct[form:fcm], (det[CR:~]), noun[preorg, sansvirg:~, CR:~], ?[org:~]|.
/"le petit Bleu", le quotidien d'Agen\
50> noun [org=+, mwe=+] @= |punct[form:fquotes]| (det[def]), (adj)*, (noun[maj, CR:~])*, noun[maj, CR:~] |punct[form:fquotes], punct[form:fcm], (det[CR:~]), noun[preorg, sansvirg:~, CR:~]|.

/la Cote Desfossés, le quotidien économique\
50> noun [org=+, mwe=+] @= (noun[maj, CR:~])+, noun[maj, person:~, lieu:~, CR:~] |punct[form:fcm], (det[CR:~]), noun[preorg, CR:~], ?[org:~]|.

/le Standard de Meudon, club historique\
50> noun [org=+, mwe=+] @= (noun[maj, CR:~])+, (prep[form:fde]), noun[maj, person:~, org:~, CR:~] |punct[form:fcm], (det[CR:~]), noun[preorg, CR:~], ?[org:~]|.

/Média-France International, la société chargée d'exporter le programme\
50> noun [firme=+, mwe=+] @= (det[def]), (adj)*, (noun[maj, CR:~])*, noun[maj, org:~, firme:~, person:~, lieu:~, CR:~] |punct[form:fcm], (det[CR:~]), noun[prefirme, postfirme:~-, CR:~], ?[org:~, firme:~, verb:~];verb[partpas]|.


/************************* noms de personnes **********************\
Marker:
/le PDG du Musée Grévin, Tititoto\
100> noun[prefirmede], prep[form:fde], (det), (adj), noun[org], punct[form:fcm], noun[maj, toutmaj:~, org:~, person=+].
100> noun[preorgde], prep[form:fde], (det), (adj), noun[org], punct[form:fcm], noun[maj, toutmaj:~, org:~, person=+].

/le leader X\
100> noun[preorg, preorgpers], noun[org, maj, CR:~, person=+].


120> noun[org=~]{noun[org], punct[hyph], noun[org]}.

/lorsqu'un noeud noun a pour unique fils un noeud noun, faire passer le trait qui signale une entité nommée du père au fils (en prévision d'une élimination du noeud père dans deduction_entite.xip)\

140> noun[org]{noun[first, last, org=+]}.
140> noun[firme]{noun[first, last, firme=+]}.
140> noun[lieu, lieu=~]{noun[first, last, lieu=+]}.
140> noun[person, person=~]{noun[first, last, person=+]}.
140> noun[date, date=~]{noun[first, last, date=+]}.
140> noun[periode, periode=~]{noun[first, last, periode=+]}.
140> noun[ville, ville=~]{noun[first, last, ville=+]}.
140> noun[pays, pays=~]{noun[first, last, pays=+]}.
140> noun[continent, continent=~]{noun[first, last, continent=+]}.
140> noun[quartier, quartier=~]{noun[first, last, quartier=+]}.
140> noun[batiment, batiment=~]{noun[first, last, batiment=+]}.
140> noun[batimentorg, batimentorg=~]{noun[first, last, batimentorg=+]}.

140> noun[lieupers]{noun[first, last, lieupers=+]}.
140> noun[perslieu]{noun[first, last, perslieu=+]}.
140> noun[lieupers1]{noun[first, last, lieupers1=+]}.
140> noun[perslieu1]{noun[first, last, perslieu1=+]}.
140> noun[orgpers]{noun[first, last, orgpers=+]}.
140> noun[persorg]{noun[first, last, persorg=+]}.
140> noun[orglieu]{noun[first, last, orglieu=+]}.


/effacement des traits en concurrence selon les traits de préférence\
150> noun[orgpers, org, person=~].
150> noun[orglieu, org, lieu=~, ville=~, pays=~, continent=~, region=~].
150> noun[orgpers, firme, person=~].
150> noun[orglieu, firme, lieu=~, ville=~, pays=~, continent=~, region=~].
150> noun[person:+, persorg:+, org=~, firme=~].
150> noun[person:+, persorg1:+, org=~, firme=~].

Marker:
//acronymes entre parentheses

160> noun[org] , punct[form:fopar] , noun[org=+,toutmaj] , punct[form:fcpar] .


