# ---------------------------------------------------------------
# FILE:		numbers.xfst
# CONTENT:	(x)fst script for converting numbers <-> digits
#		French
#
# AUTHOR:	A. Schiller
# CREATED:	03-DEc-1999
# ---------------------------------------------------------------

# ---------------------------------------------------------------
# General outline:
# - define Num1to9
# - define Num10to99
# - define Num100to999 
#	== Num1to9 "hundred" [ 0:%0 0:%0 | 0:%0 Num1to9 | Num10to99 ]
# - define Num1000to999999
#	 == [Num1to9|Num10to99|Num100to999] "thousand" 
#		[ 0:%0 0:%0 0:%0 |
#		  0:%0 0:%0 Num1to9 |
#		  0:%0 Num10to99 |
#		  Num100to999 ]
# ---------------------------------------------------------------

echo >>> separators
define HY	%-:0 ;
define SP	" ":0 ;
define ET	SP {et}:0 SP ;


echo >>> zero
define Zero	{zero}:%0 ;

# ---------------------------------------------------------------
echo >>> numbers 1 to  9
define Num2to9 [
	{deux}:2
|	{trois}:3
|	{quatre}:4
|	{cinq}:5
| 	{six}:6
|	{sept}:7
|	{huit}:8
|	{neuf}:9
];

define Num1to9 	[ {un}:1  | Num2to9 ] ;

# for "special" numbers 71-79; 91-99

define Spec2to9 [
	{douze}:{2}
|	{treize}:{3}
|	{quatorze}:{4}
|	{quinze}:{5}
|	{seize}:{6}
|	{dix}:0 HY [ {sept}:7 | {huit}:8 | {neuf}:9 ]
];


# ---------------------------------------------------------------
echo >>> numbers 10 to 99
define Num10to99 [
	{dix}:{10}
|	{onze}:{11}
|	[ 0:1 Spec2to9 ]

|[ 	[ {vingt}:2
	| {trente}:3
	| {quarante}:4
	| {cinquante}:5
	| {soixante}:6 
	| {septante}:7		# B/CH
	| {huitante}:8
	| {octante}:8		# B/CH/CA
	| {nonante}:9		# B/CH
	]	[ 0:%0 | ET {un}:1 | HY Num2to9 ]
 ]
|	[ {soixante}:7 [ HY {dix}:%0 | ET {onze}:1 | HY Spec2to9 ] ]
|	[ {quatre-vingt}:8 [ 0:%0 | HY Num1to9 ] ]
|	[ {quatre-vingt}:9 HY [{dix}:%0 | {onze}:1 | Spec2to9 ] ]
] ;

# ---------------------------------------------------------------
echo >>> numbers 100 to 999

define Num100to999 [
	[ Num2to9 SP | 0:1 ] {cent}:0 
	[ 
	  s:0 0:%0 0:%0
	| SP 0:%0 Num1to9 
	| SP Num10to99
	]
] ;

# ---------------------------------------------------------------
echo >>> numbers 1000 to 999999

define Num1000to999999 [
	[ [ Num2to9 | Num10to99 | Num100to999 ] SP | 0:1 ] {mille}:0 
	[ 0:%0 0:%0 0:%0 
	| 0:%0 0:%0 SP Num1to9
	| 0:%0 SP Num10to99
	| SP Num100to999
	]
];

# ---------------------------------------------------------------
echo >>> cardinals


define CARD  [
	[ Zero | Num1to9 | Num10to99 | 	Num100to999 | Num1000to999999 ]
] ;

# ---------------------------------------------------------------
echo >>> cardinals for years
define CARDYEAR	[ [ Num2to9 SP {mille}:0 | {mil}:1 ]
			[ 0:%0 0:%0 0:%0 
			| 0:%0 0:%0 SP Num1to9
			| 0:%0 SP Num10to99
			| SP Num100to999
			] 
		] |
	# dix-neuf cent trente-huit
		[ [ Num10to99 SP {cent}:0 ]
			[ 0:%0 0:%0 
			| 0:%0 SP Num1to9
			| SP Num10to99
			]
		];


# ---------------------------------------------------------------
echo >>> ordinals
define ORD  [
	[ Num2to9 | Num10to99 | Num100to999 | Num1000to999999 ] {ième}:0
];

define ORDRULES [ 
 	f -> v || _ {neu} 
.o.	q -> {qu} || _ {ième}
.o.	e -> 0 || _ {ième}
];

define ORD	[ ORDRULES.i .o. ORD ];

# ---------------------------------------------------------------
# roman numbers
define ROMAN1   [ (m (m (m)))
            ( [ c m | d | c d | (d) (c (c (c))) ] )
            ( [ x c | l | x l | (l) (x (x (x))) ] )
            ( [ i x | v | i v | (v) (i (i (i))) ] ) ] - [];

define ROMAN2   [ (M (M (M)))
            ( [ C M | D | C D | (D) (C (C (C))) ] )
            ( [ X C | L | X L | (L) (X (X (X))) ] )
            ( [ I X | V | I V | (V) (I (I (I))) ] ) ] - [];

define ROMAN  [ ROMAN1 | ROMAN2 ];
	
define ROMANCARD	ROMAN - [ {dix} | {DIX} ] ;

define ROMANORD 	ROMAN [ e (m e) ]  - [ ? [e|E]  ] ;
# exclude me, le, ce,  etc. !!


# ---------------------------------------------------------------
echo >>> big numbers
define DIG	[%0|1|2|3|4|5|6|7|8|9] ;
define DIG1	DIG+ [ [%.|%,] DIG+]*	;

define BIGNUM	[ [ {mi} | {bi} | {tri} ] [ {llion} | {lliard} ] ] (s);

define NUMDIG	DIG1 (" "  BIGNUM);
define NUMCARD	CARD (" "  BIGNUM);

# eg. 12 000 000
define TRIPLE	DIG (DIG (DIG)) [ %  DIG DIG DIG]+ ;

# ---------------------------------------------------------------
echo >>> dates
define DAY	(%0) [DIG - %0] | 1 DIG | 2 DIG | 3 [%0 | 1] ;
define MONTH	(%0) [DIG - %0] | 1 [%0|1|2] ;
define YEAR	(DIG (DIG)) DIG DIG ;

define DATE	[ DAY %/ MONTH %/ YEAR 
		| DAY %- MONTH %- YEAR
		];


define HOUR	(%0|1) DIG | 2 [%0|1|2|3|4] ;
define MINUTE	[%0|1|2|3|4|5] DIG | 6 %0 ;

define TIME	HOUR [h|H|%:] MINUTE (%: MINUTE) ;

# ---------------------------------------------------------------
echo >>> add features

define BASEORD	[ [ DIG - %0] DIG*] - [1] ;
define SGPL	[ %+Sg:0 | 0:s %+Pl:0 ]	;

define CARD1 	[ NUMCARD.u %+Num:0 %+Card:0 
		| CARDYEAR.u %+Num:0 %+Year:0 
		| CARDYEAR.l %+Num:0 %+Card:0 %+Year:0 
		| NUMDIG %+Num:0 %+Dig:0 %+Card:0
		| TRIPLE %+Num:0 %+Dig:0 %+Card:0
		| DATE	%+Num:0 %+Date:0 
		| TIME	%+Num:0 %+Time:0 

		| ROMANCARD	%+Num:0 %+Rom:0 %+Card:0
		| ROMANORD 	%+Num:0 %+Rom:0 %+Ord:0
		];

define ORD1 	[ {premier}	%+Num:0 %+Ord:0 %+Masc:0
		| {première}	%+Num:0 %+Ord:0 %+Fem:0
		| {segond}	%+Num:0 %+Ord:0 %+Masc:0
		| {segonde}	%+Num:0 %+Ord:0 %+Fem:0
		| 1 {er}	%+Num:0 %+Ord:0 %+Masc:0
		| 1 {er}:{ère}	%+Num:0 %+Ord:0 %+Fem:0
		| 2 {nd}	%+Num:0 %+Ord:0 %+Masc:0
		| 2 {nd} 0:e	%+Num:0 %+Ord:0 %+Fem:0
		| ORD.u		%+Num:0 %+Ord:0 %+MF:0 
		| BASEORD ({èm}) e %+Num:0 %+Dig:0 %+Ord:0 %+MF:0 
		] SGPL ;

define ORD2	[ ORD.u {ment} %+Adv:0 %+Ord:0 ];


read regex CARD1 | ORD1 | ORD2 ;
