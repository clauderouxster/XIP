
Marqueur:
1> num,punct[form:fcpar], ?[debut=+].

/l'Ancien Empire Egyptien\
1> adj[empire1:+, aadj=+], noun[lemme:empire, maj].
2> noun[lemme:empire, maj], adj[maj, aadj=+].

/le parti Socialiste\
2> ?[def], (adj), noun[parti];noun[orgadj], adj[maj, aadj=+].
2> det, (adj), noun[parti];noun[orgadj], adj[maj, aadj=+].

Désambiguïsation:

3> verb<inf>, noun = |verb[lemme:venir], prep[form:fde]| verb.

/neuf est un chiffre devant un nom de mois\
3> (adj,num) %= num |noun[mois:+]|.

/L'Eglise Réformée de France : réformée est un adjectif\
5> (adj<postprof>, verb) = |noun[org1];noun[prof]| adj.

/un mot avec majuscule qui a été reconnu comme un verbe est un nom de personne s'il suit un nom de type Monsieur ou un prénom\
5> verb[maj:+, phrasemaj:~, noun:~] += |?[tit:+];noun[pren:+], (?[partic:+])| noun[proper=+,person:+, masc=+,fem=+, sg=+,pl=+,p3=+,maj=+, CR:~].
5> adj[maj:+, phrasemaj:~, noun:~] += |?[tit:+];noun[pren:+], (?[partic:+])| noun[proper=+,person:+, masc=+,fem=+, sg=+,pl=+,p3=+,maj=+, CR:~].

/un mot qui peut être verbe/adj ou nom avec maj. est un nom qd il commence par une majuscule hors début de ligne\
7> verb, noun <maj:+, phrasemaj:~, start:~, CR:~> = noun.
7> verb[maj:+, toutmaj:~,phrasemaj:~, start:~, CR:~, debut:~] %= |?[org1:~]| noun[proper=+, maj=+].

7> verb[maj:+, toutmaj:+,phrasemaj:~, start:~, CR:~, debut:~] %= |?[org1:~]| noun[proper=+, maj=+,toutmaj=+].
7> (adj, noun) [aadj:~, maj:+, phrasemaj:~, start:~, CR:~] = ?[noun:+].
/un mot qui peut être verbe ou noun(prénom) avec maj. est un nom qd il précède un mot avec majuscule\
7> verb, noun <maj:+, phrasemaj:~, pren:+> = noun |?[maj]|.

/le conseiller belge, le reporter Untel\
7> verb, noun = |det| noun |noun;adj[nat]|.

/les 16-18 ans ; vers 10-11 heures ne sont pas des dates\
7> adv<date:+> %= adv[date=-] |noun[lemme:an];noun[lemme:heure]|.

/choisit l'interprétation prénom d'un mot ambigu suivi d'un mot inconnu avec majuscule\
8> noun<pren:+, prenamb:~> [maj:+] %= noun [maj=+,proper=+,person=+, pren=+] |noun[maj:+, guessed:+]|.

/Mars est nom de mois dans 'en Mars 1986'\
8> (noun<lemme:mars>, noun<lemme:Mars>) [maj, toutmaj:~] = |?[lemme:en]| noun[lemme:mars].

/Paris est une ville dans 'de Paris'\
8> (noun<lemme:"%c+">, noun<lemme:"%C%c+">) [maj, toutmaj:~] = |prep[def:~]| noun [lemme:"%C%c+"].

/Olympia n'est pas une ville avec l'article\
8> (noun<ville>, noun<org>) [maj, toutmaj:~] = |det;prep[def]| noun[org].
/Olympia n'est pas un prénom avec l'article\
9> (noun<pren>, noun<org>) [maj, toutmaj:~] = |det;prep[def]| noun[org].

/Libération des otages\
8> (noun<lemme:libération>, noun<lemme:Libération>) [maj] = noun[lemme:libération] |prep[form:fde]|.

/coordination de noms avec and\
8> noun[maj] = | noun[verb:~],adj*, ?[lemme:and], det*, adj*| noun.

/de l'image et du son\
8> noun, det = |?[def]| noun.

/rue est un nom entre un chiffre et un mot majuscule\
8> noun<lemme:rue>, verb = |num| ?[verb:~] |?[maj, CR:~];prep[form:fde];prep[form:fdu]|.




Marqueur:
10> Noun[lemme:"%c", maj:+, lettre=+].
10> Noun[lemme:"%C%d+", nonorg=+].
10> Noun[lemme:"RN%d+", nonorg=+].

/de Polytechnique\
10> prep, noun[lemme:polytechnique, maj:+, org=+].

