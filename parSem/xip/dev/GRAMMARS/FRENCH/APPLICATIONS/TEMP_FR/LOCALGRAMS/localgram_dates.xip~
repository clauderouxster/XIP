//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


//Aude Rebotier
/--------------  grammaire de dates ---------------------\


Séquence:

/vingt et un est un nombre\

5> Num [gdnbre=+] = num[noun:+],(punct[form:fhyph]), coord, (punct[form:fhyph]),num.


/deux noms de nombres font un seul nombre (quatre mille cinq-cent)\
9> Num [gdnbre=+] @= num[noun:+], num[noun:+], (num[noun:+]), (punct[hyph:+], num[noun:+]), (coord, num).


/les mois musulmans composés\
9> noun[time=+, mwe=+, sg=+, mois=+]= noun[mois1:+], (punct[lemme:']), (punct[form:fhyph]), noun[mois2:+].

/Jésus Christ = Jésus-Christ\
10> noun[form=fjc, masc=+, sg=+, proper=+,p3=+] = noun[lemme:Jésus], noun[lemme:Christ].



Marqueur :

/*********modifs maud*****\


/******Années seules*****\

//déplacement+modif (maud)
/un nombre de 4 chiffres commençant par 19 ou 20\
1> num[lemme:"19%d%d", 20e=+].
1> num[lemme:"20%d%d", 21e=+].

/ Dates avec annee seule : absolue incomplète (uniquement en contexte :en 1989, vers 1989, dès 1989)\
1> prep[prepannee,timesignal], num[gdnbre,time=+,date=+,annee=+,absolue=+], punct[form:~fquotes] ; sent; cm ; det;conj;conjque;verb; coord; prep; pron; ?[CR].
2> noun[prepannee,timesignal], num[gdnbre,time=+,date=+,annee=+,absolue=+], punct[form:~fquotes] ; sent; cm ; det;conj;conjque;verb; coord; prep; pron; ?[CR].


Séquence:

/******Noms de dates "classiques"*******\
//MODIF maud :
// Sur reconnaissance des dates et traits assignés
// Si pb reprendre le bloc commenté ci-dessous

//Date absolue complète
/(lundi) 12 mars 1987\
11> Noun[date=+,time=+,mwe=+,masc=+,sg=+,complete=+,absolue=+] @=  (noun[jour]), num[numjourpot];?[premier], noun[mois, CR:~],  num[CR:~].

//
//Date absolue incomplète
/mars 1987\
12> Noun[date=+,time=+,mwe=+,masc=+,sg=+,incomplete=+,absolue=+] @=  noun[mois, CR:~],  num[CR:~].

//Dates relatives incomplètes : num et/ou nom de jour-mois sans indication de l'année
//(lundi) 15 mars \
13> Noun[date=+,time=+,mwe=+,masc=+,sg=+,relative=+] @= (noun[jour]), num[numjourpot];?[premier], noun[mois, CR:~] |?[noun:~+, adj:~+, form:~fde, sfdentre:~+]|.

// lundi 15 \
14> Noun[date=+,time=+,mwe=+,masc=+,sg=+,relative=+] @= noun[jour], num[numjourpot];?[premier] |?[noun:~+, adj:~+, form:~fde, sfdentre:~+]|.


// lundi,mars \
15> Noun[date=+,time=+,masc=+,sg=+,relative=+] @= noun[jour];noun[mois, CR:~] |?[noun:~+, adj:~+, form:~fde, sfdentre:~+]|.

//annee seule
//15> noun[date=+,time=+,absolue=+] = |noun[prepannee,timesignal]| num[gdnbre].
//--------------------------------------------------------------

/*********Fin modifs maud*****\

/300 ap. JC\
15> noun[mwe=+, date=+] = num[date=+], prep, noun[form:fjc].
15> noun[mwe=+, date=+] = num[date=+], prep, det[poss:+], noun[lemme:ère].

Marqueur:

/6-21 n'est pas une date\
16> adv[lemme:"%d-%d%d", date=~].

/EURO 2000 --> 2000 date\
16> noun[lemme:"euro"], num[lemme:"2000",date=+]. 


/date de 1420\
16> verb [lemme:dater], prep[form:fde], num[gdnbre:+, date=+].

/en 1998 et 1999.\
17> prep[form:fen], num[gdnbre:+], coord, num[gdnbre:+, time=+, date=+], punct[form:~fquotes] ; sent; cm ; det;conj;verb; coord; prep[form:~fen]; pron; ?[CR].


Séquence:


/les années 1920 et 1921\
18> noun[mwe=+,time=+, date=+] @= |det[def:+]| noun[lemme:année, pl:+], num, coord, num, prep, noun[form:fjc].
18> noun[mwe=+,time=+, date=+] @= |det[def:+]| noun[lemme:année, pl:+], num, coord, num, prep, det[poss:+], noun[lemme:ère].
18> noun[mwe=+,time=+, date=+] @= |det[def:+]| noun[lemme:année, pl:+], num, coord, num |punct[form:~fquotes] ; sent; cm ; det;conj;verb; coord|.

/début 1980; le premier trimestre 1985\
//18> noun[mwe=+,time=+,date=+] @=  (adj[ord:+];adj[lemme:dernier]), noun[predate];adv[predate], (prep[form:fde]), num[date=+,lettres:~];noun[date:+].

/l'an 2000 (après JC)\
20> noun[mwe=+,time=+, date=+] = |det[def:+]| noun[lemme:an; sg:+];noun[lemme:année; sg:+], (prep[lemme:de], noun[lemme:grâce]), num[date=+];noun[date:+].

/le 9 thermidor an II\
20> noun[mwe=+,time=+, date=+] = noun[date:+], noun[lemme:an], num.


/****************périodes*********************\

Marqueur:
1> noun[lemme:renaissance, maj:+, sg:+, periode=+].
1> noun[lemme:antiquité, maj:+, sg:+, periode=+].

/s. n'est l'abréviation de siècle que s'il est en minuscule\
1> noun[lemme:"s.", maj:+, siecle=~].


Séquence:

/les années 50 et (début) 60\
3> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:année, pl:+], num[rond], coord, (noun[predate:+]), num[rond], prep, noun[form:fjc].
3> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:année, pl:+], num[rond], coord, (noun[predate:+]), num[rond], prep, det[poss:+], noun[lemme:ère].
3> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:année, pl:+], num[rond], coord, (det[def:+]), noun[predate:+], num[rond].
3> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:année, pl:+], num[rond], coord, num[rond] |punct[form:~fquotes] ; sent; cm ; det;conj;verb; coord|.

/les années 50 (av. JC)\
30> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:année, pl:+], num[rond], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).


3> noun[mwe=+,time=+, periode=+] @= adj[ord:+];num[rom:+], (coord, (det[def:+];prep[def:+]), (noun[predate]), (det[def:+];prep[def:+]), adj[ord:+];num[rom:+]), noun[lemme:siècle];noun[lemme:millénaire], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).

/le IVe siècle (avant JC) et début IIIe\
3> noun[mwe=+,time=+, periode=+] @= adj[ord:+];num[rom:+], noun[siecle:+], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]), coord, (det[def:+];prep[def:+]), (noun[predate]), adj[ord:+];num[rom:+], (noun[siecle:+]), (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).

/le IVe (et le IIIe) siècle (av. JC)\
3> noun[mwe=+,time=+, periode=+] @= adj[ord:+];num[rom:+], (coord, (det[def:+];prep[def:+]), (noun[predate]), (det[def:+];prep[def:+]), adj[ord:+];num[rom:+]), noun[siecle:+], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).

3> noun[mwe=+,time=+, periode=+] @= num, noun[siecle:+, sg], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).

/l'âge du fer\
3> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:âge], prep[form:fde], (det[def:+]), noun[age:+].

/période néolithique\
3> noun[mwe=+, time=+, periode=+] = noun[ere], adj[periode:+];adj[periode2:+].

/l'Ancien Empire\
3> noun[mwe=+, time=+, periode=+] @= ?[empire1, maj], noun[lemme:empire, maj], (adj[empire2]).



/***************expressions temporelles*************\

/un jour, un beau jour\
//100> noun[mwe=+,time=+] =  det[indef, form:~fde], adj*, noun[time].


105> adv[mwe=+,time=+,quep=+] = pron[lemme=il], pron[lemme=y],verb[aux:+,form:favoir], adv[lemme=peu] | ?[form:~fde] |.

105> adv[mwe=+,time=+,quep=+] = pron[lemme=il], pron[lemme=y],verb[aux:+,form:favoir], adv*, ?*[verb:~,noun:~], noun[time:+].


/ il y a maintenant un quart de siècle \
105> adv[mwe=+,time=+,quep=+] = pron[lemme=il], pron[lemme=y],verb[aux:+,form:favoir], adv*, det;num, noun[partit:+], prep, noun[time:+].


//-----Bloc commenté pour test chgmt maud (cf.ci-dessous)-----

/du 15 (avril) au 20 mars\
//10> Noun[date=+, time=+, mwe=+, masc=+, sg=+] @= |prep[form:fde, def]| (noun[jour]), num[CR:~], (noun[mois, CR:~]), prep[form:fa, def], (noun[jour]), num[CR:~], noun[mois], (num[CR:~]), (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]), (prep[form:fa,def], noun[postjour]). 

/de décembre 1999 à janvier 2000\
//10> Noun[date=+, time=+, mwe=+, masc=+, sg=+] @= |prep[form:fde]| noun[mois, CR:~], (num), prep[form:fa], noun[mois], num[CR:~], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]), (prep[form:fa,def], noun[postjour]). 

/(lundi 12/fin) mars 1987 (av. JC) (au soir)\
//15> Noun[date=+,time=+,mwe=+,masc=+,sg=+] @= (?[predate]), (noun[jour]), (num;?[premier];adv[date:+], (coord, num;?[premier];adv[date:+])), noun[mois, CR:~], (punct[form:fhyph], noun[mois, CR:~]), num[CR:~], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]), (prep[form:fa,def], noun[postjour]).


/(lundi) 15 mars (au soir)\
//15> Noun[date=+,time=+,mwe=+,masc=+,sg=+] @= (noun[jour]), num;?[premier];adv[date:+], (coord, (noun[jour]), num;?[premier]), noun[mois, CR:~], (prep[form:fa, def, CR:~], noun[postjour]).

/lundi 15 (au soir)\
//15> Noun[date=+,time=+,mwe=+] @= noun[jour], num;?[premier], prep[form:fa,def], noun[postjour].
//15> Noun[date=+,time=+,mwe=+] @= noun[jour], num;?[premier] |?[noun:~+, adj:~+, form:~fde, sfdentre:~+]|.

//-----fin bloc commenté-----


