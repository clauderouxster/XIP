
DiRègles:
/--------------  grammaire de dates ---------------------\


//. 
						



/lundi 12 mars 1987\
//14> Noun[date=+,time=+,mwe=+,masc=+,sg=+] -> (noun[jour]), (num), noun[mois], num.

/fin mars (1987)\
//14> Noun[date=+,time=+,mwe=+,masc=+,sg=+] -> noun[predate], (noun[jour]), (num), noun[mois], (num).

/fin  1987\
//14> Noun[date=+,time=+,mwe=+,masc=+,sg=+] -> noun[predate], (noun[jour]), (num), num.




/--------------  fin grammaire de dates ---------------------\





/PLRègles:

14>   [jour:+] < [mois:+]
14>   [jour:+] < [num:+]
14>   [prep:+] < [form:fjc]
14>   [mois:+] < [postjour:+]
14>   [form:fa] < [postjour:+]
14>   [predate:+] < [mois:+]
14>   [predate:+] < [num:+]
\

Séquence:
/deux noms de nombres font un seul nombre (quatre mille cinq-cent)\
10> Num @= num[noun:+], num[noun:+].


/(lundi 12/fin) mars 1987 (av. JC) (au soir)\

15> Noun[date=+,time=+,mwe=+,masc=+,sg=+] @= (noun[predate]), (noun[jour]), (num;adj[lemme:premier];adj[lemme:1er]), noun[mois], num, (prep, noun[form:fjc]), (prep[form:fa,def], noun[postjour]).


/(lundi) 15 mars (au soir)\
//15> Noun[date=+,time=+,mwe=+,masc=+,sg=+] @= (noun[jour]), num;adj[lemme:premier];adj[lemme:1er], noun[mois].
//15> Noun[date=+,time=+,mwe=+,masc=+,sg=+] @= (noun[jour]), num;adj[lemme:premier];adj[lemme:1er], noun[mois], prep[form:fa,def], noun[postjour].
15> Noun[date=+,time=+,mwe=+,masc=+,sg=+] @= (noun[jour]), num;adj[lemme:premier];adj[lemme:1er], noun[mois], (prep[form:fa,def], noun[postjour]).

/lundi 15 (au soir)\
15> Noun[date=+,time=+,mwe=+] @= noun[jour], num;adj[lemme:premier];adj[lemme:1er],(prep[form:fa,def]), (noun[postjour]).


/300 ap. JC\
15> noun[mwe=+, date=+] = num[date=+], prep, noun[form:fjc]. 

/en 1989, vers 1989  is a date dans certains contextes droits\
15> NOUN[mwe=+,time=+,date=+] =  |prep[form:fen];prep[form:fvers]| num[date=+] |punct ; sent; cm ; det;conj|.

/début 1980\
16> noun[mwe=+,time=+,date=+] =  noun[predate];adv[predate], num[date=+];noun[date:+].

/l'an 2000/les années 60/soixante (après JC)\

20> noun[mwe=+,time=+, date=+] = |det[def:+]| noun[lemme:an];noun[lemme:année], (prep[lemme:de], noun[lemme:grâce]), num[date=+];noun[date:+].

/le 9 thermidor an II\
20> noun[mwe=+,time=+, date=+] =  noun[date:+], noun[lemme:an], num[date=+].


/***************expressions temporelles*************\

/ trois jours, deux heures\
15> noun[mwe=+,time=+] =  |?[det:~,prep:~]| num[det:~], noun[time].


/un jour, un beau jour\
15> noun[mwe=+,time=+] =  det[indef, form:~fde], adj*, noun[time].


16> adv[mwe=+,time=+,quep=+] = pron[lemme=il], pron[lemme=y],
verb[aux:+,form:favoir], adv[lemme=peu] | ?[form:~fde] |.

16> adv[mwe=+,time=+,quep=+] = pron[lemme=il], pron[lemme=y],
verb[aux:+,form:favoir], adv*, ?*[verb:~,noun:~], noun[time:+].


/ il y a maintenant un quart de siècle \
16> adv[mwe=+,time=+,quep=+] = pron[lemme=il], pron[lemme=y],
verb[aux:+,form:favoir], adv*, det;num, noun[partit:+], prep, noun[time:+].

