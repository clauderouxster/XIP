//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


//Aude Rebotier
/--------------  grammaire de dates ---------------------\


Séquence:

/vingt et un est un nombre\

1> Num [gdnbre=+] = num[noun:+],(punct[form:fhyph]), coord, (punct[form:fhyph]),num.


/deux noms de nombres font un seul nombre (quatre mille cinq-cent)\
2> Num [gdnbre=+] @= num[noun:+], num[noun:+], (num[noun:+]), (punct[hyph:+], num[noun:+]), (coord, num).


/les mois musulmans composés\
2> noun[time=+, mwe=+, sg=+, mois=+]= noun[mois1:+], (punct[lemme:']), (punct[form:fhyph]), noun[mois2:+].

/Jésus Christ = Jésus-Christ\
2> noun[form=fjc, masc=+, sg=+, proper=+,p3=+] = noun[lemme:Jésus], noun[lemme:Christ].



/******Noms de dates "classiques"*******\
//MODIF maud :
// Sur reconnaissance des dates et traits assignés
// Si pb reprendre le bloc commenté ci-dessous

//Date absolue complète
/(lundi) 12 mars 1987\
3> Noun[date=+,time=+,mwe=+,masc=+,sg=+,complete=+,absolue=+] @=  (noun[jour]), num[numjourpot];?[premier], noun[mois, CR:~],  num[CR:~].

//
//Date absolue incomplète
/mars 1987\
4> Noun[date=+,time=+,mwe=+,masc=+,sg=+,incomplete=+,absolue=+] @=  noun[mois, CR:~],  num[CR:~].

//Dates relatives incomplètes : num et/ou nom de jour-mois sans indication de l'année
//(lundi) 15 mars \
5> Noun[date=+,time=+,mwe=+,masc=+,sg=+,relative=+] @= (noun[jour]), num[numjourpot];?[premier], noun[mois, CR:~] |?[noun:~+, adj:~+, form:~fde, sfdentre:~+]|.

// lundi 15 \
6> Noun[date=+,time=+,mwe=+,masc=+,sg=+,relative=+] @= noun[jour], num[numjourpot];?[premier] |?[noun:~+, adj:~+, form:~fde, sfdentre:~+]|.


// lundi,mars \
7> Noun[date=+,time=+,masc=+,sg=+,relative=+] @= noun[jour];noun[mois, CR:~] |?[noun:~+, adj:~+, form:~fde, sfdentre:~+]|.

/*********Fin modifs maud*****\

/300 ap. JC\
8> noun[mwe=+, date=+] = num[date=+], prep, noun[form:fjc].
8> noun[mwe=+, date=+] = num[date=+], prep, det[poss:+], noun[lemme:ère].



Marqueur:


/******Années seules (non reconnues avant)*****\

/un nombre de 4 chiffres commençant par 19 ou 20 est sans doute une date\
9> num[lemme:"19%d%d", 20e=+,date=+,time=+,absolue=+,incomplete=+].
9> num[lemme:"20%d%d", 21e=+,date=+,time=+,absolue=+,incomplete=+].

// Dates avec annee seule : absolue incomplète (uniquement en contexte):

//en 1989, vers 1989, dès 1989):
9> prep[predate,timesignal], num[gdnbre,time=+,date=+,annee=+,absolue=+,incomplete=+], punct[form:~fquotes] ; sent; cm ; det;conj;conjque;verb; coord; prep; pron; ?[CR].

//début 1999,printemps 1999:
9> noun[predate,timesignal], num[gdnbre,time=+,date=+,annee=+,absolue=+,incomplete=+].

//euro 2000, mondial 1999:
9>noun[predate], num[gdnbre,time=+,date=+,annee=+,absolue=+,incomplete=+].

//dater de +annee:
10> verb [lemme:dater], prep[form:fde], num[gdnbre:+, date=+,incomplete=+].

//6-21 n'est pas une date:
10> adv[lemme:"%d-%d%d", date=~].

//coord annees seules: en 1998 et 1999 : absolues incompletes
11> prep[form:fen], num[gdnbre:+,date:+,absolue:+,incomplete:+,time=+], coord, num[gdnbre:+, time=+, date=+,absolue=+,incomplete=+], punct[form:~fquotes] ; sent; cm ; det;conj;verb; coord; prep[form:~fen]; pron; ?[CR].


Séquence:


/les années 1920 et 1921\
12> noun[mwe=+,time=+, date=+] @= |det[def:+]| noun[lemme:année, pl:+], num, coord, num, prep, noun[form:fjc].
12> noun[mwe=+,time=+, date=+] @= |det[def:+]| noun[lemme:année, pl:+], num, coord, num, prep, det[poss:+], noun[lemme:ère].
12> noun[mwe=+,time=+, date=+] @= |det[def:+]| noun[lemme:année, pl:+], num, coord, num |punct[form:~fquotes] ; sent; cm ; det;conj;verb; coord|.

/début 1980; le premier trimestre 1985\
//18> noun[mwe=+,time=+,date=+] @=  (adj[ord:+];adj[lemme:dernier]), noun[predate];adv[predate], (prep[form:fde]), num[date=+,lettres:~];noun[date:+].

/l'an 2000 (après JC)\
13> noun[mwe=+,time=+, date=+] = |det[def:+]| noun[lemme:an; sg:+];noun[lemme:année; sg:+], (prep[lemme:de], noun[lemme:grâce]), num[date=+];noun[date:+].

/le 9 thermidor an II\
13> noun[mwe=+,time=+, date=+] = noun[date:+], noun[lemme:an], num.


/****************périodes*********************\

Marqueur:
14> noun[lemme:renaissance, maj:+, sg:+, periode=+].
14> noun[lemme:antiquité, maj:+, sg:+, periode=+].

/s. n'est l'abréviation de siècle que s'il est en minuscule\
14> noun[lemme:"s.", maj:+, siecle=~].


Séquence:

/les années 50 et (début) 60\
15> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:année, pl:+], num[rond], coord, (noun[predate:+]), num[rond], prep, noun[form:fjc].
15> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:année, pl:+], num[rond], coord, (noun[predate:+]), num[rond], prep, det[poss:+], noun[lemme:ère].
15> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:année, pl:+], num[rond], coord, (det[def:+]), noun[predate:+], num[rond].
15> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:année, pl:+], num[rond], coord, num[rond] |punct[form:~fquotes] ; sent; cm ; det;conj;verb; coord|.

/les années 50 (av. JC)\
15> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:année, pl:+], num[rond], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).


15> noun[mwe=+,time=+, periode=+] @= adj[ord:+];num[rom:+], (coord, (det[def:+];prep[def:+]), (noun[predate]), (det[def:+];prep[def:+]), adj[ord:+];num[rom:+]), noun[lemme:siècle];noun[lemme:millénaire], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).

/le IVe siècle (avant JC) et début IIIe\
15> noun[mwe=+,time=+, periode=+] @= adj[ord:+];num[rom:+], noun[siecle:+], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]), coord, (det[def:+];prep[def:+]), (noun[predate]), adj[ord:+];num[rom:+], (noun[siecle:+]), (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).

/le IVe (et le IIIe) siècle (av. JC)\
15> noun[mwe=+,time=+, periode=+] @= adj[ord:+];num[rom:+], (coord, (det[def:+];prep[def:+]), (noun[predate]), (det[def:+];prep[def:+]), adj[ord:+];num[rom:+]), noun[siecle:+], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).

15> noun[mwe=+,time=+, periode=+] @= num, noun[siecle:+, sg], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).

/l'âge du fer\
15> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:âge], prep[form:fde], (det[def:+]), noun[age:+].

/période néolithique\
15> noun[mwe=+, time=+, periode=+] = noun[ere], adj[periode:+];adj[periode2:+].

/l'Ancien Empire\
15> noun[mwe=+, time=+, periode=+] @= ?[empire1, maj], noun[lemme:empire, maj], (adj[empire2]).



/***************expressions temporelles*************\

/un jour, un beau jour\
//100> noun[mwe=+,time=+] =  det[indef, form:~fde], adj*, noun[time].


16> adv[mwe=+,time=+,quep=+] = pron[lemme=il], pron[lemme=y],verb[aux:+,form:favoir], adv[lemme=peu] | ?[form:~fde] |.

16> adv[mwe=+,time=+,quep=+] = pron[lemme=il], pron[lemme=y],verb[aux:+,form:favoir], adv*, ?*[verb:~,noun:~], noun[time:+].


/ il y a maintenant un quart de siècle \
16> adv[mwe=+,time=+,quep=+] = pron[lemme=il], pron[lemme=y],verb[aux:+,form:favoir], adv*, det;num, noun[partit:+], prep, noun[time:+].


//-----Bloc commenté pour test chgmt maud (cf.ci-dessous)-----

/du 15 (avril) au 20 mars\
//10> Noun[date=+, time=+, mwe=+, masc=+, sg=+] @= |prep[form:fde, def]| (noun[jour]), num[CR:~], (noun[mois, CR:~]), prep[form:fa, def], (noun[jour]), num[CR:~], noun[mois], (num[CR:~]), (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]), (prep[form:fa,def], noun[postjour]). 

/de décembre 1999 à janvier 2000\
//10> Noun[date=+, time=+, mwe=+, masc=+, sg=+] @= |prep[form:fde]| noun[mois, CR:~], (num), prep[form:fa], noun[mois], num[CR:~], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]), (prep[form:fa,def], noun[postjour]). 

/(lundi 12/fin) mars 1987 (av. JC) (au soir)\
//15> Noun[date=+,time=+,mwe=+,masc=+,sg=+] @= (?[predate]), (noun[jour]), (num;?[premier];adv[date:+], (coord, num;?[premier];adv[date:+])), noun[mois, CR:~], (punct[form:fhyph], noun[mois, CR:~]), num[CR:~], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]), (prep[form:fa,def], noun[postjour]).


/(lundi) 15 mars (au soir)\
//15> Noun[date=+,time=+,mwe=+,masc=+,sg=+] @= (noun[jour]), num;?[premier];adv[date:+], (coord, (noun[jour]), num;?[premier]), noun[mois, CR:~], (prep[form:fa, def, CR:~], noun[postjour]).

/lundi 15 (au soir)\
//15> Noun[date=+,time=+,mwe=+] @= noun[jour], num;?[premier], prep[form:fa,def], noun[postjour].
//15> Noun[date=+,time=+,mwe=+] @= noun[jour], num;?[premier] |?[noun:~+, adj:~+, form:~fde, sfdentre:~+]|.

//-----fin bloc commenté-----


