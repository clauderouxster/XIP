//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


// local grammars for ester



Marqueur:

//***

1> noun[prof], prep[form:fde], noun[batimentorg,org=+].
1> noun[lemme:président], prep[form:fde], noun[batimentorg,org=+].


//*** phenomenes meteo

1> noun[meteo], (adj[lemme:"tropical"]), noun[inconnu,maj,proper,meteo=+].
1> noun[meteo], (adj[lemme:"tropical"]), noun[pren,maj,proper,meteo=+].



// ###############Vehicules#############

1> noun[lemme:"lotus",maj,prevehic=+].

// le cinquantenaire de : annoté comme age dans ester 

1> det[def], noun[lemme:"cinquantenaire",ex_age=+],  prep[form:fde] .
1> det[def], noun[lemme:"centenaire",ex_age=+] , prep[form:fde] .
1> det[def], noun[lemme:"bicentenaire",ex_age=+] , prep[form:fde] .

1> noun[lemme:patron,admi=+], prep, (det), noun[org].

Séquence:
// de plus de

2> prep[form=fde,prep=+] = adv[lemme:"de plus"], prep[form:fde] .

// org : la Mission des Nations Unies au Bénin

2> noun[org=+,pol=+,mwe=+] @= noun[lemme:mission,pl:~], (adj[nat]), prep[form:fde], (det[def]), noun[org,firme:~], (prep[form:fen];prep[form:fa], noun[lieu,pays];noun[lieu,ville]) .

/ 1000, 1500 euros\

2> num = | ?[num:~] | num[datepot:~,date:~] , punct[form:fcm], num | noun | .
//2> num = | ?[num:~] | num , punct[form:fcm], num | noun[unit] | .


// **** Suites des fonctions 


2>  noun[prof=+,pol=+,mwe=+] @= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]), noun[lemme:"président"];noun[lemme:"vice-président"], (adj), prep[form:fde], (det[def]),  noun[org, pol], (adj[nat]) .

2>  noun[prof=+,pol=+,mwe=+] @= noun[presi];noun[lemme:roi], prep[form:fde], (det[def]), noun[lieu,pays];noun[region];noun[region1].

2>  noun[prof=+,admi=+,mwe=+] @= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]),  noun[lemme:"secrétaire"];noun[lemme:"sous-secrétaire"], (?[lemme:"général"]) ,  (?[lemme:adjoint];?[lemme:adjoindre];?[lemme:exécutif];adj[lemme:"permanent"]), (prep[form:fde] ,(det[def]), noun ) ,  prep[form:fde], (det[def]), (adj),  noun[org], (prep[form:fa] , (det[def]), noun , (prep[form:fde] ,noun , ( prep[form:fde] , (det[def]) , noun ))). 


2>  noun[prof=+,admi=+,mwe=+] @= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) , noun[lemme:"secrétaire"];noun[lemme:"sous-secrétaire"], ?[lemme:"général"];?[lemme:executif];adj[lemme:"permanent"], (prep[form:fa], (det[def]), noun[lemme:sport];noun[lemme:armée];noun[lemme:affaire];noun[lemme:culture], (adj)).

/le conseiller de l'Elysee pour la culture et la communication\

2> noun[prof=+,pol=+,mwe=+] @= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) , noun[lemme:"conseiller",pl:~], (prep[form:fde], (det[def]),  noun[maj,org,pol];noun[batimentorg,pol]) , prep[form:fpour];prep[form:fa], det[def], noun, (coord[lemme:et], det[def], noun ) .

//chef de X

3>  noun[prof=+,mwe=+]@= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) , noun[lemme:"chef",pl:~];noun[lemme:"président",pl:~], (prep[form:fde], noun[lemme:"honneur"]),(adj),  prep[form:fde], (det[def]), noun[org,!pol:!,!admi:!,!mil:!,non-profit:~,firme:~];noun[batimentorg], (adj[nat]).

3>  noun[prof=+,mwe=+]@= noun[lemme:"chef",pl:~] , prep[form:fde], (det[def]), noun[org,!pol:!,!admi:!,!mil:!,non-profit:~,firme:~] .

3>  noun[prof=+,mwe=+,admi=+] @= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) , noun[lemme:"chef",pl:~];noun[lemme:"président",pl:~],(adj;prep[form:fde], noun[lemme:"honneur"]), prep[form:fde], (det[def]), noun[org,non-profit];noun[org,firme] , (adj[nat]) .

3>  noun[prof=+,mwe=+,pol=+] @= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) , noun[lemme:"gouverneur",pl:~], (adj), prep[form:fde],(det),  noun[pays].

3>  noun[prof=+,mwe=+,pol=+] @= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) , noun[lemme:"ambassadeur",pl:~], (adj), prep[form:fde],(det),  noun[pays];noun[lemme:"royaume"], ( prep[form:fen];prep[form:fa] ,(det[def]) ,  noun[pays]) .

3>  noun[prof=+,mwe=+,grade=+] @=(adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) ,  noun[prof,grade], (adj), prep[form:fde],(det),  noun[pays];noun[lemme:"royaume"], ( prep[form:fen];prep[form:fa] ,(det[def]) ,  noun[pays]) .


3>  noun[prof=+,mwe=+,pol=+] @= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) , noun[lemme:"chef",pl:~], prep[form:fde], det[def], noun[lemme:"diplomatie"],  (adj), prep[form:fde],(det),  noun[pays].

3>  noun[prof=+,mwe=+,pol=+] @=(adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) ,  noun[lemme:"chef",pl:~], prep[form:fde], det[def], noun[lemme:"diplomatie"],  (adj[nat]).

3>  noun[prof=+,mwe=+,admi=+] @=(adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) ,  noun[prof,admi], (adj), prep[form:fde], (det), noun , noun[org].

3>  noun[prof=+,mwe=+,admi=+] @= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) , noun[prof,admi], (adj), prep[form:fde], (det), noun , (adj[partpas:~]), (prep[form:fde];prep[form:fa], (det[def]), noun[org]).

3>  noun[prof=+,mwe=+,grade=+] @= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) ,noun[prof,grade], (adj), prep[form:fde], (det), noun ,(adj), (prep[form:fde];prep[form:fa], (det[def]), noun[org]).

3>  noun[prof=+,mwe=+,admi=+] @= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) ,noun[lemme:représentant], (adj), prep[form:fde], (det), noun[org], (prep[form:fpour], det[def] , noun, (prep[form:fde], det[def], noun)) ,  ( prep[form:fde];prep[form:fen];prep[form:fa], noun[lieu,pays];noun[lieu,ville])  .

3>  noun[prof=+,mwe=+,pol=+] @= (adj[lemme:ancien];?[lemme:nouveau];adj[lemme:actuel]) , noun[lemme:leader], prep[form:fde], (det), noun[org,pol]. 

4> noun[prof=+,mwe=+,admi=+] @= noun[prof:+], prep[form:fde], noun[prof,mwe]. 



//################voies de circulation 

2> noun[lieu=+,line=+,mwe=+] = noun[preline], noun[maj];num .

//##########mesures#############################
// 1000, 1500 euros


2> noun[measure=+, mwe=+,unit_temper=+] = adv[lemme:"moins"];adv[lemme:"plus"],  num[det:~], noun[lemme:"degré"].

2> noun[measure=+, mwe=+,unit_temper=+] = punct[lemme:"-"];punct[lemme:"+"],  num[det:~], noun[lemme:"degré"].


2> NOUN[measure=+, mwe=+] @= num[det:~], noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h"], (coord[lemme:"et"]),  num[det:~], noun[unit,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h",cent].

2> NOUN[measure=+, mwe=+] @= num[det:~],  noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h",pren:~] , num+[rom:~] | | .

2> NOUN[measure=+, mwe=+] @= num[det:~],  noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h",pren:~] , num+[rom:~] | ?[measure:~] | .

2> NOUN[measure=+, mwe=+] @= (punct[lemme:"-"];punct[lemme:"+"]), num[det:~];noun[chiffres], (prep[form:fde]), noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h"], (coord[lemme:"et"], noun[lemme:"demi"]), (adj[lemme:brut]).

2> NOUN[measure=+, mwe=+] @= det[lemme:"un"], noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h",pren:~] , num+[card,lettres,rom:~], (noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h"]).


2> NOUN[measure=+, mwe=+] = | ?[det:~]| noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h",pren:~], num[det:~,mwe:~].

2> NOUN[measure=+, mwe=+] = num[det:~], noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h"].

2> NOUN[measure=+, mwe=+] = noun[lemme:million];noun[lemme:milliard] , prep[form:fde] ,  noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h"] .

//plusieurs dizaines de kilometres

2>  NOUN[measure=+, mwe=+] = |prep |  noun[t_mod:approx], prep[form:fde], noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h"].

2> NOUN[measure=+, mwe=+] = det, noun[t_mod:approx], prep[form:fde], noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h"].

3> NOUN[measure=+, mwe=+] = adv[lemme:"environ"];adv[lemme:"exactement"];adv[lemme:approximativement],  NOUN[measure, mwe].


// ###############Vehicules#############

// Le Karachi [ville] Express

2> noun[mwe=+,vehicule=+,entit=+] @= |?[def] | noun[ville], noun[maj,lemme:express] . 

// la navette (américaine) Challenger; la char Hotchkiss H 35

2> noun[mwe=+,vehicule=+,entit=+] @= noun[vehicule,mwe:~], (adj[geo]), noun+[maj,lettre:~,lieu:~,person:~,acron:~], (noun[lettre]), (num) | ?[maj:~] | .

2> noun[mwe=+,vehicule=+,entit=+] @= noun[vehicule,mwe:~], (adj[geo]), noun[maj,lettre:~,lieu:~,person:~,acron:~], (noun[lettre]), (num).

// le RER A

2>  noun[mwe=+,vehicule=+,entit=+]  @= noun[vehicule,mwe:~,pluslettre], noun[maj,lettre,lieu:~,person:~].

// Le porte-avion Charles de Gaulle

2> noun[mwe=+,vehicule=+,entit=+] @= noun[vehicule,mwe:~,lemme:~"ferry"],(adj[nat]), noun[person].

2> noun[mwe=+,vehicule=+,entit=+] @= noun[vehicule,mwe:~,lemme:~"ferry"],(adj[nat]), noun[person].

//########## Modeles de vehicules
// une Suzuki 4X4


2>  noun[mwe=+,vehicule=+,entit=+] @= noun[maj,prevehic], (noun[maj]), noun[lemme:"4X4"];noun[lemme:"4x4"].

2>  noun[mwe=+,vehicule=+,entit=+] @= noun[maj,prevehic], (noun[maj]), num[lemme:"4"], num[lemme:"x"] , num[lemme:"4"].

2>  noun[mwe=+,vehicule=+,entit=+] @= noun[maj,prevehic], ?*[prevehic:~,punct:~,coord:~], noun[lemme:"turbo",pl:~];noun[lemme:"AMG"], (noun[lemme:"diesel"]).

2>  noun[mwe=+,vehicule=+,entit=+] @= noun[maj,prevehic],num*, noun[maj, lemme:"Break"].

2>  noun[mwe=+,vehicule=+,entit=+] @= noun[maj,prevehic] , num[chiffres];noun[inconnu,maj,lemme:"%C%d+"].

2> noun[mwe=+,vehicule=+,entit=+] @= noun[maj,prevehic], noun[lemme:"classe"];noun[lemme:"type"], noun[lettre,maj];noun[inconnu,toutmaj];noun[inconnu,maj,lemme:"%C%d+"].

2> noun[mwe=+,vehicule=+,entit=+] @= noun[prevehic,pren:~],noun[inconnu,maj,proper].


2>  noun[mwe=+,vehicule=+,entit=+] @= |  prep[sfa], noun[lemme:"bord"],  prep[sfde], det |  noun[maj] , num[chiffres,numroi:-];noun[inconnu,maj,lemme:"%C%d+"].

2> noun[mwe=+,vehicule=+,entit=+] @= | verb[lemme:"rouler"];verb[lemme:"circuler"],  prep[sfen] | noun[maj, lettre:~,lieu:~], num[chiffres,numroi:-];noun[inconnu,maj,lemme:"%C%d+"].

2> noun[mwe=+,vehicule=+,entit=+] @= | verb[lemme:"conduire"], det |  noun[maj, prevehic], num[chiffres,numroi:-];noun[inconnu,maj,lemme:"%C%d+"].

//50> noun[lemme:"naufrage"],  prep[sfde], (det) , noun[maj,proper, vehicule=+,entit=+].
2> noun[mwe=+,vehicule=+,entit=+] @= | noun[lemme:"passager"],  prep[sfde], (det) |  noun[maj,proper,person:~,pren:~,org:~], num[chiffres,numroi:-];noun[inconnu,maj,lemme:"%C%d+"].

2> noun[mwe=+,vehicule=+,entit=+] @= | noun[lemme:"naufragé"],  prep[sfde], (det) | noun[maj,proper], num[chiffres,numroi:-];noun[inconnu,maj,lemme:"%C%d+"].


2> noun[mwe=+,vehicule=+,entit=+] @= | det[poss] | noun[maj,prevehic], num[chiffres,numroi:-];noun[inconnu,maj,lemme:"%C%d+"].


2>  noun[mwe=+,vehicule=+,entit=+] @= |noun[vehicule] | det[def], noun[maj]. 

// groupes musique 

4>  noun[mwe=+,div=+,entit=+,org=+] @= noun[lemme:"thé"],  noun[maj,org,div,plusdet,!musique:!].



4>  noun[mwe=+,div=+,entit=+,org=+] @=  | noun[prediv] |  noun+[guessed,entit=+,org=+,div=+].
4>  noun[mwe=+,div=+,entit=+,org=+] @=  noun+[guessed,entit=+,org=+,div=+] | noun[postdiv] | .

// Equipes sportives 

4> noun[mwe=+,div=+,entit=+,org=+]@=  noun[lemme:"équipe",sg], prep[sfde], noun[lieu,proper], (prep[sfde], noun[sport,event:~], (prep[sfa], num[rom])).

4> noun[mwe=+,div=+,entit=+,org=+]@=  noun[lemme:"équipe",sg], prep[sfde] ,noun[sport,event:~], (prep[sfde],  noun[lieu,proper]).

// emissioms de radio/tv

4> noun[mwe=+,div=+,entit=+,org=+] = | verb[lemme:"être"] | det[def], noun[lemme:"téléphone"], verb[lemme:"sonner"]. 

// prix

4>  noun[mwe=+,award=+,entit=+] @=   noun[preaward],   noun+[maj,proper,inconnu].

4>  noun[mwe=+,award=+,entit=+] @=  | ?[ord:~] |  noun[preaward],   noun[person].

4>  noun[mwe=+,award=+,entit=+] @=    noun[preaward], noun[lemme:"Nobel"], (prep[form:fde], (det), noun).



// "Le/un/du César du Meilleur espoir féminin

4>  noun[mwe=+,award=+,entit=+] @= |det;prep[def]|  noun[preaward], prep[form:fde], (det), adj[lemme:"meilleur"], noun[inaward], (adj). 

4>  noun[mwe=+,award=+,entit=+] @= |det;prep[def]|  noun[preaward], prep[form:fde], (det), adj[lemme:"meilleur"], noun[inaward], prep[form:fde], noun[inaward] . 

5>  noun[mwe=+,award=+,entit=+] @= |det;prep[def]|  noun[award], prep[form:fde], (det), adj[lemme:"meilleur"], noun[inaward], (adj). 

5>  noun[mwe=+,award=+,entit=+] @= |det;prep[def]|  noun[award], prep[form:fde], (det), adj[lemme:"meilleur"], noun[inaward], prep[form:fde], noun[inaward] . 

//anglais 

5>  noun[mwe=+,award=+,entit=+] @= |det;prep[def]| noun[lemme:"Award"] , prep[lemme:of], noun+[inconnu:+,maj].

5>  noun[mwe=+,award=+,entit=+] @= |det;prep[def]| noun+[inconnu:+,maj], noun[lemme:"Award"].

/ la loi Aubry \


5>  noun[mwe=+,docu=+,entit=+] @= |det[def] | noun[lemme:"loi"], noun[person].

/la loi de 1998\


5> noun[mwe=+,docu=+,entit=+] @= |det[def];prep[form:fde]| noun[lemme:"loi"], prep[form:fde], noun[lemme:finance], (num[datepot]).

5> noun[mwe=+,docu=+,entit=+] @= |det[def] | noun[lemme:"accord",pl],  prep[form:fde] , noun[lieu].

5>  noun[mwe=+,docu=+,entit=+] @= |det[def];prep[form:fde]|  noun[lemme:"loi"];noun[lemme:"directive"], prep[form:fde], num.

/ la convention de Vienne \ 

5> noun[mwe=+,docu=+,entit=+] @= |det[def] | noun[lemme:"convention"], prep[form:fde], noun[lieu].

/ le traité de XX\ 

5> noun[mwe=+,docu=+,entit=+] @= |?[def] | noun[lemme:"traité"], prep[form:fde], noun.

// Modif Infomafgic
// de 5 a 7 euros 
//ajout modif maud sur ces 2 regles : le num suivant la prep ne doit porter le trait annee

5> np[mwe=+,entit=+,unit:!]= ?[lemme:"moins"];?[lemme:plus],  num[annee:~], prep[form:fa],  noun[measure,!unit:!].


5> pp[mwe=+,entit=+,unit:!] @= prep, num[annee:~], prep[form:fa], noun[measure,!unit:!].

// (quelques) 5 a 7 euros

5> np[mwe=+,entit=+,unit:!] @= (det),  num[annee:~], prep[form:fa], noun[measure,!unit:!].

// produits 
 
5> noun[docu=+,entit=+] @= | det[def,fem] | noun[lemme:"résolution"], num+. 

5> noun[docu=+,entit=+] @= noun[lemme:"plan"], noun[person];noun[proper,guessed].

5> noun[titreOeuvre=+,entit=+,mwe=+] = | noun[lemme:rythme];noun[lemme:son], prep[form:fde] | det[def], noun[lemme:Marseillaise] . 


//******************A DISCUTER
// le ministre français des Affaires étrangères, XXX --> personne

5>noun[person=+,mwe=+,entit=+] @=  | | noun[prof,ari];noun[prof,pol,notfonc:~];noun[prof,admi];noun[prof,reli];noun[prof,mwe], (punct[form:fcm]) , (det[def] , (?[nat])) ,(noun[civil,tit]),   noun[person] .

5>noun[person=+,mwe=+,entit=+] @=  |?[lemme:~être] | noun[prof,ari];noun[prof,pol,notfonc:~];noun[prof,admi];noun[prof,reli];noun[prof,mwe], (punct[form:fcm]) , (det[def] , (?[nat])) ,(noun[civil,tit]),   noun[person] .


5>noun[person=+,mwe=+,entit=+] @= |?[lemme:~être]|  (det[def]), noun[prof,ari];noun[prof,pol];noun[prof,admi];noun[prof,reli];noun[prof,mwe], coord[lemme:et], noun[prof,ari];noun[prof,pol];noun[prof,admi];noun[prof,reli];noun[prof,mwe], (punct[form:fcm]), (noun[civil,tit]), noun[person].

5>noun[person=+,mwe=+,entit=+] @= | |  (det[def]), noun[prof,ari];noun[prof,pol];noun[prof,admi];noun[prof,reli];noun[prof,mwe], coord[lemme:et], noun[prof,ari];noun[prof,pol];noun[prof,admi];noun[prof,reli];noun[prof,mwe], (punct[form:fcm]), (noun[civil,tit]), noun[person].

//Machin chouette, le minstre de blabla, 
// 

//5> noun[person=+,mwe=+,entit=+] @= | ?[prof:~] | noun[person] ,  (punct[form:fcm]) , (det[def]), noun[prof,ari];noun[prof,pl:~,pol];noun[prof,pl:~,admi];noun[prof,pl:~,reli] | punct;coord;verb;sent;adv | .


//5> noun[person=+,mwe=+,entit=+] @= | | noun[person] ,  (punct[form:fcm]) , (det[def]), noun[prof,ari];noun[prof,pl:~,pol];noun[prof,admi];noun[prof,pl:~,reli]  | punct;coord;verb;sent;adv | .

// COMMENTE POUR CAUSE DE RATTACHEMENTS FAUX
//5> noun[person=+,mwe=+,entit=+] @= noun[person] ,  (punct[form:fcm]) , det[def], (adj[lemme:ancien];adj[lemme:nouveau];adj[lemme:actuel]) , noun[prof,ari];noun[prof,pol];noun[prof,admi];noun[prof,reli];noun[prof,mwe] | punct;coord;verb;sent;prep;adv | .
 
5> noun[person=+,mwe=+,entit=+] @= noun[person] ,  (punct[form:fcm]) ,  noun[prof,ari];noun[prof,pol];noun[prof,admi];noun[prof,reli];noun[prof,mwe], coord[lemme:et], noun[prof,pol];noun[prof,admi];noun[prof,reli];noun[prof,ari];noun[prof,mwe]  | punct;coord;verb;sent;prep;adv | .

5> noun[person=+,mwe=+,entit=+] @= noun[person] ,  (punct[form:fcm]) , noun[prof,ari];noun[prof,pol];noun[prof,admi];noun[prof,reli];noun[prof,mwe] | punct;coord;verb;sent;prep;adv | .

5> noun[person=+,mwe=+,entit=+] @= noun[person] ,  (punct[form:fcm]) , adj[lemme:ancien];adj[lemme:nouveau];adj[lemme:actuel], noun[prof,ari];noun[prof,pol];noun[prof,admi];noun[prof,reli];noun[prof,mwe] | punct;coord;verb;sent;prep;adv | .


//5> noun[person=+,mwe=+,entit=+] @= noun[person] ,  (punct[form:fcm]) , det[def], (adj[lemme:ancien];adj[lemme:nouveau];adj[lemme:actuel]) , noun[prof,ari];noun[prof,pol];noun[prof,admi];noun[prof,reli];noun[prof,mwe] | punct;coord;verb;sent;prep;adv | .


//5> noun[person=+,mwe=+,entit=+] @= | | noun[person] ,  (punct[form:fcm]) , (det[def]), noun[prof,ari];noun[prof,pol];noun[prof,admi];noun[prof,reli];noun[prof,mwe]   | punct;coord;verb;sent;adv | .


/le ministre delegue aupres du ministre gnagna \

11>  noun[prof=+,pol=+,mwe=+] @= noun[lemme:"ministre"];noun[lemme:"vice-ministre"];noun[lemme:"porte-parole"], (adj[nat]), verb[lemme:"déléguer", partpas];adj[lemme:"délégué"] , prep[lemme:"auprès de"] ,( det[def]), noun[mwe,prof] .

/ le représentant du secretaire general de l'ONU au darfour\

11>  noun[prof=+,admi =+,mwe=+] =@ noun[lemme:"représentant"] , prep[form:fde], (det[def]), noun[prof]. 

11>  noun[prof=+,admi =+,mwe=+] =@ noun[lemme:chef], prep[form:fde], det[def], noun[lemme:mission], ?[lemme:conjoint];?[lemme:conjoindre],  noun[org], noun[org]. 

// Sa majeste Machin, Titre --> pers


//5>noun[person=+,mwe=+,entit=+]   @=  noun[person], punct[form:fcm], noun[prof] | ?[person:~] |.

// A VERIFIER

4>  noun[prof=+,mwe=+] @= noun[prof,mwe,!admi:!,!pol:!], prep[form:fde];prep[form:fen];prep[form:fa], noun[lieu,pays];noun[lieu,ville].

4> noun[prof=+,mwe=+,admi=+] @= noun[lemme:"représentant"], (adj), prep[form:fde], (det[def]),  noun[prof] , (prep[form:fen];prep[form:fa], noun[lieu,pays];noun[lieu,ville]) . 

// URL 3 W point France Inter point com

4> noun[url=+,mwe=+] @= num[lemme:3], noun[lemme:w], ?[lemme:point], noun;adj, ( noun;adj), (noun;adj), ?[lemme:point], prefix[lemme:co],  noun[lemme:m].

4> noun[url=+,mwe=+] @=  noun;adj, ( noun;adj), (noun;adj), ?[lemme:point], prefix[lemme:co],  noun[lemme:m].


5> noun[url=+,mwe=+] @=  noun, ?[lemme:point], noun[lemme:Fr].


/lieux\

5> noun[batiment=+,lieu=+,entit=+] = noun[lemme:"mosquée"], adj[lemme:"rouge"].

Marqueur:

6> ?[def], noun[lemme:"président",pol=+,pl:~]. 

6> prep[form:fde], noun[lemme:"président",pol=+,pl:~]. 


// par defaut on considere que x milliard et x million sont des monnaies

6> num[lemme:"%d+ milliards",measure=+,unit_monnaie=+,mwe=+,etportion:~], ?[form:~fde] .

6> num[lemme:"%d+ millions",measure=+,unit_monnaie=+,mwe=+,etportion:~], ?[form:~fde,lemme:~"d'entre"].

6> num[lemme:"%c+ milliards",measure=+,unit_monnaie=+,mwe=+,etportion:~], ?[form:~fde].

6> num[lemme:"%c+ millions",measure=+,unit_monnaie=+,mwe=+,etportion:~], ?[form:~fde].

// 6 metres sur 2

50> noun[mwe,unit_len], prep[lemme:"sur"], num[unit_len=+,mwe=+].

50> num[unit_vit=+,mwe=+,measure=+], coord;punct[form:fcm], noun[mwe,unit_vit].

50> prep[sfa], noun[lemme:"bord"],  prep[sfde], det , noun[maj,vehicule=+,entit=+].
50> verb[lemme:"rouler"], prep[sfen],  noun[maj, lettre:~,vehicule=+,entit=+,lieu:~].
50> verb[lemme:"circuler"], prep[sfen],  noun[maj, vehicule=+,entit=+,lieu:~].
50> verb[lemme:"conduire"], det,  noun[maj, prevehic, vehicule=+,entit=+].

//50> noun[lemme:"naufrage"],  prep[sfde], (det) , noun[maj,proper, vehicule=+,entit=+].
50> noun[lemme:"passager"],  prep[sfde], (det) , noun[maj,proper, vehicule=+,entit=+,person:~,pren:~,org:~].
50> noun[lemme:"naufragé"],  prep[sfde], (det) , noun[maj,proper, vehicule=+,entit=+].


50> det[poss], noun[maj,prevehic,vehicule=+,entit=+].

50> noun[prediv], noun[guessed,entit=+,org=+,div=+].
50> noun[guessed,entit=+,org=+,div=+], noun[postdiv].

50>  det[lemme:"le"], noun[maj,org=+,div,plusdet,entit=+].

// TEST
//50> det[def], noun[person,bidon=+].

//50>  det[indef], noun[prof=~,pol=~].
//50>  det[indef], noun[prof=~,admi=~].


// PRODUITS DIVERS 

50> ?[def], noun[lemme:"terre",maj,lieu=+,geog=+].
50> ?[def], noun[lemme:"lune",maj,lieu=+,geog=+,oronyme=+].
50> ?[def], noun[lemme:"soleil",maj,lieu=+,geog=+,oronyme=+].


// la roquette Katioucha

40> noun[lemme:"roquette"] , noun[proper,inconnu,prodmisc=+]  .


Séquence:

/quelques dizaine de million d'euros\


60> noun[traits:!] = det, noun[t_mod:approx], prep[form:fde] , noun[unit,!traits:!].  
