//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


// local grammars for ester



Marqueur:


//*** phenomenes meteo

1> noun[meteo], (adj[lemme:"tropical"]), noun[inconnu,maj,proper,meteo=+].
1> noun[meteo], (adj[lemme:"tropical"]), noun[pren,maj,proper,meteo=+].



// ###############Vehicules#############

1> noun[lemme:"lotus",maj,prevehic=+].

// le cinquantenaire de : annoté comme age dans ester 

1> det[def], noun[lemme:"cinquantenaire",ex_age=+],  prep[form:fde] .
1> det[def], noun[lemme:"centenaire",ex_age=+] , prep[form:fde] .
1> det[def], noun[lemme:"bicentenaire",ex_age=+] , prep[form:fde] .



Séquence:

// **** Suites des fonctions 




2>  noun[prof=+,admi=+,mwe=+] @= noun[lemme:"secrétaire"], (?[lemme:"général"];?[lemme:exécutif];adj[lemme:"permanent"]), (prep[form:fde] ,(det[def]), noun ) ,  prep[form:fde], (det[def]), (adj),  noun[org]. 


2>  noun[prof=+,admi=+,mwe=+] @= noun[lemme:"secrétaire"], ?[lemme:"général"];?[lemme:executif];adj[lemme:"permanent"] .
 
//chef de X

2>  noun[prof=+,mwe=+]@= noun[lemme:"chef"];noun[lemme:"président"],(adj;prep[form:fde], noun[lemme:"honneur"]),  prep[form:fde], (det[def]), noun[org,!pol:!,!admi:!,!mil:!,non-profit:~,firme:~], (adj[nat]).

2>  noun[prof=+,mwe=+,admi=+] @= noun[lemme:"chef"];noun[lemme:"président"],(adj;prep[form:fde], noun[lemme:"honneur"]), prep[form:fde], (det[def]), noun[org,non-profit];noun[org,firme] , (adj[nat]) .

2>  noun[prof=+,mwe=+,pol=+] @= noun[lemme:"gouverneur"], (adj), prep[form:fde],(det),  noun[pays].

2>  noun[prof=+,mwe=+,pol=+] @= noun[lemme:"ambassadeur"], (adj), prep[form:fde],(det),  noun[pays].


3>  noun[prof=+,mwe=+,admi=+] @= noun[prof,admi], (adj), prep[form:fde], (det), noun , (prep[form:fde];prep[form:fa], (det[def]), noun[org]).

//################voies de circulation 

2> noun[lieu=+,line=+,mwe=+] = noun[preline], noun[maj];num .

//##########mesures#############################

2> noun[measure=+, mwe=+,unit_temper=+] = adv[lemme:"moins"];adv[lemme:"plus"],  num[det:~], noun[lemme:"degré"].

2> NOUN[measure=+, mwe=+] @= num[det:~], noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h"], (coord[lemme:"et"]),  num[det:~], noun[unit,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h",cent].

2> NOUN[measure=+, mwe=+] @= num[det:~],  noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h",pren:~] , num+[rom:~] | | .

2> NOUN[measure=+, mwe=+] @= num[det:~],  noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h",pren:~] , num+[rom:~] | ?[measure:~] | .

2> NOUN[measure=+, mwe=+] = (punct[lemme:"-"];punct[lemme:"+"]), num[det:~], (prep[form:fde]), noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h"].

2> NOUN[measure=+, mwe=+] @= det[lemme:"un"], noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h",pren:~] , num+[card,lettres,rom:~], (noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h"]).


2> NOUN[measure=+, mwe=+] = | ?[det:~]| noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h",pren:~], num[det:~,mwe:~].

2> NOUN[measure=+, mwe=+] = num[det:~], noun[!unit:!,measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,lemme:~"h"].





// ###############Vehicules#############

// la navette (américaine) Challenger; la char Hotchkiss H 35

2> noun[mwe=+,vehicule=+,entit=+] @= noun[vehicule,mwe:~], (adj[geo]), noun+[maj,lettre:~,lieu:~,person:~,acron:~], (noun[lettre]), (num) | ?[maj:~] | .

2> noun[mwe=+,vehicule=+,entit=+] @= noun[vehicule,mwe:~], (adj[geo]), noun[maj,lettre:~,lieu:~,person:~,acron:~], (noun[lettre]), (num).

// le RER A

2>  noun[mwe=+,vehicule=+,entit=+]  @= noun[vehicule,mwe:~,pluslettre], noun[maj,lettre,lieu:~,person:~].

// Le porte-avion Charles de Gaulle

2> noun[mwe=+,vehicule=+,entit=+] @= noun[vehicule,mwe:~,lemme:~"ferry"],(adj[geo]), noun[person].

2> noun[mwe=+,vehicule=+,entit=+] @= noun[vehicule,mwe:~,lemme:~"ferry"],(adj[geo]), noun[person].

//########## Modeles de vehicules
// une Suzuki 4X4


2>  noun[mwe=+,vehicule=+,entit=+] @= noun[maj,prevehic], (noun[maj]), noun[lemme:"4X4"];noun[lemme:"4x4"].

2>  noun[mwe=+,vehicule=+,entit=+] @= noun[maj,prevehic], (noun[maj]), num[lemme:"4"], num[lemme:"x"] , num[lemme:"4"].

2>  noun[mwe=+,vehicule=+,entit=+] @= noun[maj,prevehic], ?*[prevehic:~,punct:~,coord:~], noun[lemme:"turbo",pl:~];noun[lemme:"AMG"], (noun[lemme:"diesel"]).

2>  noun[mwe=+,vehicule=+,entit=+] @= noun[maj,prevehic],num*, noun[maj, lemme:"Break"].

2>  noun[mwe=+,vehicule=+,entit=+] @= noun[maj,prevehic] , num[chiffres];noun[inconnu,maj,lemme:"%C%d+"].

2> noun[mwe=+,vehicule=+,entit=+] @= noun[maj,prevehic], noun[lemme:"classe"];noun[lemme:"type"], noun[lettre,maj];noun[inconnu,toutmaj];noun[inconnu,maj,lemme:"%C%d+"].

2> noun[mwe=+,vehicule=+,entit=+] @= noun[prevehic],noun[inconnu,maj,proper].


2>  noun[mwe=+,vehicule=+,entit=+] @= |  prep[sfa], noun[lemme:"bord"],  prep[sfde], det |  noun[maj] , num[chiffres,numroi:-];noun[inconnu,maj,lemme:"%C%d+"].

2> noun[mwe=+,vehicule=+,entit=+] @= | verb[lemme:"rouler"];verb[lemme:"circuler"],  prep[sfen] | noun[maj, lettre:~,lieu:~], num[chiffres,numroi:-];noun[inconnu,maj,lemme:"%C%d+"].

2> noun[mwe=+,vehicule=+,entit=+] @= | verb[lemme:"conduire"], det |  noun[maj, prevehic], num[chiffres,numroi:-];noun[inconnu,maj,lemme:"%C%d+"].

//50> noun[lemme:"naufrage"],  prep[sfde], (det) , noun[maj,proper, vehicule=+,entit=+].
2> noun[mwe=+,vehicule=+,entit=+] @= | noun[lemme:"passager"],  prep[sfde], (det) |  noun[maj,proper,person:~,pren:~,org:~], num[chiffres,numroi:-];noun[inconnu,maj,lemme:"%C%d+"].

2> noun[mwe=+,vehicule=+,entit=+] @= | noun[lemme:"naufragé"],  prep[sfde], (det) | noun[maj,proper], num[chiffres,numroi:-];noun[inconnu,maj,lemme:"%C%d+"].


2> noun[mwe=+,vehicule=+,entit=+] @= | det[poss] | noun[maj,prevehic], num[chiffres,numroi:-];noun[inconnu,maj,lemme:"%C%d+"].


2>  noun[mwe=+,vehicule=+,entit=+] @= |noun[vehicule] | det[def], noun[maj]. 

// groupes musique 

4>  noun[mwe=+,div=+,entit=+,org=+] @= noun[lemme:"thé"],  noun[maj,org,div,plusdet,!musique:!].



4>  noun[mwe=+,div=+,entit=+,org=+] @=  | noun[prediv] |  noun+[guessed,entit=+,org=+,div=+].
4>  noun[mwe=+,div=+,entit=+,org=+] @=  noun+[guessed,entit=+,org=+,div=+] | noun[postdiv] | .

// Equipes sportives 

4> noun[mwe=+,div=+,entit=+,org=+]@=  noun[lemme:"équipe",sg], prep[sfde], noun[lieu,proper], (prep[sfde], noun[sport,event:~], (prep[sfa], num[rom])).

4> noun[mwe=+,div=+,entit=+,org=+]@=  noun[lemme:"équipe",sg], prep[sfde] ,noun[sport,event:~], (prep[sfde],  noun[lieu,proper]).

// emissioms de radio/tv

4> noun[mwe=+,div=+,entit=+,org=+] = | verb[lemme:"être"] | det[def], noun[lemme:"téléphone"], verb[lemme:"sonner"]. 

// prix

4>  noun[mwe=+,award=+,entit=+] @=   noun[preaward],   noun+[maj,proper,inconnu].

4>  noun[mwe=+,award=+,entit=+] @=  | ?[ord:~] |  noun[preaward],   noun[person].

4>  noun[mwe=+,award=+,entit=+] @=    noun[preaward], noun[lemme:"Nobel"], (prep[form:fde], (det), noun).



// "Le/un/du César du Meilleur espoir féminin

4>  noun[mwe=+,award=+,entit=+] @= |det;prep[def]|  noun[preaward], prep[form:fde], (det), adj[lemme:"meilleur"], noun[inaward], (adj). 

4>  noun[mwe=+,award=+,entit=+] @= |det;prep[def]|  noun[preaward], prep[form:fde], (det), adj[lemme:"meilleur"], noun[inaward], prep[form:fde], noun[inaward] . 

5>  noun[mwe=+,award=+,entit=+] @= |det;prep[def]|  noun[award], prep[form:fde], (det), adj[lemme:"meilleur"], noun[inaward], (adj). 

5>  noun[mwe=+,award=+,entit=+] @= |det;prep[def]|  noun[award], prep[form:fde], (det), adj[lemme:"meilleur"], noun[inaward], prep[form:fde], noun[inaward] . 

//anglais 

5>  noun[mwe=+,award=+,entit=+] @= |det;prep[def]| noun[lemme:"Award"] , prep[lemme:of], noun+[inconnu:+,maj].

5>  noun[mwe=+,award=+,entit=+] @= |det;prep[def]| noun+[inconnu:+,maj], noun[lemme:"Award"].

/ la loi Aubry \


5>  noun[mwe=+,docu=+,entit=+] @= |det[def] | noun[lemme:"loi"], noun[person].

5> noun[mwe=+,docu=+,entit=+] @= |det[def];prep[form:fde]| noun[lemme:"loi"], prep[form:fde], noun[lemme:finance], (num[datepot]).

5> noun[mwe=+,docu=+,entit=+] @= |det[def] | noun[lemme:"accord",pl],  prep[form:fde] , noun[ville].

/ la convention de Vienne \ 

5> noun[mwe=+,docu=+,entit=+] @= |det[def] | noun[lemme:"convention"], prep[form:fde], noun[lieu].

/ le traité de XX\ 

5> noun[mwe=+,docu=+,entit=+] @= |?[def] | noun[lemme:"traité"], prep[form:fde], noun.

// Modif Infomafgic
// de 5 a 7 euros 
//ajout modif maud sur ces 2 regles : le num suivant la prep ne doit porter le trait annee

5> pp[mwe=+,entit=+,unit:!] @= prep, num[annee:~], prep[form:fa], noun[measure,!unit:!].

// 5 a 7 euros

5> np[mwe=+,entit=+,unit:!] @=  num[annee:~], prep[form:fa], noun[measure,!unit:!].

// produits 

5> noun[docu=+,entit=+] @= | det[def,fem] | noun[lemme:"résolution"], num+. 


//******************A DISCUTER
// le ministre français des Affaires étrangères, XXX --> personne

5>noun[person=+,mwe=+,entit=+] @= |?[def]| noun[prof], punct[form:fcm], noun[person] .

Marqueur:

// par defaut on considere que x milliard et x million sont des monnaies

6> num[lemme:"%d+ milliards",measure=+,unit_monnaie=+,mwe=+].

6> num[lemme:"%d+ millions",measure=+,unit_monnaie=+,mwe=+].

6> num[lemme:"%c+ milliards",measure=+,unit_monnaie=+,mwe=+].

6> num[lemme:"%c+ millions",measure=+,unit_monnaie=+,mwe=+].

// 6 metres sur 2

50> noun[mwe,unit_len], prep[lemme:"sur"], num[unit_len=+,mwe=+].

50> num[unit_vit=+,mwe=+,measure=+], coord;punct[form:fcm], noun[mwe,unit_vit].

50> prep[sfa], noun[lemme:"bord"],  prep[sfde], det , noun[maj,vehicule=+,entit=+].
50> verb[lemme:"rouler"], prep[sfen],  noun[maj, lettre:~,vehicule=+,entit=+,lieu:~].
50> verb[lemme:"circuler"], prep[sfen],  noun[maj, vehicule=+,entit=+,lieu:~].
50> verb[lemme:"conduire"], det,  noun[maj, prevehic, vehicule=+,entit=+].

//50> noun[lemme:"naufrage"],  prep[sfde], (det) , noun[maj,proper, vehicule=+,entit=+].
50> noun[lemme:"passager"],  prep[sfde], (det) , noun[maj,proper, vehicule=+,entit=+,person:~,pren:~,org:~].
50> noun[lemme:"naufragé"],  prep[sfde], (det) , noun[maj,proper, vehicule=+,entit=+].


50> det[poss], noun[maj,prevehic,vehicule=+,entit=+].

50> noun[prediv], noun[guessed,entit=+,org=+,div=+].
50> noun[guessed,entit=+,org=+,div=+], noun[postdiv].

50>  det[lemme:"le"], noun[maj,org=+,div,plusdet,entit=+].

// TEST
50> det[def], noun[person,bidon=+].

//50>  det[indef], noun[prof=~,pol=~].
//50>  det[indef], noun[prof=~,admi=~].
