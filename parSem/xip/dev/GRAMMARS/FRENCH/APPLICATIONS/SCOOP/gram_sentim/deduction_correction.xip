// deduction_generique.xip
//créé Driss Sadoun


Déduction:


// SPECIFIQUE STRUCTURE


|IV{prep,verb#1[lemme:"améliorer",negative=+,topic_obj=+,exp_subj=+]},punct[lemme:":"],NP,punct*[lemme:"%,"],NP{?*,noun#2}|
        if (~OBJ(#1,#2))
        OBJ(#1,#2)

// vaut le détour : détour n'est pas sujet

if ( ^SUBJ[right](#1[lemme:"valoir"],#2[lemme:"détour"]))
   OBJ(#1,#2)

// serveur peu aimable et inefficaces 

|AP{adv#1,adj#2[positive], coord, adj[negative]}|
       ADJMOD(#2,#1)

// menu sans interet, indigne de ... : NMOD indigne menu

|NP#{?*,#1},PP{prep[lemme:"sans"],NP{?*,noun#2[lemme:"intérêt"]}},punct,AP{?*,#3}|
        if ( ^NMOD(#2,#5) )
        NMOD(#1,#5)



/***************marquage contextuel  sur dependances ************************ \


|verb#1[lemme:"conforter",positive=+,topic_obj=+,exp_subj=+]|
    if (OBJ(#1,#2[lemme:"place"]))
    ~
   
//ferme ta gueule
|#1[negative=+,topic_subj=+,exp_obj=+,expression=+]|
       if (OBJ(#1[lemme:"fermer"],#2[lemme:"gueule"]))
   ~

// trainer dans la boue

|#1[negative=+,topic_subj=+,expression=+]|
       if (VMOD(#1[lemme:"traîner"],#2[lemme:"boue"]))
   ~

// attendre ou vouloir + EN-PERS

|#1[positive=+,topic_obj=+,exp_subj=+]|
if (OBJ(#1[lemme:"vouloir"],#2) & PERSONNE(#2) )
   ~

|#1[positive=+,topic_obj=+,exp_subj=+]|
if (OBJ(#1[lemme:"attendre"],#2) & PERSONNE(#2) )
   ~

// prendre l'eau

|#1[negative=+,topic_subj=+,expression=+]|
if (OBJ(#1[lemme:"prendre"],#2[lemme:"eau"]))
   ~


// en miettes

|#1[lemme:"miette",sents=+,terminal,negative=+]|
        if (PREPOBJ(#1,#2[lemme:"en"]))
        ~


// digne mais pas "digne de"

|#2[lemme:"digne",positive=+,sents=+,terminal]| 
     if (~ADJMOD(#2,?))
     ~

// mentir à merveille

|#2[positive=~,sents=~]|
        if (VMOD[POSIT1](#1[negative],#2[lemme:"à merveille"]))
        ~

// joli destastre

 |#2[positive=~,sents=~]|
if (NMOD(#1[negative],#2[lemme:"joli"]))
   ~
// il y a mieux

|#3[negative=+,expression=+]|
if ( SUBJ[imperso](#1[lemme:"avoir"],#2[lemme:"il"]) & VMOD[posit1](#1,#3[lemme:"mieux"]) )
   ~

|#1[negative=+,topic_obj=+,exp_subj=+,expression=+]|
if (SUBJ[passive](#1[lemme:"limiter"],#2[lemme:"choix"]))
   ~

|#1[negative=+,topic_obj=+,exp_subj=+,expression=+]|
if (SUBJ[passive](#1[lemme:"soigner"],#2[lemme:"cuisine"]))
   ~

|#1[negative=+,topic_obj=+,exp_subj=+,expression=+]|
if (SUBJ[passive](#1[lemme:"élever"],#2[lemme:"prix"]))
   ~

// j'y retournerais ; je n'y retournearis plus 

|#1[positive=+,topic_obj=+,exp_subj=+,expression=+]|
        if (SUBJ(#1[lemme:"retourner",fut],#2[p1]) )
                ~

|AP{ADVP[lemme:"pas assez"],#1[adj,negative=+,changed=+,sents=+]}|
         ~
                    
|#2[negative=+,topic_obj=+,exp_subj=+,expression=+]|
        if  ( VMOD(#2[lemme:"rester"],#1[lemme:"faim"]))
        ~

|#2[negative=+,topic_subj=+,exp_obj=+,expression=+]|
        if ( VMOD(#2[lemme:"pédaler"],#1[lemme:"choucroute"]) )
        ~
|#2[negative=+,topic_subj=+,exp_obj=+,expression=+]|
        if ( VMOD(#2[lemme:"pédaler"],#1[lemme:"semoule"]) )
        ~
|#2[negative=+,topic_obj=+,exp_subj=+]|
        if ( SUBJ(#2[lemme:"éviter"],#1[lemme:"amateur"]) )
        ~

|#2[negative=+,topic_subj=+,exp_obj=+,expression=+]|
        if (VMOD(#2[lemme:"baigner"],#1[lemme:"jus"]) & PREPOBJ(#1,#3[lemme:"dans"]) ) 
        ~

|#2[negative=+,topic_subj=+,exp_obj=+,expression=+]|
        if (VMOD(#2[lemme:"laisser"],#1[lemme:"désirer"]))
        ~

// être à revoir
|#2[negative=+,exp_subj=+,topic_obj=+,expression=+]|
if (VMOD(#1[copule],#2[lemme:"revoir"]) & PREPOBJ(#2,#3[lemme:"à"]) & SUBJ(#1,#4) )
   OBJ(#2,#4)
   
// manque de
|#2[negative=+,topic_subj=+,topic_obj=+]|
        if (VMOD(#2[lemme:"manquer"],#1) & PREPOBJ(#1,#3[lemme:"de"]))
        ~
|#2[negative=+,lexneg=~]|
        if (NMOD(#2[lemme:"manque"],#1[sents:~]) & PREPOBJ(#1,#3[lemme:"de"]))
        ~

// incapable de mechancete
|#1[negative=~,sents=~,lexneg=+]|
        if ( ADJMOD(#1[lemme:"incapable"],#2[negative]) )
        ~
        
// on y mange bien

|#1[positive=+,sents=+,exp_subj=+]|
        if (VMOD[POSIT1](#1[lemme:"manger"],#2[lemme:"bien",adv]))
        ~



//|#1[sents=+,negative=+,topic_subj=+,exp_obj=+]|
//if (VMOD(#1[lemme:"manquer"],#2[lemme:"profondeur"]) )
//   ~

|#2[negative=+,topic_obj=+,exp_subj=+]|
        if (OBJ(#2[lemme:"retenir"],#1[lemme:"adresse"]))
        ~

//|FV{#2[lemme:"se"],#1[topic_subj=+,exp_obj=+,negative=+]}|
//        ~

|#2[negative=+,topic_subj=+,exp_obj=+]|
         if(SUBJ(#2,#1[lemme:"niveau"]))
         ~


|#2[sents=+,negative=+]|
if ( NMOD(#1[lemme:"style"],#2[lemme:"plat"]) | ( OBJ[spred](#3,#2[lemme:"plat"]) & SUBJ(#3,#1[lemme:"style"]) ) )
   ~

|#1[lemme:"indispensable",sents=+,positive=+]|
        if (OBJ[spred](#2,#1))
        ~

|SC{?*,FV{NEG,VERB#1}},ADV*,BG{CONJQUE}|
        if (^NEGAT(#1))
        ~


|SC{?*,FV{NEG,VERB#1}},ADV*,CONJQUE|
        if (^NEGAT(#1))
        ~
// tenir en haleine

|#1[sents=+,positive=+,topic_subj=+,exp_obj=+]|
    if ( VMOD(#1[lemme:tenir],#2[lemme:"haleine"]))
        ~


|NOUN#1[lemme:"culte",sents=+,positive=+]|
  if ( NMOD(#2[main-concept],#1) | ( SUBJ(#3,#2[main-concept]) & OBJ[spred](#3,#1) ))
	~

|ADV#2[sents=+,positive=+]|
if (ADJMOD(#1[lemme:"écrit"],#2[lemme:"bien"]))
   ~
|ADV#2[sents=+,positive=+]|
if (VMOD(#1[lemme:"écrire"],#2[lemme:"bien"]))
   ~
//laisser tomber

|verb#2[negative=+,sents=+, topic_obj=+,exp_subj=+]|
if (OBJ(#1[lemme:"laisser"],#2[lemme:"tomber",inf]))
   ~

|verb#1[positive=+,sents=+, topic_subj=+,exp_obj=+]|
if (OBJ(#1[lemme:"faire"],#2[lemme:"rêver",inf]))
   ~

// couper le souffle

|VERB#1[lemme:"couper",sents=+,positive=+,topic_subj=+,expression=+]|
  if (  OBJ(#1,#2[lemme="souffle"]) & ~VMOD(#3[lemme:"être"],#1))
	~
|VERB#1[lemme:"couper",sents=+,positive=+,topic_subj=+,expression=+]|
  if (  OBJ(#1,#2[lemme="souffle"]) & VMOD(#3[lemme:"être"],#1) & SUBJ(#3,#4))
	SUBJ(#1,#4)


|VERB#1[lemme:"passer",sents=+,negative=+,topic_subj=+,expression=+]|
  if (  OBJ(#1,#2[lemme="chemin"]))
	~

// addition salée
|#2[sents=+,negative=+]| 
if (OBJ[SPRED](#1,#2[lemme:"salé"]) & ( SUBJ(#1,#3[lemme:"addition"]) | SUBJ(#1,#3[lemme:"note"]) ))
   ~

|#1[sents=+, negative=+, topic_subj=+, exp_obj=+]| 
if (OBJ(#1[lemme:"tomber"],#2[lemme:"bas"]) )
   ~


// laisser un souvenir impérissable
// |#1[topic_subj:+,positive=+]|
//if ( OBJ(#1[lemme:"laisser"],#2) )
 //  ~


/***************corrections diverses ************************ \

/ marquage objet QUEP \

//je ne suis pas sûr que Luc deteste le film. -> ADJMOD(sûr,deteste)
|AP{ ?*, #1[quep : +,last : +]} , ?*[inf:~,fin:~,quep:~], CONJQUE,?*,FV{?*,#2[last:+]}|
	ADJMOD[dir = + ,propque = +, arg = +](#1_VERB,#2_OBJ)

|#2[sent=+,negative=+]| 
if (COORD(#1,#2[lemme:"froid"],#3[negative]) )
   ~

/ Une bonne idée, mais la forme est horrible \

if (SUBJ(#1,#2) & COORDITEMS(#2,#3) & ~SUBJ(?,#3))
	SUBJ(#1,#3)

/ rien d'aussi joli et vibrant que ,,,\

|PP{PREP,AP{ADV*,ADJ#1,COORD,ADJ#2}}|
        if ( NMOD[posit1](#3,#2) )
        NMOD[posit1=+](#3,#1)


/la faiblesse et l'invraisemblance de l'intrigue \

if ( COORDITEMS(#2,#3) & NMOD(#3,#4) & ~NMOD(#2,?) )
	NMOD(#2,#4)

/il est loin d'etre vulgaire\


|SC{?*,FV{?*,verb#1[copule]}},ADV*,IV{PREP[lemme:"loin de"],adv*,verb#2[copule]},AP{?*,ADJ#3[last]}|
	if (OBJ[spred](#2,#3) & ~NEGAT(#1))
	OBJ[spred=+](#1,#3),NEGAT(#3)

/il n'est pas loin d'etre XXX\
|SC{?*,FV{?*,verb#1[copule]}},ADV*,IV{PREP[lemme:"loin de"],adv*,verb#2[copule]},AP{?*,ADJ#3[last]}|
	if (OBJ[spred](#2,#3) & NEGAT(#1))
	OBJ[spred=+](#1,#3)

