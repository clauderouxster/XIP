// deduction_jeuxvideo_2.xip
// dépendances des domaines et des produits des jeux vidéo
// créé: 18.9.2007 (Sigrid Maurel)
// modifié: 1.7.2008 (Sigrid Maurel)


Déduction:

//----------------------------------------------------------------------------------------------------
// annulation de "bon jeu" si à la fin de la phrase
// évaluation si mieux avec ou sans cette règle?
// PB: C'est vraiment un bon jeu.
//// @+ et bon jeu .
//| COORD#10[lemme:et], NP{?*, noun#2}, SENT#3[end:+]; PUNCT#3[end:+] |
//if ( ^SENTIMENT(#1, #2[lemme:jeu]) )
//~.
//
//// @+ et bon jeu
//| COORD#10[lemme:et], NP{?*, noun#2} |
//if ( ^SENTIMENT(#1, #2[lemme:jeu, end:+]) )
//~.

// règles plus "sûres" que les précédentes, mais fonctionnent seulement si "bon jeu" est une phrase indépendante
// Bon jeu .
| NP[start:+]{?*, noun#2}, SENT#3[end:+]; PUNCT#3[end:+] |
if ( ^SENTIMENT(#1, #2[lemme:jeu]) )
~.

// Bon jeu
| NP[start:+]{?*, noun#2} |
if ( ^SENTIMENT(#1, #2[lemme:jeu, end:+]) )
~.



//----------------------------------------------------------------------------------------------------
// domaines
// ajout d'un 3e argument pour faire pareil que "nucléaire"
// bande son
| noun#1[lemme:bande-son]; noun#1[lemme:"bande son"]; noun#1[lemme:bande-original];
 noun#1[lemme:"bande original"]; noun#1[surface:"B.O"]; noun#1[surface:"B.O."]; noun#1[surface:"B.o"];
 noun#1[surface:"B.o."]; noun#1[surface:BO]; noun#1[surface:bo]; noun#1[lemme:bruit];
 noun#1[lemme:musique]; noun#1[surface:zic]; noun#1[surface:zik] |
DOMAINE[domaine=+](#1, #1, ##EMPTY[lemme=BANDE-SON]).

// durée de vie
| noun#1[lemme:"durée de vie"] |
DOMAINE[domaine=+](#1, #1, ##EMPTY[lemme=DUREE-DE-VIE]).

// genre jeu
| noun#1[genrejeu:+] |
DOMAINE[domaine=+](#1, #1, ##EMPTY[lemme=GENRE]).

// graphisme
| noun#1[lemme:couleur]; adj#1[lemme:graphique]; noun#1[lemme:graphisme]; noun#1[lemme:intensité];
 noun#1[lemme:résolution] |
DOMAINE[domaine=+](#1, #1, ##EMPTY[lemme=GRAPHISME]).

// jeu
| noun#1[lemme:gameplay]; noun#1[lemme:"game play"]; noun#1[lemme:jeu]; noun#1[lemme:"jeu vidéo"];
 noun#1[lemme:soft] |
DOMAINE[domaine=+](#1, #1, ##EMPTY[lemme=JEU]).

// jouabilité
| noun#1[lemme:jouabilité] |
DOMAINE[domaine=+](#1, #1, ##EMPTY[lemme=JOUABILITE]).

// plate-forme
| noun#1[plateforme:+]; adj#1[plateforme:+] |
DOMAINE[domaine=+](#1, #1, ##EMPTY[lemme=PLATE-FORME]).

// scénario
| noun#1[lemme:histoire]; noun#1[lemme:plot]; noun#1[lemme:scénario]; noun#1[surface:scénar] |
DOMAINE[domaine=+](#1, #1, ##EMPTY[lemme=SCENARIO]).



//----------------------------------------------------------------------------------------------------
// noms de produits
// se basant sur la chaine de caractère traitée dans localgram_jeuxvideo.xip
// {Guardian Heroes | guardianHeroes}
// 1er argument: nom normalisé; 2e: chaine telle que dans le texte
| #1[produit:+]{?, noun#2[produit_surf:+], ?, noun#3[produit_norm:+], ?} |
PRODUIT(#3, #2).
