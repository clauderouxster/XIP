// deduction_nucleaire_1.xip
// dépendances du nucleaire
// créé: 6.3.2008 (Sigrid Maurel)
// modifié: 2.9.2008 (Sigrid Maurel)


// variables d'offset
Variables:
int gauche;
int droite;
int gauche2;
int droite2;
string str="-";
string nl="\n";

//// impression de l'offset
//Script:
//| noun#1[lemme:centrale, nucl:+]; noun#1[lemme:générateur, nucl:+]; noun#1[lemme:réacteur, nucl:+];
// noun#1[lemme:site, nucl:+] |{
//print(gauche);
//print(str);
//print(droite2);
//print(nl);}



//----------------------------------------------------------------------------------------------------
Déduction:

// domaines
// accident
| noun#1[lemme:accident, nucl:+]; noun#1[lemme:arme, nucl:+]; noun#1[lemme:bombe, nucl:+];
 noun#1[lemme:pollution, nucl:+]; noun#1[lemme:radiation, nucl:+]; noun#1[lemme:rayonnement, nucl:+];
 noun#1[lemme:terrorisme, nucl:+]; noun#1[lemme:urgence, nucl:+] |
if ( NMOD(#1[noun:+, nucl:+], #2[adj:+, nucl:+]) & Offset(#1, gauche, droite) & Offset(#2, gauche2, droite2) )
DOMAINE[nucl=+](#1, #2, ##EMPTY[lemme=ACCIDENT]).

// agriculture
| noun#1[lemme:agriculture]; noun#1[lemme:ogm] |
DOMAINE[nucl=+](#1, #1, ##EMPTY[lemme=AGRICULTURE]).

| np{?*, noun#1[lemme:culture]; noun#1[lemme:sémence]}, ?*, ap{?*, adj#2[lemme:bio]; adj#2[lemme:biologique];
 adj#2[lemme:biotechnologique]; adj#2[lemme:conventionnel]; adj#2[lemme:traditionel]} |
if ( NMOD(#1, #2) )
DOMAINE[nucl=+](#1, #1, ##EMPTY[lemme=AGRICULTURE]).

// partis (p.e. Les Verts), associations (p.e. Green Peace)
| noun#1[nucl:+, parti:+, !sousdomaine:!] |
DOMAINE[nucl=+](#1, #1, ##EMPTY[lemme=ASSOCIATION]).

// une centrale nucléaire
| noun#1[lemme:centrale, nucl:+]; noun#1[lemme:générateur, nucl:+]; noun#1[lemme:réacteur, nucl:+];
 noun#1[lemme:site, nucl:+] |
if ( NMOD(#1[noun:+, nucl:+], #2[adj:+, nucl:+]) & Offset(#1, gauche, droite) & Offset(#2, gauche2, droite2) )
DOMAINE[nucl=+](#1, #2, ##EMPTY[lemme=CENTRALE]).

// une centrale à fission nucléaire
| noun#1[lemme:centrale, nucl:+] |
if ( NMOD(#1[noun:+, nucl:+], #2[noun:+, nucl:+]) & NMOD(#2, #3[adj:+, nucl:+])
 & Offset(#1, gauche, droite) & Offset(#3, gauche2, droite2) )
DOMAINE[nucl=+](#1, #3, ##EMPTY[lemme=CENTRALE]).

// danger
| noun#1[lemme:cancer]; noun#1[lemme:carbone]; noun#1[lemme:cuivre]; noun#1[lemme:chlorure];
 noun#1[lemme:dioxine]; noun#1[lemme:"métal lourd"]; noun#1[lemme:morpholine]; noun#1[lemme:phosphore];
 noun#1[lemme:sodium]; noun#1[lemme:zinc] |
DOMAINE[nucl=+](#1, #1, ##EMPTY[lemme=DANGER]).

| noun#1[lemme:contamination, nucl:+]; noun#1[lemme:risque, nucl:+] |
if ( NMOD(#1[noun:+, nucl:+], #2[adj:+, nucl:+]) & Offset(#1, gauche, droite) & Offset(#2, gauche2, droite2) )
DOMAINE[nucl=+](#1, #2, ##EMPTY[lemme=DANGER]).

// déchet
| noun#1[lemme:confinement, nucl:+]; noun#1[lemme:déchet, nucl:+]; noun#1[lemme:rejet, nucl:+];
 noun#1[lemme:résidu, nucl:+] |
if ( NMOD(#1[noun:+, nucl:+], #2[adj:+, nucl:+]) & Offset(#1, gauche, droite) & Offset(#2, gauche2, droite2) )
DOMAINE[nucl=+](#1, #2, ##EMPTY[lemme=DECHET]).

// (in)dépendance
| noun#1[lemme:dépendance, nucl:+]; noun#1[lemme:efficacité]; noun#1[lemme:indépendance, nucl:+] |
if ( NMOD(#1[noun:+, nucl:+], #2[adj:+, nucl:+]) )
DOMAINE[nucl=+](#1, #2, ##EMPTY[lemme=DEPENDANCE]).

// électricité
| noun#1[lemme:électricité, nucl:+] |
DOMAINE[nucl=+](#1, #1, ##EMPTY[lemme=ELECTRICITE]).

// énergie
| noun#1[lemme:combustible, nucl:+]; noun#1[lemme:élément, nucl:+]; noun#1[lemme:énergie, nucl:+];
 noun#1[lemme:fission, nucl:+]; noun#1[lemme:fusion, nucl:+]; noun#1[lemme:industrie, nucl:+];
 noun#1[lemme:matière, nucl:+]; noun#1[lemme:minerai, nucl:+]; noun#1[lemme:réaction, nucl:+] |
if ( NMOD(#1[noun:+, nucl:+], #2[adj:+, nucl:+]) & Offset(#1, gauche, droite) & Offset(#2, gauche2, droite2) )
DOMAINE[nucl=+](#1, #2, ##EMPTY[lemme=ENERGIE]).

// de l'énergie de fission nucléaire
| noun#1[lemme:énergie, nucl:+] |
if ( NMOD(#1[noun:+, nucl:+], #2[noun:+, nucl:+]) & NMOD(#2, #3[adj:+, nucl:+])
 & Offset(#1, gauche, droite) & Offset(#3, gauche2, droite2) )
DOMAINE[nucl=+](#1, #3, ##EMPTY[lemme=ENERGIE]).

// autres énergies
| noun#1[lemme:biocarburant]; noun#1[lemme:bois]; noun#1[lemme:charbon]; noun#1[lemme:éolien];
 noun#1[lemme:fioul]; noun#1[lemme:gaz]; noun#1[lemme:géothermie]; noun#1[lemme:hydrolique];
 noun#1[lemme:pétrole]; noun#1[lemme:solaire] |
DOMAINE[nucl=+](#1, #1, ##EMPTY[lemme=ENERGIE-ALT]).

| np{?*, noun#1[lemme:énergie, nucl:+]}, ?*, ap{?*, adj#2[lemme:éolien]; adj#2[lemme:géothermique];
 adj#2[lemme:hydrolique]; adj#2[lemme:hydrolien]; adj#2[lemme:solaire]} |
if ( NMOD(#1, #2) )
DOMAINE[nucl=+](#1, #2, ##EMPTY[lemme=ENERGIE-ALT]).

// laboratoires (p.e. CEA), entreprises (p.e. Andra)
| noun#1[nucl:+, firme:+, !sousdomaine:!] |
DOMAINE[nucl=+](#1, #1, ##EMPTY[lemme=ENTREPRISE]).

// information, confiance, ...
| noun#1[lemme:confiance, nucl:+]; noun#1[lemme:méfiance, nucl:+]; noun#1[lemme:politique, nucl:+];
 noun#1[lemme:prolifération, nucl:+]; noun#1[lemme:secret, nucl:+] |
DOMAINE[nucl=+](#1, #1, ##EMPTY[lemme=INFORMATION]).

// villes (p.e. La Hague)
| noun#1[nucl:+, ville:+, !sousdomaine:!] |
DOMAINE[nucl=+](#1, #1, ##EMPTY[lemme=LIEU]).

// protection de l'environnement
| np{?*, noun#1[lemme:protection, nucl:+]}, ?*, pp{?*, np{?*, noun#2[lemme:écosystème];
 noun#2[lemme:environnement]}} |
if ( NMOD(#1, #2) )
DOMAINE[nucl=+](#1, #2, ##EMPTY[lemme=PROTECTION]).

// radioactivité
| noun#1[lemme:becquerel, nucl:+]; noun#1[lemme:nucléaire, nucl:+]; noun#1[lemme:plutonium, nucl:+];
 noun#1[lemme:radioactivité, nucl:+]; noun#1[lemme:uranium, nucl:+] |
DOMAINE[nucl=+](#1, #1, ##EMPTY[lemme=RADIOACTIVITE]).

// consommation, économie, source
| noun#1[lemme:consommation, nucl:+]; noun#1[lemme:économie, nucl:+]; noun#1[lemme:source, nucl:+] |
if ( NMOD(#1[noun:+, nucl:+], #2[noun:+, lemme:énergie, nucl:+]) )
DOMAINE[nucl=+](#1, #2, ##EMPTY[lemme=SOURCE]).

// technologie
| noun#1[lemme:technologie, nucl:+] |
if ( NMOD(#1[noun:+, nucl:+], #2[adj:+, nucl:+]) )
DOMAINE[nucl=+](#1, #2, ##EMPTY[lemme=TECHNOLOGIE]).

// traitement
| noun#1[lemme:collecte, nucl:+]; noun#1[lemme:recyclage, nucl:+]; noun#1[lemme:stockage, nucl:+];
 noun#1[lemme:transport, nucl:+]; noun#1[lemme:tri, nucl:+] |
//if (NMOD(#1[noun:+, nucl:+], #2[adj:+, nucl:+]) & Offset(#1, gauche, droite) & Offset(#2, gauche2, droite2))
//DOMAINE[nucl=+](#1, #2, ##EMPTY[lemme=TRAITEMENT]).
DOMAINE[nucl=+](#1, #1, ##EMPTY[lemme=TRAITEMENT]).

// entité nucléaire non-definie
| noun#1[nucl:+] |
if ( ~DOMAINE(#1, #10, #11) )
DOMAINE[nucl=+](#1, #1, ##EMPTY[lemme=NON-DEF]).



//----------------------------------------------------------------------------------------------------
// les phrases qui contiennent au moins une entité nommée ou un concept du nucléaire, et pas de sentiments
Déduction:
| #1[cat:0] |
if ( DOMAINE(#10, #11, #2[empty:+]) & ~SENTIMENT(#12, #13) )
SENTIMENT[nucl=+, sentType=nucl](#1).
