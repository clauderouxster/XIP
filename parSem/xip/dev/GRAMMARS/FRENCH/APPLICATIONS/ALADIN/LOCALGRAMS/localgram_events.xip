//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Marqueur:

1>?[def], noun[lemme:"euro",maj,debutEvent=+,sport=+].
1>?[def], ?[lemme:"mondial",debutEvent=+,sport=+].
1>?[lemme:"concours",maj,debutEvent=+].
1>?[def], noun[lemme:"Mundial",maj,debutEvent=+,sport=+].
1>?[def], noun[lemme:"mondial",maj,debutEvent=+,sport=+].

1>prep[def], noun[lemme:"euro",maj,debutEvent=+,sport=+,div=+].
1>prep[def], noun[lemme:"mondial",maj,debutEvent=+,sport=+,div=+].
1>prep[def], noun[lemme:"Mundial",maj,debutEvent=+,sport=+,div=+].
1> ?[def], noun[lemme:tour,maj,debutEvent=+,sport=+,div=+], num[datepot].

DiRègles:


2> noun[event=+,mwe=+, proper=+,completeEvent=+] -> | ?[indef:~] | adj[ord,first],
				 noun[!culturel:!,debutEvent,sport:~],
				 adj*,
				 prep[form:fde],
				 noun[!genre:!,genre,event:~],
				 prep*[form:fde],
				 det*[def],
				 coord*,
				 noun*[!genre:!,debutEvent:~,finEvent:~,date:~,url:~],
				 noun[lieu,last,batimentorg:~,quartier:~].


2> noun[event=+,mwe=+, proper=+,completeEvent=+] -> | ?[indef:~] | noun[first,!culturel:!,debutEvent,sport:~,pl:~],
				 adj*,
				 prep[form:fde],
				 noun[!genre:!,genre,event:~],
				 prep*[form:fde],
				 det*[def],
				 coord*,
				 noun*[!genre:!,debutEvent:~,finEvent:~,date:~,url:~],
				 noun[lieu,last,batimentorg:~,quartier:~].


2> noun[event=+,mwe=+, proper=+,completeEvent=+ ] -> | | noun[first,!culturel:!,debutEvent,sport:~,pl:~],
				 adj*,
				 prep[form:fde],
				 noun[!genre:!,genre,event:~],
				 prep*[form:fde],
				 det*[def],
				 coord*,
				 noun*[!genre:!,debutEvent:~,finEvent:~,date:~,url:~],
				 noun[lieu,last,batimentorg:~,quartier:~].

2> noun[event=+,mwe=+, proper=+,completeEvent=+ ] ->  | |  noun[first,!culturel:!,debutEvent,sport:~],
				 adj*,
				 prep[form:fde],
				 noun*[!genre:!,genre,event:~],
				 prep*[form:fde],
				 det*[def],
				 coord*,
				 noun*[!genre:!,debutEvent:~,finEvent:~,date:~,url:~],
				 noun[!genre:!,genre, debutEvent:~, finEvent:~,date:~,url:~].


2> noun[event=+,mwe=+, proper=+,completeEvent=+ ] ->  |?[def,pl:~];adj[ord] |  noun[first,!culturel:!,debutEvent,sport:~],
				 adj*,
				 prep[form:fde],
				 noun*[!genre:!,genre,event:~],
				 prep*[form:fde],
				 det*[def],
				 coord*,
				 noun*[!genre:!,debutEvent:~,finEvent:~,date:~,url:~],
				 noun[!genre:!,debutEvent:~, finEvent:~,date:~,url:~,last].

3> noun[event=+,mwe=+, proper=+] ->| ?[indef:~] | noun[first,!culturel:!,debutEvent,sport:~,pl:~],
				 adj*,
				 prep[form:fde],
				 noun[!genre:!,genre,event:~],
				 adj[nat,last].

3> noun[event=+,mwe=+, proper=+] ->|  | noun[first,!culturel:!,debutEvent,sport:~,pl:~],
				 adj*,
				 prep[form:fde],
				 noun[!genre:!,genre,event:~],
				 adj[nat,last].


3> noun[event=+,mwe=+, proper=+] -> | ?[indef:~] | noun[first,!culturel:!,debutEvent,sport:~,pl:~],
				 adj*,
				 prep[form:fde],
				 noun[!genre:!,genre,event:~],
				 noun[nat,last].

3> noun[event=+,mwe=+, proper=+] -> | | noun[first,!culturel:!,debutEvent,sport:~,pl:~],
				 adj*,
				 prep[form:fde],
				 noun[!genre:!,genre,event:~],
				 noun[nat,last].

4>  noun[event=+,mwe=+, proper=+,completeEvent=+] ->| ?[indef:~] |  
					noun[first,!culturel:!,debutEvent,sport:~,pl:~], 
					prep[form:fde],
					noun*[lieu],
					noun[lieu,last].

4>  noun[event=+,mwe=+, proper=+,completeEvent=+] ->|  |  noun[first,!culturel:!,debutEvent,sport:~,pl:~], (adj), prep[form:fde], (?[def]), noun[lieu,last].


4>  noun[event=+,mwe=+, proper=+,completeEvent=+] ->| ?[indef:~] |  noun[first,!culturel:!,debutEvent,sport:~,pl:~], (adj), prep[form:fde], (?[def]), noun[lieu,last].

//5> noun[event=+,mwe=+, proper=+] -> | ?[indef:~] | noun[first,!culturel:!,debutEvent,sport:~,pl:~],
//				 adj*,
//				 noun*[debutEvent:~,finEvent:~,date:~,url:~],
//				 prep*[form:fde],
//				 coord*,
//				 noun[!genre:!,genre,last,event:~].

//5> noun[event=+,mwe=+, proper=+] -> | | noun[first,!culturel:!,debutEvent,sport:~,pl:~],
//				 adj*,
//				 noun*[debutEvent:~,sport:~,finEvent:~,date:~,url:~],
//				 prep*[form:fde],
//				 coord*,
//				 noun[!genre:!,genre,last,event:~].

5> noun[event=+,mwe=+, proper=+] -> | ?[indef:~] | noun[first,!culturel:!,debutEvent,sport:~,pl:~],
				 adj*,
				 noun+[debutEvent:~,finEvent:~,date:~,url:~,genre:~],
				 coord*,
				 noun[!genre:!,genre,last,event:~].

5> noun[event=+,mwe=+, proper=+] -> | | noun[first,!culturel:!,debutEvent,sport:~,pl:~],
				 adj*,
				 noun+[debutEvent:~,sport:~,finEvent:~,date:~,url:~,genre:~],
				 coord*,
				 noun[!genre:!,genre,last,event:~].


6> noun[event=+,mwe=+, proper=+] -> | | noun+[maj,debutEvent:~,finEvent:~],
				        noun[!genre:!,genre,event:~],
					           adj*,
						   noun[last,finEvent,!culturel:!,event:~].

6> noun[event=+,mwe=+, proper=+] -> |?[indef:~] | noun+[maj,debutEvent:~,finEvent:~],
				      noun[!genre:!,genre,event:~],
					           adj*,
						   noun[last,finEvent,!culturel:!,event:~].


6>  noun[event=+,mwe=+, proper=+] ->   |?[indef:~] | noun[debutEvent,sport,!sport:!,first],
						     noun*[date:~,url:~],
						     adj*,
						     prep*[form:fde],
						     noun[last,lieu].

/la coupe d'Afrique des nations\

6>  noun[event=+,mwe=+, proper=+] ->   |?[indef:~]| noun[debutEvent,sport,!sport:!,first],
						     noun*[date:~,url:~],
						     adj*,
						     prep*[form:fde],
						     noun[lieu],
						     det*,
						     noun[last,lemme:"nation",pl,maj].

6>  noun[event=+,mwe=+, proper=+] ->   |?[indef:~] | noun[debutEvent,sport,!sport:!,first],
						     noun*[date:~,url:~],
						     adj*,
						     prep*[form:fde],
						     noun[debutEvent:~,sport,!sport:!,last].

6>  noun[event=+,mwe=+, proper=+] ->   | | noun[debutEvent,sport,!sport:!,first],
						     noun*[date:~,url:~],
						     adj*,
						     prep*[form:fde],
						     noun[debutEvent:~,sport,!sport:!,last].
Séquence:


//Festival Jazz à Vienne

7> noun[event=+,mwe=+, proper=+] @=  noun[debutEvent,!culturel:!,event:~,sport:~], noun[genre], prep[form:fa], noun[ville].

7>  noun[event=+,mwe=+, proper=+] @= |?[def] | noun[debutEvent,sport,event:~,!sport:!,!div:!], (adj), num[datepot,time] ,(prep[form:fde], noun[sport,debutEvent:~,event:~]).

7> noun[event=+,mwe=+, proper=+] @= noun+[maj,guessed], noun[finEvent,!culturel:!,event:~].


7> noun[event=+,mwe=+, proper=+] @= noun[debutEvent,!culturel:!,event:~,sport:~], (adj),  (noun[person]), noun[person].

7> noun[event=+,mwe=+, proper=+] @= noun[event,mwe,!genre:!,!culturel:!,sport:~,completeEvent:~], noun[person,pren:~].

7>  noun[event=+,mwe=+, proper=+] @= noun[person,celeb] , noun[finEvent,!culturel:!,event:~].

7>  noun[event=+,mwe=+, proper=+] @= noun+[maj], noun[finEvent,!culturel:!,event:~].

7> noun[event=+,mwe=+, proper=+,sport=+,div=+] @= noun[lemme:"jeu"];noun[lemme:"jeux olympiques"], (?[nat]), prep[form:fde], noun[lieu] , (num[date]) . 



7> noun[event=+,mwe=+, proper=+,sport=+,div=+] @= noun[lemme:"jeu",maj], ?[nat].


// guerres & conflits

// la premiere guerre mondiale 


7> noun[event=+,mwe=+] @= |?[def] | adj[ord], noun[guerre,!guerre:!], adj .

// la guerre (d'independance) de Chypre
7> noun[event=+,mwe=+] @= |?[def] | (adj[ord]),  noun[guerre,!guerre:!],(prep[form:fde], noun),  prep[form:fde] ,   noun[lieu]. 

// la guerre de cent ans/des six jours

7> noun[event=+,mwe=+]@= |?[def] | noun[guerre,!guerre:!],(adj),  prep[form:fde], num, noun[lemme:"jour"];noun[lemme:"an"].


7> noun[event=+,mwe=+] @= |?[def] | (adj[ord]),  noun[guerre,!guerre:!], adj[nat]. 


// la guerre de 39-45

8> noun[event=+,mwe=+] @= |?[def] | (adj[ord]),  noun[guerre,!guerre:!], prep[form:fde], num;noun[date] .

// l'expedition machin

9>  noun[event=+,mwe=+] @= |?[def] | (adj[ord]), noun[debutevent,culturel:~,guerre:~], noun+[maj].

//le Mondial allemand

9> noun [event=+,mwe=+,sport=+,div=+] @= ?[debutEvent:+,sport:+], adj[nat];noun[nat].

9> noun [event=+,mwe=+,sport=+,div=+] @= ?[debutEvent:+,sport:+], prep[form:fde], (det[def]), noun[lieu].


9>   noun[event=+,mwe=+,sport=+] @=   noun[lemme:"RCW"], num[datepot].


//--------------------
// maud
// AJOUT POUR LES DATES
//--------------------


Marqueur:

// une datepot suivant un evenement est une date sure.
20> noun[event,mwe],(prep[form:fde]), num[gdnbre,datepot=~,date=+, annee=+].



// Caroline : pour annotation infomagic 

// le 12eme festival de Prouchmout 
// et le festival Prouchmout 2006 sont groupes en 1 evenement


Séquence:

30> noun[event=+,mwe=+] @=  adj[ord], noun[event] , (num[date]). 

30>  noun[event=+,mwe=+] @=  (noun[debutevent]), noun[event], num[date].


