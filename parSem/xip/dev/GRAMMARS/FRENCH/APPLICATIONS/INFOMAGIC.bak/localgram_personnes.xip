//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Séquence:
/juge d'instruction\
1> Noun [prof=+] = noun[lemme:juge, !sg:!, !pl:!, !masc:!, !fem:!], prep[form:fde], noun[lemme:instruction].

/avocat général, procureur général\
1> Noun [prof=+] = noun[prof:+, !sg:!, !pl:!, !masc:!, !fem:!], adj[lemme:général].

/chef de l'Etat Major\

1> Noun [prof=+] = noun[lemme:"chef"], prep[form:fde], (det[def]), noun[lemme:"état", maj], noun[lemme:"Major"] .

/chef de l'Etat, secrétaire d'Etat\
1> Noun [prof=+] = noun[prof:+, !sg:!, !pl:!, !masc:!, !fem:!, !p3:!, preorgde=~], prep[form:fde], (det[def]), noun[lemme:"état", maj].


/le maître d'école\
1> noun[mwe=+, prof=+] = noun[lemme:maître], prep[form:fde], noun[lemme:"école"].

/premier ministre\
1> noun[mwe=+, prof=+] = ?[lemme:premier, !maj:!, !sg:!, !pl:!, !p3:!], noun[lemme:ministre].

/président/procureur de la République\
1> noun[mwe=+, prof=+] = ?[prof, !maj:!, !sg:!, !pl:!, !p3:!, preorgde=~], prep[form:fde], det[def], noun[lemme:république, maj] |?[adj:~, nat:~]|.

/envoyé spécial\
1> Noun [prof=+, prof2=+] = noun[prof:+, !sg:!, !pl:!, !masc:!, !fem:!], adj[lemme:"spécial"].



Marqueur:

/M. Tout-le-Monde n'est pas une personne\
5> noun[person=-] {?[lemme:tout], ?*, det, ?*, noun[lemme:monde];noun[lemme:Monde]}.

5> ?[lemme:tout, person=-], det[hyph], noun[lemme:monde].

/les prénoms non ambigus sont des noms de personnes\
5> noun[maj:+, pren:+, prenamb:~, phrasemaj:~, toutmaj:~, person=+].
5> noun[maj:+, pren:+, prenamb:~, prenamb1:~, person=+].

5> noun[celeb, lemme:"al-%C%c+", min=+].

/Gervais dans un groupe Saint-Gervais n'est pas une personne\
7> noun[saintTiret:+]{?[saint:+], punct[hyph], noun[person=~]}.

/Pierre et Jean ne sont pas des personnes dans Pierre-Jean\
7> noun[pren:+]{noun[person:~], punct[hyph], noun[person=~]}.

/le général\
7> noun[lemme:général, masc:+, prof=+].


/néolibéral\
8> noun[postprof=+]{prefix[preprof], noun[postprof]}.
8> adj[postprof=+]{prefix[preprof], adj[postprof]}.


/l'entraîneur du club\
10> noun[preorgde, prof], prep[form:fde], (det), (adj), noun, (noun), (adj), punct [form:fcm, nonorg=+], noun[maj, toutmaj:~, org:~, person=+].
10> noun[preorgde], prep[form:fde], (det), noun[nonorg=+], (punct [form:fcm, nonorg=+]), noun[maj, phrasemaj:~, org:+, prof=+].

/le PDG du musée, Untel\
10> noun[prefirmede, prof], prep[form:fde], (det), (adj), noun, (noun), (adj), punct [form:fcm, nonorg=+], noun[maj, toutmaj:~, org:~, person=+].
10> noun[prefirmede], prep[form:fde], (det), noun[nonorg=+], (punct [form:fcm, nonorg=+]), noun[maj, phrasemaj:~, org:+, prof=+].

/Ariane 5 n'est pas une personne\
10> noun [pren_rois:-, person=~], num.

/Roy n'est pas un prénom quand il suit un article défini\
10> det[def:+], noun[lemme:"Roy", pren=~, person=~].
10> prep[def], noun[lemme:"Roy", pren=~, person=~].

/enlever le trait personne aux prénoms modérément ambigus quand ils sont en début de phrase (Benjamin)\
10> noun [person=~, pren:+, prenamb1:+, startbis:+].

/Augustin n'est pas une personne dans Saint-Augustin (groupe non reconnu dans NTM)\
10> ?[saint:+], (punct[hyph]), noun [person=~, pren:+].

/docteur n'est pas un titre quand il est précédé d'un article\
//10> det[masc], (adj)*, (coord, adj)*, noun[lemme:docteur, tit=~].

Séquence:

/sainte Nitouche n'est pas une personne\
20> Noun[mwe=+, fem=+, sg=+, p3=+] = ?[saint:+, fem:+], noun[lemme:nitouche].

/les acteurs Nô\
20> Noun[mwe=+, prof=+] = noun[acteur:+], noun[lemme:"nô"].


/********** avec un titre ***************\

/Messieurs X et Y\
30> Noun[mwe=+, proper=+, person=+] @= ?[tit:+, pl:+], (?[partic:+,coord:~]),  noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+]), (punct[form:fcm], noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+])), coord, (?[partic:+,coord:~]),  noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+]).

30> Noun[mwe=+, proper=+, person=+] @= ?[tit:+, pl:+], (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, lieu:~, inconnu:+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+]), (punct[form:fcm], noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+])), coord, (?[partic:+,coord:~]),  noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+]).

30> Noun[mwe=+, proper=+, person=+] @= ?[tit:+, pl:+], (?[partic:+,coord:~]),  noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+]), (punct[form:fcm], noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+])), coord, (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, lieu:~, inconnu:+].

30> Noun[mwe=+, proper=+, person=+] @= ?[tit:+, pl:+], (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, lieu:~, inconnu:+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+]), (punct[form:fcm], noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+])), coord, (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, inconnu:+].

30> Noun[mwe=+, proper=+, person=+] @= ?[tit:+, pl:+], (?[partic:+,coord:~]),  noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+]), (punct[form:fcm], (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, lieu:~, inconnu:+]), coord, (?[partic:+,coord:~]),  noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+]).

30> Noun[mwe=+, proper=+, person=+] @= ?[tit:+, pl:+], (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, lieu:~, inconnu:+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+]), (punct[form:fcm], (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, lieu:~, inconnu:+]), coord, (?[partic:+,coord:~]),  noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+]).

30> Noun[mwe=+, proper=+, person=+] @= ?[tit:+, pl:+], (?[partic:+,coord:~]),  noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+]), (punct[form:fcm], (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, lieu:~, inconnu:+]), coord, (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, lieu:~, inconnu:+].

30> Noun[mwe=+, proper=+, person=+] @= ?[tit:+, pl:+], (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, lieu:~, inconnu:+], ((?[partic:+,coord:~]),  noun[maj:+]), ((?[partic:+,coord:~]),  noun[maj:+]), (punct[form:fcm], (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, lieu:~, inconnu:+]), coord, (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, lieu:~, inconnu:+].



/Monsieur Untel\
30> Noun[mwe=+, proper=+, person=+] @= ?[tit:+, sg:+], (?[partic:+,coord:~]),  noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+, phrasemaj:~];noun[phrasemaj:+, inconnu:+];noun[phrasemaj:+, celeb:+]), ((?[partic:+,coord:~]),  noun[maj:+, phrasemaj:~];noun[phrasemaj:+, inconnu:+]).

30> Noun[mwe=+, proper=+, person=+] @= ?[tit:+, sg:+], (prep[form:fde]), (det[def:+]),  noun[maj:+, person:~-, org:~+, lieu:~, inconnu:+], ((?[partic:+,coord:~]),  noun[maj:+, phrasemaj:~];noun[phrasemaj:+, inconnu:+];noun[phrasemaj:+, celeb:+]), ((?[partic:+,coord:~]),  noun[maj:+, phrasemaj:~];noun[phrasemaj:+, inconnu:+]).

30> Noun[mwe=+, proper=+, person=+] @= |?[tit:+, pl:+]| (?[partic:+,coord:~]),  noun[maj:+, person:~-, org:~+], ((?[partic:+,coord:~]),  noun[maj:+, phrasemaj:~];noun[phrasemaj:+, inconnu:+];noun[phrasemaj:+, celeb:+]), ((?[partic:+,coord:~]),  noun[maj:+, phrasemaj:~];noun[phrasemaj:+, inconnu:+]).


/Saint Augustin\
30> Noun[mwe=+, proper=+, saintsTiret=+] @= ?[saint:+, toutmaj:~, !maj:!], noun[maj:+, person:~-, org:~+], (noun[maj:+]), (noun[maj:+]).

/Saint-Augustin\
30> Noun[mwe=+, proper=+, saintTiret=+] @= ?[saint:+, toutmaj:~], punct[hyph:+], noun[maj:+, person:~-, org:~+], (noun[maj:+]), (noun[maj:+]).

/************ avec une relation de famille ********************\

/le père Rigaud Dupont\
//10> Noun[mwe=+, proper=+, person=+, maj=+] @= |noun[famille:+]| noun[maj:+, pren:~, person:~, org:~+, first:~, phrasemaj:~];noun[pren:~, phrasemaj:+, inconnu:+], (?[partic]), (noun[maj:+]), (noun[maj:+]).

/**************** avec un prénom ********************\
/A. Z. Rigaud\
30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[initial:+], (noun[initial:+];noun[lettre:+])*, (?[partic:+,coord:~];prep[form:fde]), (noun[maj, inconnu:+];noun[maj, pren:+];noun[celeb:+])+.


/INA\

30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[lettre:+] , noun[maj,guessed];noun[celeb:+].

/ Alphonse Lange\

30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb:~, phrasemaj:~], (noun[initial:+];noun[lettre:+]), (?[partic:+,coord:~]), (det[def:+]), noun[maj:+, prof:~,celeb:~], prep[form:fde], (det[def:+]), noun[maj:+, pren:~, org:~, preorg:~, org1:~, lieu:~, toutmaj:~, prof:~], ((?[partic:+,coord:~]), (det[def:+]),noun[maj:+, toutmaj:~, prof:~]).

// for INA : Jacques CHANCEL EXTRAITS
//30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun+[maj:+, pren:+, prenamb:~, phrasemaj:~,celeb:~], (noun[initial:+];noun[lettre:+]), (?[partic:+,coord:~]), (det[def:+]), noun[pren:~,maj:+, prof:~, quartier2:~,celeb:~], (noun[maj:+, org:~, ang:~, prof:~, quartier2:~,guessed:+]), ((?[partic:+,coord:~]), (det[def:+]),noun[maj:+, ang:~, prof:~, quartier2:~,guessed:+];noun[maj:+, ang:~, prof:~, quartier2:~,celeb:+] ).

//30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb:~, phrasemaj:~], (noun[initial:+];noun[lettre:+]), (?[partic:+,coord:~]), (det[def:+]), noun[maj:+, prof:~, quartier2:~], (noun[maj:+, org:~, ang:~, prof:~, quartier2:~]), ((?[partic:+,coord:~]), (det[def:+]),noun[maj:+, ang:~, prof:~, quartier2:~]).


30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb:~, phrasemaj:~], (noun[initial:+];noun[lettre:+]), (?[partic:+,coord:~, phrasemaj:~,coord:~,ang:~]), (det[def:+, phrasemaj:~]), noun[maj:+, phrasemaj:~, prof:~, quartier2:~,celeb:~,batimentorg1:~,quartier1:~];noun[celeb:+, min], ((?[partic:+,coord:~,coord:~,ang:~]), (det[def:+]),noun[maj:+, prof:~, quartier2:~,quartier1:~,firme:~,batimentorg1:~]), ((?[partic:+,coord:~,coord:~,ang:~]), (det[def:+]),noun[batimentorg1:~,maj:+, prof:~, quartier2:~,firme:~,quartier1:~]).

/en majuscules\
30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb:~], (noun[initial:+];noun[lettre:+]), (?[partic:+,coord:~]), noun[maj:+, inconnu, quartier2:~, ang:~];noun[celeb:+, min], ((?[partic:+,coord:~]), noun[maj:+, inconnu, quartier2:~, ang:~]), ((?[partic:+,coord:~]), noun[maj:+, inconnu, quartier2:~, ang:~]).


/Antoine de la Verpinière\
/ INA: le nom peut etre en toutmaj\
30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb:~], (noun[initial:+];noun[lettre:+]), (prep[form:fde]), (det[def:+]), noun[maj:+, pren:~, org:~, preorg:~, org1:~, prof:~, quartier2:~,celeb:~], ((?[partic:+,coord:~]), (det[def:+]),noun[maj:+, phrasemaj:~, prof:~, quartier2:~,celeb:~]), ((?[partic:+,coord:~]), (det[def:+]),noun[maj:+, prof:~, quartier2:~,org:~]).


//30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb:~], (noun[initial:+];noun[lettre:+]), (prep[form:fde]), (det[def:+]), noun[maj:+, pren:~, org:~, preorg:~, org1:~, toutmaj:~, prof:~, quartier2:~], ((?[partic:+,coord:~]), (det[def:+]),noun[maj:+, phrasemaj:~, prof:~, quartier2:~,org:~]), ((?[partic:+,coord:~]), (det[def:+]),noun[maj:+, prof:~, quartier2:~]).


 
/en majuscules\
30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb:~], (noun[initial:+];noun[lettre:+]), (prep[form:fde]), (det[def:+]), noun[maj:+, pren:~, org:~, preorg:~, org1:~, toutmaj:~, quartier2:~,celeb:~];noun[celeb:+], ((?[partic:+,coord:~]), (det[def:+]), noun[phrasemaj:+, inconnu:+, ang:~, quartier2:~];noun[phrasemaj:+,  celeb:+]), ((?[partic:+,coord:~]), (det[def:+]),noun[maj:+, inconnu:+, ang:~, quartier2:~];noun[phrasemaj:+, celeb:+]).


/Antoine de GAUDEMAR\
30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb:~], (noun[initial:+];noun[lettre:+]), (prep[form:fde, def:~]), noun[maj:+, pren:~, org:~, preorg:~, org1:~, quartier2:~, phrasemaj:~, prof:~], ((?[partic:+,coord:~]), (det[def:+]),noun[maj:+, phrasemaj:~, prof:~, quartier2:~,proper]), ((?[partic:+,coord:~]), (det[def:+]),noun[maj:+, prof:~, quartier2:~,proper]).

/en majuscules\
30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb:~], (noun[initial:+];noun[lettre:+]), (prep[form:fde, def:~]), noun[maj:+, pren:~, org:~, preorg:~, org1:~, quartier2:~, ang:~], ((?[partic:+,coord:~]), (det[def:+]),noun[phrasemaj:+, inconnu:+, ang:~, quartier2:~];noun[phrasemaj:+, celeb:+]), ((?[partic:+,coord:~]), (det[def:+]),noun[maj:+, inconnu:+, ang:~, quartier2:~];noun[phrasemaj:+, quartier2:~, celeb:+]).

/Marine Rigaud, João Simões Lopes Neto\
30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb2:~], (noun[initial:+];noun[lettre:+]), (noun[maj:+, pren:+, prenamb2:~, prof:~, quartier2:~,celeb:~]), (?[partic:+,coord:~,ang:~]), noun[maj:+, inconnu:+, ang:~, quartier2:~];noun[maj:+, pren:+], ((?[partic:+,coord:~,ang:~]), (det[def:+]), noun[maj:+, inconnu:+, quartier2:~]).

/Marine Le Pen, Noël de la Verpinière\
30> Noun[mwe=+, proper=+, person=+, maj=+] @= noun[maj:+, pren:+, prenamb2:~, phrasemaj:~], (noun[initial:+];noun[lettre:+]), (prep[form:fde]), (det[def:+]), noun[maj:+, inconnu:+, quartier2:~, pren:~, org:~, preorg:~, org1:~, lieu:~, toutmaj:~], ((?[partic:+,coord:~]), (det[def:+]), noun[maj:+, inconnu:+, quartier2:~]).

30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb2:~, phrasemaj:~], (noun[initial:+];noun[lettre:+]), (?[partic:+,coord:~,ang:~]), (det[def:+]), noun[maj:+, inconnu:+, quartier2:~];noun[maj:+, pren:+], ((?[partic:+,coord:~,ang:~];prep[form:fde]), (det[def:+]), noun[maj:+, inconnu:+, quartier2:~, toutmaj:~, org:~, lieu:~];noun[phrasemaj:+, celeb:+]).

/en majuscules\
30> Noun[mwe=+, proper=+, person=+, maj=+] @= noun[maj:+, pren:+, prenamb2:~], (noun[initial:+];noun[lettre:+]), (prep[form:fde]), (det[def:+]), noun[phrasemaj:+, celeb:+], ((?[partic:+,coord:~]), (det[def:+]), noun[maj:+, inconnu:+, quartier2:~]).

30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb2:~], (noun[initial:+];noun[lettre:+]), (?[partic:+,coord:~]), (det[def:+]), noun[phrasemaj:+, celeb:+], ((?[partic:+,coord:~];prep[form:fde]), (det[def:+]), noun[phrasemaj:+, celeb:+]).


/France Rigaud\
30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= |?[det:~]| noun[maj:+, pren:+], (noun[initial:+];noun[lettre:+]), (?[partic:+,coord:~]), (det[def:+]), noun[maj:+, inconnu:+, ang:~, quartier2:~];noun[maj:+, pren:+];noun[celeb:+], ((?[partic:+,coord:~];prep[form:fde]), (det[def:+]), noun[maj:+, inconnu:+, org:~, lieu:~, quartier2:~, toutmaj:~]).


30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= |?[det:~]| noun[maj:+, pren:+], (noun[initial:+];noun[lettre:+]), (?[partic:+,coord:~]), (det[def:+]), noun[maj:+, inconnu:+, ang:~, quartier2:~];noun[maj:+, pren:+], ((?[partic:+,coord:~];prep[form:fde]), (det[def:+]), noun[maj:+, inconnu:+, org:~, lieu:~, quartier2:~, toutmaj:~];noun[celeb:+]).

30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, start:+], (noun[initial:+];noun[lettre:+]), (?[partic:+,coord:~,ang:~]), (det[def:+]), noun[maj:+, inconnu:+, ang:~, quartier2:~];noun[maj:+, pren:+], ((?[partic:+,coord:~,ang:~];prep[form:fde]), (det[def:+]), noun[maj:+, inconnu:+, toutmaj:~, lieu:~, org:~, quartier2:~];noun[celeb:+]).

30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, start:+], (noun[initial:+];noun[lettre:+]), (?[partic:+,coord:~,ang:~]), (det[def:+]), noun[maj:+, inconnu:+, ang:~, quartier2:~];noun[maj:+, pren:+];noun[celeb:+], ((?[partic:+,coord:~,ang:~];prep[form:fde]), (det[def:+]), noun[maj:+, inconnu:+, toutmaj:~, lieu:~, org:~, quartier2:~]).


/Zine el-Abidine Ben Ali, Jaber Al-Ahmad Al-Sabah\
30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+], (noun[initial:+];noun[lettre:+]), ?[partic:+,coord:~,ang:~], (det[def:+]), (punct[hyph:+]), noun[maj:+, prof:~], ((?[partic:+,coord:~,ang:~]), (det[def:+]), (punct[hyph:+]), noun[maj:+, phrasemaj:~, prof:~];noun[phrasemaj:+, inconnu:+];noun[phrasemaj:+, celeb:+]).

/Kim Un Yong\
30> Noun[mwe=+, proper=+, person=+, maj=+, marque_pers=+] @= noun[maj:+, pren:+, prenamb:~, phrasemaj:~], (noun[initial:+];noun[lettre:+]), (?[partic:+,coord:~,ang:~]), ?[maj:+], noun[maj:+, quartier2:~].

/***** nationalités et professions****\


Marqueur:
/le(s) chanteur(s) Untel \
40> noun[prof:+, pl:+], (adj[nat:+ ];adj[postProf:+];noun[org:+ ]), noun[maj:+, person:~-, org:~+ , lieu:~, proper=+, person=+, marque_pers=+, orgpers1=+], ?[partic:~ ,maj:~];?[CR:+].
40> noun[prof:+, pl:+], (adj[nat:+ ];adj[postProf:+];noun[org:+ ]), noun[maj:+, person:~-, org:~+ , lieu:~, proper=+, person=+, marque_pers=+, orgpers1=~], coord;punct[form:fcm].

/les chanteurs, Untel \
40> noun[prof:+, pl:+], (adj[nat:+ ];adj[postProf:+]; noun[org:+ ]), punct[form:fcm], noun[maj:+, person:~-, org:~+ ,lieu:~, proper=+, person=+, marque_pers=+], punct[form:fcm];coord.

/les chanteurs Untel et Untel\
40> noun[prof:+, pl:+], (adj[nat:+ ];adj[postProf:+]; noun[org:+ ]), noun[person:+], punct[form:fcm];coord, noun[maj:+, org:~+ ,proper=+, person=+, marque_pers=+], ?[partic:~ ,maj: ~];?[CR:+].


/ses frères, Untel \
40> noun[famille, pl], (adj[nat:+ ];adj[postProf:+];noun[org:+ ]), punct[form:fcm], noun[maj:+, person:~-, org:~+ ,lieu:~, proper=+, person=+, marque_pers=+], punct[form:fcm];coord.

/ses soeurs Untel et Untel\
45> noun[famille, pl], (adj[nat:+ ];adj[postProf:+]; noun[org:+ ]), noun[maj:+], punct[form:fcm];coord, noun[maj:+, org:~+ ,proper=+, person=+, marque_pers=+], ?[partic:~ ,maj: ~];?[CR:+].

/le maire PDCI, Aké M'Bo\
45> noun[prof:+, sg:+, phrasemaj:~], noun[toutmaj, org=+, pren:~,celeb:~,proper].

/le maire de la Ciotat Rigaud\
45> noun[prof:+, sg:+], (adj[nat:+];adj[postProf:+];noun[nat:+];noun[org:+]), ((prep[form:fde]), (det), noun[villedef];noun[pays, lieudef];noun[region, nondef:~];noun[continent];noun[org]), noun[maj:+, person:~-, org:~+ ,nat:~, phrasemaj:~, proper=+, person=+, marque_pers=+], ?[partic:~, maj:~];?[CR:+].

/l'oncle Archi\
45> noun[famille:+], (adj[nat:+];adj[postProf:+];noun[nat:+];noun[org:+]), ((prep[form:fde]), (det), noun[villedef];noun[pays, lieudef];noun[region, nondef:~];noun[continent];noun[org]), noun[maj:+, person:~-, org:~+ ,nat:~, phrasemaj:~, proper=+, person=+, marque_pers=+], ?[partic:~, maj:~];?[CR:+].

/en majuscules\
45> noun[famille:+], (adj[nat:+];adj[postProf:+];noun[nat:+];noun[org:+]), ((prep[form:fde]), (det), noun[villedef];noun[pays, lieudef];noun[region, nondef:~];noun[continent];noun[org]), noun[maj:+, person:~-, org:~+ ,nat:~, inconnu, ang:~, proper=+, person=+, marque_pers=+], ?[inconnu:~];?[CR:+].


/sa femme Ouata\
45>det[poss], noun[lemme:femme], noun[maj, org:~, phrasemaj:~, toutmaj:~, person=+].

/le maire (UDF) Rigaud, le journaliste belge Untel\
45> noun[prof:+, sg:+], (adj[nat:+];adj[postProf:+];noun[nat:+];noun[org:+]), ((prep[form:fde, def:~]), noun[lieu];noun[org]), noun[maj:+, person:~-, org:~+, nat:~, phrasemaj:~, proper=+, person=+, marque_pers=+], ?[partic:~, maj:~];?[CR:+].


/Bessard (,) le poète \
45> ?[form:~fdans, det:~, maj:~], noun[maj:+, person:~-, org:~+, org2:~, tit:~, lieu:~, ville:~, pays:~, ptCard:~, region:~, toutmaj:~, start:~, proper=+, person=+, marque_pers=+], (punct[form:fcm]), det, (adj)*, (adj[preProf]), noun[prof:+, sg:+], (adj[nat]), (punct[form:fcm]), ?[pren:~, pers:~, celeb:~, form:~fcm, nat:~].

45> noun[maj:+, person:~-, org:~+, tit:~, lieu:~, ville:~, pays:~, ptCard:~, region:~, toutmaj:~, start:+, inconnu:+, proper=+, person=+, marque_pers=+], (punct[form:fcm]), det, (adj)*, (adj[preProf]), noun[prof:+, sg:+], (adj[nat]), (punct[form:fcm]), ?[pren:~, pers:~, celeb:~, form:~fcm, nat:~].


/Rigaud, (ancien) maire de Trifouillis\
45> noun[maj:+, person:~-, org:~, start:~, lieu:~, ville:~, region:~, pays:~, toutmaj:~, proper=+, person=+, marque_pers=+], punct[form:fcm], (adj[preProf]), noun[prof:+, sg:+, prof1:~, prof2:~].

45> noun[maj:+, person:~-, org:~, lieu:~, ville:~, region:~, pays:~, inconnu, proper=+, person=+, marque_pers=+,toutmaj:~], punct[form:fcm], (adj[preProf]), noun[prof:+, sg:+, prof1:~, prof2:~].


/le maire, Rigaud\
45>det, (adj)*, noun[prof:+, sg:+, prof1:~, prof2:~], (adj[nat:+];adj[postProf:+]; noun[org:+]), (prep[form:fde], noun[loc];noun[org]), punct[form:fcm], noun[maj:+, person:~-, org:~, lieu:~, proper=+, person=+, marque_pers=+], ?[partic:~ ,maj: ~];?[CR:+].

/Le Japonais TOYOTA\
45> ?[det:+], noun[nat:+], noun[maj:+, toutmaj:+, person:~-, org:~+, proper=+, org=+], ?[partic:~ ,maj:~];?[CR:+].

/La Suissesse Kessard\
45> ?[maj:~], noun[maj, org:~, person=+], punct[form:fopar], num, noun[lemme:an, pl], punct[form:fcpar].
46> ?[det:+], noun[nat:+];adj[nat], noun[maj:+, person:+, nonorg=+].
47> ?[det:+], noun[nat:+];adj[nat], noun[maj:+, person:~-, org:~+, firme:~, nonorg:~, proper=+, org=+, person=+, marque_pers=+], ?[partic:~ ,maj:~];?[CR:+].

/l'épouse de Trifouillet\
45> ?[det:+], noun[famillede:+], prep[form:fde], noun[maj:+, person:~, proper=+, person=+], ?[maj:~, partic:~];?[CR].

Séquence:
/les chanteurs Rigaud Bressard (et Nilolo Nilu)\

50> Noun[mwe=+, proper=+, person=+, maj=+] @= |noun[prof:+, pl:+];noun[famille:+, pl:+], (adj[nat:+ ];adj[postProf:+]; noun[org:+ ])| (?[partic:+,coord:~]), noun[maj:+, phrasemaj:~, person:~, org:~+ , lieu:~, marque_pers:~,nat:~], ((?[partic:+,coord:~ ]), noun[maj:+ ]), ((?[partic:+,coord:~ ]), noun[maj:+ ]).

/les chanteurs, Rigaud Bressard (et Nilolo Nilu)\

50> Noun[mwe=+, proper=+, person=+, maj=+] @= |noun[prof:+, pl:+], (adj[nat:+ ];adj[postProf:+]; noun[org:+ ]), punct[form:fcm]| (?[partic:+,coord:~]), noun[maj:+, person:~, org:~+, lieu:~, marque_pers:~,nat:~], ((?[partic:+,coord:~ ]), noun[maj:+ ]), ((?[partic:+,coord:~ ]), noun[maj:+ ]) |punct[form:fcm];coord|.

/les chanteurs (Rigaud,) van Bressard (et Nilolo Nilu)\

52> Noun[mwe=+, proper=+, person=+, maj=+] @= |noun[prof:+, pl:+], (adj[nat:+ ];adj[postProf:+];noun[org:+ ]), (punct[form:fcm]), noun[person:+], punct[form:fcm]| (?[partic:+,coord:~]), noun[maj:+, person:~, org:~+, lieu:~, marque_pers:~], ((?[partic:+,coord:~]), noun[maj:~]), ((?[partic:+,coord:~]), noun[maj:+]), ((?[partic:+,coord:~]), noun[maj:+]) |coord, (?[partic:+,coord:~]), noun[maj:+, person:~-, org:~+ ]|.

/les chanteurs (Rigaud, van Bressard et) Nilolo Nilu\

55> Noun[mwe=+, proper=+, person=+, maj=+] @= |noun[prof:+, pl:+];noun[famille:+, pl], (adj[nat:+ ];noun[org:+ ]), (punct[form:fcm]), noun[person:+], (punct[form:fcm], noun[person:+]), coord| (?[partic:+,coord:~]), noun[maj:+, person:~, org:~+ ,marque_pers:~], ((?[partic:+,coord:~]), noun[maj:~]), ((?[partic:+,coord:~]), noun[maj:+]), ((?[partic:+,coord:~]), noun[maj:+]).



50> Noun[mwe=+, proper=+, person=+, marque_pers=+] @= |noun[prof:+, sg:+];noun[famille, sg], (adj[nat:+];adj[postProf:+];noun[nat:+];noun[org:+]), ((prep[form:fde]), (det), noun[lieu, villedef];noun[lieu, pays, lieudef];noun[continent];noun[org])| noun[maj:+, person:~-, org:~+, nat:~, org2:~, firme2:~, marque_pers:~, phrasemaj:~, prof:~].

/le maire (de La Ciotat) (Loulou) Rigaud Bessard, le journaliste belge Rigaud Bessard, son frère Eddam Ritani\


50> Noun[mwe=+, proper=+, person=+, marque_pers=+] @= |noun[prof:+, sg:+];noun[famille, sg], (adj[nat:+];adj[postProf:+];noun[nat:+];noun[org:+]), ((prep[form:fde]), (det), noun[lieu, villedef];noun[lieu, pays, lieudef];noun[continent];noun[org])| (?[partic:+,coord:~]), noun[maj:+, person:~-, org:~+, nat:~, org2:~, firme2:~, marque_pers:~, phrasemaj:~, prof:~], ((?[partic:+,coord:~]), noun[maj:+]), ((?[partic:+,coord:~]), noun[maj:+]).

/LE MAIRE RIGAUD BESSARD\
50> Noun[mwe=+, proper=+, person=+, marque_pers=+] @= |noun[prof:+, sg:+];noun[famille, sg], (adj[nat:+];adj[postProf:+];noun[nat:+];noun[org:+]), ((prep[form:fde]), (det), noun[lieu, villedef];noun[lieu, pays, lieudef];noun[continent];noun[org])| (?[partic:+,coord:~]), noun[maj:+, person:~-, org:~+, nat:~, marque_pers:~, phrasemaj:~, inconnu, ang:~], ((?[partic:+,coord:~]), noun[maj:+]), ((?[partic:+,coord:~]), noun[maj:+]).

/le maire (UDF) (Loulou) Rigaud Bessard, le journaliste belge Rigaud Bessard\
50> Noun[mwe=+, proper=+, person=+, marque_pers=+] @= |noun[prof:+, sg:+], (adj[nat:+];adj[postProf:+];noun[nat:+];noun[org:+]), ((prep[form:fde, def:~]), noun[lieu, lemme:~"France"];noun[org])| (?[partic:+,coord:~]), noun[maj:+, person:~-, org:~+, org2:~, firme2:~, lieu:~, nat:~, marque_pers:~, prof:~], ((?[partic:+,coord:~]), noun[maj:+]), ((?[partic:+,coord:~]), noun[maj:+]).

/la présidente de l'association, Ayako Nakajima\
50> Noun[mwe=+, proper=+, person=+, marque_pers=+] @= |noun[preorgde], prep[form:fde], (det), noun, punct [form:fcm]| noun[maj, phrasemaj:~, org:~], (punct[hyph]), (?[partic]), (punct[hyph]), noun[maj, org:~], (noun[maj, org:~]).


/Wu Xieqian(,) le ministre chinois\
50> Noun[mwe=+, proper=+, person=+, maj=+] @= |?[det:~, form:~fdans]| noun[maj:+, person:~-, org:~+, org2:~, org1:~, tit:~, lieu:~, pays:~, region:~, ville:~, nat:~, toutmaj:~, start:~], (?[partic:+,coord:~]), noun[maj:+, org1:~, org2:~], ((?[partic:+,coord:~]), noun[maj:+, org2:~]) |(punct[form:fcm]), det, (adj)*, (adj[preProf]), noun[prof:+, sg:+], ?[marque_pers:~, inconnu:~, celeb:~, pren:~, pers:~]|.

50> Noun[mwe=+, proper=+, person=+, maj=+] @= noun[maj:+, person:~-, org:~+, tit:~, lieu:~, pays:~, region:~, ville:~, nat:~, toutmaj:~, start:+], (?[partic:+,coord:~]), noun[maj:+], ((?[partic:+,coord:~]), noun[maj:+]) |(punct[form:fcm]), det, (adj)*, (adj[preProf]), noun[prof:+, sg:+], ?[marque_pers:~, inconnu:~, celeb:~, pren:~, pers:~]|.


/Rigaud, maire de Trifouillis\
50> Noun[mwe=+, proper=+, person=+, maj=+] @= (?[partic:+,coord:~]), noun[maj:+, person:~-, org:~, nat:~,  lieu:~, pays:~, region:~, ville:~, start:~], (?[partic:+,coord:~]), noun[maj:+], ((?[partic:+,coord:~]), noun[maj:+]) |punct[form:fcm], (adj[preProf]), noun[prof:+, sg:+, prof1:~, prof2:~]|.

50> Noun[mwe=+, proper=+, person=+, maj=+] @= (?[partic:+,coord:~]), noun[maj:+, person:~-, org:~, nat:~,  lieu:~, pays:~, region:~, ville:~, inconnu:+], (?[partic:+,coord:~]), noun[maj:+], ((?[partic:+,coord:~]), noun[maj:+]) |punct[form:fcm], (adj[preProf]), noun[prof:+, sg:+, prof1:~, prof2:~]|.


/le maire (de Grenoble), Alphonse Rigaud\
50> Noun[mwe=+, proper=+, person=+, maj=+] @= |det, (adj)*, noun[prof:+, sg:+, prof1:~], (adj[nat:+];adj[postProf:+];noun[org:+]), (prep[form:fde], noun[lieu];noun[org]), punct[form:fcm]| noun[maj:+, person:~-, org:~, marque_pers:~], ((?[partic:+,coord:~]), noun[maj:+]), ((?[partic:+,coord:~]), noun[maj:+]).

/le ministre d'Etat aux Affaires étrangères, Kanwar Natwar Singh.\
50> Noun[mwe=+, proper=+, person=+, maj=+] @= |?[def], (adj)*,noun[prof:+], (adj[nat:+];adj[postProf:+]), prep[form:fde];prep[form:fa], noun[lemme:affaire, pl], adj+, (punct[form:fcm])| noun[maj:+, person:~-, org:~,  lieu:~, pays:~, region:~, ville:~, marque_pers:~], ((?[partic:+,coord:~]), noun[maj:+]), ((?[partic:+,coord:~]), noun[maj:+]).

/le ministre de l'Intérieur afghan, Kanwar Natwar Singh.\
50> Noun[mwe=+, proper=+, person=+, maj=+] @= |det, (adj)*, noun[prof:+], (adj[nat:+];adj[postProf:+]), prep[form:fde];prep[form:fa], det, noun[ministere], (adj[maj:~]), (punct[form:fcm])| noun[maj:+, person:~-, org:~,  lieu:~, pays:~, region:~, ville:~, marque_pers:~, nat:~], ((?[partic:+,coord:~]), noun[maj:+]), ((?[partic:+,coord:~]), noun[maj:+]).

//16> Noun[mwe=+, proper=+, person=+, maj=+] @= ?#1[lemme:"[avec,poète,saint,étude,santé,intérieur]"], where(print("ICI:"+#1+" "+#1[traits]+#1[surface]+"\n")).


/Le Japonais TOYOTA SC\
50> Noun[mwe=+, proper=+, firme=+, maj=+, toutmaj=+] @= |noun[nat:+]| noun[maj:+, toutmaj:+, person:~-, org:~+, marque_pers:~],  (noun[maj:+]), (noun[maj:+]).

/La Suissesse Bessard Rigaud (38 ans)\
50> Noun[mwe=+, proper=+, person=+, nonorg=+] @= noun[maj], (?[partic]), noun+[maj] |punct[form:fopar], num, noun[lemme:an], punct[form:fcpar]|.

/La Suissesse Bessard Rigaud\
51> Noun[mwe=+, proper=+, person=+, org=+] @= |det, adj*, noun[nat:+]| noun[maj:+, person:~-, org:~+, marque_pers:~, nonorg:~], ((?[partic:+,coord:~]), noun[maj]), ((?[partic:+,coord:~]), noun[maj:+]).


/le cousin de Trifouillet Bessard, le fils de Clin Bond\
50> Noun[mwe=+, proper=+, person=+] @= |?[det:+], noun[famillede:+, sg], prep[form:fde, def:~]| noun[maj:+, person:~], (?[partic]), (noun[maj:+, person:~-]).

/Saint-Jean L'Evangéliste, Louis le Pieux, Pline l'Ancien\
//50> noun[proper=+, person=+] @= noun[pren], det[def, masc, sg], adj[maj].
50> Noun[mwe=+, proper=+, person=+] @= noun[sainttiret, masc];noun[saintsTiret, masc];noun[pren, masc];noun[celeb, maj], (punct[hyph]), det[masc, sg, def], (punct[hyph]), adj[maj];noun[maj, lieu:~, pays:~, region:~, org:~, firme:~].

/Isabelle la Catholique\
50> Noun[mwe=+, proper=+, person=+] @= noun[sainttiret, fem];noun[saintsTiret, fem];noun[pren, fem], (punct[hyph]), det[fem, sg, def], (punct[hyph]), adj[maj, fem, sg];noun[maj, lieu:~, pays:~, region:~, org:~, firme:~].

/Ivan le Terrible - prénom dont le genre est inconnu\
50> Noun[mwe=+, proper=+, person=+] @= noun[sainttiret];noun[saintsTiret];noun[pren, masc:~, fem:~];noun[celeb, maj], (punct[hyph]), det[sg, def], (punct[hyph]), adj[sg, maj];noun[maj, lieu:~, pays:~, region:~, org:~, firme:~].

/personnes célèbres\
55> noun[mwe=+, proper=+, person=+] = det[def], noun[celebdef, celebmin:~, celebamb:~].
55> noun[mwe=+, proper=+, person=+] = det[def], noun[celebdef, celebamb:~, toutmaj:~].

/ Soro Guillaume: corpus cote d'ivoire \

56>noun[mwe=+, proper=+, person=+] = noun[maj, proper,celeb], noun[pren].

Marqueur:
/les Italiens Geovani et Rigotto\
60> noun[nat:+, pl:+], noun[person], coord, noun[maj:+, person:~-, org:~+, inconnu, person=+].
65> noun[nat:+, pl:+], noun[org], coord, noun[maj:+, inconnu, org=+].


/ses compatriotes Rainey, Magee et Sarron\
60> noun[nat:+, pl:+], noun[person], punct[form:fcm], noun[maj:+, person:~-, org:~+, inconnu, person=+], coord, noun[maj:+, person:~-, org:~+, inconnu, person=+].
65> noun[nat:+, pl:+], noun[org], punct[form:fcm], noun[maj:+, inconnu, org=+], coord, noun[maj:+, person:~-, inconnu, org=+].


80> noun[saintTiret:+, lieu:~, person=+, lieu=+].
80> noun[saintsTiret:+, person=+, ville=+].

/personnes célèbres\
80> noun[celeb:+, celebdef:~, person=+, maj:+, phrasemaj:~, start:~].
80> noun[celeb:+, celebdef:~, person=+, maj:+, phrasemaj:~, celebamb:~].
80> noun[celeb:+, celebdef:~, person=+, maj:+, celebamb1:~, celebamb:~].
80> noun[celeb:+, celebdef:~, person=+, min:+].
80> ?[def], noun[celebdef, celebmin:~, celebamb:~, person=+].
80> ?[def], noun[celebdef, celebamb:~, toutmaj:~, person=+].

/Saint(-)Martin n'est pas une personne dans la Saint-Martin\
85> det, noun[saintTiret:+, person=~, lieu=~].
85> det, noun[saintsTiret:+, lemme:~"saint vierge", person=~].


/Saint-Martin n'est pas une personne dans Villeneuve-Saint-Martin\
85> noun[ville:+], punct[hyph], noun[saintTiret:+, person=~].

/Saint-Martin n'est pas une personne dans à Saint-Martin\
85> prep[form:fa, def:~], noun[saintTiret:+, person=~].

/la sainte Vierge, Saint Augustin ne sont pas des villes\
85> noun[saintstiret, lieu=~, ville=~]{?, noun[saintpers]}.

Désambiguïsation:
90> noun[celeb, nondef] += |det| noun[celeb=~, person=~].



