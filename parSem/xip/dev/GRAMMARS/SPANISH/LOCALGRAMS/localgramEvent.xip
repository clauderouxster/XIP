//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2008 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   
//!   File   : localgraEvent.xip
//!
//!   Content:	definition of compounds and local rules for events
//!   		entities
//!
//!   Language:	SPANISH
//! ENCODING:	ISO-LATIN-1
//!
//!   Date       : June 2008
//!
//!   Author     : Caroline Hagege (using input data given by UCM)  
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Sequence:


// Batalla de Xwer
// e.g. Batalla de Gallipoli
5> noun[event=+,mwe=+] @= noun[lemma:batalla,maj], prep[lemma:de], (art), (prep[lemma:de];art;?[maj])*, ?[maj:+] .


// Revolución de <data>
// e.g Revolución de 1848
5> noun[event=+,mwe=+] @= noun[lemma:"revolución"], prep[lemma:de], ?[dig:+,lemma:"%d%d%d%d"] .

// Revolucion + Demonym
// e.g Revolución Francesa
5> noun[event=+,mwe=+] @= noun[lemma:"revolución"], adj[maj,nationality:+] .

// Revolucion + Demonym
// e.g Revolución de <Month>
// Revolución de Febrero
5> noun[event=+,mwe=+] @= noun[lemma:"revolución",maj], prep[lemma:de], ?[month] .


// Vuelta a <location>
// eg Vuelta a Andalucia
5> noun[event=+,mwe=+] @= noun[lemma:vuelta,maj], prep[lemma:a], ?[location] .

// el Tour de Francia
5> noun[event=+,mwe=+] @= noun[surface:"Tour"], prep[lemma:de], ?[surface:"Francia"] .

// El Mundial de Stuttgart
5> ? = | art | noun[lemma:mundial,maj:+,event=+] | prep[lemma:de];num | .


// Campeonato
// eg Campeonato 2005 ; Campeonato de la Federacion de Futbol de Asia Ocidental
5> noun[event=+,mwe=+] @= noun[lemma:campeonato,maj], (?[dig:+, lemma:"%d%d%d%d"]), (?[maj]; prep[lemma:de]; art)*, (?[lemma:y]), ?[maj] .
5> noun[event=+,mwe=+] @= noun[lemma:campeonato,maj], ?[dig:+, lemma:"%d%d%d%d"] .

// Juegos Olimpicos de Verano;Invierno de Atenas;Grenoble
5> noun[event=+,mwe=+] @= noun[lemma:juego,pl,maj], ?[lemma:"olímpico"], (prep;art;?[maj])*, ?[maj:+] .

// Guerra de Maj+
// Guerra de Yom Kippur;Guerra de Abjasia; Guerra de 1812
5> noun[event=+,mwe=+] @= noun[surface:"Guerra"], prep[lemma:de], ?+[maj:+]; ?[dig:+, lemma:"%d%d%d%d"] .

// Guerra Civil + demonym ; de country
5> noun[event=+,mwe=+] @= noun[surface:"Guerra"], adj[lemma:civil], ?[nationality].
5> noun[event=+,mwe=+] @= noun[surface:"Guerra"], (adj[lemma:civil]), prep[lemma:de], (art), noun[location].


// Guerra + Adj[maj] 
// Guerra Fría
5> noun[event=+,mwe=+] @= noun[surface:"Guerra"], ?[nationality,maj];?[lemma:frío,maj] .


// Coronación de <person>
5> noun[event=+,mwe=+] @= noun[lemma:"coronación",maj], prep[lemma:de], (art), noun[mwe,human];?+[maj] .

// Día Mundial de XXXX
5> noun[event=+,mwe=+] @= noun[lemma:"día",maj], adj[lemma:mundial], (prep;art;?[maj])*, ?[maj] .


// 11 de setiembre (de 2001)
5> noun[event=+,mwe=+] @= | art | ?[lemma:11], prep[lemma:de], ?[lemma:setiembre], (prep[lemma:de], ?[lemma:2001]) .

// 11-S
5> noun[event=+,mwe=+] @= | art | ?[lemma:11], ?[lemma:"-"], ?[surface:"S"] .


// Convencion Republicana, III Festival de Cine, etc. see lexEvent.xip
// concatenacion de un nombre introdutor de eventos seguido por algo con mayuscula
10> noun[mwe=+,event=+]  @= (?[dig,roman]), noun[introd_event,maj], (adj[maj]), (prep[lemma:de];art;?[maj:+];?[surface:y])*, ?[maj:+] .
 
// event (sigla) -> sigla = event tb
15> ? @= ?[event], punct[paren], noun[toutmaj,event=+], punct[paren] .
