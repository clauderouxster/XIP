//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2008 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   
//!   File   : localgramGeo.xip
//!
//!   Content:	definition of compounds and local rules for geographi
//!   		entities
//!
//!   Language:	SPANISH
//! ENCODING:	ISO-LATIN-1
//!
//!   Date       : May 2008
//!
//!   Author     : Caroline Hagege (using input data given by UCM)
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Sequence:


// Fully lexicalized local rules
// From UCM list

5> noun[mountain=+] = ?[surface:"Alto"], ?[surface:"Abedules"]; ?[surface:"Rey"]; ?[surface:"Guriezo"] .
5> noun[mountain=+] = ?[surface:"Alto"], prep[lemma:de], ?[surface:"Guaraja"];?[surface:"Imunía"] .
5> noun[mountain=+] = ?[surface:"Alto"], prep[lemma:de], ?[surface:"Peña"], ?[surface:"Negra"] .
5> noun[mountain=+] = ?[surface:"Alto"], prep[lemma:de], art[def,masc], ?[surface:"Caballo"];?[surface:Mirlo];?[surface:"Telégrafo"];?[surface:"Toral"] .
5> noun[mountain=+] = ?[surface:"Alto"], prep[lemma:de], art[def,fem], ?[surface:"Triguera"] .

5> noun[mountain=+] = noun[lemma:"páramo", maj], prep[lemma:de], art[def,fem], ?[surface:"Llora"].


// Estado de Florida
10> noun[state=+,mwe=+] @= ?[lemma:estado], prep[lemma:de], ?[state:+] .


//Estados Unidos
10> noun[country=+] = ?[surface:Estados,!number:!,!gender:!], ?[surface: Unidos] .

//Reino Unido
10> noun[country=+] = ?[surface:Reino,!number:!,!gender:!], ?[surface: Unido].


// Republica Checa
10> noun[country=+] = ?[surface:"República"], adj[lemma:checo,maj] .

// Republica de Colombia
10> noun[country=+] = ?[surface:"República"], prep[lemma:de], noun[country].


// Nueva Zelanda/Zelandia
10> noun[country=+] = adj[lemma:nuevo, maj:+], noun[lemma:"Zeland(i)a"] .

//Comunidad Autonoma;Valenciana (de XXX)
11> noun[mwe=+,region=+] @=  noun[surface:"Comunidad"], ?[nationality,maj] .
11> noun[mwe=+,region=+] @=  noun[surface:"Comunidad"], (?[surface:"Autónoma"]), prep[lemma:de], noun[location,maj] .


//Principado de XXX (being XXX something geographical)
11> noun[mwe=+,region=+] @= noun[surface:"Principado"], prep[lemma:de], noun[local] .

// Ciudad de México, Ciudad de Guatemala...
15> noun[mwe=+,city=+] = noun[surface:Ciudad], prep[lemma:de], noun[local] .

// Guatemala City, México City
15> noun[mwe=+,city=+] = noun[country], ?[surface:"City"] .

// Tercer mundo
15> noun[mwe=+,location=+] = ?[surface:"Tercer"], noun[lemma:mundo] .

// Norte de Francia
15> noun[mwe=+,region=+] @= ?[card_p], prep[lemma:de], noun[country] .

// Africa del Norte
15> noun[mwe=+,region=+] @= ?[location:+], prep[lemma:de], (art[lemma:el]), noun[card_p] .

// Europa occidental
15> noun[mwe=+,region=+] @= ?[location:+,org:~], adj[card_p] .

// Santo Domingo Norte
15> noun[mwe=+,region=+] @= ?[location:+,org:~], noun[card_p] .


// Bahamas Centrales
// restriction to avoid to take as location *Santander Central Hispano
15> noun[mwe=+, region=+] @= ?[location:+,org:~], ?[lemma:central] .


// Republica Popular (Democratica;Islámica;Socialista) de (Location)
15> noun[mwe=+,country=+] @= noun[lemma:república,maj:+], (adj[lemma:popular];adj[lemma:democrático];?[lemma:socialista];?[lemma:islámico])+, (prep[lemma:de]), ?[location] .

// Medio Oriente ; Oriente Medio
15> noun[mwe=+,region=+] @= ?[lemma:medio,maj], noun[lemma:oriente,maj] .
15> noun[mwe=+,region=+] @= noun[lemma:oriente,maj], ?[lemma:medio,maj];?[lemma:próximo,maj] .


// Barrio Chino
15> noun[mwe=+,location=+] @= noun[lemma:barrio, maj], ?[maj] .

// avenida/calle XXXX
// restriction on number e.g. *las calles de Paris
15> noun[mwe=+,location=+,street=+] @= ?[street,common,sg:+], ?+[prop] .
15> noun[mwe=+,location=+,street=+] @= ?[street, common,sg:+], (?[maj];prep[lemma:de];art)*, ?[maj] .

// Villa Adelina/Clara/Soriano
15> noun[mwe=+,location=+] @= noun[surface:Villa], ?[maj] .


// Lagos, Mares y Ocanos
15> noun[mwe=+,location=+] @= noun[lemma:océano, maj], ?[maj,surface:"%+ico"] .
15> noun[mwe=+,location=+,water=+] @= ?[introd_geo,water:+,maj:+], (prep[lemma:de];art[def];?[maj])*, ?[maj:+] .
// Bahía y Golfo
15> noun[mwe=+,location=+,water=+] @= noun[lemma:bahía,maj];noun[lemma:golfo,maj], prep[lemma:de], ?[maj] .

//////////////////////////////////
// Montanas
//////////////////////////////////

// Cerro Poyo
15> noun[mwe=+,location=+,mountain=+] @= ?[introd_geo,mountain:+,maj:+], (prep[lemma:de];art[def];?[maj])*, ?[maj:+] .
// con el introdutor Cueto (is also a name place)
15> noun[mwe=+,location=+,mountain=+] @= noun[surface:"Cueto"], (prep[lemma:de];art[def];?[maj])*, ?[maj:+] .
// con introductor "Monte". It is not codified as introd_geo:+, mountain:+, because of possible ambiguity
// when it is used alone (Monte in Corsica, Monte in Argentina are places)
15> noun[mwe=+,location=+,mountain=+] @= noun[surface:"Monte"] ,(prep[lemma:de];art[def];?[maj])*, ?[maj:+] .

// Con Cuetu
15> noun[mwe=+,location=+,mountain=+] @= noun[surface:"Cuetu"], (art[def];prep[lemma:de];?[maj])*, ?[maj:+] .

// Con Pic
15> noun[mwe=+,location=+,mountain=+] @= noun[surface:"Pic(u)"], (prep[lemma:de];art[def];?[maj])*, ?[maj:+] .
15> noun[mwe=+,location=+,mountain=+] @= noun[surface:"Pic"], ?[surface:"d"], ?[surface:"'"], ? .

// Con Pico
// Not introduced as general introd_geo in the lexicon because of possible ambiguities
// restriction for "pico de Cuervo" which is a weapon and not a mountain
15> noun[mwe=+,location=+,mountain=+] @= noun[surface:"Pico(s)"], (prep[lemma:de];art[def];?[maj])*, ?[maj:+,lemma:~"cuervo"] .
15> noun[mwe=+,location=+,mountain=+] @= noun[surface:"Pico"], noun[prop:+,mwe:+,human:+] .

// Con Puig
15> noun[mwe=+,location=+,mountain=+] @= noun[surface:"Puig"], (prep[lemma:de];art[def];?[maj])*, ?[maj:+] .
15> noun[mwe=+,location=+,mountain=+] @= noun[surface:"Puig"], ?[surface:"d"], ?[surface:"'"], ?[maj] .



// Saint <firstname> e.g. Saint Paul precedido por "en" "cerca de" "lejos de"
20> noun[mwe=+,location=+] @= | prep[lemma:en]; prep[lemma:"cerca de"]; prep[lemma:"lejos de"] | ?[surface:Saint];?[surface:"San"], ?[prop,maj] .



// With introd_geo elements
// very general to refine


30> noun[mwe=+,location=+] @= ?[introd_geo,maj:+], (prep[lemma:de]), (art[def]), noun[maj:+] .

30> ? @= | noun[introd_geo,maj:~], (prep[lemma:de]) | (art[def,maj]), (?[maj:+, start_loc=+];prep[lemma:de];art)*, ?[maj:+, end_loc=+] .

// With admin_area elements
// to refine
30> noun[mwe=+,location=+,admin_area=+] @= noun[admin_area,common,maj:+], (prep[lemma:de];art;?[maj])*, ?[maj] .

30> ? @= | noun[admin_area,common,maj:~], (prep[lemma:de]) | (art[def,maj]), (?[maj:+, start_loc=+];prep[lemma:de];art)*, ?[maj:+, end_loc=+] .


// El alcade de XXX
30> ? @= | ?[lemma:alcalde], prep[lemma:de] | (?[maj:+,location:~,start_loc=+];prep[lemma:de];art)*, ?[maj:+,location:~,end_loc=+] .
 

// moradores de XXX
30> ? @= | ?[lemma:morador], prep[lemma:de] | (?[maj:+,location:~,start_loc=+];prep[lemma:de];art)*, ?[maj:+,location:~,end_loc=+] .



