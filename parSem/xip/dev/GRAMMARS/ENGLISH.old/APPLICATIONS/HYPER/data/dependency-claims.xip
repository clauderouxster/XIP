DependencyRules:

////////////////////////////////
///////// CLAIMS ///////////////
////////////////////////////////


//lexicon

//NUCL_PARTICLE(find,out)

| #1[noclaim=+] |
if ( NUCL[particle](#1[lemme:find],#2[lemme:out]) )
   ~.

| #1[mental,know:~,noclaim:~,hedge:~,mentscoclaim=+] |
  ~.

| #1[scopeclaim,know:~,noclaim:~,hedge:~,mentscoclaim=+] |
  ~.

| #1[art,know:~,noclaim:~,hedge:~,artclaim=+] |
  ~.



////////////////////////

if ( ^$1(#1[deict,lemme:~this],#2[mentscoclaim]) | ^$1(#1[mentscoclaim],#2[deict,lemme:~this]) )
   $1[claim=+](#1,#2).

if ( ^$1(#1[deict,lemme:~this],#2[artclaim]) | ^$1(#1[artclaim],#2[deict,lemme:~this]) )
   $1[claim=+](#1,#2).

//These results strongly suggest 
//DETD_CLAIM(results,These)
//SUBJ(suggest_HEDGE:+,results)


if ( ^$1[claim](#1,#2) & SUBJ(#3[hedge],#1) )
   $1[claim=~](#1,#2).

if ( ^$1[claim](#1,#2) & SUBJ(#3[noclaim],#1) )
   $1[claim=~](#1,#2).

//our results suggest 

if ( ^$1[claim](#2,#1) & SUBJ(#3[hedge],#1) )
   $1[claim=~](#2,#1).

if ( ^$1[claim](#2,#1) & SUBJ(#3[noclaim],#1) )
   $1[claim=~](#2,#1).

//Here we provide a technological approach

if ( ^$1[claim](#1,#2) & OBJ(#1,#3[noclaim]) )
   $1[claim=~](#1,#2).

if ( ^$1[claim](#1,#2) & OBJ(#1,#3[hedge]) )
   $1[claim=~](#1,#2).


//Our analysis of known HuD-associated mRNAs in neurons identified
//DETD_CONTRIBUTION(analysis,Our)
//SUBJ(identified,analysis)
 

if ( ^SUBJ(#1[mentscoclaim],#2[deict]) & DETD(#3[mentscoclaim],#1) )
   SUBJ[claim=+](#1,#2).

if ( ^SUBJ-N(#1[mentscoclaim],#2[deict]) & DETD(#3[mentscoclaim],#1) )
   SUBJ-N[claim=+](#1,#2).


if ( SUBJ[claim](#1[mentscoclaim],#2[deict]) & ^DETD(#3[mentscoclaim],#1) )
   SUBJ[claim=+](#1,#3).

if ( SUBJ-N[claim](#1[mentscoclaim],#2[deict]) & ^DETD(#3[mentscoclaim],#1) )
   SUBJ-N[claim=+](#1,#3).

if ( ^SUBJ(#1[art],#2[deict]) & DETD(#3[mentsco,noclaim:~,know:~,hedge:~],#1) )
   SUBJ[claim=+](#1,#2).

if ( ^SUBJ-N(#1[artclaim],#2[deict]) & DETD(#3[mentscoclaim],#1) )
   SUBJ-N[claim=+](#1,#2).

if ( SUBJ[claim](#1[artclaim],#2[deict]) & ^DETD(#3[mentscoclaim],#1) )
   SUBJ[claim=+](#1,#3).

if ( SUBJ-N[claim](#1[artclaim],#2[deict]) & ^DETD(#3[mentscoclaim],#1) )
   SUBJ-N[claim=+](#1,#3).


//Here we used mass spectrometry to identify

| IV#3{#4[lemme:to],#5[mentscoclaim]} | 
if ( SUBJ(#2,#1[deict]) )
   SUBJ-N[claim=+](#5,#1).

//We have shown previously
//MANNER_POST(shown,previously)
//SUBJ-N_CONTRIBUTION_CLAIM_PRE(shown,We)

if ( ^$1[claim](#1,#2) & $2(#1,#3[old]) )
   $1[claim=~](#1,#2)

if ( ^$1[claim](#2,#1) & $2(#1,#3[old]) )
   $1[claim=~](#2,#1)

//These data demonstrate
//SUBJ-N(#1demonstrate,#2data)
//DETD_CONTRIBUTION(#2data,#3These)

if ( ^SUBJ-N(#1[mentscoclaim],#2[mentsco]) & $1(#2,#3[deict]) )
   SUBJ-N[claim=+](#1,#2).

if ( SUBJ-N[claim](#1[mentscoclaim],#2[mentsco]) & ^$1(#2,#3[deict]) )
   $1[claim=+](#2,#3).


| TOP#1{ ?+ } |
if ( $1[claim](#2,#3) & ~CLAIM(#1) )
   CLAIM(#1).



/
| #1[hedge] |
if ( ^$1[claim](#2,#3) )
   $1[claim=~](#2,#3).

| #1[hedge] |
if ( ^CLAIM(#2) )
   ~.
\


| TOP#1{ ?+ } |
if ( ~CLAIM(#1) )
   SENT(#1).


