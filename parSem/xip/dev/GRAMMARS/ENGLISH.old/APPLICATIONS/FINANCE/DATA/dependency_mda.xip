DependencyRules:

///////////////////////////////////////////////////////////////////////////////
///////////////////////////// Presentation of data in tables  \\\\\\\\\\\\\\\\\
///////////////////////////////////////////////////////////////////////////////

//////////////// as follows

//Marketing revenues, operating earnings, depreciation and certain costs are as follows (in thousands):
//ASL(as,follows)
//MAIN(are)
//PRESENT(are)

if ( ASL(#2,#3[lemme:follow]) & ^SENT(#4) )
   SENT[pres-data=+](#4).

//////////////// information is
//Supplemental volume and price information is:

if ( SUBJ(#1[lemme:be],#2[info]) & ^SENT(#3) )
   SENT[pres-data=+](#3).

//////////////// The following are
//The following are our current assumptions regarding ...
//SUBJ-N_DEICT_PRE(are,following)
//DETD((following,the)

if ( SUBJ-N[pre](#1,#2[deict]) & DETD(#2,#4[lemme:the]) & ^SENT(#3) )
   SENT[pres-data=+](#3).


// indicate / deict / info
//as shown in the table above
//MOD_POST(shown,table)

if ( ^SENT[md-dep,deict,pres-data:~](#1) )
   SENT[pres-data=+](#1).
   
//if ( ^SENT[md-dep,pres-data:~](#1) )
//   SENT[pres-data=+](#1).
   
//if ( ^SENT[md-dep,pres-data:~](#1) )
//   SENT[pres-data=+](#1).

  

// Pred-defined topics in the sentence

| #1[pretopic] |
if ( SENT[pres-data](#2) )
   TOPIC-PART[pres-data=+,pretopic=+](#1).


// If there are no pred-defined topics in the sentence

if ( ^MAIN(#1) & VDOMAIN[passive](#1,#2) )
   MAIN[passive=+](#1).

if ( SENT[pres-data](#1) & SUBJ(#2,#3) & MAIN[passive:~](#2) & ~TOPIC-PART[pretopic](?) )
   TOPIC-PART[pres-data=+](#3)

if ( SENT[pres-data](#1) & SUBJ-N(#2,#3) & MAIN[passive:~](#2) & ~TOPIC-PART[pretopic](?) )
   TOPIC-PART[pres-data=+](#3)


if ( SENT[pres-data](#1) & OBJ(#2,#3) & MAIN[passive](#2) & ~TOPIC-PART[pretopic](?) )
   TOPIC-PART[pres-data=+](#3)

if ( SENT[pres-data](#1) & OBJ-N(#2,#3) & MAIN[passive](#2) & ~TOPIC-PART[pretopic](?) )
   TOPIC-PART[pres-data=+](#3)

//MOD_PRE(information,price)

if ( MOD[pre](#1[info],#2) & SENT[pres-data](#3) & ~TOPIC-PART[pretopic](?) )
   TOPIC-PART[pres-data=+](#2).




///////////////////////////////////////////////////////////////////////////////
///////////////////////////// Presentation with verbs  \\\\\\\\\\\\\\\\\
///////////////////////////////////////////////////////////////////////////////

//PRESENTATION VERBS are the MAIN of the sentences

// SUBJ and OBJ of MAIN verb

//Mortgage loans are not reported at fair value in our consolidated balance sheets unless the mortgage loan is considered impaired, in which case the impairment is recognized as a realized investment loss in our consolidated statements of income.
//VDOMAIN_PASSIVE(reported,are)
//SUBJ_PRE(are,loans)
//NUCL_VLINK_PASSIVE(are,reported)

if ( SUBJ[pre](#1,#2) & VDOMAIN[passive](#3,#1) & NUCL[vlink,passive](#1,#3) & ~SUBJ-N(#3,#2) )
   OBJ-N[mda-dep=+](#3,#2).


//In the past, during such cyclical periods, the Company has seen costs escalate to the point where desired levels of insurance were either unavailable or unaffordable.
//EXPERIENCER_PRE(seen,Company)

if ( EXPERIENCER[pre](#1,#2) )
   SUBJ-N[mda-dep=+](#1,#2).

///////////////////////// NOMAIN \\\\\\\\\\\\\\\\\\\\\\\\


//Our income tax for 2012, 2011, and 2010 includes reductions of $9.3 million, $6.8 million, and $2.7 million, respectively, to reflect the impact of the decrease in the U.K. corporation tax rate changes on our net deferred tax liability related to our U.K. operations.
//MAIN(reflect)
//IV{to reflect} 

| iv{?[lemme:to], #1[nomain=+]} |
  ~.

//The non-GAAP financial measures of operating revenue, operating income or operating loss, and after-tax operating income differ from revenue, income before income tax, and net income as presented in our consolidated operating results and in income statements prepared in accordance with GAAP due to the exclusion of before-tax realized investment gains or losses, non-operating retirement-related gains or losses, and certain other items.
//MOD_POST_PASTPART(statements,prepared)

| #2[nomain=+] |
if ( MOD[post,pastpart](#1,#2) )
   ~.

//Pursuant to a stipulation of settlement, we settled the action Expo brought against us regarding the Purchase of the Property and in June 2011, deposited the settlement amount with the Court, in exchange for which we received a stipulation of discontinuance, with prejudice, as well as general releases.
//NP{the action} SC{NP{Expo} FV{brought}} PP{against NP{us}}


| np, sc{np, fv{?*,#1[last,nomain=+]}} |
  ~.

//The market sources from which we obtain or derive the fair values of our assets and liabilities carried at market value include quoted market prices for actual trades, price quotes from third party pricing vendors, price quotes we obtain from outside brokers, matrix pricing, discounted cash flow, and observable prices for similar publicly traded or privately traded issues that incorporate the credit quality and industry sector of the issuer.
//MOD_PRE_RELATIV(obtain,which)

| #1[nomain=~] |
if ( MOD[pre,relativ](#1,#2) )
   ~.


// A verb in a subordinate clause should not be MAIN

// Conditionals without "if"
//Should an incident occur , management evaluates the claim based on its nature , the facts and circumstances and the applicability of insurance coverage .
//MAIN_MODAL(occur)
//NUCL_VLINK_MODAL(Should,occur)


| #2[nomain=+] |
if ( NUCL[vlink](#1[lemme:should,start],#2) )
   ~.

//An -ing form without an auxiliary should not be MAIN
//Our determination of investment strategy relies more on long-term measures such as reserve adequacy analysis and the relationship between the portfolio yields supporting our various product lines and the aggregate discount rates embedded in the reserves.
//NUCL_VLINK_PROGRESS(am,supporting)

| #1[vprog,nomain=+] |
if ( ~NUCL[vlink,progress](#2,#1) )
   ~.

// embedded verbs

//In 2011, as part of an Internal Revenue Service (IRS) settlement, we released a $4.1 million valuation allowance related to basis differences in foreign subsidiaries and net operating loss carryforwards in foreign jurisdictions for which we previously believed we would not realize a tax benefit.
//EMBED_COMPLTHAT(realize,believed)

| #1[nomain=+] |
if ( EMBED[complthat](#1,#2) )
   ~.


//Historically, prices received for crude oil and natural gas have been volatile and unpredictable with price volatility expected to continue.
//EMBED_PASTPART(expected,been)
//SUBJ-N_PRE(been,gas)

// except: Our analysis of this study, which was completed during the fourth quarter of 2011, showed that lower termination rates than we had previously assumed were beginning to emerge in industry and in our own company experience.
//EMBED_PASTPART(showed,completed)
//EMBED_RELATIV(completed,showed)


| #1[nomain=+] |
if ( EMBED[pastpart](#1,#2) & ( SUBJ-N(#2,#4) | SUBJ(#2,#4) ) & ~EMBED[relativ](#2,#3) )
   ~.

//For securities which we do not intend to sell and it is not more likely than not that we will be required to sell before recovery in value, other-than-temporary impairment losses recognized in earnings generally represent the difference between the amortized cost of the security and the present value of our best estimate of cash flows expected to be collected, discounted using the effective interest rate implicit in the security at the date of acquisition.
//EMBED_INFINIT(sell,intend)

| #1[nomain=+] |
if ( EMBED[infinit](#1,#2) )
   ~.



// the SUBJ is a relative pronoun
//Percentage Rent – revenue arising from retail tenant leases that is contingent upon the sales of tenants exceeding defined thresholds.
//SUBJ_PRE_RELATIV(is,that)

| #1[nomain=+] |
if ( SUBJ[pre,relativ](#1,#2) )
   ~.

//Whether we expect to recover the entire amortized cost basis of the security
//CONNECTOR_SUBORD(expect,Whether)

| #1[nomain=+] |
if ( CONNECTOR[subord](#1,?) )
   ~.

//Likewise, if it is later determined that it is more likely than not that those deferred tax assets would be realized, the previously provided valuation allowance would be reversed.
//CONNECTOR_COMPLTHAT(realized,that)


| #1[nomain=+] |
if ( CONNECTOR[complthat](#1,?) )
   ~.

//In periods when the pound weakens relative to the preceding period, as occurred in 2012 compared to 2011, translating into dollars decreases current period results relative to the prior periods.
//TEMP_PRE_RELATIV(weakens,when)

| #1[nomain=+] |
if ( TEMP[pre,relativ](#1,?) )
   ~.

//For awards where adequate shares are not anticipated to be available or that only permit settlement in cash, the fair value is re-measured each reporting period.
//LOC_PRE_RELATIV(are,where)

| #1[nomain=+] |
if ( LOC[pre,relativ](#1,?) )
   ~.

//Tax positions that previously failed to meet the more likely than not threshold but that now satisfy the recognition threshold are recognized in the first subsequent financial reporting period in which that threshold is met.
//SUBJ-N_PRE(satisfy,that)

| #1[nomain=+] |
if ( SUBJ-N[pre](#1,#2[lemme:that]) )
   ~.

//MOD_PRE_RELATIV(is,which)

| #1[nomain=+] |
if ( MOD[pre,relativ](#1,#2) )
   ~.

//For securities which we do not intend to sell and it is not more likely than not that we will be required to sell before recovery in value, other-than-temporary impairment losses recognized in earnings generally represent the difference between the amortized cost of the security and the present value of our best estimate of cash flows expected to be collected, discounted using the effective interest rate implicit in the security at the date of acquisition.
//MOD_POST_SENTENCE_RELATIV(securities,intend)

| #2[nomain=+] |
if ( MOD[post,relativ](#1,#2) )
   ~.


// Propagation of nomain from fv to to modal auxiliaries 

| #2[nomain=+] |
if ( VDOMAIN[modal](#1[nomain],#2) )
   ~.

// Propagation of nomain from modal auxiliaries to fv

| #1[nomain=+] |
if ( VDOMAIN[modal](#1,#2[nomain]) )
   ~.


//Crude oil revenues increased in 2012 relative to 2011 and in 2011 relative to 2010 because of increased field level purchase volumes and generally higher average crude oil prices as shown in the table above.
//ASL(as,shown)

| #1[verb,nomain=+] |
if ( ASL(?,#1) )
   ~.

//However, industry competitors and Company suppliers are aware of the present market opportunity and seek to capture such advantage which would reduce unit margins.
//SC{BG{which} FV{would}}

| sc{bg{?[lemme:which]}, ?*, fv{?*,?[last,nomain=+]}} |
  ~.

//This increase was primarily due to higher real estate taxes and reimbursable operating expenses, and attributable to tenants at our Rego Park II property whose space was placed into service during 2010.
//SC{BG{NP{whose space}} FV{was}}

| sc{bg{np{?[lemme:whose],?+}}, ?*, fv{?*,?[last,nomain=+]}} |
  ~.

//(who are also directors of the Company and trustees of Vornado)
//SUBJ_PRE(are,who)

| #1[nomain=+] |
if ( SUBJ(#1,#2[lemme:who]) )
   ~.

// "... see " is usually imperative

| #1[lemme:see,bf,nomain=+] |
if ( ~SUBJ(#1,?) )
  ~.


////////////////////////// POTENTIAL MAIN \\\\\\\\\\\\\\\\\\

//finite verb that is last in an SC beginning with an NP

//The initial burst in unit margins was most prevalent during the third quarter of 2011 with favorable unit margins continuing into 2012, although they diminished as competition and additional industry infrastructure development progressed in the region.
//{SC{NP{The AP{initial} burst} PP{in NP{unit margins}} FV{was}}

| sc{np, ?*, fv{?*,#1[potmain=+,last,nomain:~]}} |
  ~.

//These events, combined with the effect of the Company's efforts to improve distribution of the Company's products (as described under the GDS discussion above), resulted in reduced revenue in the first quarter.
//MOD_POST_PASTPART(events,combined)

| #2[potmain=~] |
if ( MOD[post,pastpart](#1,#2) )
   ~.



////////////////////////// MAIN \\\\\\\\\\\\\\\\\\\\\


// beginning of MAIN

// if MAIN is COORD, we change it for the verb
//" Our Unum US segment reported an increase in segment operating income of 3.7 percent in 2012 compared to 2011, with growth in premium income, consistent risk results, and continued favorable expense management."
//COORD(and,continued)
//COORD(and,reported)
//MAIN(and)

if ( ^MAIN(#1) & COORD(#1,#2[verb]) & COORD(#1,#3[verb]) )
   MAIN(#2).

if ( MAIN(#2) & COORD(#1,#2[verb]) & COORD(#1,#3[verb]) & ~MAIN(#3) )
   MAIN(#3).

//if MAIN contains an adverb, we delete it

| fv#1{adv,#2[last]} |
if ( ^MAIN(#1) )
   MAIN(#2)

if ( ^MAIN(#1[nomain]) )
   ~.



// a modal/passive word should not be a MAIN, it should be the infinitive instead


//if the sentence doesn't have a main, an fv[nomain:~]  will be the MAIN
//Crude oil revenues increased

//| fv{#1[aux:~,last,verb]} |
| fv{#1[nomain:~,last,verb]} |
if ( ~MAIN(#2) )
   MAIN(#1).

| #1[nomain=+] |
if ( ^MAIN(#1) & VDOMAIN[modal](#2,#1) )
   MAIN(#2).

| #2[nomain=+] |
if ( ^MAIN(#2) & NUCL[vlink](#2,#1) )
   MAIN(#1).

//If the MAIN is a nomain, it is deleted

if ( ^MAIN(#1[nomain]) )
   ~.

//The remaining fv is tha MAIN

| fv{?*, #1[nomain:~,last,verb]} |
if ( ~MAIN(#2) )
   MAIN(#1).

| fv{?*, #1[nomain:~,last,aux]} |
if ( ~MAIN(#2) )
   MAIN(#1).

// if the MAIN is an auxiliary

//
//For those fixed maturity securities with an unrealized loss for which we have not recognized an other-than-temporary impairment, we believe we will recover the entire amortized cost, we do not intend to sell the security, and we do not believe it is more likely than not we will be required to sell the security before recovery of its amortized cost.
//MAIN(do)
//VDOMAIN_INSIST(believe,do)

if ( ^MAIN(#1[lemme:do]) & VDOMAIN[insist](#2,#1) )
   MAIN(#2).

//If we fail to distribute the required amount of income to our stockholders, or fail to meet other REIT requirements, we may fail to qualify as a REIT, which may result in substantial adverse tax consequences.
//VDOMAIN_MODAL(fail,may)
//MAIN(may)

if ( ^MAIN(#1) & VDOMAIN[modal](#2,#1) )
   MAIN(#2).

// POTMAIN is MAIN
/
| #2[potmain,form:~fbe,form:~fhave,aux:~,form:~fdo] |
if ( MAIN(#1[potmain:~]) )
   MAIN[proba=+](#2).
\
| #2[potmain,form:~fbe,form:~fhave,aux:~,form:~fdo] |
if ( ~MAIN(#2) )
   MAIN(#2).




   


//end of MAIN

/////////////// NEGATION OF MAIN

//The Company's adoption of ASU 2011-11 is not expected to result in significant additional disclosures.
//MOD_NEG(expected,not)

if (^MAIN(#1) & MOD[neg](#1,#2[adv]) )
   MAIN[neg=+](#1).


/////////////// MARKING MAIN for TENSE: FORECAST/PRES/PAST  \\\\\\\\\\\\\\\\\\

////////////// FORECAST

//// in case the general GRAMMAR does not have the MODAL feature, we add it

//Above that level, operating earnings will grow and below that level, losses result.
//VDOMAIN_MODAL(grow,will)
//MAIN(grow)

if ( VDOMAIN[modal](#1,#2) & ^MAIN(#1) )
   MAIN[modal=+](#1).

//SVC of the main propositions of the sentences; verb: future or modal
//SVC of sentential or non-finite verb complement

//// the MAIN is a forecast 

if ( ^MAIN[da:~,past:~](#1[forecast]) )
   MAIN[forecast=+,da=+](#1).



//// the MAIN has a modal verb

if ( ^MAIN[da:~,modal](#1) )
   MAIN[forecast=+,da=+](#1).

//// the SUBJ of the MAIN is a forecast

//Plans call for up to four additional horizontal wells being initiated during 2013.
//SUBJ-N_PRE(call,Plans)

if ( ^MAIN[da:~,past:~](#1) & SUBJ-N(#1,#2[noun,forecast]) )
   MAIN[forecast=+,da=+](#1).

//// the OBJ of the MAIN is a forecast

//The Company has the following major objectives for 2013:
//OBJ-N(has,objectives)

if ( ^MAIN[da:~,past:~](#1) & OBJ-N(#1,#2[noun,forecast]) )
   MAIN[forecast=+,da=+](#1).

// except: The U.K. pension plan had a surplus ...

| noun#3 |
if ( ^MAIN[da,forecast,modal:~](#1[forecast:~]) & $1(#1,#2[noun,forecast]) & MOD[pre](#2,#3) )
   MAIN[forecast=~,da=~](#1).


//// PAST/PRES

//Historically, prices received for crude oil and natural gas have been volatile and unpredictable with price volatility expected to continue.
//NUCL_VLINK_PERFECT(have,been)

if ( ^MAIN[forecast:~](#1) & NUCL[perfect](?,#1) )
   MAIN[past=+](#1).

//With demand improvement, industry capacity has been strained allowing rate increases and improved overall profitability.
//VDOMAIN_PERFECT_PASSIVE(strained,has)

if ( ^MAIN[forecast:~](#1) & VDOMAIN[perfect](#1,?) )
   MAIN[past=+](#1).



//Crude oil marketing operating earnings are also affected by the valuations of the Company's forward month commodity contracts (derivative instruments) as of the various report dates.

//VDOMAIN_PASSIVE(affected,are)
//MAIN_PASSIVE(affected)


if ( ^MAIN[pres:~,forecast:~](#1) & VDOMAIN[passive](#1,?[past]) )
   MAIN[past=+](#1).

if ( ^MAIN[past:~,forecast:~](#1) & VDOMAIN[passive](#1,?[pres]) )
   MAIN[pres=+](#1).

//In contrast, crude oil prices were as generally rising during 2011 and 2010 producing inventory valuation gains of $3,021,000 and $2,272,000, respectively.
//NUCL_VLINK_PROGRESS(were,rising)

if ( ^MAIN[pres:~,modal:~,forecast:~](#1) & NUCL[progress](?[past],#1) )
   MAIN[past=+](#1).

if ( ^MAIN[past:~,forecast:~](#1) & NUCL[progress](?[pres],#1) )
   MAIN[pres=+](#1).


//Volume increases primarily resulted from new well production established by the Company's customer base in the Eagle Ford shale trend of South Texas beginning in 2011, while prices fluctuated upwards with general market conditions.

if ( ^MAIN[pres:~,forecast:~](#1[past]) )
   MAIN[past=+](#1).

if ( ^MAIN[modal:~,past:~,forecast:~](#1[pres]) )
   MAIN[pres=+](#1).





/////////////// DISCOURSE ACTS ([da]): MAIN -> STATEMENT OF FACTS / FORECAST / OPINION \\\\\\\\\\\\\\\\\\\\\

// MAIN is transformed into 
// - statement of facts
// - forcast
// - opinion


////////////// OPINION

if ( ^MAIN(#1[opinion]) )
   MAIN[opinion=+,da=+](#1).
  
// exception: verbs that have the nopassive feature are not opinion verbs when they are passive

if ( ^MAIN[opinion,da,passive](#1[nopassive]) )
   MAIN[opinion=~,da=~](#1).

////////////// STATEMENT OF FACTS


//SVC of the main propositions of the sentences
//Verb: finite, present/past without verbal or sentential complement


if ( ^MAIN[fact:~,da:~,past](#1) )
   MAIN[fact=+,da=+](#1).

if ( ^MAIN[fact:~,da:~,pres](#1) )
   MAIN[fact=+,da=+](#1).




/////////////////////////////////////////////////////////////////////////////

/////////////////////////// PRES-DEP \\\\\\\\\\\\\\\\\\\\\\\\\


// we only need SUBJ-N, OBJ-N

if ( ^SUBJ(#1,?) & SUBJ-N(#1,?) )
   ~.

if ( ^OBJ(#1,?) & OBJ-N(#1,?) )
   ~.

// numerical OBJ: they cannot be topics of presentations 

| #1[money,!noobj=+] |
  ~.

| #1[num,!noobj=+] |
  ~.

| #1[dig,!noobj=+] |
  ~.

//NP{0.9 points} 

| np{#1[dig], #2[!noobj=+]} |
  ~.

// 9.9 billion revenue passenger miles

| #1[unit,!noobj=+] |
  ~.

//TIMEX

| #1[!noobj=+] |
if ( TIMEX3(#1) )
   ~.

| np#1{#2[!noobj=+]{?+}} |
if ( TIMEX3(#1) )
   ~.


// Bringing SUBJ and MAIN together in case they are separated due to NUCL issues

//income (OCI) at December 31, 2012 and will be recognized in future periods as contracts settle.
//MAIN(recognized)
//VDOMAIN_MODAL_PASSIVE(recognized,will)
//SUBJ_PRE(will,income)

if ( MAIN(#1) & VDOMAIN[passive,modal](#1,#2) & SUBJ[pre](#2,#3) )
   OBJ-N(#1,#3).

//

if ( MAIN(#1) & SUBJ(#1,#2) & ~SUBJ-N(#1,?) )
   PRES-DEP[subj=+](#1,#2).

if ( MAIN(#1) & SUBJ-N(#1,#2[lemme:~SOMEONE]) )
   PRES-DEP[subj=+](#1,#2).



if ( MAIN(#1) & OBJ[sentence:~](#1,#2) )
   PRES-DEP[obj=+](#1,#2).

if ( MAIN(#1) & OBJ-N[sentence:~](#1,#2) )
   PRES-DEP[obj=+](#1,#2).

if ( ^PRES-DEP(#1[md],#2) & SENT[pres-data](#3) )
   ~.

if ( ^PRES-DEP(#1,#2[md]) & SENT[pres-data](#3) )
   ~.

/////////////// particular constituents \\\\\\\\\\\\\\

//I don't want to present numbers

//numerical SUBJ, OBJ

if ( ^PRES-DEP(#1,#2[noobj]) )
   ~.

if ( ^PRES-DEP(#1[noobj],#2) )
   ~.

if ( ^PRES-DEP[subj](#1,#2) & PRES-DEP[obj](#1,#3) )
   PRES-DEP[obj=+](#2,#1,#3).

if ( PRES-DEP[obj](#2,#1,#3) & ^PRES-DEP[obj](#1,#3) )
   ~.

if ( ^PRES-DEP[subj](#1,#2) )
   PRES-DEP(#2,#1).

////////////////////////////////////////////
//////////////// exceptions \\\\\\\\\\\\\\\\
////////////////////////////////////////////

if ( ^PRES-DEP[subj](#1,#2[endword]) )
   ~.

//imperative sentences are not presentations

| fv{#1[start,aux:~]} |
if ( ^PRES-DEP(?,?) )
  ~.

| fv{#1[start,aux:~]} |
if ( ^PRES-DEP(?,?,?) )
  ~.


// "... see " is usually imperative

| #1[lemme:see,bf] |
if ( ^PRES-DEP(#1,?) & ~SUBJ(#1,?) )
  ~.

| #1[lemme:see,bf] |
if ( ^PRES-DEP(?,#1) & ~SUBJ(#1,?) )
  ~.

| #1[lemme:see,bf] |
if ( ^PRES-DEP(?,#1,?)  & ~SUBJ(#1,?) )
  ~.


// references are not presentations
//(1) Reflects the volume purchased from third parties at the oil and natural gas field level and pipeline pooling points.
//DIG{( 1 )}

| dig[start]{ #1[paren], ?, #2[paren] } |
if ( ^PRES-DEP(#4,#5) )
   ~.





/////////////////////////// EXPLANATION \\\\\\\\\\\\\\\\\\\\\\\\\

//Volume increases primarily resulted from new well production established by the Company's customer base in the Eagle Ford shale trend of South Texas beginning in 2011, while prices fluctuated upwards with general market conditions.
//PRES-DEP(increases,resulted)

if ( ^PRES-DEP[subj](#1,#2[explanation]) )
   PRES-DEP[explanation=+](#1,#2)

if ( ^PRES-DEP[obj](#1[explanation],#2) )
   PRES-DEP[explanation=+](#1,#2)

if ( ^PRES-DEP(#1,#2[explanation],#3) )
   PRES-DEP[explanation=+](#1,#2,#3)


/////////////// contextual features

/////////////// cause-result \\\\\\\\\\\\

//have effect on
//In our opinion, the outcome of such matters in the aggregate will not have a material effect on our financial condition, results of operations or cash flows.
//OBJ-N(have,effect)
//PRES-DEP_OBJ_SUBJ(outcome,have,effect)

if ( ^PRES-DEP[obj,subj](#1,#2[lemme:have],#3[lemme:effect]) )
   PRES-DEP[explanation=+](#1,#2,#3)

//responsible for
//We are ultimately responsible for any loss borne by FNSIC.
//PRES-DEP_SUBJ(We,are)
//QUALIF(We,responsible)

if ( ^PRES-DEP[subj](#1,#2) & SUBJATTR(#1,#3[adj]) )
   PRES-DEP[qualif=+,subj=~](#1,#2,#3).
  
if ( ^PRES-DEP[subj](#1,#2) & SUBJATTR(#1,#3[nadj]) )
   PRES-DEP[qualif=+,subj=~](#1,#2,#3).

//exception:  If our estimates of future cash flows, anticipated holding periods, or fair values change, based on market conditions or otherwise, our evaluation of impairment charges may be different and such differences could be material to our consolidated financial statements.
//PRES-DEP_QUALIF(be,be,material)



//////////////////Barrière, C. (2002). Investigating the causal relation in informative texts. Terminology, 7(2), 135-154.
//conjunctional patterns
//as X, Y
//if X, Y
//when X, Y
//X, so Y
//once X, Y
//without X, Y
//X so that Y
//unless X, Y
//X due to Y
//X, thus Y

//Two significant factors affecting comparative crude oil segment operating earnings are inventory valuations and forward commodity contract (derivatives or mark-to-market) valuations.
//PRES-DEP_SUBJ(factors,are)
/
if ( ^PRES-DEP[subj](#1[explanation],#2) )
   PRES-DEP[explanation=+](#1,#2)
\

//analysis shows





///////////////////////// TOPICS from LINE ITEMS \\\\\\\\\\\\\\\\\



//propagate LIWORD

| ?[!liword] |
    ~.

//////////////////////////

// extend SUBJ and OBJ to chunks

if ( ^PRES-DEP[obj](#1,#2) & HEAD(#2,#3) )
    PRES-DEP(#1,#3).

if ( ^PRES-DEP[subj](#1,#2) & HEAD(#1,#3) )
    PRES-DEP(#3,#2).

if ( ^PRES-DEP(#1,#2,#3) & HEAD(#1,#4) )
    PRES-DEP(#4,#2,#3).

if ( ^PRES-DEP(#1,#2,#3) & HEAD(#3,#4) )
    PRES-DEP(#1,#2,#4).




/
| #1[liword] |
if ( ^PRES-DEP(#1,#2) )
   PRES-DEP[liword=+](#1,#2).

| #2[liword] |
if ( ^PRES-DEP(#1,#2) )
   PRES-DEP[liword=+](#1,#2).

| #1[liword] |
if ( ^PRES-DEP(#1,#2,#3) )
   PRES-DEP[liword=+](#1,#2,#3).

| #2[liword] |
if ( ^PRES-DEP(#1,#2,#3) )
   PRES-DEP[liword=+](#1,#2,#3).

| #3[liword] |
if ( ^PRES-DEP(#1,#2,#3) )
   PRES-DEP[liword=+](#1,#2,#3).
\

