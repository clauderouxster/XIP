DependencyRules:

// SENT

| TOP#1{ ?+ } |
  SENT(#1).


// These rules have been placed here from dependecy_10K-off-ind.xip, because they are needed in other modules as well

// Sentences with metadiscourse

// contextual metadiscourse

// as follows

| #1[lemme:as], #2{#3[lemme:follow,deict=+,md=+,10K-off=+]} |
  ~.

// set forth

| #1{#2[lemme:set,indicate=+,md=+,10K-off=+]}, #3[lemme:forth] |
  ~.

// contextual features for md words:

//Table of contents
//MOD_INFO_POST(Table,Contents)

| #1[info=~] |
if ( MOD[post](#1[lemme:table],#2[lemme:content]) )
   ~.


//to provide adequate information - indicate verb when it is an infinitive, is not metadiscourse

| IV{?[lemme:to],#2[indicate=~]} |
  ~.

//we access and evaluate financial information regarding Bloomberg from private sources - if an info verb is followed by "regarding", and it is not the OBJ of an indicate verb, it is no info, and "regarding" is not an indicate
//PREPD(Bloomberg,regarding)
//MOD_POST(information,Bloomberg)
//OBJ-N(evaluate,information)
//OBJ-N(receive,information)

| #1[indicate=~] |
if ( PREPD(#2,#1[lemme:regarding]) & MOD[post](#3[info],#2) & OBJ-N(#4[indicate:~],#3) )
   ~.

| #3[info=~] |
if ( PREPD(#2,#1[lemme:regarding]) & MOD[post](#3[info],#2) & OBJ-N(#4[indicate:~],#3) )
   ~.

//mark sentences with metadiscourse elements

| #1[info] |
if ( ^SENT(#2) )
   SENT[info=+](#2).

| #1[deict] |
if ( ^SENT(#2) )
   SENT[deict=+](#2).

| #1[indicate] |
if ( ^SENT(#2) )
   SENT[indicate=+](#2).

// missing dependencies

//Below is a summary of our outstanding debt and maturities as of December 31, 2012.
//NUCL_INFO_SUBJCOMPL(is,summary)
//MOD_DEICT_PRE(is,Below)

if ( NUCL[subjcompl](#1,#2[info]) & MOD[pre](#1,#3[deict]) )
   DEP(#3,#2).

//Following is additional information regarding American's domestic and international RASM and capacity:
//SUBJ-N_PRE_GERUND(is,Following)
//NUCL_SUBJCOMPL(is,information)

if ( NUCL[subjcompl](#1,#2[info]) & SUBJ[pre](#1,#3[deict]) )
   DEP(#3,#2).



//mark dependencies with metadiscourse elements



| #1[info] |
if ( ^$1(#1,#2) )
   $1[info=+](#1,#2).

| #2[info] |
if ( ^$1(#1,#2) )
   $1[info=+](#1,#2).

| #1[deict] |
if ( ^$1(#1,#2) )
   $1[deict=+](#1,#2).

| #2[deict] |
if ( ^$1(#1,#2) )
   $1[deict=+](#1,#2).

| #1[indicate] |
if ( ^$1(#1,#2) )
   $1[indicate=+](#1,#2).

| #2[indicate] |
if ( ^$1(#1,#2) )
   $1[indicate=+](#1,#2).


//mark dependencies with 2 metadiscourse arguments

if ( ^$1[info,deict](#1[md],#2[md]) )
   $1[md-dep=+](#1,#2)

if ( ^$1[info,indicate](#1[md],#2[md]) )
   $1[md-dep=+](#1,#2)

if ( ^$1[deict,indicate](#1[md],#2[md]) )
   $1[md-dep=+](#1,#2)

//mark sentences with metadiscourse dependencies

if ( ^SENT(#1) & $1[md-dep](#2,#3) )
   SENT[md-dep=+](#1).

