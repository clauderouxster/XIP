//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2003 -2007 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : English XIP grammar
//!   filename   : desamb_entit.xip
//!   Date       : 28/07/2004
//!   Author     : Caroline Brun
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
/some specific  disambiguation rules for entities\


Tagging:

1> noun = noun[proper:~] | noun[lemme:"agreement"] | .

1> noun[hmm_verb:+,lemme:"play"] %= | ?[wh:+], ?*[verb:~], aux, (det), (adj;nadj)*, noun[proper] | verb[inf=+] | prep | .

//Total 

1> nadj<lemme:"total",maj> %= | noun[orghead];noun[lemme:"giant"] | noun[proper=+, org=+,bus=+]. 

// XXX[first], Texas

1> noun<maj,guess,proper,start> = noun[city=+,location=+,org=+] | punct[comma], noun[country];noun[usastate] | . 

// Francoise Fabian

1> noun<lemme:"Fabian"> = | noun[firstname] | noun[celeb=+,firstname=~].

1> noun<lemme:"mitt",maj> = noun[firstname=+] | noun[proper] | .

// Stonewall Riots

1> verb<lemme:"stonewall",maj> %= noun | noun[lemme:"riot"] | .

// Eraser - like ==> like = prep

1> prep<lemme:"like">, verb = | ?[noun], punct[form:fhyph] | prep. 

// XXX county

1> noun<maj,proper>, adj = noun[proper] | noun[locpost] |.

// the XXX family

1> noun<maj,proper,guess>, adj = | det | noun | noun[famlink] | .

// William H. Wbester

1> noun<lemme:"Webster"> = | noun[firstname], (noun[initial]) | noun<celeb=+,location=~,org=~> .

1> noun<lemme:"West",proper> = | nadj | noun[modloc] .

1> noun<firstname>,noun<firstname:~> = | det[def] | noun[firstname:~] |noun[fam:~] | .

1> noun<firstname> = ?[firstname=~] | prep , (det), ?[proper:~] | .

//some missing (and ambiguous) units

1> nadj<lemme:"Christian",maj> %= | noun[proper], punct[comma] | noun[firstname=+].

1> dig, noun<firstname> = noun | noun[proper] | . 

1> noun<lemme:"News"> = noun[bus=~,ambig=+].

1> noun<lemme:"Brent"> = noun[firstname=~] | adj[lemme:"crude"] |. 

1> noun<firstname> =  noun[firstname=~,place=+] | punct[form:fhyph], verb[lemme:"base",partpas] |.

1> noun,verb<lemme:"physic",maj> = noun | noun[proper] |.


1> noun<country> = | noun[firstname,celeb:~,fam:~], noun[initial] | noun[country=~].

1> noun<celeb> = noun[celeb=~] | punct[form:fhyph] |.

1> dig<lemme:"di">, noun  = |noun[maj] |  noun | noun[maj] | .

1> noun, verb<lemme:"win",toutmaj> = | noun[toutmaj] | verb | noun[toutmaj];nadj[toutmaj];adj[toutmaj] | .
1> noun, verb<lemme:"remove",toutmaj> = | noun[toutmaj] | verb | noun[toutmaj];nadj[toutmaj];adj[toutmaj] | .

1> adj<lemme:"Finnish"> %= nadj[country=+].

1> noun<lemme:"inch"> = | dig;num  | noun[unit=+].

1> noun<lemme:"foot"> = | dig;num  | noun[unit=+].

//MCV HOLDING --> MCV not dig, but proper noun

1> dig<lemme:"MCV"> %= noun[proper=+] | noun[orghead] | .

// Brent Heating Oil --> Brent not firstname

1> noun<lemme:"Brent"> = noun[firstname=~] | noun[lemme:"heat"] | .
1> noun<lemme:"Brent"> = noun[firstname=~] | noun[lemme:"oil"] | .


// manila is never and adj
1> noun, adj<lemme:"manila"> = noun.

1> nadj, noun<lemme:"Argentine"> = |?[det:~] | noun.

1> nadj, noun<lemme:"Argentine"> = | |  noun.
 
1> noun, verb<lemme:"buy",toutmaj> = verb.

1> adv<lemme:"AM">, noun<lemme:"Am"> = noun | nadj[maj,lemme:"international"] | .

1> noun<lemme:"Indiana">, prep = | ?[toutmaj], (punct) | prep | ?[toutmaj] |.

1> noun<lemme:"one"> = num | num |.

1> pron<lemme:us,toutmaj> , noun = |?[toutmaj:~] | noun |?[toutmaj:~] | .

1> noun<url> = noun[proper=~].

1> noun<email> = noun[proper=~].

// test with rc

//? = ?[endwithcr2=+] | ?[cr2] |.

1> adv, noun<lemme:"Piccadily"> = noun.
//VIKEF

1> noun<lemme:"Fillmore",place> %= noun[celeb:+,place:~,proper:+].

 // Chinese Foreign Minister Li Zhaoxing : Li is not a dig

1> dig[maj], noun =  |noun[hmm_tit];noun[title] | noun |noun[proper] |.

// T. S. Eliot : T. is not a measure but a noun with feature initial

1> meas[maj:+,abr:+]  %= noun[maj:+,initial] | noun[maj:+,initial] |.


// Hollywood is an adj (?) , but when it has a cap letter
// it should always be a noun

1> adj<maj:+,lemme:"hollywood"> %= noun[location:+,org:+,city:+,place:+,proper:+,noun:+,lemme:"Hollywood"].


//Mr Bush : Bush always a proper noun when with a cap letter

1> noun<lemme:"floor">, verb = | punct[form:fhyph]| noun | dig |.

1> noun<lemme:"Means">, verb = | ?[guille] | verb.

1> noun, verb<lemme:"limit",partpas> = | punct[comma];noun[lemme:fund];noun[lemme:"partner"];noun[lemme:"management"]  |  noun.

1> noun<lemme:"bush",maj> %=  noun[maj:+,proper:+,lemme:"Bush",celeb=+].

2> verb<lemme:"bush",maj> %=  noun[maj:+,proper:+,lemme:"Bush"].

2> noun<lemme:"Administration"> = noun[orghead=~] | noun[lemme:"service"] |.

2> noun<proper,maj>, adj = | noun[firstname], (noun[initial]) | noun.



//The Atlantic 


2> nadj<lemme:"Atlantic">  %= |det[def] | noun[place:+,proper:+] .


2> noun<lemme:"Pacific"> %=  |det[def] | noun[place:+,proper:+] .

// Brock Associates Inc. --> Associates is a noun and not a verb
// "Ogden Projects Inc. --> idem

3> verb<lemme:associate,maj>, noun = |noun[proper]|  noun |noun[orgend]|.
3> verb<lemme:project,maj>, noun = |noun[proper]|  noun |noun[orgend]|.


// Federal Trade Commission : Comission is a noun and not a verb
//does not work problem

3> verb<lemme:commission>, noun  = |?[lemme=trade] | noun.

//Caen Peace Memorial : Memorial is a noun
3> nadj<maj,lemme:memorial>, noun = |noun[maj] | noun.


// add feature orghead to investment when start
// with cap letter and preceded by proper noun
// Tal Investment


3> noun<lemme:investment,sg,maj> = |noun[proper]| noun[orghead=+] | ?[noun:~]| .
3> noun<lemme:investment,sg,maj> = |noun[proper]| noun[orghead=+] | | .

3> noun<lemme:management,sg,maj> = |noun[proper], noun*| noun[orghead=+] | ?[noun:~]| .
3> noun<lemme:management,sg,maj> = |noun[proper], noun*| noun[orghead=+] | | .

3> noun<lemme:group,sg> = |noun[proper];nadj[maj];adj[maj] | noun[orghead=+].


3> noun<lemme:unit,sg> = |noun[proper];nadj[maj];adj[maj] | noun[orghead=+].

3> noun<lemme:coast,sg> = |noun[proper];noun[maj] , (adj;nadj) | noun[locpost=+].

3> noun<lemme:forest,sg> = |noun[proper];noun[maj], (adj;nadj) | noun[locpost=+].

3> noun<lemme:peninsula,sg> = |noun[proper];noun[maj] , (adj;nadj) | noun[locpost=+].

3> noun<lemme:canyon,sg> = |noun[proper];noun[maj] , (adj;nadj) | noun[locpost=+].

3> noun<lemme:border,sg> = |noun[proper];noun[maj] , (adj;nadj) | noun[locpost=+].


3> noun<lemme:hotel,maj,pl>  = |noun[proper];noun[maj];nadj[maj], (adj;nadj) | noun[orghead=+].

3> noun<lemme:hotel,maj,sg>  = |noun[proper];noun[maj], (adj;nadj) | noun[orghead=+,alsoloc=+].


4> verb<maj:+,first:+> , noun<firstname:+> = noun | noun[proper] |.

//AGS Specialist Partners 


//4> noun<lemme:partner,maj,pl> = |noun[proper,orgend:~];noun[maj,orgend:~], (adj;nadj) | noun[orghead=+].




//Enron Power Marketing


4>  verb<lemme:market,vprog,maj> , noun  = |noun[proper];noun[maj], (adj;nadj) | noun[orghead=+].
 


// Electronic Manufacturing Services Group

4> verb<lemme:service,maj>, noun  = |noun[proper];noun[maj], (adj;nadj) | noun | noun[orghead];noun[orgend] | .


//Tylan General Inc.

4> noun<guess>, adj<guess> =  noun | (noun;nadj;adj), noun[orghead];noun[orgend]| .

// XXX Brothers

4> noun<lemme:brother,maj,pl> = |noun[bus];noun[org] | noun[orgend=+].

// Management Services : not an  org

5> noun<lemme:"service"> = | noun[lemme:"management"] | noun[orghead=~].

//DEUSTCH BANK

5> noun<lemme:"Association"> = | noun[lemme:"memorandum"];noun[lemme:"article"], prep[form:fof] | noun[orghead=~].

5>noun<lemme:"security">, noun<proper> = | prep[form:fof] | noun<lemme:"security"> .

//Administration Expenses : not an org

5> noun<lemme:"administration"> =  noun[orghead=~] | noun[proper:~];noun[lawmod] |.
5> noun<lemme:"Administration"> =  noun[orghead=~] | noun[proper:~];noun[lawmod] |.

//5> noun<lemme:"company"> =  noun[orghead=~] | noun[proper:~];noun[lawmod] |.
//5> noun<lemme:"Company"> =  noun[orghead=~] | noun[proper:~];noun[lawmod]  |.

5>  noun<lemme:"Associate"> =  noun[orghead=~,orgmod=~] | prep| .

5> noun<lemme:"Fund"> = noun[orghead=~] | noun[lemme:"investment"] | . 

//end DEUSTCH BANK
//Larry Elder --> Elder is noun and not nadj

//5> nadj[maj:+] %= | noun[firstname:+,fam:~,place:~,location:~,month:~]| noun[proper=+]. 

//Fraser Foll --> is this context Fraser is a firstname
5> noun<proper:+,lemme:"Fraser"> = noun [firstname=+] | noun[proper] | .

5>  noun<firstname,proper>, noun<proper,place> = noun[firstname,proper] | noun[proper,fam]| .


//il a fistname is the last of a serie of firstname, it is just proper

//5> noun<firstname,proper> = |?[comma:~] , noun[firstname] | noun[firstname=~] | ?[proper:~]| .

//5> noun<firstname,proper> = | noun[firstname,start] | noun[firstname=~] | ?[proper:~]| .


//for the justice case WARNING :

5> noun<maj,lemme:"justice"> = noun[title=+,proper=+] |noun[firstname];noun[title]|.


//Japanese prime minister : prime is not a verb

5> nadj<lemme:"prime">,verb = |nadj[country:+] | nadj[hmm_verb=~] |noun[maj:+,title:+] ; noun[prof:+] | .


// Stamford, CA ---> CA is a usastate and not an adverb in such context

5> noun<usastate,maj>, adv = |noun[city:+], punct[comma:+]| noun.


// Customer Operations: add the feature orgEnd 
// to operation

//5> noun<lemme:"Operation",pl> = |noun[maj,proper] | noun[orgEnd=+]. 


// director of Catalyst

5> noun<maj> = | noun[lemme:"director"], prep[form:fof] | noun[bus=+] .

// Information Engineering

5> noun<lemme:"engineer",vprog> = |noun[lemme:"information"];adj[lemme:"electrical"];adj[lemme:"corporate",maj]| noun[orgend=~].

// Research Assistantship

// Xerox

5> noun<lemme:"Xerox">, verb =  noun[changed=+].

5> noun<lemme:"Research"> = noun[orgmod:~] | noun[lemme:"Assistantship"] | .

5>  noun<lemme:"Technology"> = noun[orgmod:~] | noun[lemme="review"] |.

5> noun<lemme:"Wilson"> = |noun[firstname]|  noun[firstname:~].

5> noun<lemme:"Po",toutmaj:+> = noun[place=~].


5> noun<lemme:"Communication"> = noun[orghead=~] | noun[lemme:"Research",orghead=+] |.
 
//Brown

5> noun<lemme:"brown",maj> = | noun[firstname] | noun[proper=+]. 

//#### for flat version only ####################################
// ELisabeth I --> I is a dig rom and not a pron
// NON: "with Sophie I used to speak ..." 
//5> pron<lemme:"I"> %= | noun[maj,firstname] | dig[rom=+].

5> noun[maj,toutmaj:~] = | ?[det:~] , noun[firstname,usastate:~,toutmaj:~], noun[particlename,toutmaj:~];prep[form:fof,toutmaj:~] | noun[proper=+].

// Roberto de Miranda : Miranda not firstname

5> noun[proper] =| noun[firstname:+,usastate:~], noun[particlename:+] | noun[firstname=~].

//5> noun[maj] = | noun[firstname:+,usastate:~,place:~] | noun[proper=+] | ?[maj:~] |. 

5> noun[proper,place, toutmaj:~,city:~,country:~] = | noun[firstname:+] |  noun[place=~].

5> noun[proper,locpost, toutmaj:~] = | ?[title:~,det:~,adj:~],noun[firstname:+,place:~] |  noun[locpost=~].

//5> noun[proper,bus,toutmaj:~] = | noun[firstname:+] |  noun[bus=~] | ?[orgend:~,orghead:~,orgmod:~] |.

//William Hill said : Hill is not a locpost

5> noun[proper,locpost] = | ?[det:~], noun[firstname,place:~] | noun[locpost=~] | |. 

5> noun[proper,locpost] = | noun[firstname,place:~] | noun[locpost=~] | verb | .

5> noun<lemme:"Mervyn"> = noun[bus=~,firstname=+] | noun[proper,maj,fam]| .

// Matsushita Elec Indus/Indu

5>  noun<lemme:"Indus"> = | noun[bus], (?) | noun[place=~,misc=~,orgend=+].

5>  noun<lemme:"Indu"> = | noun[bus], (?) | noun[orgend=+].

// Y Welcomes Z" 

5> noun<lemme:"welcome",pl,maj>, verb = | noun[maj] | verb | noun[maj] | .

// KSOVO

6> noun<particlename,lemme:"al">, adv = noun | noun[proper] |. 

// GM: O. Shah

6> noun<lemme:"shah",maj> = | noun[initial] | noun[proper=+].

//D.Schoof

6> dig<lemme:"D"> %= noun[let=+,maj=+] | punct , noun[guess,proper] | .

// mick jagger


6> noun<lemme:jagger> %= | noun[firstname] |  noun[maj=+,lemme="Jagger",celeb=+,proper=+].

//J. Elan

6> noun<lemme:"elan",maj> %= | noun[initial] | noun[proper=+].


// Mailing System --> System is not an orgend

6> noun<lemme:"System"> = | noun[vprog] | noun[orgend=~].

6> noun<lemme:"Software"> = | punct | noun[orgend=~].

6> dig<rom> %=  noun[proper] | noun[orghead];noun[orgend] |  .

6> noun<lemme:"Book"> = noun[orghead=~] | ?[proper:~] | .


6> noun<maj> = | noun[firstname], noun[initial] | noun[proper=+,countable=~].

//Polish

6> ?<lemme:polish,maj> %= nadj[country=+].

//Pontificia Universidade Catolica do Rio de Janeiro

6> verb[surface:"do"] %= prep[form=fof] | noun[lemme:"Rio de Janeiro"] | .

//in Washington

6> noun<lemme:"Washington"> %= | prep[lemme:"in"] | noun[city=+,alsoloc=+,proper=+,ambig=~,location=+,org=+]  .


5> noun<lemme:"Major">, nadj = nadj | ?[proper:~] |.


// Mr. Grey / E. Gray --> Grey = noun


6> nadj<maj,country:~> %= | noun[title];noun[hmm_tit];noun[initial] | noun[proper=+] .


// Sen. Orrin Hatch --> Hatch proper

6> noun<maj,vprog:~> = | noun[title];noun[hmm_tit] , noun[firstname] | noun[proper=+].

//INA 

6>noun<lemme:"Ina",toutmaj> %= noun[proper=+,bus=+,place=~].

// The Reston company" 
6> noun<maj,proper> %= |det | noun[proper=+,bus=+] | noun[lemme:"company"] | .


// Senior French officials --> French =nadj

6> noun<proper>,nadj<country> = nadj | noun[c_person:+] |.

// General 

6> noun<lemme:"General"> = noun[title=+] | noun[firstname];noun[guess,maj] |.

6> nadj<lemme:"general",maj> %= adj[title=~] | noun[lemme:"command"] |.

//Bankers Trust Co.

6> noun<surface:"Bankers"> = noun[countable=~] | noun[orghead] | .

// David Ruder 

6> adj<maj,partpas:~> %=  | noun[firstname,proper]| noun[proper=+] | ?[maj:~] |.

// XXX Securities

6> noun<pl,lemme:"Security"> = noun[orghead=+] |?[orgend:~] | .

6> noun<pl,maj,lemme:"ecurity"> = noun[orghead=+] |?[orgend:~] | .

// the Moscow Mayer --> Mayer not org

6> noun<lemme:"Mayer"> = | noun[city] | noun[bus=~].

6> noun<country>,noun<firstname> = noun[country] | (part), noun[title];noun[prof] | .

// Test: conferences au pl


6>noun<lemme:"conference",maj,pl> = noun[eventMod=+].

6> noun<fam>, noun<location> = |noun[firstname], ?*[proper,maj,guess] | noun[fam].

// Willian Hague

6> noun<lemme:"Hague"> = | ?[det:~] |noun[city=~,org=~] .

6>  noun<lemme:"Shah">, noun<countable> = | noun[firstname] | noun[title,proper=+].

6> noun<lemme:"Harry">, verb = noun | noun[proper] |.

6> noun<lemme:"GB",country>, noun<hmm_meas> = |dig| noun[hmm_meas,country=~,place=~,location=~,org=~] | noun[lemme:"memory"] | .

6> noun<proper:~,prep:~,maj,p_on:~> %= |noun[prof];noun[title]| noun[proper=+,firstname=+] | noun[guess,proper,maj,tempexpr:~] | .

6> noun<proper>, ? = | det[def] | noun[guess,location=+,place=+,propag=+] |noun[lemme:"region"] | .

6> noun<title>,noun<lemme:"president"> = noun[title] | noun[firstname];noun[celeb] | .

6> noun<lemme:"Washington",ambig> = |  prep[lemme:"in"] | noun[ambig=~,celeb=~] .

6> noun<lemme:"Roma"> = noun[city=~,proper=~,org=~,location=~] | noun[lemme:"people"];noun[lemme:"child"] | .

6> noun<lemme:"Christian"> = |noun[prof] | noun[firstname] .

6> pron[surface:"He"] %= | noun[title];noun[prof] | noun[proper=+,firstname=+] | noun[proper] |.

6> noun<lemme:"Protection"> = noun[proper=~] | noun[orghead] | .

6> noun<guess,org,maj> = noun .
6> noun<guess,place,maj> = noun.

6> noun<proper,celeb,maj> = noun[proper] | part | .

6> noun<lemme:"Jan"> = noun[ambig=~,tempexpr=~,month=~,time=~] | noun[fam];noun[celeb] |.

// AB 2012.06.20 Jans is a Dutch first name, not a month name (i.e. as above rule, but in any context)
6> noun<surface:Jans> = noun[ambig=~,tempexpr=~,month=~,time=~].

// AB 2012.06.20 Marche(s) is a region in Italy, not a month name
6> noun<surface:Marches> = noun[ambig=~,tempexpr=~,month=~,time=~].
6> noun<surface:Marche> = noun[ambig=~,tempexpr=~,month=~,time=~].

// AB 2012.06.20 Mays is a surname, not a month name
6> noun<surface:Mays> = noun[ambig=~,tempexpr=~,month=~,time=~].

6> noun<firstname,maj>, noun<misc> = |noun[famlink] | noun[firstname] .
6> noun[lemme:"My"], pron = pron.

6> noun<lemme:"Oscar",firstname> = noun[firstname:~] | prep |.

6> noun<place> = |noun[lemme:"performance"], prep[lemme:"as"] | noun[place=~].


6> noun<surface:"Spears"> = |noun[lemme:"Britney"] | noun .
Marker:

// XXX von Nmaxi


7> noun[proper,firstname,ambig:~], noun[proper,fam=+,locpost:~].
// XXX von Nmaxi

7> noun[proper,guess,maj,firstname=+], noun[particlename,lemme:~"de"], noun[celeb].

7> noun[proper,guess,maj,firstname=+], noun[particlename,lemme:~"de"], noun[maj,proper,guess,fam=+].

7> noun[lemme:"mayor"], prep[lemme:"of"], noun[guess,org=+,city=+,place=+,proper].

7> noun[lemme:"Ariel",firstname=+,place=~], noun[lemme:"Sharon", firstname=~,celeb=+].

7> noun[lemme:"president",title=+], noun[firstname].

7> noun[lemme:"Indiana",firstname=+,place=~], noun[lemme:"Jones",celeb=+].

7> noun[title], noun[firstname], noun[firstname=~,fam=+,particlename:~,locpost:~].

7> adj[lemme:"financial"], noun[lemme:"group"], noun[guess,bus=+,org=+,propag=+].

7> verb, ?[prof], noun[proper,guess,fam=+,celeb=+].

7> noun[title], noun[firstname:~,firstname=+], noun[firstname=~,fam=+].

7> noun[firstname], noun[lemme:"Valencia",firstname=+], noun[proper].

7> noun[lemme:"Tennessee",firstname=+,usastate=~,place=~], noun[lemme:"Williams"].

7> noun[city], noun[maj,lemme:"chronicle",sg,orghead=+].

7> noun[lemme:"ovum",maj,org=+].


7> noun[lemme:"riot",maj,eventmod=+].

