//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2003 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : English XIP grammar
//!   filename   : localgram1.xip
//!   Date       : 10/02/2003
//!   Author     : Francois Trouilleux - Caroline Hagege
//!  
//!x
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


//script:

//appel();

Multiwords:
1> "Secretary-General" = noun[prof=+,sg=+].
1> "[c,C]hief [e,E]xecutive [O,o]fficer"  = noun[prof=+,sg=+].

//split:

//| ?*,  nadj#[lemme:endsent] | .

Tagging:


// "as soon as possible"


//Franco-German
2> noun<lemme:Franco> %= nadj[country=+] | punct[form:fhyph,left,right] , nadj[country] |.




/------------ HYPHENS ---------------------------\


Sequence:

// this one 

3> pron = det[lemme:"this"], pron[lemme:"one"] | ?[noun:~] | .
3> pron = det[lemme:"this"], pron[lemme:"one"] | | .

// xxx-sur-mer 

3>  noun [proper:+,city:+,place:+,mwe:+] @= noun[proper], punct[form:fhyph], noun[lemme:"Sur"];noun[lemme:"sur"], punct[form:fhyph], nadj[lemme:"mer"] .

// MWE titles

3> noun[title=+,mwe=+] = noun[title], prep[lemme:"of"], noun[place].

//Kurt-Schumacher-Strasse

3>  noun[mwe=+,city=+,proper=+,place=+] = noun[city], punct[form:fhyph,left,right],noun[city].

3>  noun[mwe=+,address=+,proper=+,place=+]  = noun[maj], punct[form:fhyph], noun[maj], punct[form:fhyph], noun [lemme:"Strasse"].

// Master-Feeder Fund Limited

3> noun = ?[noun:+,lemme:"master"], punct[form:fhyph,left:+,right:+], ?[noun:+,lemme:"feeder"], noun[lemme:"Fund"], noun[lemme:"Limited"].

//3> adv = conj[lemme:"as_soon_as"], adj[lemme:"possible"] |~noun|.

//by far

3> adv = prep[lemme:"by"], adv[lemme:"far"] | det;punct |.


3> noun[place:+,mwe:+] = dig[lemme:81], noun[lemme:"Theater"].
 
/------------Some dig nodes---------------\

/A funny test 
/2> noun[tutu=+] =  noun#1[lemme:"bank%c+"], prep, noun#2,
                             where (#1[lemme]:#2[lemme]).\

// To treat stuff like "T&N" as a unique noun

3> noun[maj:+] = noun[maj,let];meas[maj], noun[lemme:"&"],  noun[maj,let];meas[maj]. 


// director-general as a unique noun
3> noun[mwe:+,features:!] = noun[lemme:director,!features:!], punct[form:fhyph,left,right], nadj. 

/self-goverment : pass the features of the second noun\

3> noun[mwe:+,features:!] = noun[lemme:self] ,  punct[form:fhyph,left,right], noun[!features:!].

3> noun[mwe:+,features:!] =  noun[!features:!,country:~,city:~],  punct[form:fhyph,left,right], prep | ?[left:~,right:~]| .

3> nadj[mwe:+,nadj:+,adj:+,noun:+,prepers:+] = noun[place:+],  punct[form:fhyph,left,right], verb[lemme:bear];adj[lemme:born].


4> dig = dig[ord], conj[coord], dig[ord,!features:!].


// ADJ{first - choice}, ADJ{second - hand}
5> adj[mwe=+] = num[ord] , punct[form:fhyph,left,right], ? .

//Adj Bio-vascular 
5>  adj[mwe=+,features:!] = adj[!features:!] , punct[form:fhyph,left,right] , adj[!features:!].

5> noun [mwe=+,features:!] = adj[lemme:bio] , punct[form:fhyph,left,right] , noun[!features:!].

5>  nadj[mwe=+,features:!] = nadj[!features:!] , punct[form:fhyph,left,right] , nadj[!features:!].


// three-day summit -> ADJ{three - day}
// 12-year-old -> ADJ{12 - year} - old
// 12-year old -> ADJ{12 - year} old
5> adj[quant=+,meas=+,mwe=+,tempexpr:!] = num[card,rom:~];dig[ord:~] , 
                               punct[form:fhyph,left,right], 
                               meas; noun[sg, !tempexpr:!]; nadj[adj=~,sg] 
                               | (punct[form:fhyph]), adj;noun;nadj |.

// O'Malley

5> noun[features:!] = noun[lemme:O], punct[quote:+,right:+,left:+], noun[lemme:"%C%c+",!features:!].

/------Some  Proper Name Nodes ------\
/-- with hyphens ... ---\


5> noun[proper:+,mwe:+,maj:+] @= adv[lemme:"al."];noun[lemme:"al",particlename], punct[form:fhyph,left,right], noun[maj].


9> noun[proper:+,mwe:+,maj:+] @= noun[maj], punct[form:fhyph,left,right], noun[maj,month:~] .


// three tokens

8> noun[proper=+,mwe=+] = noun[proper];noun[maj,guess];?[toutmaj], 
                    punct[form:fhyph,left,right],
                    ?[maj],
                    punct[form:fhyph,left,right],
                    ?[guess];noun[proper];noun[let];?[maj].

// To treat elements like Italo-Irish 

8> nadj[mwe:+,country:+] =  noun[proper];noun[maj,guess];nadj[maj],
                         punct[form:fhyph,left,right],
                         nadj[country].

// Iran-Iraq
//8> noun[proper=+,mwe=+,country=+,place=+] = noun[proper,country:+], 
 //       punct[form:fhyph,left,right], 
 //       noun[proper,country].


// Complex firstnames

//special case: first token is a proper name, second is a japanese title like "Sato-san" 

8> noun[proper=+,mwe=+,person:+] =  noun[proper,toutmaj:~];noun[maj,guess,toutmaj:~],  punct[form:fhyph,left,right], noun[title]. 

7> noun[proper=+,mwe=+,firstname=+,maj=+] @= noun[proper,particlename:~,firstname:+,place:~],
                    punct[form:fhyph,left,right],
                    ?[guess,maj];noun[proper], ( punct[form:fhyph,left,right],noun[proper]).
//actor-director
8> noun[mwe:+,features:!] = noun[prof:+,!features:!],  punct[form:fhyph,left,right] , noun[prof:+,!features:!].


// first token is a proper name 

8> noun[proper=+,mwe=+,maj=+] = noun[proper,place:~,particlename:~,prefix:~];noun[maj,guess];?[toutmaj,particlename:~], 
                    punct[form:fhyph,left,right],
                    ?[guess,maj];noun[proper,place:~,month:~];noun[let].




// special case: first token is a proper name, second word is known and lower-case (e.g. Tae-woo)
8> noun[proper=+,mwe=+,maj=+] = | ?[maj,det:~,firstname:~] |
                           noun[proper,particlename:~];noun[maj,guess];?[toutmaj,particlename:~], 
                           punct[form:fhyph,left,right],
                           ?[maj:~].

// first token is a common noun
8> noun[proper=+,mwe=+,maj=+] = noun[maj,proper:~];nadj[maj], 
                           punct[form:fhyph,left,right],
                           ?[guess];noun[proper];noun[let].

// CH - Jan. 2003
//8> noun[mwe=+,proper=+] = | det[def,maj:~] | noun[maj], noun[maj] | ?[maj:~] | .

// ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
// Affixes with hyphenation
// ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

// CH 17th September
// Grouping prefixes with base forms
15> nadj[features:!] = ?[prefix:+], punct[form:fhyph,left:+,right:+], nadj[!features:!].

15> adj[features:!] = ?[prefix:+], punct[form:fhyph,left:+,right:+], adj[!features:!].

15> noun[features:!] = ?[prefix:+], punct[form:fhyph,left:+,right:+], noun[!features:!].

15> verb[features:!] = ?[prefix:+], punct[form:fhyph,left:+,right:+], verb[!features:!].


15> dig[mwe:+] =  dig, punct[form:fhyph,first:~,last:~], dig ,  punct[form:fhyph,first:~,last:~] , dig[year:~,shortyear:~].



//Saint-Laurent-sur-mer

16> noun[mwe:+,place:+,org:+]  =  noun[last:~],
                            punct[form:fhyph,left,right,first:~,last:~],
                            nadj[last:~,lemme:"sur"],
                            punct[form:fhyph,left,right,first:~,last:~],
                            noun[lemme:"Mer"].
                   

// CH add
// e.g Vina del Mar
16> noun[mwe:+,place:+,org:+] = noun[last:~,proper,place], noun[lemme:del,last:~], noun[surface:Mar] .


//13> noun[mwe:+]  -> noun+[last:~],
//                    nadj+[last:~],
//                    punct+[form:fhyph,left,right,first:~,last:~],
 //                   noun[last].    

IDRules:



// 56 (899) as a digit : the last element
// can be a parenthesis,
// need to be careful with the overlap with the last rule 


13> dig[mwe=+,date=+] -> dig[first,year,day:~,dec:~],
                         punct[form:fhyph,first:~,last:~],
                         dig[first:~,rom:~,time:~,dec:~],
                         punct[form:fhyph,first:~,last:~],
                         dig[first:~,rom:~,time:~,dec:~].


13> dig[mwe=+,date=+] -> dig[first,dec:~,day],
                         punct[form:fslash,first:~,last:~],
                         dig[day],
                         dig[year,last].

// digit compounds
// Same rule as after but I need the feature time ...

13> dig[mwe=+,time=+] -> | ?[form:~fslash] | dig[last:~,time:+,rom:~,day:~,dec:~],dig*[last:~,rom:~,dec:~],
                  punct*[form:fopar],
                  punct*[form:fcpar,first:~],
                  punct*[form:fhyph,first:~],
                  dig[last,first:~,rom:~,time:~,dec:~,year:~].


// for phone numbers ??? (by Francois)

//13> dig[mwe=+] -> dig+[last:~,rom:~,day:~,dec:~],
  //                punct*[form:fopar],
  //                punct*[form:fcpar,first:~],
       //           punct*[form:fhyph,first:~],
  //                dig[last,first:~,rom:~,time:~,dec:~].
//


// Was previously in localgram2.xip
// Has to be before localdates

Sequence: 

// CH
// joining percentages separated by hyphen
15> dig[mwe=+,percent=+] = dig[percent], punct[form:fhyph], dig[percent] .



// CH
// AS MANY AS | digit | is a quantifier
15> quant[mwe:+] = ?[lemme:as], ?[lemme:many], ?[lemme:as] | ?[card:+] | .
 
// the (last;first) half of is a  quantifier
15> quant[mwe=+] = det[def], nadj[preperiod];num[ord,tempexpr=~], noun[lemme:half];noun[lemme:part];num[ord], prep[lemme:of] .




// CH
// A COUPLE OF as quantifier
15> quant[mwe:+] = ?[lemme:a], ?[lemme:couple], ?[lemme:of] .

15> quant[mwe=+] = ?[lemme:couple], ?[lemme:of] .

// CB 

// , after all, --> after all as adv

15> adv[notly=+] = |punct[comma] | ?[lemme:after], ?[lemme:all] | punct[comma] |.



Marker:

17> noun[title], noun[let,maj,initial=+], noun[proper].
17> noun[firstname], noun[let,maj,initial=+], noun[proper].




Sequence: 

// complex preposition that are not taken into account e.g. in the beginning of
// they have to be in this file, just before localgrammars dealing with temporality

// in the beginning of
20> prep[mwe=+,time_anchor=+] = prep[lemme:in], det[def], noun[lemme:begin,vprog], prep[lemme:of] .

// in order to is a prepositional locution when preceeded by punctuation, by non-ambiguous noun or adj
20> prep[mwe=+,form=finfto] = | punct; ?[noun:+,verb:~,adv:~] | prep[lemme:in], noun[lemme:order], prep[lemme:to].
