//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2003 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : English XIP grammar
//!   filename   : localgram2.xip
//!   Date       : 10/02/2003
//!   Author     : Caroline Hagege
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

// This file contains both sequence and tagging rules.
// They are some adjustments (groupings and disambiguation that can be done)

Sequence:

// A TEST IN CASE TWO GUESS NOUNS



// Same as below but the digit may be a fractional e.g. 2,3-benzofuran

// CH 27th September
// Grouping together a digit followed by an hyphen followed by an adjective
10> nadj = dig[dig], punct[form:fhyph], nadj.



// AFFIXES (regles sur les prefixes) mises dans localgram1


// CH 18th October
// words with feature cardpoint separated by hyfen are grouped together
// South East -- South and East
10> nadj[features:!,mwe=+] = nadj[cardpoint],
                             (punct[form:fhyph,left,right]),
                             (conj[coord]),
                             nadj[!features:!,cardpoint].

// South East -- South and East
10> adv[features:!,mwe=+] = adv[cardpoint],
                             (punct[form:fhyph,left,right]),
                             (conj[coord]),
                             adv[!features:!,cardpoint].




// CH
// Having together a word followed by an hyphen[left:+, right:~] and the hyphen when followed by comma or coordinator
// e.g. onion- or garlic-like
10> nadj = ?[noun:+], punct[form:fhyph, left:+, right:~] | punct[comma];?[coord] | .


// CH 11 Oct.
// Grouping a noun followd by an hyphen followed by a ing form when they are followed by an adjective or noun
10> nadj = ?[noun:+], punct[form:fhyph,left:+,right:+], ?[vprog] | ?[noun:+] | .

// CH 31 july
// Grouping of adjectival forms separated by hyphen eg. fast-growing
10> nadj = ?[adj:+], punct[form:fhyph,left:+,right:+], ?[adj:+].


10> nadj = ?[noun:+], punct[form:fhyph,left:+,right:+], ?[noun:+,mwe:~] | ?[noun:+] | .

// CH 31 July
// Grouping nouns and past participle separated by hypen eg. object-oriented
10> nadj = noun, punct[form:fhyph,left:+,right:+], verb[partpas:+].

// CH 21th September
// Grouping adjectives and past participle separated by hyphen e.g. Soviet-blessed
// We give the nadj category to the result
10> adj = adj, punct[form:fhyph,left:+,right:+], verb[partpas:+].

// CH 12th September
// Grouping noun-adjectives and past participle separated by hypeh e.g. ill-prepared
10> nadj = nadj, punct[form:fhyph,left:+,right:+], verb[partpas:+].

// CH 24th August
// Grouping two proper nouns separated by hyphen e.g. The annual meeting of INDONESIA-MALAYSIA forest experts
10> noun[proper:+] = noun[proper:+], punct[form:fhyph,left:+,right:+], noun[proper:+].


// CH 3rd September 
// Grouping two nouns separated by hyphen e.g. health-care
// I'm not sure . Check
10> noun = noun, punct[form:fhyph,left:+,right:+], noun[date:~].

// CH 12th September
// Grouping together an nadj followed by hyphen followed by noun before another noun
// e.g. A West Berliner counter-culture group
10> noun = nadj, punct[form:fhyph,left:+,right:+], noun[date:~] | noun | .


// CH 28th September
// Grouping together an adj followed by hyphen followed by noun before another noun
10> noun = adj, punct[form:fhyph,left:+,right:+], noun[date:~] | noun | .


// CH 28th September
// Grouping together a noun followed by an hyphen followed by like
// eg. alpha-like virus 
10> nadj = noun, punct[form:fhyph,left:+,right:+], ?[form:flike] .

// CH 1st October
// Grouping together an abbreviation followed by an hyphen followed by a noun or adjective
10> nadj = noun[abr], punct[form:fhyph,left:+,right:+], ?[adj] .

10> noun = noun[abr], punct[form:fhyph,left:+,right:+], noun[noun] .






// CH 7th August
// Grouping together a form of have followed by TO in order to construct a modal
// CHECK IF NO BAD SIDE EFFECT !!!!
10> aux = verb[form:fhave], prep[form:finfto].


// CH 8th August
// Grouping the words EACH and OTHER together
// In a later stage correct in the lexicon
// In the file lexicon.xip (and feautures.xip) 2 new values of form are added especially for this rule
10> pron = quant[form:feach], adj[form:fother].



// ADJ{15 - year} old -> ADJ{ADJ{15 - year} old}
10> adj[quant:+] = adj[quant], (punct[form:fhyph]), ?[adj] .


// SEE OTHER ADVP in chunker.xip

// CH 22th Aug.
// Grouping together HOW + MUCH
// The result is given either the det[wh:+] or adv[wh:+]
10> ADVP[wh:+,form:fmuch] = ?[form:fhow], ?[form:fmuch] | ?[noun:~, adj:~] |.

10> det[wh:+,form:fmuch] = ?[form:fhow], ?[form:fmuch] | ?[prep:~, verb:~, coord:~, adv:~, punct:~] |.


// CH 22th Aug.
// Grouping together HOW + MANY
// The result gives either a det or an adv
10> ADVP[wh:+,form:fmany] = adv[form:fhow], ?[form:fmany] | ?[noun:~, adj:~] |.

10> det[wh:+,form:fmany] = ?[form:fhow], ?[form:fmany] | ?[prep:~, verb:~, coord:~, adv:~, punct:~] |.



// CH 
// Grouping together "later" and "on" if this expression is followed by a punctuation mark, a conjunction, a verb 
10> adv[notly:+] = ?[form:flater], prep[p_on:+] | conj;verb;punct |.


// CH
// AS MANY AS | digit | is a quantifier
10> quant[mwe:+] = ?[lemme:as], ?[lemme:many], ?[lemme:as] | ?[card:+] | .
 


// CH 23th Nov.
// Treating complex expressions involving AS
// If we have a sequence AS + adv;adj + AS followed by an adjective or a noun that has not the features pron and nom
// OR if this sequence is followed by anything with no verbs until the end of the sentence
// Then we grouped it and give it a tag prep with feature form:fas
// If the sequence AS+ adj;adv + AS is followed by a determiner, or a nominative pronoun,
// Then we give it the tag conj with feature fas
10> prep[lemme:as] = prep[lemme:as], ?[adj], prep[lemme:as] | (?[adj]), ?+[noun,nom:~,pron:~], ?[verb:~,aux:~] | .

10> prep[lemme:as] = prep[lemme:as], ?[adv], prep[lemme:as] | (?[adj]), ?+[noun,nom:~,pron:~], ?[verb:~,aux:~] | .


10> conj[lemme:as] = prep[lemme:as], ?[adj], prep[lemme:as] | pron[nom,personal] | .

10> conj[lemme:as] = prep[lemme:as], ?[adv], prep[lemme:as] | pron[nom,personal] | .

10> prep[lemme:as] = prep[lemme:as], ?[adj], prep[lemme:as] | (?[adj]), ?+[noun,nom:~,pron:~], aux;verb | .

10> prep[lemme:as] = prep[lemme:as], ?[adv], prep[lemme:as] | (?[adj]), ?+[noun,nom:~,pron:~], aux; verb | .



//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! 
//!
//!  Date: July 2001
//!
//!  Copyright (c) 2001 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


IDrules:


//==================================== ADVs ========================================

//The tempadv:+] feature is required to avoid these adverbs from being
//included in finite verb chunks that follow the verb they relate to

//SC{NP{He} FV{left}} ADV{10 days ago}
15> ADV[tempexpr:+,mwe:+] -> dig[first:+], noun, adv[form:fago,last:+].

//SC{NP{He} FV{left}} ADV{ten days ago}
//NP{Ethiopia 's government two weeks} TEST{ago said}
15> ADV[tempexpr:+,mwe:+] -> num[first:+], noun, adv[form:fago,last:+].

//a year ago  - a week ago etc
15> ADV[tempexpr:+,mwe:+] -> det[first:+], noun, adv[form:fago,last:+].

//a_few+QUANT decades ago
//some time ago
//many years ago
15> ADV[tempexpr:+,mwe:+] -> quant[first:+], noun, adv[form:fago,last:+].


//several+adj years ago
15> ADV[tempexpr:+,mwe:+] -> adj[first:+], noun, adv[form:fago,last:+].


// "long ago" or "not (that | so) long ago" are taken as ADV[tempadv:+]
// NOTE : other expressions with "ago" are treated in the chunker

15> ADV[tempexpr:+,mwe:+] -> adv[form:fnot], (conj[form:fthat]), (adv[form:fso]), adv[form:flong], adv[form:fago,last:+] .
15> ADV[tempexpr:+,mwe:+] -> (adv[form:fso]), adv[form:flong], adv[form:fago,last:+] .



//"two days before/after/later
15> ADV[tempexpr:+,mwe:+] -> dig[first:+], noun[period], adv[time:+,last:+].
15> ADV[tempexpr:+,mwe:+] -> num[first:+], noun[period], adv[time:+,last:+].



//"at least 2 people were injured". At most ....
//15> ADV -> prep[form:fat], quant[sup:+,last:+].
15> ADV[quantadv:+,mwe:+] -> prep[form:fat], quant[sup:+,last:+].

// CH more/less than...
15> ADV[quantadv:+,mwe:+] -> quant[compar:+], conj[form:fthan] .


// CH Grouping "so far away" and "so far as"
15> ADV[mwe:+] -> adv[form:fso], adv[form:ffar], adv[p_away] .

15> CONJ[conj:+, sub:+,mwe:+] -> adv[form:fso], adv[form:ffar], conj[lemme:as] .


// CH Grouping "so far" when followed by anything that is not a preposition

20> ADV[mwe:+] -> adv[form:fso], adv[form:ffar] | ?[prep:~,form:~fthan] | .


//every/each day/week/month/year/Spring/time
//!!the last[preperiod:+] ADV{few weeks} 
//!!these ADV{few weeks}
//!!through ADV{all time}
//NP{The AP{same} revolution+NOUN every+QUANT year}
//a ADV{year more} - last:+
// CH 20 juillet
15> ADV[tempexpr:+,mwe:+] ->  quant[preperiod:+], noun[period:+,last:+].


// CH 8th October
// NP's like the 50's
15> NP[tempexpr:+] -> det, dig, part . 


//CH 24 juillet. NP's like "(the) last/next year" have a special feature.
//They will be subject only if followed by a principal finite verb. 
//Otherwise they will be considered as adverbials. 
// next is an adj and last a nadj and both have feature preperiod:+

15> NP[tempexpr:+] -> (det), (quant), nadj[preperiod:+], ( AP+), noun[period:+,last:+].

15> NP[tempexpr:+] -> num[card:+], noun[period:+,last:+].

//Should "Last week" etc be considered as adverbs or NPs??????
//last night/week/month/year/Monday
//NP{Gorbachev 's visit} ADV{next spring}
//NP{a AP{half-hearted} birth control campaign} ADV{last year} / ADV{twelve months ago}
 
//??!!SC{BG{before} NP{AP{next} summer} , NP{Joe Gagen} FV{said}}

//!!15>GROUP{SC{the NP{millenium 's} ADV{last decade} 


//15> ADV -> |?[part:~ ; det:~ ; gen:~]| nadj[preperiod:+], noun |?[part:~]|.  /"last" is a nadj\
//15> ADV -> |?[part:~ ; det:~ ; gen:~]| adj[preperiod:+], noun |?[part:~]|.   /"next" is an adj\

//ABOVE RULE DOES NOT CAPTURE "Last month" etc at start of sentence!
//15> ADV -> || nadj[preperiod:+], noun |?[part:~]|.  /"last" is a nadj\
//15> ADV -> || adj[preperiod:+], noun |?[part:~]|.   /"next" is an adj\



//too ambiguous!
//a year   - a day/week/month/year  etc
//!!as ADV{a year} in which
//!!a night patrol 
//!!a year after he ..
//SC{With FV{just ADV{a week} passed}} SC{BG{since}
//15> ADV ->  |?[prep:~ ; adv:~]| det[indef:+], noun[aperiod:+] |?[noun:~ ; nadj:~ ; prep:~]|.

//ADV{in three decades}????
//ADV{for nine years}???
//ADV{in the past two months}
//for ADV{as long as} TEST{five years}
//TEST{three times} NP{that much} .}


//15> TEST -> num[first:+], noun[period:+] |prep|
//NP{its second annual} TEST{First Night}
//!!PP{within NP{the AP{next}}} TEST{six months}
//NP{ravine three days}! after ...
//15> TEST -> |?[det:~]| num[first:+,ord:~], noun[period:+].


//CH 
//====================== Numeral Grouping ========================================

15> num[card:+] -> dig, num.

// dig to dig is considered as dig e.g. 65 to 69 CH 3rd Sept.
15> dig[card:+] -> dig[card:+], prep[form:finfto], dig[card:+] .

// CH Grouping of monetary expressions like US $ 
15> meas[symbol:+] -> noun[abr, country], meas[symbol] .

// CH Grouping together a measure followed by a numeral
20> num[card:+,symbol:+] -> meas[symbol:+], num[card:+] .


// CH Grouping together expressions of measure with "per cent"
20> num[card:+,symbol:+,num:+] -> num[card:+], noun[percent:+] .

20> num[card:+, symbol:+,num:+] -> dig[card:+], noun[percent:+].





//CH 6th August
//====================== Quantification ========================================

15> quant[quant:+, predet:+] -> det[def:+], nadj[form:fwhole].



//10> [det:+] < [part:+]
//10> [det:+] < [adj:+]
//10> [adj:+] < [title:+]
//10> [det:+] < [quant:+]
//10> [det:+] < [num:+]
//10> [det:+] < [dig:+]
//10> [adj:+] < [noun:+]
//10> [adj:+] < [nadj:+]
//10> [adj:+] < [adv:+]
//10> [adj:+] < [conj:+]
//[punct:+,form:fcm] < [coord:+] 


Tagging:

// If an Named Entity was found within a NP that is followed in the same NP by an NADJ
// If this NADJ starts with a capital letter, then we want to take away the adjective tag
// e.g. Moday Corp Subsidiary(noun)
30> nadj[maj:+,toutmaj:~,sg:+] %= | noun[proper:+] | noun[maj=+,sg=+] .
30> nadj[maj:+,toutmaj:~,pl:+] %= | noun[proper:+] | noun[maj=+,pl=+] .


