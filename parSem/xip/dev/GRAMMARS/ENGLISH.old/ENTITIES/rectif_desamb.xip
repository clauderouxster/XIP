//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2003 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : English XIP grammar
//!   filename   : rectif_desamb.xip
//!   Date       : 28/11/2003
//!   Author     : Caroline Hagege
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


////////////////////////////////////////////////////////////////////////////////////////////
// Specific problems proper nouns / other categories
// When proper nouns are badly tagged because the string that represent them can also be 
// from another category. In some very restricted contexts it is possible to correct it.
////////////////////////////////////////////////////////////////////////////////////////////


Tagging:



// If we have something that has been tagged as verb that is starting by a capital letter that is not all capital, that is finishing a sentence and is preceeded by firstname (letter) then it is a proper noun
// e.g. This program was created by Stephen B. Strum (*verb)

10> verb[maj:+, toutmaj:~] %= | ?[firstname,toutmaj:~], (?[initial:+])  | noun[proper:+, fam:+, maj:+] | ?[endbis:+] | .

10> verb[maj:+, toutmaj:~] %= | punct[comma], ?[firstname,toutmaj:~], (?[initial:+])  | noun[proper:+, fam:+, maj:+] | punct[comma] | .

// Tagging rules for ambiguous AFTER/BEFORE 
// They have to be in this file because we want a previous disambiguation of other sentence lexical units before applying this rule
// before/after precedes a NP just followed by unambiguous verb followed by no verbs until the end of sentence.
30> prep[lemme:after] %= conj[after:+,reftime:+,form:fafter,time:+,sub:+] | ?[det:+], (?[adj]), ?[noun:+], verb[noun:~], ?*[verb:~], ?[endbis:+] | .
30> prep[lemme:after] %= conj[after:+,reftime:+,form:fafter,time:+,sub:+] | ?[noun:+,proper:+], verb[noun:~], ?*[verb:~], ?[endbis:+] | .
30> prep[lemme:before] %= conj[before:+,reftime:+,form:fbefore,time:+,sub:+] | ?[det:+], (?[adj]), ?[noun:+], verb[noun:~], ?*[verb:~], ?[endbis:+] | .
30> prep[lemme:before] %= conj[before:+,reftime:+,form:fbefore,time:+,sub:+] |?[noun:+,proper:+], verb[noun:~], ?*[verb:~], ?[endbis:+] | .


/// LIKE mistagged as a verb
// e.g. the plot is dumped on you like a video. 
// if like is followed by a determiner, is tagged as a verb and there is already a verb before but no coordination and no conjunction, then it is a prep and not a verb
40> verb[lemme:like, p3:~,changed:~] %= | ?[startbis:+], ?*, verb[aux:~], ?+[conj:~,verb:~] | prep | det | .

