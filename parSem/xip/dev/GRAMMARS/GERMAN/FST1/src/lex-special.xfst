# ============================================================================
# TYPE:		(x)fst script
# CONTENT:	punctuations, symbols, etc.
#
# LANGUAGE:	GERMAN
# ENCODING:	UTF-8
#
# AUTHOR:	A. Schiller
#
# CREATED:	15-Mar-2002
# UPDATED:	25-Jul-2006  - changed to UTF-8
#
# ============================================================================

echo >>> digital numbers
define DIG	[ %0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 ];
define DIG1	DIG - %0 ;

define NUMSEP	[ %. | %, ];
define NUMOP	[ "\u002A"	# [*] # ASTERISK
		| "\u002B"	# [+] # PLUS SIGN
		| "\u002D"	# [-] # HYPHEN-MINUS
		| "\u002F"	# [/] # SOLIDUS
		| "\u00B1"	# [±] # PLUS-MINUS SIGN
		| "\u003A"	# [:] # COLON
		| "\u003C"	# [<] # LESS-THAN SIGN
		| "\u003D"	# [=] # EQUALS SIGN
		| "\u003E"	# [>] # GREATER-THAN SIGN
		| "\u00D7"	# [×] # MULTIPLICATION SIGN
		| "\u00F7"	# [÷] # DIVISION SIGN
		];

define MATH	NUMOP - [ %: | %- ] ;

define ORD	[ DIG+ %. ];
define CARD0 	[ [ DIG | NUMSEP | NUMOP ]* & $[DIG] ];
define CARD1	CARD0 - [?* %- ] ;

define CARDSPACE [ DIG1 (DIG (DIG)) ] [ " " DIG DIG DIG]* (NUMSEP DIG+);

define CARD	[ CARD1 | CARDSPACE ];


define ROMAN1	[ (m (m (m)))
	    ( [ c m | d | c d | (d) (c (c (c))) ] ) 
	    ( [ x c | l | x l | (l) (x (x (x))) ] ) 
	    ( [ i x | v | i v | (v) (i (i (i))) ] ) ] - [];

define ROMAN2	[ (M (M (M)))
	    ( [ C M | D | C D | (D) (C (C (C))) ] ) 
	    ( [ X C | L | X L | (L) (X (X (X))) ] ) 
	    ( [ I X | V | I V | (V) (I (I (I))) ] ) ] - [];
define ROMAN  [ ROMAN1 | ROMAN2 ];

# -------------------------------------------------------------------
echo >>> punctuations, symbols, etc.
# currency symbols	
define CURR	[ "\u0024"	# [$] # DOLLAR SIGN
		| "\u00A2"	# [¢] # CENT SIGN
		| "\u00A3"	# [£] # POUND SIGN
		| "\u00A4"	# [¤] # CURRENCY SIGN
		| "\u00A5"	# [¥] # YEN SIGN
		| "\u20A0"	# [₠] # EURO-CURRENCY SIGN
		| "\u20A1"	# [₡] # COLON SIGN
		| "\u20A2"	# [₢] # CRUZEIRO SIGN
		| "\u20A3"	# [₣] # FRENCH FRANC SIGN
		| "\u20A4"	# [₤] # LIRA SIGN
		| "\u20A5"	# [₥] # MILL SIGN
		| "\u20A6"	# [₦] # NAIRA SIGN
		| "\u20A7"	# [₧] # PESETA SIGN
		| "\u20A8"	# [₨] # RUPEE SIGN
		| "\u20A9"	# [₩] # WON SIGN
		| "\u20AA"	# [₪] # NEW SHEQEL SIGN
		| "\u20AB"	# [₫] # DONG SIGN
		| "\u20AC"	# [€] # EURO SIGN
		| "\u20AD"	# [₭] # KIP SIGN
		| "\u20AE"	# [₮] # TUGRIK SIGN
		| "\u20AF"	# [₯] # DRACHMA SIGN
		| "\u20B0"	# [₰] # GERMAN PENNY SIGN
		| "\u20B1"	# [₱] # PESO SIGN
		];

define SUP	[ "\u00B2"	# [²] # SUPERSCRIPT TWO
		| "\u00B3"	# [³] # SUPERSCRIPT THREE
		];

define TIME	[ h | m i n (%.) | (m) s (e [c|k] (%.)) ];
define UNIT [
  (q|c) ([ m | c | d | k ]) m				# (Quadrat/Kubik) Meter
| ([ m | c | d | k ]) m SUP				# Quadrat-/Kubik- Meter
| ([ m | c | d | k ]) m %/ TIME				# Meter pro Stund
| ([ m | c | d | k ]) g (%/ TIME)			# Gramm
| TIME							# Sekunde
| ([ m | c | d | h ]) l					# Liter
| (k) W (TIME)						# Watt
| t							# Tonne
| h a							# Hektar
| (k) c a l (%.)					# Kalorie
| [ T | G | M | K | k ] [b|B] ({yte}|{it})		# Byte, Bit
];

# letters
define UPP	[A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Ä|Ö|Ü];
define LOW	[a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|x|y|z|ä|ö|ü|ß];

# other symbols
define SYMBOL	[ %@ | %# | %^ | %& | %~ 
		| "\u00B4"	# [´] # ACUTE ACCENT
		| "\u00B8"	# [¸] # CEDILLA
		];

# sentence initial punctuations
define PBEGIN	[ "\u00A1"	# [¡] # INVERTED EXCLAMATION MARK
		| "\u00BF"	# [¿] # INVERTED QUESTION MARK
		];

# sentence final punctuations
define PFINAL 	[ %. | %! | %? ];

# quotation marks
define DQUOTE	[ "\u0022"	# ["] # QUOTATION MARK
		| "\u00AB"	# [«] # LEFT-POINTING DOUBLE ANGLE QUOTATION MARK
		| "\u00BB"	# [»] # RIGHT-POINTING DOUBLE ANGLE QUOTATION MARK
		| %< %< | %> %> | %' %'
		];

define SQUOTE	[ "\u0027"	# ['] # APOSTROPHE
		| "\u0060"	# [`] # GRAVE ACCENT
		];

define LQUOTE  [  "\u2018"	# [‘] # LEFT SINGLE QUOTATION MARK
		| "\u201B"	# [‛] # SINGLE HIGH-REVERSED-9 QUOTATION MARK
		| "\u201C"	# [“] # LEFT DOUBLE QUOTATION MARK
		| "\u201E"	# [„] # DOUBLE LOW-9 QUOTATION MARK
		| "\u201F"	# [‟] # DOUBLE HIGH-REVERSED-9 QUOTATION MARK
		| %` %` | %, %, 
		];

define RQUOTE	[ "\u2019"	# [’] # RIGHT SINGLE QUOTATION MARK
		| "\u201A"	# [‚] # SINGLE LOW-9 QUOTATION MARK
		| "\u201D"	# [”] # RIGHT DOUBLE QUOTATION MARK
		];

# -----------------------------------------------------------------------------
echo >>> date and time expressions
define DIG1	DIG - %0 ;

define YEAR1	[ %' | %` ] DIG DIG ;
define YEAR	[ (DIG (DIG)) DIG DIG ];
define AYEAR	YEAR | YEAR1 ;
define YEAR4	[ 1 9 | 2 %0 ] [ DIG DIG ];	# very likely a year if stand alone
define YEAR0	AYEAR - YEAR4 ;	    		# less likely ...

define DDAY	(%0) DIG1 | [ 1 | 2 ] DIG | 3 [ %0 |1 ] ;
define DMONTH   (%0) DIG1 | 1 [ %0 | 1 | 2 ] ;
define DDATE	DDAY %. (" ") DMONTH %. ((" ") AYEAR );

define HOUR	[ (%0) | 1 ] DIG | 2 [%0|1|2|3|4] ;
define MINUTE	[ (%0) | 1 | 2 | 3 | 4 | 5 ] DIG ;

define TIME	HOUR [%: | %. ] MINUTE ( %: MINUTE) ;

# -----------------------------------------------------------------------------

define MONTH1	[ {Januar}:{Jan} 
		| {Februar}:{Feb}
		| {März}:{Mär}
		| {April}:{Apr} 
		| {Mai}
		| {Juni}:{Jun}
		| {Juli}:{Jul}
		| {August}:{Aug}
		| {September}:[{Sep} (t) ]
		| {Oktober}:{Okt}
		| {November}:{Nov}
		| {Dezember}:{Dez}
		] (0:%.);

define DAY1	[ {Sonntag}:{So} 
		| {Montag}:{Mo}
		| {Dienstag}:{Di}
		| {Mittwoch}:{Mi}
		| {Donnerstag}:{Do}
		| {Freitag}:{Fr}
		| {Samstag}:{Sa}
		] (0:%.) ;


define ADAY	[DAY1|DAY1.u] (%,) " " ({den} " ") ;
define AMONTH   [MONTH1|MONTH1.u];

define DATE	(ADAY) DDAY %. (" ") AMONTH (" " AYEAR) ;

define CLOCK	HOUR [%:|%.] MINUTE (" ") {Uhr} ;

# ------------------------------------------------------------------------------
define MUSIC1 [
  [c|d|f|g|h] ( e s (e s) | i s (i s) )
| [e|a] ( s (e s) | i s  )
| b ] ( %- [ [D|d] u r | [M|m] o l l ] ) ;

define MUSIC2 MUSIC1 .o. [ c->C, d->D, e->E, f->F, g->G, h->H, b->B || .#. _ ].l;

# ------------------------------------------------------------------------------
#some interjections
define INTERJ [
  (b|p) [ a+ | ä+ | e e+ | i+ | o+ | ö+ | u+ | ü ] h+ 
| b r r+
];

# ------------------------------------------------------------------------------
define TRUNC	%+Cpd:%-  ;

define ONE	[ 1 |i | I] ;

define TOKBD	"+TOK+":0 ;

read regex [
  [i|I]			%+Dig:0 %+Rom:0 %+Card:0 %+Sg:0 (TRUNC)
| [ ROMAN - ONE ]	%+Dig:0 %+Rom:0 %+Card:0 %+Pl:0 (TRUNC)
| ROMAN %. 		%+Dig:0 %+Rom:0 %+Ord:0
| 1			%+Dig:0 %+Dec:0 %+Card:0 %+Sg:0 (TRUNC)
| [CARD - [ORD|ONE]] 	%+Dig:0 %+Dec:0 %+Card:0 %+Pl:0 (TRUNC)
| ORD			%+Dig:0 %+Dec:0 %+Ord:0

| UPP 		%+Symbol:0 %+Letter:0 %+Upp:0 (TRUNC)
| LOW 		%+Symbol:0 %+Letter:0 %+Low:0 (TRUNC)
| [UPP %.]+	%+Symbol:0 %+Init:0

| CURR	   	%+Symbol:0 %+Curr:0
| MATH		%+Symbol:0 %+Math:0
| UNIT		%+Symbol:0 %+Unit:0
| %%		%+Symbol:0 %+Percent:0

#| CARD CURR	%+Dig:0 %+Curr:0 %+Card:0
#| CARD MATH	%+Dig:0 %+Math:0 %+Card:0
#| CARD UNIT	%+Dig:0 %+Unit:0 %+Card:0
#| CARD %%	%+Dig:0 %+Percent:0 %+Card:0

| CARD %+Dig:0 %+Dec:0 %+Card:0 TOKBD [
	  CURR	   	%+Noun:0 %+Invar:0 %+Curr:0
	| MATH		%+Noun:0 %+Invar:0 %+Math:0
	| UNIT		%+Noun:0 %+Invar:0 %+Unit:0
	| %%		%+Noun:0 %+Invar:0 %+Percent:0
	]


| "\u00A7"	%+Symbol:0 %+Paragraph:0	# [§] # SECTION SIGN
| "\u00A9"	%+Symbol:0 %+Copyright:0	# [©] # COPYRIGHT SIGN
| "\u00B0"	%+Symbol:0 %+Degree:0		# [°] # DEGREE SIGN

| MUSIC1	%+Symbol:0 %+Music:0 %+Low:0 (TRUNC)
| MUSIC2	%+Symbol:0 %+Music:0 %+Upp:0 (TRUNC)
| INTERJ	%+Itj:0

| SYMBOL	%+Symbol:0

| DDAY %.	%+Dig:0 %+Expr:0 %+Day:0
| DMONTH %.	%+Dig:0 %+Expr:0 %+Month:0
| YEAR4		%+Dig:0 %+Expr:0 %+Year:0

| DATE		%+Dig:0 %+Expr:0 %+Date:0
| DDATE		%+Dig:0 %+Expr:0 %+Date:0
| TIME		%+Dig:0 %+Expr:0 %+Time:0	
| HOUR		%+Dig:0 %+Expr:0 %+Hour:0	
| MINUTE	%+Dig:0 %+Expr:0 %+Minute:0	
| CLOCK		%+Dig:0 %+Expr:0 %+Time:0

| PBEGIN	%+Punct:0 %+Begin:0
| PFINAL	%+Punct:0 %+Sent:0

| SQUOTE	%+Punct:0 %+Quote:0 %+Single:0
| DQUOTE	%+Punct:0 %+Quote:0 %+Double:0
| LQUOTE	%+Punct:0 %+Quote:0 %+Left:0
| RQUOTE	%+Punct:0 %+Quote:0 %+Right:0

| [%,]		%+Punct:0 %+Comma:0
| %:		%+Punct:0 %+Colon:0
| %;		%+Punct:0 %+Semicolon:0
| [%. %.+]	%+Punct:0 %+Dots:0
#| %-		%+Punct:0 %+Hyphen:0
| %-+		%+Punct:0 %+Dash:0

| "\u2012"	%+Punct:0 %+Dash:0	# 2012    FIGURE DASH
| "\u2013"	%+Punct:0 %+Dash:0	# 2013    EN DASH
| "\u2014"	%+Punct:0 %+Dash:0	# 2014    EM DASH
| "\u2015"	%+Punct:0 %+Dash:0	# 2015    HORIZONTAL BAR
| %/		%+Punct:0 %+Slash:0
| %\		%+Punct:0 %+Backslash:0
| %_		%+Symbol:0 %+Underscore:0

| "("		%+Punct:0 %+Paren:0 %+Left:0
| ")"		%+Punct:0 %+Paren:0 %+Right:0
| "["		%+Punct:0 %+Bracket:0 %+Left:0
| "]"		%+Punct:0 %+Bracket:0 %+Right:0
| "{"		%+Punct:0 %+Bracket:0 %+Left:0
| "}"		%+Punct:0 %+Bracket:0 %+Right:0
| [%<]		%+Punct:0 %+Angle:0 %+Left:0
| [%>]		%+Punct:0 %+Angle:0 %+Right:0

| "\u2022"	%+Punct:0 %+Bullet:0 	# [•] # BULLET
| "\u00B7"	%+Punct:0 %+Bullet:0	# [·] # MIDDLE DOT

# some additional entries:
| [ {jein}|{jain} ]	%+Ptcl:0 %+SAdv:0
];

