! -------------------------------------------------------------------------
! TYPE:		lexc
! CONTENT:	German morphology: derivational continuation classes
!
! AUTHOR:	Anne Schiller, XRCE
! DATE:		09-Jan-2002
! -------------------------------------------------------------------------
! NOTE: inflectional continuations must be defined elsewhere
! -------------------------------------------------------------------------



! -------------------------------------------------------------------------
! diminutives
LEXICON }Dim
  ^}chen+Noun+Neut	N_0 ;		! [der] Tisch -> [das] Tischchen
  ^}lein+Noun+Neut	N_0 ;		! [der] Tisch -> [das] Tischlein
  ^}elchen+Noun+Neut	N_0 ;		! [der] Ring -> [das] Ringelchen

! with umlaut:
LEXICON }Dim$	^$	}Dim ;	! [der] Arm -> [das] Ärmchen, Ärmlein

! -------------------------------------------------------------------------
LEXICON	}in	
  ^}in^~+Noun+Fem	Nf_en ;		! [der] Freund -> [die] Freundin
  ^}In^~+Noun+Fem	Nf_en ;		! [der] Freund -> [die] FreundIn


! with umlaut:
LEXICON }in$	^$	}in ;	! [der] Bauer -> [die] Bäuerin

! -------------------------------------------------------------------------
LEXICON }ung
  ^V^}ung@U.zu.off@@U.ge.off@+Noun+Fem	Nf_en ;		! anwenden -> Anwendung
					! atmen -> Atmung

! -------------------------------------------------------------------------
LEXICON }heit
  ^}heit+Noun+Fem	Nf_en ;		! dumm -> [die] Dummheit

LEXICON }keit
  ^}keit+Noun+Fem	Nf_en ;		! wichtig -> [die] Wichtigkeit

LEXICON }igkeit
  ^}igkeit+Noun+Fem	Nf_en ;		! müde -> [die] Müdigkeit

! -------------------------------------------------------------------------
LEXICON }Bar
  ^V^}bar@C.un@@U.zu.off@@U.ge.off@+Adj		A_reg ;		! anwenden -> (un)anwendbar
  ^V^}bar@C.un@@U.zu.off@@U.ge.off@+Adj		}keit ;		! anwenden -> (Un)Anwendbarkeit


! -------------------------------------------------------------------------
LEXICON }Er
  %^}er+Noun+Masc		N_0n ;		! [die] Technik -> [der] Techniker
  %^}er+Noun+Masc		}in ;		! [die] Technik -> [die] Technikerin

! with umlaut
LEXICON }Er$		^$	}Er ;		! England -> [ein] Engländer

LEXICON }Er2
				}Er ;		! Berlin -> [ein] Berliner; [eine] Berlinerin
  %^}er+Adj+Invar+Upp		Upp ;		! Berlin -> [eine] Berliner [Spezialität]

! with umlaut
LEXICON }Er2$		^$	}Er2 ;		! Weinstadt -> [ein] Weinstädter

LEXICON }isch
  %^}isch@C.un@+Adj		A_reg ;		! Bretone -> (un)bretonisch

! with umlaut
LEXICON }isch$		^$	}isch ;		! Affe -> äffisch

LEXICON }Isch
  				}isch ;		! Bretone -> bretonisch
  %^}isch+Noun+Lang+Neut	N_Sgs ;		! Bretone -> [das] Bretonisch

! with umlaut
LEXICON }Isch$		^$	}Isch ;		! Franzose -> französisch

LEXICON }ErIsch
				}Er2 ;		! Berlin -> Berliner
  %^}erisch@C.un@+Adj		A_reg ;		! Berlin -> berlinerisch
  %^}erisch+Noun+Lang+Neut	N_Sgs ;		! [das] Berlinerisch

LEXICON }Ese
  %^}ese+Noun+Masc		Nm_Wk ;		! China -> Chinese; Nepal -> Nepalese
  %^}ese+Noun+Masc		}in ;		! China -> Chinesin; Nepal -> Nepalesin
  %^}esisch@C.un@+Adj		A_reg ;		! China -> chinesisch; Nepal -> nepalesisch
  %^}esisch+Noun+Lang+Neut	N_Sgs ;		! [das] Chinesisch


! -------------------------------------------------------------------------
LEXICON }weise
  ^}weise+Adv			inv ;		! dumm -> dummerweise

! -------------------------------------------------------------------------
LEXICON }Vr
 @U.ge.off@@U.sep.on@@U.zu.off@	}Vr2 ;

LEXICON }Vr2
  ^1^}er+Noun+Masc		N_0n ;		! spielen => Spieler
  ^1^}er+Noun+Masc		}in ;		! spielen => Spielerin
  ^1^}erei+Noun+Fem		Nf_en ;		! spielen => Spielerei
  ^1^}erisch@C.un@+Adj	A_reg ;		! spielen => spielerisch

! ^1^}er+Noun+Masc%^}schaft+Noun+Fem	Nf_en ;	! spielen => Spilerschaft
