! -------------------------------------------------------------------------
! TYPE:		lexc
! CONTENT:	German morphology : numerals
!		(determiners, pronouns, prepositions, conjunctions ...)
!
! AUTHOR:	Anne Schiller, XRCE
! DATE:		21-Feb-2003
! -------------------------------------------------------------------------

 
! ==============================================================================
LEXICON Numerals

 [-]		Digital ;
 null		/Card1 ;
 null		/CardDeriv ;

 millionen+Num+Card		Num/Deriv ;
 millionstel		/Fract ;
 milliarden+Num+Card		Num/Deriv ;
 milliardstel		/Fract ;
 trillionen+Num+Card		Num/Deriv ;
 trillionstel		/Fract ;
 trilliarden+Num+Card		Num/Deriv ;
 trilliardstel		/Fract ;

! ==============================================================================
! Year Expressions, eg.1980 = "neunzehnhundertachzig"
! ==============================================================================
 elf^. 		Num-100 ;
 zwölf^.		Num-100 ;
 drei^.zehn^.	Num-100 ;
 vier^.zehn^.	Num-100 ;
 fünf^.zehn^.	Num-100 ;
 sech^.zehn^.	Num-100 ;
 sieb^.zehn^.	Num-100 ;
 acht^.zehn^.	Num-100 ;
 neun^.zehn^.	Num-100 ;

! ==============================================================================
! numbers form 1 to 999
! ==============================================================================
		Num1to999 ;

! ==============================================================================
! numbers from 1000 to 999999
! ==============================================================================
		T1to999 ;



! -----------------------------------------------------------------------------

! --------------------------------------------------------------------------
! number stems
! --------------------------------------------------------------------------

LEXICON Num1to9
 ein^.ein^.halb	/halb ;
 andert^.halb	/halb ;

! cardinals
 eins		/Card1Sg ;
 zwei		/Card+Flex ;
 drei		/Card+Flex ;
 vier		/Card ;
 fünf		/Card ;
 sechs		/Card ;
 sieben		/Card ;
 acht		/Card ;
 neun		/Card ;

! ordinals
 ein:erst	/Ord ;
 zwei:zweit	/Ord ;
 drei:dritt	/Ord ;
 vier:viert	/Ord ;
 fünf:fünft	/Ord ;
 sechs:sechst	/Ord ;
 sieben:siebt	/Ord ;
 sieben:siebent	/Ord ;
 acht		/Ord ;
 neun:neunt	/Ord ;

! fractals
 eintel		/Fract ;		! hundert%und%eintel ???
 zweitel	/Fract ;		! hundert%und%zweitel ???
 drittel	/Fract ;
 viertel	/Fract ;
 fünftel	/Fract ;
 sechstel	/Fract ;
 siebtel	/Fract ;
 siebentel	/Fract ;
 achtel		/Fract ;
 neuntel	/Fract ;

! derivations (-er, -fach, -teiler, ...)
 ein		/CardDeriv ;
 zwei		/CardDeriv ;
 drei		/CardDeriv ;
 vier		/CardDeriv ;
 fünf		/CardDeriv ;
 sechs		/CardDeriv ;
 sieben		/CardDeriv ;
 acht		/CardDeriv ;
 neun		/CardDeriv ;

LEXICON Num10to19
 zehn		/NumReg ;
 elf		/NumReg ;
 zwölf		/NumReg ;
 drei^.zehn	/NumReg ;
 vier^.zehn	/NumReg ;
 fünf^.zehn	/NumReg ;
 sech^.zehn	/NumReg ;
 sieb^.zehn	/NumReg ;
 acht^.zehn	/NumReg ;
 neun^.zehn	/NumReg ;

LEXICON Num-zig
 zwanzig	/NumRegS ;
 dreißig	/NumRegS ;
 vierzig	/NumRegS ;
 fünfzig	/NumRegS ;
 sechzig	/NumRegS ;
 siebzig	/NumRegS ;
 achtzig	/NumRegS ;
 neunzig	/NumRegS ;

LEXICON Num20to99
		Num-zig ;
 ein^.und	Num-zig ;
 zwei^.und^.	Num-zig ;
 drei^.und^.	Num-zig ;
 vier^.und^.	Num-zig ;
 fünf^.und^.	Num-zig ;
 sechs^.und^.	Num-zig ;
 sieben^.und^.	Num-zig ;
 acht^.und^.	Num-zig ;
 neun^.und^.	Num-zig ;


LEXICON Num1to99	Num1to9 ; Num10to19 ; Num20to99 ;


! --------------------------------------------------------------------------
LEXICON Num-100
 hundert	/NumRegS ;
 hundert^.	Num1to99 ;
 hundert^.und^.	Num1to99 ;

LEXICON Num100to999
		Num-100 ;
 ein^.		Num-100 ;
 zwei^.		Num-100 ;
 drei^.		Num-100 ;
 vier^.		Num-100 ;
 fünf^.		Num-100 ;
 sechs^.		Num-100 ;
 sieben^.	Num-100 ;
 acht^.		Num-100 ;
 neun^.		Num-100 ;

LEXICON Num1to999	Num1to99 ; Num100to999 ;

! --------------------------------------------------------------------------
! headings for "tausend"

LEXICON T1to999
		T1to99 ;
		T100to999 ;

LEXICON T1to99
		/tausend ;
 ein^.		/tausend ;
 zwei^.		/tausend ;
 drei^.		/tausend ;
 vier^.		/tausend ;
 fünf^.		/tausend ;
 sechs^.		/tausend ;
 sieben^.	/tausend ;
 acht^.		/tausend ;
 neun^.		/tausend ;
 zehn^.		/tausend ;
 elf^.		/tausend ;
 zwölf^.		/tausend ;
 dreizehn^.	/tausend ;
 vierzehn^.	/tausend ;
 fünfzehn^.	/tausend ;
 sechzehn^.	/tausend ;
 siebzehn^.	/tausend ;
 achtzehn^.	/tausend ;
 neunzehn^.	/tausend ;
		T20to99 ;

LEXICON T-zig
 zwanzig^.	/tausend ;
 dreißig^.	/tausend ;
 vierzig^.	/tausend ;
 fünfzig^.	/tausend ;
 sechzig^.	/tausend ;
 siebzig^.	/tausend ;
 achtzig^.	/tausend ;
 neunzig^.	/tausend ;

LEXICON T20to99
		T-zig ;
 ein^.und^.	T-zig ;
 zwei^.und^.	T-zig ;
 drei^.und^.	T-zig ;
 vier^.und^.	T-zig ;
 fünf^.und^.	T-zig ;
 sechs^.und^.	T-zig ;
 sieben^.und^.	T-zig ;
 acht^.und^.	T-zig ;
 neun^.und^.	T-zig ;

LEXICON T-100
 hundert^.	/tausend ;
 hundert^.	T1to99 ;
 hundert^.und^.	T1to99 ;

LEXICON T100to999
		T-100 ;
 ein^.		T-100 ;
 zwei^.		T-100 ;
 drei^.		T-100 ;
 vier^.		T-100 ;
 fünf^.		T-100 ;
 sechs^.		T-100 ;
 sieben^.	T-100 ;
 acht^.		T-100 ;
 neun^.		T-100 ;

LEXICON /tausend
 tausend	/NumRegS ;
 tausend^.	Num1to999 ;
 tausend^.und^.	Num1to999 ;





! --------------------------------------------------------------------------

LEXICON /Card1Sg	 
 +Num+Card+Sg:0				inv ;	! eins 
 +Num+Card^]+Noun+Fem			Nf=en ;	! eine Eins (no compound)

LEXICON /Card1	 
 +Num+Card+Pl:0				inv ;	! drei Kinder
 +Num+Card^]+Noun+Fem			Nf=en ;	! eine Drei (no compound)

LEXICON /Card-halb
 ^.ein^.halb				/halb ; ! dreieinhalb

LEXICON /Card	/Card1 ; /Card-halb ;

LEXICON /Card+Flex
 					/Card ;	 ! zwei Frauen
 000+Num+Card+Gen:^+er			inv ;	 ! zweier Frauen
 000+Num+Card+Dat:^+en			inv ;	 ! mit zweien

LEXICON /CardDeriv
 +Num+Card:0				Trunc ;
 +Num+Card:0				Num/Deriv ;
 +Num+Card:0				/Card-er ;

LEXICON /halb
 +Num+Card:0				inv ;
 +Num+Card:0				Trunc ;
 +Num+Card:0				Num/Deriv ;

LEXICON /Ord
 +Num+Ord+Pred				inv ;		! zu dritt
 +Num+Ord				NAdjFlex ;	! cpd: [Rang]-zweiter
 +Num+Ord^}ens+Adv			inv ;
 +Num+Ord				Trunc ;		! dritt- [beste]

LEXICON /Fract
 +Num+Fract				inv ;		! drittel
 +Num+Fract				Trunc ;		! drittel-
 +Num+Fract^]+Noun+Neut			N=0n ;		! das Drittel (no compound)

LEXICON /Card-er
 ^}er+Noun+Masc				N_0n ;		! der Dreier
 ^}er+Adj+Invar				inv ;		! die zwanziger Jahre
 ^}erlei+PAdj+Indef+Invar		inv ;		! dreierlei Dinge
 ^}erlei+PAdj+Indef+Invar+Pred		inv ;		! das ist dreierlei
 
LEXICON /NumReg
					/Card ;
					/CardDeriv ;
 0:t					/Ord ;
 tel					/Fract ;

LEXICON /NumRegS
					/Card ;
					/CardDeriv ;
 0:st					/Ord ;
 stel					/Fract ;



LEXICON Num/Deriv
 ^}fach+Adj			A_pos ;
 ^}mal+Adv			inv ;

! --------------------------------------------------------------------------
! digital compounds and derivations
! --------------------------------------------------------------------------
LEXICON Digital
 < [%0|1|2|3|4|5|6|7|8|9]+
   (%- [%0|1|2|3|4|5|6|7|8|9]+)
   %+Dig:0 >	Dig/Deriv ;	! 12-15jährige

LEXICON Dig/Deriv
 				Trunc ;
 < ("-") >			Num/Deriv ;
 < ("-") >			/Card-er ;




