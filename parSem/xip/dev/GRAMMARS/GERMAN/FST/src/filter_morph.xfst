# -------------------------------------------------------------------------
# Xerox Research Center Europe - Grenoble
#
# Copyright (C)  Xerox Corporation
#
# -------------------------------------------------------------------------
# TYPE:		(x)fst
# CONTENT:	mark some lemmas as "non-preferred" readings
# AUTHOR:	Anne Schiller
# DATE:		19-Jul-2005
# -------------------------------------------------------------------------
# USAGE: 	fst -l [ThisFile]
# -------------------------------------------------------------------------

# NOTE: add filter AFTER other mappings

# remove or mark depreciated readings

define BD	"#" |  "-" ;
define CPD	%+Cpd | BD ;
define PREF	?* BD ;

define NADJ		%+Noun %+NAdj | CPD;
define NINF	 	%+Noun %+NInf | CPD ;

define NOUN		%+Noun | CPD ;
define PROP		%+Prop | CPD ;
define VERB		%+Verb | CPD ;
define ADJ		%+Adj | CPD ;

		
define REMOVE [
	{ab=ordnen} 	NADJ		# Abgeordente
|	{an=stellen}	NADJ		# Angestellte
|	{vor=sitzen} 	NADJ		# Vorsitzende

|	{ein=kommen}	NINF		# Einkommen
|	{unter=nehmen}	NINF		# Unternehmen
|	{unternehmen}	NINF		# Unternehmen

|	{mit=Arbeiter}	NOUN		# Mitarbeiter
|	{her=Steller}	NOUN

];

define PREFER [
 	{Alternative}	NOUN		#
|	{Initiative}	NOUN		#
|	{lieben}	NADJ		# Geliebte(r)
];

define FILTER [
	{träge}		NADJ		# Träger
|	{forsch}	NADJ		# Forscher
|	{firm}		NADJ		# [die] Firmen
|	{spitz}		NADJ		# Spitze
|	{früh}		NADJ		# früher+Adv

|	{fremden}	NINF		# [den] Fremden
|	{kranken}	NINF		# [den] Kranken

|	{Bilder}	NOUN
|	{Leider}	NOUN
|	{Lage}		PROP

|	{heuen}		VERB		# heute+Adv
|	{fasen}		VERB		# fast+Adv
|	{einen}		VERB		# ein+Det

| 	{blauen}	VERB		# blau+Adj
| 	{grünen}	VERB		# grün+Adj
| 	{jungen}	VERB		# jung+Adj
| 	{frischen}	VERB		# frisch+Adj
| 	{langen}	VERB		# lang+Adj
|	{fremden}	VERB 		# fremd+Adj
|	{schnellen}	VERB		# schnell+Adj

|	{Datum}		CPD		# prefer plural "Daten"


];



read regex [

	%+Filter <- [..] 	|| .#. (PREF) $[FILTER] _ ? .#. 
.o.	%+Prefer <- [..] 	|| .#. (PREF) $[PREFER] _ ? .#. 	

.o.	~$[REMOVE]
];
