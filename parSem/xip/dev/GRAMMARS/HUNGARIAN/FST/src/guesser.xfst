# ============================================================================
# CONTENT:	(x)fst script for guesser FST (for POS tagger)
#
# LANGUAGE:	HUNGARIAN
# ENCODING:	UTF-8 [€ÁÖŐÚÜ]
#
# AUTHOR:	A. Schiller, A. Sandor
#
# CREATED:	08-Apr-1999
# UPDATED:	12-Jul-2010
#
# ============================================================================
# USAGE: 	xfst -l [ThisFile]
# ============================================================================


define UppChar	[A|Á|B|C|D|E|É|F|G|H|I|Í|J|K|L|M|N|O|P|Q|R|S|T|U|Ó|Ö|Ő|Ú|Ü|Ű|V|W|X|Y|Z] ;
define LowChar	[a|á|b|c|d|e|é|f|g|h|i|í|j|k|l|m|n|o|p|q|r|s|t|u|ó|ö|ő|ú|ü|ű|v|w|x|y|z|%'] ;
define Vowel	[a|á|e|é|i|í|o|ó|u|ú|ü|ű] ;
define Digit	[1|2|3|4|5|6|7|8|9|%0] ;

define Any	[UppChar|LowChar|Vowel|Digit] ;

read regex 
[ [ [ UppChar LowChar+ ]  ((%.) %- (UppChar) LowChar+) ((%.) %- Digit+) ] %+Prop:0 %+Sg:0 %+Nom:0 "+PROPN_NOM":0]
|
[ [ [ UppChar LowChar+ ]  ((%.) %-) (UppChar) Digit* ( (%.) %-) LowChar* t ] %+Prop:0 %+Sg:0 %+Acc:0 "+PROPN_ACC":0]
|
[ UppChar LowChar* Digit* ( (%.) %- ( UppChar ) LowChar* ) 
[ 
	[ n a k %+Prop:0 %+Sg:0 %+GenDat:0 "+PROPN_CASE":0 ] |		
	[ n e k	%+Prop:0 %+Sg:0 %+GenDat:0 "+PROPN_CASE":0 ] |	
	[ b a n	%+Prop:0 %+Sg:0 %+Ine:0 "+PROPN_CASE":0 ] |	
	[ b e n	%+Prop:0 %+Sg:0 %+Ine:0 "+PROPN_CASE":0 ] |
	[ b a %+Prop:0 %+Sg:0 %+Ill:0 "+PROPN_CASE":0 ] |
	[ b e %+Prop:0 %+Sg:0 %+Ill:0 "+PROPN_CASE":0 ] |
	[ b ó l	%+Prop:0 %+Sg:0 %+Ela:0 "+PROPN_CASE":0 ] |
	[ b ő l	%+Prop:0 %+Sg:0 %+Ela:0 "+PROPN_CASE":0 ] |
	[ [a|á|e|é|i|í|o|ó|ö|ő|u|ú|ü|ű] n %+Prop:0 %+Sg:0 %+Sup:0 "+PROPN_CASE":0 ] |
	[ r a %+Prop:0 %+Sg:0 %+Sub:0 "+PROPN_CASE":0 ] |
	[ r e %+Prop:0 %+Sg:0 %+Sub:0 "+PROPN_CASE":0 ] |
	[ r ó l	%+Prop:0 %+Sg:0 %+Del:0 "+PROPN_CASE":0 ] |
	[ r ő l %+Prop:0 %+Sg:0 %+Del:0 "+PROPN_CASE":0 ] |
	[ n á l %+Prop:0 %+Sg:0 %+Ade:0 "+PROPN_CASE":0 ] |
	[ n é l	%+Prop:0 %+Sg:0 %+Ade:0 "+PROPN_CASE":0 ] |
	[ h o z	%+Prop:0 %+Sg:0 %+All:0 "+PROPN_CASE":0 ] |
	[ h e z	%+Prop:0 %+Sg:0 %+All:0 "+PROPN_CASE":0 ] |
	[ h ö z	%+Prop:0 %+Sg:0 %+All:0 "+PROPN_CASE":0 ] |
	[ t ó l	%+Prop:0 %+Sg:0 %+Abl:0 "+PROPN_CASE":0 ] |
	[ t ő l	%+Prop:0 %+Sg:0 %+Abl:0 "+PROPN_CASE":0 ] |
	[ i g	%+Prop:0 %+Sg:0 %+Term:0 "+PROPN_CASE":0 ] |
	[ k é n t %+Prop:0 %+Sg:0 %+EssF:0 "+PROPN_CASE":0 ] |
	[ k i n t %+Prop:0 %+Sg:0 %+EssF:0 "+PROPN_CASE":0 ] |
	[ v é %+Prop:0 %+Sg:0 %+Tra:0 "+PROPN_CASE":0 ] |
	[ v á %+Prop:0 %+Sg:0 %+Tra:0 "+PROPN_CASE":0 ] |
	[ v a l	%+Prop:0 %+Sg:0 %+Ins:0 "+PROPN_CASE":0 ] |
	[ v e l	%+Prop:0 %+Sg:0 %+Ins:0 "+PROPN_CASE":0 ] |
	[ é r t	 %+Prop:0 %+Sg:0 %+CausFin:0 "+PROPN_CASE":0 ] 
]	 
]
|
[ UppChar %. %+Prop:0 %+Sg:0 %+Nom:0 "+PROPN_NOM":0 ]
|
[ UppChar LowChar+ %- (a|e|ö) t %+Prop:0 %+Sg:0 %+Acc:0 "+PROPN_ACC":0 ]
|
[ LowChar+ [ {ista} | [ (%-) i (v) ] | {ikus} ]  %+NAdj:0 %+Sg:0 %+Nom:0 "+NADJ_NOM":0 ]
[ LowChar+ [ {isták} | [i[e|a]k] ]  %+NAdj:0 %+Pl:0 %+Nom:0 "+NADJ_NOM":0 ]
|
[ LowChar+  (%- LowChar+) %+Noun:0 %+Sg:0 %+Nom:0 "+NOUN_NOM":0 ]
|
[ LowChar+ [ú|ű|ó|ő] ((a|e) k ) %+NAdj:0 %+Pl:0 %+Nom:0 "+NADJ_NOM":0 ]
|
[ LowChar+ b b ([a|e] k) %+NAdj:0 %+Pl:0 %+Nom:0 "+NADJ_NOM":0 ]
|
[ UppChar LowChar+ %- (n y) i ((a|e) k ) %+NAdj:0 %+Pl:0 %+Nom:0 "+NADJ_NOM":0 ]
|
[ UppChar+ (%- [LowChar+|UppChar+]) %+Prop:0 %+Sg:0 %+Nom:0 "+PROPN_NOM":0 ]
|
[ [Any]+ %- f é l e %+NAdj:0 %+Sg:0 %+Nom:0 "+NADJ_NOM":0 ]
|
[ %- LowChar+ %+Suffix:0 "+SUFF":0]
|
[ Digit* [%.|%,]* Digit+ %- LowChar+ %+Dig:0 "+NUMBER":0 ]
|
[ [ UppChar | LowChar ] [ %) | %/ ]  ( %- LowChar+ ) %+List:0 "+LS":0 ]
|
[ [ Digit+ " " Digit+ ]+ ( %- LowChar+ ) %+Dig:0 "+NUMBER":0 ]
|
# added by A. Schiller:
[ Any+ %- %+CpdPart:0 "+TRUNC":0 ]
;

