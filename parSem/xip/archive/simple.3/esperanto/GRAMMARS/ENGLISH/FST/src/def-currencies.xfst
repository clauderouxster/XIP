# ============================================================================
# CONTENT:      Currency names
# 
# LANGUAGE:     ENGLISH
#
# AUTHOR:       A. Schiller
#
# CREATED:      16-Mar-2006
# ============================================================================
# USAGE:        xfst -l [ThisFile]
# ============================================================================

define NEW	{new } ;
define NVO	{nuevo} 0:s { } ;
define SEP	{-} | { } ; 

# D-mark, deutsch mark, German mark etc.
define GER	[ ({East }|{West }) {German } | {D-} | {deutsch} ((e) { }) ];

# prefixes for dinar 
define DNR	[ {Algerian} | {Bahreini} | {Serbian} | {Iraqi} | {Jordanian}
		| {Kuwaiti} | {Libyan} | {Sudanese} | {Tunisian} | {Yemeni}
		| {Yugoslawian} ] { } ;

# prefixes for dollar 
define DLR	[ {Australian} | {Barbados} | {Bermuda} | {Bermudian} | {Brunei}
		| {Bahamian} | {Belize} | {Canadian} | {Fiji} | {Guyana} 
		| {Hong Kong} | {Jamaican} | {Cayman Islands} | {Liberian}
		| {Namibian} | {New Zealand} | {Solomon Islands} | {Singapore}
		| {Surinam} | {Trinidad and Tobago} | (NEW) {Taiwan} | {US}
		| {East Caribbean} | {Zimbabwe} | {Tuvaluan} 
		] { } ;


# prefixes for franc
define FRN	[ {Burundian} | {Congolese} | {Swiss} | {Djibouti} | {Guinea} 
		| {Comoro} | {Rwanda} | {Mali} | {Malagasy} 
		| {CFA} | {UIC} | {CFP} 
		| {Belgian} | {French} | {Luxembourg} 
		] { } ;

# inflection of name "real" 
define REAL	[ {real}:[{réis}|{reais}]	# Portuguese
		| {real} 0:s			# English spelling
		];


define CURRNAMES [
# singular : plural

  0:{li} {sente}		# LSL	Lesotho, 100 lisente = 1 loti
| 0:{ma} {loti}			# LSL	Lesotho, Maloti
| (DLR) {dollar} 0:s		# USD	United States of America, Dollars (and more ...)
| (DNR) {dinar} 0:s		# DZD	Algeria, Algeria Dinars (and more ...)
| (GER) {mark} (0:s)		# DEM	Germany, Deutsche Mark 
| (FRN) {franc} 0:s		# CHF	Switzerland, Francs  (and more ...)
| (NEW) {ban} 0:i		# RON	Romania, 100 bani = 1 Leu
| (NEW) {kwanza} (0:s)		# AOA	Angola, Kwanza
| (NEW) {leu}:{lei}		# RON	Romania, New Lei
| (NEW) {lira} 0:s 		# TRY	Turkey, New Lira
| (NEW) {shekel} 0:s		# ILS	Israel, New Shekels
| (NEW) {shekel}:{shekalim}	# ILS	Israel, New Shekels
| (NEW) {sheqel} 0:s		# ILS	Israel, New Shekels
| (NEW) {sheqel}:{sheqalim}	# ILS	Israel, New Shekels
| (NEW) {zloty} 0:s		# PLN	Poland, Zloty
| (NEW|NVO) {peso} 0:s		# ARS	Argentina, Pesos (and more ...)
| (NEW) {sol} (0:e) 0:s		# PEN	Peru, New Sol
| (NVO) {sol} 0:{es}		# PEN	Peru, Nuevos Soles
| ({Brazil }) REAL		# BRL	Brazil, Brazil Real
| {D-Mark} (0:s)		# DEM	Germany, Deutsche Mark 
| {afghani} 0:s			# AFN	Afghanistan, Afghanis
| {agor} a:{ot}			# ILS	Israel, 100 agorot = 1 shekel
| {agur} a:{ot}			# ILS	Israel, 100 agorot = 1 shekel
| {ariary}			# MGA	Madagascar, Ariary
| {avo} 0:s			# MOP	Macau, 100 avos = 1 Pataca
| {baht} (0:s)			# THB	Thailand, Baht
| {baisa} 0:s			# OMR	Oman, 100 baisas = 1 Rial
| {baiza} 0:s			# OMR	Oman, 100 baizas = 1 Rial
| {balboa} 0:s			# PAB	Panama, Balboa
| {birr} (0:s)			# ETB	Ethiopia, Birr
| {bolivar} (0:e) 0:s		# VEB	Venezuela, Bolivares
| {boliviano} 0:s		# BOB	Bolivia, Bolivianos
| {bolívar} 0:{es}		# VEB	Venezuela, Bolivares
| {butut} (0:s)			# GMD	Gambia, 100 butut = 1 Dalasi
| {cedi} (0:s)			# GHC	Ghana, Cedis
| {centas}:[{centai}|{centu}]	# LTL	Lithuania, 100 centu = 1 Litas
| {centavo} 0:s			# ARS	Argentina, 100 centavos = 1 Peso
| {centesimo} 0:s		# ESP	Spain, 100 centesimos =  1 Peseta
| {centesim} o:i		# ITL	Italy, 100 centesimi = 1 Lira 
| {centime} 0:s			# CHF	Switzerland, 100 centimes = 1 Franc
| {cent} 0:s			# USD	United States of America, 100 cents = 1 Dollar
| {centésimo} 0:s		# PAB	Panama Spain, 100 centésimos =  1 balboa
| {chetrum} (0:s)		# BTN	Bhutan, 100 chetrum = Ngultrum
| {chiao}			# CNY	China, 10 jiao/chiao = 1 yuan
| {chon}			# KPW	Korea, 100 chon = 1 Won
| {colon} 0:s			# CRC	Costa Rica, Colones (and more ...)
| {colón}:{colones}		# CRC	Costa Rica, Colones (and more ...)
| {copeck} 0:s			# RUB	Russia, 100 kopecks = 1 ruble
| {cordoba} 0:s			# NIO	Nicaragua, Cordobas
| {céntimo} 0:s			# CRC	Costa Rica, 100 céntimos = 1 colón
| {cêntimo} 0:s			# STD	São Tome and Principe, 100 cêntimos = 1 dobra
| {córdoba} 0:s			# NIO	Nicaragua, Cordobas
| {dalasi} (0:s)		# GMD	Gambia, Dalasi
| {denar} 0:s			# MKD	Macedonia, Denars
| {deni}			# MKD	Macedonia, 100 deni = 1 Denar
| {diram} 0:s			# TJS	Tajikistan, 100 dirams = 1 Somoni
| {dirham} 0:s			# AED	United Arab Emirates, Dirhams (and more ...)
| {dobra} 0:s			# STD	São Tome and Principe, Dobras
| {dong} (0:s)			# VND	Viet Nam, Dong
| {drachma} [0:e|0:s]		# GRD	Greece, Drachma 
| {dram} 0:s			# AMD	Armenia, Drams
| {dông} (0:s)			# VND	Viet Nam, Dong
| {escudo} 0:s			# CVE	Cape Verde, Escudos
| {euro} 0:s			# EUR	Euro Member Countries, Euro
| {eyrir}:{aurar}		# ISK	Iceland, 100 aurar = 1 Króna
| {feninga}			# BAM	Bosnia and Herzegovina, 1 marka = 100 feninga
| {fen}				# CNY	China, 10 fen = 1 jian/chian
| {fillér} 			# HUF	Hungary, 100 filér =  1 Forint
| {fils}			# BHD	Bahrain, 100 fils = 1 Dinar
| {finmark} 0:s			# FIM	Finland, Markka 
| {florin} 0:s			# NLG	The Netherlands, Guilder (and more ...)
| {forint} 			# HUF	Hungary, Forint
| {franco} 0:s			# CHF	Switzerland, Francs  (Italian)
| {franc} 0:s			# CHF	Switzerland, Francs  (and more ...)
| {franken}			# CHF	Switzerland, Francs  (German)
| {gourde} 0:s			# HTG	Haiti, Gourdes
| {groschen}			# ATS	Austria, 100 groschen = 1 Schilling  
| {grosz} 0:y			# PLN	Poland, 100 groszy = 1 Zloty
| {guarani} (0:e) 0:s		# PYG	Paraguay, Guarani
| {guaraní} 0:{es}		# PYG	Paraguay, Guarani
| {guilder} 0:s			# NLG	The Netherlands, Guilder (and more ...)
| {gulden} (0:s)		# ANG	Netherlands Antilles, Guilders
| {halala} 0:s			# SAR	Saudi Arabia, 100 halala = 1 Riyal
| {haler} [0:s|0:u]		# CZK	Czech Republic, 1 haliers/heller = 1 Koruna
| {halier} [0:s|0:u]		# CZK	Czech Republic, 1 haliers/heller = 1 Koruna
| {hallalah} 0:s		# SAR	Saudi Arabia, 100 halala = 1 Riyal
| {hao} (0:s)			# VND	Viet Nam, 10 hao = 1 Dong
| {hao} (0:s)			# VND	Viet Nam, 10 hào = 1 Dong
| {heller} (0:s)		# CZK	Czech Republic, 1 haliers/heller = 1 Koruna
| {hryvna} (0:s)		# UAH	Ukraine, Hryvnia
| {hryvnia} (0:s)		# UAH	Ukraine, Hryvnia
| {hào} (0:s)			# VND	Viet Nam, 10 hào = 1 Dong
| {iraimbilanja}		# MGA	Madagascar, 5 iraimbilanja = 1 Ariary
| {jeon}			# KPW	Korea, 100 chon/jeon = 1 Won
| {jiao}			# CNY	China, 10 jiao/chiao = 1 yuan
| {kapiejka} 0:s		# BYR	Belarus, 100 kapiejkas = 1 rouble
| {khoum} 0:s			# MRO	Mauritania, 5 khoums = 1 ouguiya
| {kina} 0:s			# PGK	Papua New Guinea, Kina
| {kip} (0:s)			# LAK	Laos, Kips
| {kobo} (0:s)			# NGN	Nigeria, 100 kobo = 1 Naira
| {kopeck} 0:s			# RUB	Russia, 100 kopecks = 1 ruble
| {kopek} 0:s			# RUB	Russia, 100 kopecks = 1 ruble
| {kopiyka} (0:s)		# UAH	Ukraine, 100 kopiykas = 1 Hryvnia
| {koron} [a 0:s | a:y |a:0]	# CZK	Czech Republic, Koruny
| {korun} [a 0:s | a:y |a:0]	# CZK	Czech Republic, Koruny
| {krona}:{kronor}		# SEK	Sweden, Kronor
| {krone} 0:r			# DKK	Denmark, Kroner (and more ...)
| {kron} a:{ur}			# ISK	Iceland, Kronur
| {kroon} [0:i|0:s]		# EEK	Estonia, Krooni
| {krón} a:{ur}			# ISK	Iceland, Kronur
| {kuna} (0:s)			# HRK	Croatia, Kuna
| {kurus}			# TRY	Turkey, 100 kurus = 1 Lira
| {kuruş}			# TRY	Turkey, 100 kurus = 1 Lira
| {kwacha}			# ZMK	Zambia, Kwacha
| {kwacha} 0:s			# MWK	Malawi, Kwachas
| {kyat} 0:s			# MMK	Myanmar (Burma), Kyats
| {laari} (0:s)			# MVR	Maldives, 100 lari/laari = 1 Rufiyaa
| {laree} (0:s)			# MVR	Maldives, 100 lari/laari = 1 Rufiyaa ?
| {lari}			# GEL	Georgia, Lari
| {lari} (0:s)			# MVR	Maldives, 100 lari/laari = 1 Rufiyaa
| {lats}:{lati}			# LVL	Latvia, Lati
| {lek} 0:e			# ALL	Albania, Leke
| {lempira} 0:s			# HNL	Honduras, Lempiras
| {leone} 0:s			# SLL	Sierra Leone, Leone
| {lepton}:{lepta}		# GRD	Greece, 100 lepta = 1 Drachma 
| {lev} 0:a			# BGN	Bulgaria, Leva
| {lipa} (0:s)			# HRK	Croatia, 100 lipa = 1 Kuna
| {lira}:{liri}			# MTL	Malta, Liri
| {lir} a:e			# ITL	Italy, Lira 
| {litas}:[{litai}|{litu}]	# LTL	Lithuania, Litai
| {li}:{ema} {langeni}		# SZL	Swaziland, Emalangeni
| {luma}			# AMD	Armenia, 100 luma = 1 Dram
| {lwei} (0:s)			# AOA	Angola, 100 lwei kwanza
| {manat} (0:s)			# TMM	Turkmenistan, Manats
| {marka}			# BAM	Bosnia and Herzegovina, Convertible Marka
| {markka} 0:a			# FIM	Finland, Markka 
| {metica} l:{is}		# MZM	Mozambique, Meticais
| {millieme} 0:s		# EGP	Egypt, 1000 milliemes = 1 Pound
| {millime} 0:s			# TND	Tunisia, 1000 millimes = 1 Dinar
| {mongo} 0:s			# MNT	Mongolia, 100 mongos = 1 Tugrik
| {naira} 0:s			# NGN	Nigeria, Nairas
| {nakfa} (0:s)			# ERN	Eritrea, Nakfa
| {ngultrum} 0:s		# BTN	Bhutan, Ngultrum
| {ngwee}			# ZMK	Zambia, 100 ngwee = 1 Kwacha
| {ouguiya} 0:s			# MRO	Mauritania, Ouguiyas
| {oyra}			#	1 Faroese króna = 1 oyra
| {pa'anga} (0:s)		# TOP	Tonga, Pa'anga
| {pais} [a:e|a 0:s]		# BDT	Bangladesh, 100 poisha/paise = 1 Taka
| {para} 0:s			# CSD	Serbia, 100 paras = 1 Dinar
| {pataca} 0:s			# MOP	Macau, Patacas
| {penni} [0:ä|0:s]		# FIM	Finland, 100 penniä = 1 Markka 
| {penny}:[{pence}|{pennies}]	# GBP	United Kingdom, 100 pence = 1 Pound
| {peseta} 0:s			# ESP	Spain, Peseta 
| {pesewa} (0:s)		# GHC	Ghana, 100 pesewas = 1 Cedi
| {pfennig} [0:e|0:s]		# DEM	Germany, 100 pfennige = 1 Deutsche Mark 
| {piaster} 0:s			# EGP	Egypt, 100 piasters = 1 Pound
| {piastre} 0:s			# LBP	Lebanon, 100 piastres = 1 Pound
| {pice}			# BDT	Bangladesh, 100 poisha/paise = Taka
| {piso} 0:s			# PHP	Philippines, Pesos
| {poisha}			# BDT	Bangladesh, 100 poisha/paisa = Taka
| {pound} 0:s ({ sterling})	# GBP	United Kingdom, Pounds
| {pula} 0:s			# BWP	Botswana, Pulas
| {pul} [0:s|0:i]		# AFN	Afghanistan, 1 Afghani = 100 puls
| {punt} 0:s			# IEP	Ireland, Pound/Punt
| {pya} 0:s			# MMK	Myanmar (Burma), 100 pyas = 1 Kyat
| {qepik} (0:s) 		# AZN 	Azerbaijan, 100 qepik =	1 manat
| {qintar}:{qindarka} 		# ALL	Albania, 100 qindarka = 1 Lek
| {qirsh}:{qirush}		# JOD 	Jordan, 100 qirush = 1 Dinar
| {quetzal} (0:e) 0:s		# GTQ	Guatemala, Quetzales
| {qursh}:{qurush}		# JOD 	Jordan, 100 qirush = 1 Dinar
| {rand}			# ZAR	South Africa, Rand
| {rappen}			# CHF	Switzerland, 100 rappen = 1 Franken  (German)
| {rial} 0:s			# IRR	Iran, Rials
| {rial} 0:s SEP {omani}	# OMR	Oman, Rials
| {riel} 0:s			# KHR	Cambodia, Riels
| {ringgit} 0:s			# MYR	Malaysia, Ringgits
| {riyal} 0:s			# SAR	Saudi Arabia, Riyals
| {riyal} 0:s SEP {omani}	# OMR	Oman, Rials
| {rouble} 0:s			# RUB	Russia, Rubles
| {ruble} 0:s			# RUB	Russia, Rubles
| {rufiyaa}			# MVR	Maldives, Rufiyaa
| {rupee} 0:s			# INR	India, Rupees
| {rupiah} (0:s)		# IDR	Indonesia, Rupiahs
| {santim}			# ETB	Ethiopia, 100 santim = 1 Birr	
| {santim} [s | s:i]		# LVL	Latvia, 100 santims = 1 Lats
| {satang} (0:s)		# THB	Thailand, 100 satang = 1 baht
| {scellino} (0:s)		# SOS	Somalia, Shillings (Italian)
| {schilling} (0:s)		# ATS	Austria, Schilling  
| {sene}			# WST	Samoa, 100 sene = 1 Tala
| {seniti}			# TOP	Tonga, 100 seniti = 1 Pa'anga
| {sentimo} (0:s)		# PHP	Philippines, 100 centavos/sentimo = 1 piso
| {sent} [0:i]			# EEK	Estonia, 100 senti = 1 Kroon
| {sen}				# JPY	Japan, 100 sen = 1 Yen
| {shilingi} (0:s)		# TZS	Tanzania, Shillings
| {shilling} (0:s)		# SOS	Somalia, Shillings
| {shilling} 0:s		# KES	Kenya, Shillings (and more ...)
| {somoni}			# TJS	Tajikistan, Somoni
| {som} 0:s			# KGS	Kyrgyzstan, Soms
| {soum} (0:s)			# UZS	Uzbekistan, Soms
| {sterling pound} 0:s		# GBP	United Kingdom, Pounds];
| {stotink} a:i			# BGN	Bulgaria, 100 stotinki = 1 Lev
| {stotin} [0:s|0:{ov}|0:i]	# SIT	Slovenia, 100 stotinov = 1 Tolar
| {sucre} 0:s			# ECS	Ecuador, Sucre
| {sum} (0:s)			# UZS	Uzbekistan, Soms
| {taka}			# BDT	Bangladesh, Taka
| {tala}			# WST	Samoa, Tala
| {tambala} (0:s)		# MWK	Malawi, 100 tambala = kwacha
| {tenge} (0:s)			# KZT	Kazakhstan, Tenge
| {tetri} 			# GEL	Georgia, 100 tetri = 1 Lari
| {thebe}			# BWP	Botswana, 100 thebe = 1 Pula
| {tijn} (0:s)			# KZT	Kazakhstan, 100 tiyin/tijn = 1 Tenge
| {tiyin} (0:s)			# KZT	Kazakhstan, 100 tiyin/tijn = 1 Tenge
| {toea} 0:s			# PGK	Papua New Guinea, 100 toeas = 1 Kina
| {tolar} 0:s			# SIT	Slovenia, Tolars
| {tughrik} 0:s			# MNT	Mongolia, Tugriks
| {tugrik} 0:s			# MNT	Mongolia, Tugriks
| {tugrug} 0:s			# MNT	Mongolia, Tugriks
| {tukhrik} 0:s			# MNT	Mongolia, Tugriks
| {vatu} (0:s)			# VUV	Vanuatu, Vatu
| {won}				# KPW	Korea, Won
| {yen}				# JPY	Japan, Yen
| {yuan}			# CNY	China, Yuan Renminbi
| {złot} [y:e | y 0:{ch} ]	# PLN	Poland, Zloty
| {öre}				# SEK	Sweden, 100 öre = 1 Krona
| {øre}				# DKK	Denmark, 100 øre = 1 Krone

| {unidad} 0:{es} { de formento}	# CLF	Chile, Unidades de formento (Funds code)
| {unidad} 0:{es} { de valor real}	# COU 	Columbia, Unidad de Valor Real
];


# (colloquial) names for coins
define COINNAMES [
  {dime} 0:s			# United Stat 10-cent coin
| {nickel} 0:s			# United States five-cent coin
| {quid} 0:s			# British slang for one pound sterling
| {yo-yo} 0:s			# slang word in Ireland for euro
| {buck} 0:s			# slang term for a dollar
| {greenback} 0:s		# nickname for the U.S. dollar bill
| {tuppence} (0:s)		# = two pence
| {twopence} (0:s)		# British 2 pence
];

# old names

define CURROLD [
  {abazi} (0:s)			# currency of Georgia until 1833
| {anna} 0:s			# formerly used in India = 1/16 rupee.
| {austral} 0:{es}		# basic monetary unit of Argentina 1985-91
| {crown} 0:s			# former English coin (and other countries...)
| (REAL { }) {cruzeiro} 0:s	# the monetary unit of Brazil 1942-86 and 1990-93
| {cruzeiro} 0:s ({ } REAL)	# the monetary unit of Brazil 1942-86 and 1990-93
| {dinero} 0:s			# former silver coin of Peru
| {ekwele} 0:s			# currency of Equatorial Guinea 1975-85. 			
| {ekuele} 0:s			# currency of Equatorial Guinea 1975-85. 
| {fanam} 0:s			# currency issued by the Madras Presidency until 1815
| {farthing} 0:s		# old British coin valued one quarter of a penny
| {grosh}			# a small silver coin issued by a number of countries
| {inti} (0:s)			# currency in Peru 1985-91
| {karbovanets} 0:i		# unit of currency in the Ukraine 1917-20, 1942-45, 1992-96.
| {luigin} o:i			# currency issued 1994-96 in Italian city Seborga
| {mohar} 0:s			# currency of Nepal until 1932
| {mohru} 0:s			# former name of nepalese rupee 
| {quid} 0:s			
| {pruta}:{prutot}		# currency in Israel prior to 1960
| {real} (0:e) (0:s)		# former monetary unit and coin of Spain and its possessions
| {somal} [o 0:s| o:i]		# currency of Italian Somaliland between 1950 and 1962
| {syli} 0:s			# currency of Guinea between 1971 and 1985
| {thaler}			# former silver coin used throughout Europe
| {zaïre} 0:s			# Congo's former currency
| {zaire} 0:s			# Congo's former currency
| {li}:{ma} {kuta}		# Congo's former currency : 1 zaire = 100 makuta
];


define CURRALL [
  CURRNAMES.u 		%+Noun:0 %+Curr:0 %+Sg:0 
| CURRNAMES 		%+Noun:0 %+Curr:0 %+Pl:0 
| COINNAMES.u 		%+Noun:0 %+Curr:0 %+Sg:0 
| COINNAMES		%+Noun:0 %+Curr:0 %+Pl:0 
| CURROLD.u 		%+Noun:0 %+Curr:0 %+Sg:0 
| CURROLD		%+Noun:0 %+Curr:0 %+Pl:0 
];


# NOTE: capitalized variants should be treated by normalizer

# ----------------------------------------------------------------------------------------------
# add genitive 's
#	dollar's 	-> dollar's
# 	dollars's 	-> dollars'
#	
define GENRULE [
	s -> 0 || [ s | ş | x | z ] %+Gen %' _ 
.o.	%+Gen -> 0
];

 
read regex [
	CURRALL | [ [ CURRALL %+Gen 0:{'s} ] .o. GENRULE ]
];
