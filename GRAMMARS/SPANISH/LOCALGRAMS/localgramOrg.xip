//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2008 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   
//!   File   : localgramOrg.xip
//!
//!   Content:	definition of compounds and local rules for geographi
//!   		entities
//!
//!   Language:	SPANISH
//! ENCODING:	ISO-LATIN-1
//!
//!   Date       : August 2008
//!
//!   Author     : Enrique Cabrero Caroline Hagege 
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Sequence:


// Veo Televisión - cadena de television espanola
5> noun[mwe=+,company=+] @= ?[surface:"Veo"]; ?[surface:"VEO"], ?[surface:"Televisión"] .


// Cadena SER
5> noun[mwe=+,company=+] @= ?[surface:"Cadena"], ?[maj:+]; ?[lemma:"%d+"], ?*[maj:+] .

// Radio Madrid
5> noun[mwe=+,company=+] @= ?[surface:"Radio"], ?[location,maj:+] .


// EL PAIS
5> noun[mwe=+,company=+] @= ?[surface:"EL"], ?[surface:"PAÍS"] | ?[toutmaj:~] | .

// The NY Times
5> noun[mwe=+,company=+] @= (?[surface:"The"]), ?[surface:"New York"]; ?[surface:"NY"], ?[surface:"Times"], (?[surface:"Magazine"]) .

// A company name followed by hifen followed by place is a company name
// Mahou-San Miguel
5> noun[mwe=+,company=+] @= ?[company:+,src:~guess, propag:~], punct[lemma:"-"], ?[location] .



// Rule for organization names (companies)

// e.g. La Juanita SL
10> noun[mwe=+,company=+] @= | ?[maj:~] | ?[maj,art], (?[maj,art:~];conj[coord])+, ?[sufix_company] .
10> noun[mwe=+,company=+] @= | | ?[maj,art], (?[maj,art:~];conj[coord])+, ?[sufix_company] .


// e.g. Toto SA - Orascom Telecom
10> noun[mwe=+,company=+] @= (?[maj,art:~];conj[coord])+, ?[sufix_company].


// Compañia Colombiana Automotriz
10> noun[mwe=+,company=+] @= ?[prefix_company], (?[prep];art;?[maj])*, ?[maj] .


// Diario de Noticias- Diario 123 ....
20> noun[mwe=+,company=+] @= noun[lemma:diario,maj], num. 
20> noun[mwe=+,company=+] @= noun[lemma:diario,maj], (prep[lemma:de];?[maj])*, ?[maj] .

// El Tiempo - constraint on following word in order to avoid to take as newspaper
// El Tiempo en el mundo (statistically meteo rather than newspaper)
20> noun[mwe=+,company=+] @= ?[lemma:"el",maj], ?[lemma:tiempo,maj] | ?[lemma:~en] | .


// Ediciones Zeta
20> noun[mwe=+,company=+] @= noun[lemma:"edición",pl:+,maj:+], (?[maj];art;prep[lemma:de])*, ?[maj] .





// Fuerzas Aereas + demonym
15> noun[mwe=+,org=+] @= noun[surface:"Fuerzas Aéreas"], ?[nationality] .
15> noun[mwe=+,org=+] @= noun[surface:"Fuerzas Aéreas"], prep[lemma:de], ?[country] .


// Ejército de Country
15> noun[mwe=+,org=+] @= noun[lemma:ejército,maj], prep[lemma:de], ?[country] .

// Iglesia Catolica, Protestana etc.
15> noun[mwe=+,org=+] @= noun[lemma:iglesia,maj], (prep[lemma:de]), ?+[maj] .
// Completar ...

// Scotland Yard
15> noun[mwe=+,org=+] @= ?[surface:"Scotland"], ?[surface:"Yard"] .

// Organizacion Internacional del Trabajo.
15> noun[mwe=+,org=+] @= noun[lemma:"organización"];noun[lemma:fundación];noun[lemma:sociedad,maj], (?[maj];prep[lemma:de];art)*, ?[maj] .

// Instituto Nacional de XXXX
15> noun[mwe=+,org=+] @= noun[lemma:instituto,maj], (?[maj]; prep[lemma:de]; art)*, ?[maj] .

// Centro para el Desarollo Industrial
15> noun[mwe=+,org=+] @= noun[lemma:centro,maj], (?[maj]; prep; art)*, ?[maj] .

// Alianza del Pueblo para la Democracia
15> noun[mwe=+,org=+] @= noun[lemma:"Alianza",maj], (?[maj,human:~,company:~]; prep; art)*, ?[maj,human:~,company:~] .
//15> noun[mwe=+,org=+] @= noun[lemma:"Alianza",maj], ( (prep), (art), ?[maj,human:~,company:~])+ .
// Deberia funcionar pero no funciona 

// Seguridad Social
15> noun[mwe=+,org=+] @= noun[lemma:seguridad, maj], adj[lemma:social] .

// Cortes de XXX
15> noun[mwe=+,org=+] @= noun[lemma:Cortes], (prep[lemma:de];art;?[maj])*, ?[maj:+] .

// Corte Internacioanl de Justicia
15> noun[mwe=+,org=+] @= noun[lemma:corte,maj], (prep[lemma:de];art;?[maj])*, ?[maj:+] .


// Tribunal Constitucional
15> noun[mwe=+,org=+] @= noun[surface:Tribunal], ?[surface:Constitucional], (prep[lemma:de], ?[location]);(?[nationality]).

// Tribunal Superior;Internacional de Justicia
15> noun[mwe=+,org=+] @= noun[surface:Tribunal], adj[maj], prep[lemma:de], noun[surface:Justicia] .

// Departamento de Estado
15> noun[mwe=+,org=+] @= noun[lemma:departamento,maj], prep[lemma:de], noun[surface:Estado].


// Juzgado XXXX
15> noun[mwe=+,org=+] @= noun[lemma:juzgado,maj], (prep[lemma:de];art;?[maj])*, ?[maj:+] .

// Fondo Comun de Inversiones
15> noun[mwe=+,org=+] @= noun[lemma:fondo,maj], (prep[lemma:de]; art; ?[maj])*, ?[maj:+] .


// Unión Europea, Africana, X Mundial ....
15> noun[mwe=+,org=+] @= noun[lemma:"unión", maj], (prep[lemma:de];art;?[maj])*, ?[maj:+] .

// Ministerio;Secretaría;Congreso del Interior; de Relaciones Exteriores, de Economia etc.
15> noun[mwe=+,org=+] @= noun[lemma:"ministerio",maj];noun[lemma:secretaría,maj];noun[lemma:"subsecretaría",maj];noun[lemma:congreso,maj], (prep[lemma:de];art[def];?[maj];conj[lemma:"y"])*, ?[maj] .
// integrar articulo


// Fiscalía General/Procuradoría General
15> noun[mwe=+,org=+] @= noun[lemma:"fiscalía"];noun[lemma:"procuradoría"], (?[maj];prep[lemma:de];art)*, ?[maj] .

// Cámara baja/de Representantes/de los Comunes/alta
15> noun[mwe=+,org=+] @= noun[lemma:"cámara",maj], adj[lemma:alto];adj[lemma:bajo] .
15> noun[mwe=+,org=+] @= noun[lemma:"cámara",maj], (adj[nationality]), prep[lemma:de], (prep[lemma:de];art;?[maj])*, ?[maj] .

// Servicio de Impuestos Internos/de Nacional de Aduana
15> noun[mwe=+,org=+] @= noun[lemma:"servicio",maj], (adj[lemma:"%+al(es)"]), (prep[lemma:de];?[maj])*, ?[maj].


// FC Barcelona - Sevilla FC
15> noun[mwe=+,org=+] @= ?[surface:"FC"], ?[location] .
15> noun[mwe=+,org=+] @= ?[location], ?[surface:"FC"] .

// Real Madrid
15> noun[mwe=+,org=+] @= ?[surface:"Real"], ?[surface:Madrid] .

// Manchester United
15> noun[mwe=+,org=+] @= ?[surface:"Manchester"], ?[surface:"United"] .

// Olympique de Marseille;Marsella
15> noun[mwe=+,org=+] @= ?[surface:"Olympique"], prep[lemma:de], ?[surface:"Marseille"];noun[lemma:"Marsella"] .

// Ajax de Amsterdam
15> noun[mwe=+,org=+] @= ?[surface:"Ajax"], prep[lemma:de], noun[lemma:"Amsterdam"] .


// Universidad Complutense de Madrid/ La Escuela Técnica Superior
15> noun[mwe=+,org=+] @= noun[school,maj], (?[maj]), (prep;?[maj];art[def])*, ?[maj] .
// Universidad Carlos III
15> noun[mwe=+,org=+] @= noun[school,maj], noun[human:+,mwe:+] .

// Partido Comunista
15> noun[mwe=+,org=+] @= noun[lemma:"partido",maj], (prep[lemma:de];art;?[maj])*, ?[maj,lemma:~"político"] .

// Democracia Christiana
15> noun[mwe=+,org=+] @= noun[surface:"Democracia"], ?[surface:"Cristiana"] .

// Coalición Canaria/Andalucista etc.
15> noun[mwe=+,org=+] @= noun[lemma:"coalición",maj], ?[maj:+] .

// Confederación de Trabajadores del Cobre
15> noun[mwe=+, org=+] @= noun[lemma:"confederación",maj], (adj[lemma:"%+al"]), (prep[lemma:de];?[maj];art[def])*, ?[maj].


// Izquierda Asturiana;Andaluza;Castellana
15> noun[mwe=+,org=+] @=  noun[lemma:"izquierda",maj], ?[nationality:+] .

// Junta Asesora de Inteligencia Exterior
// Junta Aragonesa - Junta de Galicia
15> noun[mwe=+,org=+] @= noun[lemma:junta,maj], (?[maj];prep[lemma:de])*, ?[maj] .

// Chunta Arragonesista. 
15> noun[mwe=+,org=+] @=  noun[surface:"Chunta"], ?[maj] .

// Xunta de Galicia
15> noun[mwe=+,org=+] @= noun[surface:"Xunta"], prep[lemma:de], ?[location,maj] .


// Ayuntamiento de XXXX
15> noun[mwe=+,org=+] @= noun[lemma:"ayuntamiento"],prep[lemma:de], ?[location,maj] .

// Nueva Canarias (partido)
15> noun[mwe=+,org=+] @= ?[surface:"Nueva"], ?[surface:"Canarias"] .



// Asamblea Legislativa de(l) XXX
15> noun[mwe=+,org=+] @= noun[lemma:"asamblea",maj], (adj[maj]), (prep[lemma:de];art;?[maj])*, ?[maj] .

// Asamblea nacional
// When the adjective following asamblea is in lower case
15> noun[mwe=+,org=+] @= noun[lemma:"asamblea",maj], adj .

// Movimiento;Comité Internacional de XXX
15> noun[mwe=+,org=+] @ = noun[lemma:"movimiento",maj];?[surface:"Comité"], (?[maj]), (prep;art;?[maj])*, ?[maj].

// Frente de Libertación (Nacional)
15> noun[mwe=+,org=+] @= noun[lemma:"frente", maj], (adj[maj]), prep[lemma:de];prep[lemma:para], (prep[lemma:de];art[def];?[maj])*, ?[maj] .

// Cruz Roja Argentina / Cruz Roja Española Colmenar Viejo
// Cruz Roja esta presente en el léxico fst.
15> noun[mwe=+,org=+] @= noun[lemma:"Cruz Roja"], ?+[maj] .


// Asociación/Fundación XXXX
// Asociación Astronómica Israeli
15> noun[mwe=+,org=+] @= noun[lemma:"asociación",maj];noun[surface:"Fundación"], (?[maj]), (prep[lemma:de];art;?[maj])*, ?[maj] .

// Dirección XXXX
15> noun[mwe=+,org=+] @= noun[lemma:"dirección",maj], (?[maj]), (prep[lemma:de];art;?[maj])*, ?[maj] .

// Federación XXXX
15> noun[mwe=+,org=+] @= noun[lemma:"federación",maj], (prep[lemma:de];art;?[maj])*, ?[maj] .




// Aministía Internacional
20> noun[mwe=+,org=+] @= ?[lemma:"amnistía",maj], ?[lemma:internacional,maj] .

// Autoridad Nacional Palestina
20> noun[mwe=+,org=+] @= noun[lemma:autoridad,maj], adj[lemma:nacional,maj], ?[surface:Palestina] .

// Administracion (de) Bush, Gobierno (de) Zapatero
20> noun[mwe=+,org=+] @= noun[introd_org], (prep[lemma:de]), ?[human] .

// Gobierno Vasco - Gobierno de Bolivia
20> noun[mwe=+,org=+] = noun[introd_org,maj:+], ?[nationality] .
20> noun[mwe=+,org=+] = noun[introd_org,maj:+], prep[lemma:de], noun[maj, location:+] .

// Policía del Valle
20> noun[mwe=+,org=+] @= noun[lemma:policía,maj], prep[lemma:de], (art), ?+[maj] .

// Policía Municipal;Militar;Regional etc.
// Policía Nacional Civil
20> noun[mwe=+,org=+] @= noun[lemma:policía,maj], adj+[maj] .


// Aduana;Guardia (nacional) de XXX
20> noun[mwe=+,org=+] @= noun[lemma:aduana,maj];noun[lemma:guardia,maj], (adj[maj]), (prep[lemma:de];art[def],?[maj])*, ?[maj] .

// Consejo de la Orden ; Consejo Administrativo para la Reforma ...
20> noun[mwe=+,org=+] @= noun[lemma:consejo,maj], (adj[maj]), (prep[lemma:de];art;?[maj])*, ?[maj], (?[nationality:+]) .

// Banco (central) de Francia
20> noun[mwe=+,org=+] @= noun[lemma:banco,maj], (adj[maj,lemma:"%+al"]), prep[lemma:de], noun[location,maj].

// Banco Central (de Reserva)
20> noun[mwe=+,org=+] @= noun[lemma:banco,maj], adj[maj,lemma:"%+al"], (prep[lemma:de], ?[maj]) .



// Reglas contextuales

// El presidente de RTVE/ del Gobierno 
// El presidente del Gobierno español
// EN COURS - rajouter les gentiliques
30> ? @= | noun[lemma:presidente];noun[lemma:funcionario];noun[lemma:trabajador];noun[lemma:gerente], prep[lemma:de], (art) | ?[maj,start_org=+], (prep[lemma:de]), ?*[end_org=+,prep:~,art:~] .

// La revista América Economía
// e.g. el holding (eléctrico) CGE, la aseguradora ABS
30> ? @= | noun[introd_company,common,maj:~], (adj[maj:~]), (?[nationality]) | ?[maj,start_org=+], ?*[maj,end_org=+] .



// Higher level rules
// Elements which are between brackets after a recognized NE are NE of the same type 
// works for ORG
// e.g. Movimiento al Socialismo (MAS) -> ORG(MAS)
40> ? @= | noun[mwe:+,org], punct[fopar] | ?[maj:+,org=+,acron=+] | punct[fcpar] | .

// When an acronym recognized as an organization is followed by the extended name between brackets
// e.g. el holding CGE (Compañia General de Electricidad)
40> ? @= | ?[toutmaj,org], punct[fopar] | ?[maj:+,toutmaj:~,start_org=+], ?*[maj,toutmaj:~,end_org=+] | punct[fcpar] | .



 
