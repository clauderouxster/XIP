//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Séquence:

/la radio d'Etat\
2> noun[mwe=+, preorg=+] = noun[preorg, !sg:!, !masc:!, !fem:!], prep[form:fde], noun[lemme:état, maj].

/la chaîne de télévision\
2> noun[mwe=+, preorg=+, fem=+] = noun[lemme:chaîne, !sg:!], prep[form:fde], noun[lemme:télévision];noun[lemme:TV].

/les Brigades Rouges\
2> noun[mwe=+, org=+, fem=+, pl=+] = noun[lemme:brigade, pl, !maj:!], ?[lemme:rouge, pl].

/Comité de soutien à François Kolber\
2> noun[mwe=+, org=+, masc=+, sg=+] = noun[lemme:comité, sg, !maj:!], prep[form:fde], noun[lemme:soutien], (prep[form:fde], adj*, noun, (adj)), prep[form:fa], noun+[maj].

/les Forces armées\
2> noun[mwe=+, org1pl=+, orgadj=+, anton=+, maj=+] = noun[lemme:force, pl, maj], adj[lemme:armé, pl, fem];noun[lemme:armée, pl].

/le département d'Etat\
2> noun[mwe=+, org=+, masc=+, sg=+] = noun[lemme:département, sg, !maj:!], prep[form:fde], noun[lemme:état, maj].

  
marqueur:
/marquage d'éléments qui peuvent faire partie de noms d'organisations\

4> noun[lemme:parlement,org=+,pol=+], ?[nat].

4> noun[preorg, maj, toutmaj:~, org1=+].

4> noun[org2maj, maj, phrasemaj:~, org2=+].
4> noun[firme2maj, maj, phrasemaj:~, firme2=+].

4> noun[lemme:force, pl, maj, org1pl=+, anton=+, orgadj=+].

4> noun[lemme:fabricant, prefirme=~], (adj), (prep[form:fde], noun[maj:~], (adj)), noun[person].
4> noun[person], punct[form:fcm], (det), noun[lemme:fabricant, prefirme=~].

/comité de soutien\
4> noun[lemme:comité, org1=~], prep[form:fde], noun[lemme:soutien].

/Générale\
4> noun[lemme:général, maj, sg, fem, firme1=+].

/Assurances\
4> noun[lemme:assurance, maj, pl, firme2=+].

/Le Boeing\
4> det, noun[nondef, firme=~, org=~,prevehic=+].
4> prep[def], noun[nondef, firme=~, org=~].

/un Zodiac\
4> det[indef], noun[firme:+, firme=~, org=~].

/une 205 Peugeot\
4> det[indef], num, noun[firme=~, org=~].

/le Sénat\
4> ?[def], noun[majorg, maj, org=+].

/du Monde\
4> ?[def], noun[orgdef, maj, org=+].

/Solidarité\
4> ?[def:~], noun[lemme:solidarité, maj, org=+], ?[adj:~, maj:~].


/le président de Baudoin\
//5> noun[preorgde, persorg:~], prep[form:fde], (det), (adj[maj:~]), noun[maj, phrasemaj:~, org=+], ?[adj:~, maj:~, ang:~].
5> noun[preorgde], prep[form:fde], (det), (adj[maj:~]), noun[maj, phrasemaj:~, nonorg:~, person:~, org1:~, lieu:~, firme1pl:~, org=+], ?[adj:~, maj:~, ang:~].
5> noun[preorgde], prep[form:fde], (det), (adj[maj:~]), noun[maj, phrasemaj:~, person:~, org1:~, firme1pl:~, org=~], prep[form:fde], ?[maj].

/le Parti fédéraliste flamand Volksunie\
5> noun[orgadj, maj:~], (adj)+, noun[maj, phrasemaj:~, person:~, lieu:~, org=+, nonorg2=+].

/le parti Socialiste juif Bund\
5> noun[parti, nonorg=+], (adj)*, (adj[maj];adj[postprof];adj[nat])+, (adj)*, noun[maj, phrasemaj:~, person:~, lieu:~, org=+, nonorg2=+], ?[maj:~];?[person].
5> noun[parti, nonorg=+], (adj)*, (adj[maj];adj[postprof];adj[nat])+, (adj)*, noun[maj, phrasemaj:~, person:~, lieu:~], ?[maj, person:~].


/le PDG de Baudoin ; une filiale britannique de Baudoin\
5> noun[prefirmede], (adj), prep[form:fde], (det[def], (adj[maj:~])), noun[maj, phrasemaj:~, preorg:~, prefirme:~, batimentorg1:~, batimentorg:~, org:~, nonorg:~, firme=+, pays:~,ville:~].

/l'association X\
3> ?[def], (adj[maj:~]), noun[preorgpers, preorg=+, sansde=+], (adj[nat]), noun[maj, phrasemaj:~, person:~, lieu:~].
4> ?[def], (adj[maj:~]), noun[preorg, maj:~];noun[preorg, toutmaj], (adj), noun[maj, phrasemaj:~, person:+, persorg=+], ?[maj:~].
5> ?[def], (adj[maj:~]), noun[preorg, maj:~];noun[preorg, toutmaj], (adj), noun[maj, person:~,phrasemaj:~, lieu:~, org=+], ?[maj:~, adj:~].
/l'association Clair de Terre\
6> ?[def], (adj[maj:~]), noun[preorg, maj:~];noun[preorg, toutmaj], (adj), noun[maj, phrasemaj:~,person:~, persorg:~, org=~], prep[form:fde], noun[maj].

/le magazine britannique, Tatler\
5> ?[def], (adj[maj:~]), noun[preorg, maj:~];noun[preorg, toutmaj], (adj[nat]), punct[form:fcm], (punct[form:fquotes]), noun[maj, phrasemaj:~, persorg:~, person:~, org=+], (punct[form:fquotes]), punct.


/les associations X, Y et Z\
5> ?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), noun[maj, phrasemaj:~, org=+], punct[form:fcm]; coord.

6> ?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~], (noun[maj])*, (adj)*, punct[form:fcm], noun[maj, phrasemaj:~, org=+], punct[form:fcm];coord.

7> ?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~], (noun[maj])*, (adj)*, punct[form:fcm], noun[maj, org=+], punct[form:fcm];coord.

8> ?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~], (noun[maj]), (punct[form:fcm], (det), noun[maj, phrasemaj:~, org=+], (noun[maj]))*, coord, noun[maj, phrasemaj:~, org=+], ?[maj:~].


/le président de leur compagnie Pierre Eelsen \
5> noun[preorgde, prof], prep[form:fde], ?[def];det[poss], (adj[maj:~]), noun[prefirme, sg], (adj), (prep[form:fde], noun[maj:~]), (adj[nat]), (prep[form:fde], noun[maj:~]), noun[maj, phrasemaj:~, person, persorg=+], ?[maj:~].

/l'entreprise X\
5> ?[def], (adj[maj:~]), noun[prefirme, sg], (adj), (prep[form:fde], noun[maj:~]), (adj[nat]), (prep[form:fde], noun[maj:~]), noun[maj, phrasemaj:~, org:~, persorg:~, firme=+, orgpers=+, orglieu=+,nat:~,person:~], ?[maj:~].
5> det[poss], (adj[maj:~]), noun[prefirme, sg], (adj), (prep[form:fde], noun[maj:~]), (adj[nat]), (prep[form:fde], noun[maj:~]), noun[maj, phrasemaj:~, org:~, persorg:~, firme=+, orgpers=+, orglieu=+], ?[maj:~].
6> ?[def], (adj[maj:~]), noun[prefirme, sg], (adj), (prep[form:fde], noun[maj:~]), (adj[nat]), (prep[form:fde], noun[maj:~]), noun[maj, phrasemaj:~, org:~, persorg:~, firme=~, orgpers=~, orglieu=~], coord;?[ang], ?[maj].

/un représentant de la banque, David Paulombi.\
4> noun[prof, sg], prep[form:fde], det[def], (adj[maj:~]), noun[prefirme, maj:~], (adj[nat]);adj, punct[form:fcm], (punct[form:fquotes]), noun[maj, phrasemaj:~, person, persorg=+], (punct[form:fquotes]), punct.
4> noun[prof, sg], prep[form:fde, def], (adj[maj:~]), noun[prefirme, maj:~], (adj[nat]);adj, punct[form:fcm], (punct[form:fquotes]), noun[maj, phrasemaj:~, person, persorg=+], (punct[form:fquotes]), punct.

/le fabricant britannique, Tatler\
5> ?[def], (adj[maj:~]), noun[prefirme, sansvirg:~, maj:~], (adj[nat]);adj, punct[form:fcm], (punct[form:fquotes]), noun[maj, phrasemaj:~, persorg:~, firme=+], (punct[form:fquotes]), punct.


/les compagnies X, Y et Z\
//5> ?[def, pl], (adj[maj:~]), noun[prefirme, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~, org:~, firme=+], ((det), noun[maj]), (punct[form:fcm], (det), noun[maj, phrasemaj:~, org:~, firme=+], (noun[maj]))+, coord, (det), noun[maj, phrasemaj:~, org:~, firme=+].

5> ?[def, pl], (adj[maj:~]), noun[prefirme, pl], (adj), (adj[nat]), noun[maj, phrasemaj:~, org:~, firme=+], punct[form:fcm]; coord.

6> ?[def, pl], (adj[maj:~]), noun[prefirme, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~], (noun[maj])*, (adj)*, punct[form:fcm], noun[maj, phrasemaj:~, org:~, firme=+], punct[form:fcm];coord.

7> ?[def, pl], (adj[maj:~]), noun[prefirme, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~], (noun[maj])*, (adj)*, punct[form:fcm], noun[maj, org:~, firme=+], punct[form:fcm];coord.

8> ?[def, pl], (adj[maj:~]), noun[prefirme, pl], (adj), (adj[nat]), (det), noun[maj, phrasemaj:~], (noun[maj]), (punct[form:fcm], (det), noun[maj, phrasemaj:~, org:~, firme=+], (noun[maj]))*, coord, noun[maj, phrasemaj:~, firme=+], ?[maj:~].



/les actions Darty\
5> noun[prefirmepl, pl], (adj),  noun[maj, phrasemaj:~, firme=+].
5> prep[form:fpar], noun[prefirmeindef, sg], (adj),  noun[maj, phrasemaj:~, firme=+].
5> det[indef], noun[prefirmeindef, sg], (adj),  noun[maj, phrasemaj:~, firme=+].



séquence:
/le Monde (de l'Education): déjà fait quand guillemets dans localgram_entit.xip\
10> noun[org=+, mwe=+] @= det[def], (punct[form:fquotes]), noun[orgdef, maj,!div:!], (?[lemonde]), (prep[form:fde], (det[def]), noun[lemonde]), (punct[form:fquotes]).
10> noun[org=+, firme=+, mwe=+] @= |?[def], (punct[form:fquotes])| noun[orgdef, maj], prep[form:fde], (det[def]), noun[lemonde].
10> noun[org=+, firme=+, mwe=+] @= |?[def], (punct[form:fquotes])| noun[orgdef, maj], ?[lemonde].

/la Poste\

10>  noun[org=+, mwe=+] @=det[def,fem,lemme:"le"], noun[maj,lemme:"poste"].

/l'Internationale socialiste, la IVe Internationale ouvrière\
10> noun[org=+, mwe=+, fem=+, sg=+] = ?[def], (num;adj[ord]), noun[maj, lemme:international, fem, sg], ?[lemme:ouvrier, fem, sg];?[lemme:socialiste, fem, sg].

/la Croix-Rouge Internationale\
10> noun[org=+, mwe=+, fem=+] = noun[lemme:"Croix-Rouge", !maj:!, !sg:!], ?[nat];?[lemme:international].

/la Communauté européenne\
10> noun[org=+, mwe=+, fem=+] = noun[lemme:"communauté", !maj:!, !sg:!], ?[lemme:européen, fem] |?[form:~fde]|.

/le Jihad islamique plaestinien\
10> noun[org=+, mwe=+, fem=+, sg=+] @= | det[fem, sg] |  noun[org, !maj:!, fem, sg], (adj[nat, fem, sg];adj[postprof, fem, sg])+.

10> noun[org=+, mwe=+, masc=+, sg=+] @= |det[masc, sg];prep[form:fde,det:+] |  noun[org, !maj:!, masc, sg], (adj[nat, masc, sg];adj[postprof, masc, sg])+.


/l'Eymet Cricket Club\
//10> noun[org=+, maj=+, mwe=+] @= |?[def], (adj[maj:~])| (?[maj, phrasemaj:~])+, noun[org2], (prep[form:fde];?[of], (noun[maj])+, (?[ang]), noun[maj]).
//10> noun[org=+, maj=+, mwe=+] @= ?[maj, phrasemaj:~, start], (?[maj, phrasemaj:~])*, noun[org2], (prep[form:fde];?[of], (noun[maj])+, (?[ang]), noun[maj]).

10> noun[org=+, maj=+, mwe=+] @= ?[maj, phrasemaj:~], (?[maj, phrasemaj:~])*, noun[org2],( prep[form:fde];?[of] , noun*[maj], (?[ang]), noun[maj] ).

10> noun[org=+, maj=+, mwe=+,sport=+] @=  noun[lemme:"FC"];noun[lemme:"AJ"];noun[lemme:"YUC"];noun[lemme:"FAP"], (noun[sport]),  (prep[form:fde]), noun[ville].



10> noun[org=+, maj=+, mwe=+] @= ?[maj, phrasemaj:~,!sport:!], (?[maj, phrasemaj:~])*, noun[org2], (prep[form:fde];?[of]) .
/la National Westminster Bank\

10> noun[firme=+, maj=+] @= noun[maj, phrasemaj:~];noun[toutmaj, phrasemaj:~];noun[maj, phrasemaj:~, inconnu] , (?[maj, phrasemaj:~])*, (noun[firme2,non-profit:~];adj[firme2])+.

10> noun[non-profit=+, maj=+,org=+] @= noun[maj, phrasemaj:~];noun[toutmaj, phrasemaj:~];noun[maj, phrasemaj:~, inconnu] , (?[maj, phrasemaj:~])*, noun[firme2,non-profit].

/l'Institute of National Finance\
10> noun[org=+, maj=+, mwe=+] @= noun[org1, ang, maj], (noun[maj]), (?[ang]), (noun[maj,finevent:~])+.


/Pacific FM\
10> noun[org=+, maj=+, mwe=+] @= (?[maj, phrasemaj:~])+, noun[org2, sansdef].

/le Special Air Service\
10> noun[org=+, maj=+, mwe=+] = |?[def], (adj[maj:~])| (?[maj, phrasemaj:~])+, noun[org2maj, maj].

/le secrétaire de l'Union locale CGT\
10> noun[org=+, maj=+, mwe=+] @= |noun[preorgde], prep[form:fde], (det[def])| (adj), (noun[maj, phrasemaj:~, org:~, org1:~, firme1pl:~, person:~, nonorg:~, lieu:~])+, (adj)*, (?[ang]), (noun[maj, person:~])*, (?[ang]), (noun[maj, person:~])*, (prep, (det), (adj),  noun[maj, person:~]), (adj)*, (noun[maj, person:~])*.
/le directeur de Paris VI \
10> noun[org=+, maj=+, mwe=+] @= |noun[preorgde], prep[form:fde], (det[def])| (adj), (noun[maj, phrasemaj:~, ville])+, num[rom].
/le secrétaire de France Plus\
10> noun[org=+, maj=+, mwe=+] @= |noun[preorgde], prep[form:fde]| (adj), (noun[maj, phrasemaj:~, org:~, org1:~, firme1pl:~, person:~, nonorg:~,person:~])+, (adj)*, (?[ang]), (noun[maj,person:~])+, (?[ang]), (noun[maj,person:~])*, (prep, (det), (adj),  noun[maj,person:~]), (adj)*, (noun[maj,person:~])*.

/Paris X \
10> noun[batimentorg=+, sg=+] @= noun[ville, maj], (punct[form:fhyph]), num[rom] |?[noun:~, adj:~]|.

/du Comité central\
//10> noun[org=+, maj=+, mwe=+] @= |noun[preorgde], prep[form:fde], (det[def])| noun[maj, phrasemaj:~], adj, (prep, (det), (adj),  noun[maj]).

/le responsable de la Nouvelle action royaliste\
10> noun[org=+, maj=+, mwe=+] @= |noun[preorgde], prep[form:fde], (det[def])| adj[maj, phrasemaj:~], (noun)+, (adj)*, (prep, (det), (adj),  noun[maj]).

/le PDG de X Y\
10> noun[firme=+, maj=+, mwe=+] @= |noun[prefirmede], prep[form:fde], (det[def])| noun[maj, phrasemaj:~], noun[maj, person:~], (noun[maj, person:~])*.

/filiale commune de Ford et de V W\
40> noun[firme=+, maj=+, mwe=+] @= |noun[prefirmede], (adj), prep[form:fde], (det[def], (adj[maj:~])), noun[maj, phrasemaj:~, firme:+], coord, prep[form:fde]| (det), (adj)*,  (noun[maj, preorg:~, prefirme:~, batimentorg1:~])+.


Séquence:
/***** construction directe avec un nom *****\

/les éditions Londreys\
10> noun[firme=+, maj=+, mwe=+] @= noun[firme1pl, pl, !maj:!, !masc:!, !fem:!], (prep[form:fde]), (det[def]), (adj)*, (noun[maj, person:~])+, (adj)*.

/sa société, la Cefaldi,\
10> noun[firme=+, maj=+, mwe=+] @= |det[poss];det[indef], (adj[maj:~]), noun[prefirme, maj:~], (adj[nat]), punct[form:fcm]| (det), noun[maj, phrasemaj:~, org:~, firme:~, person:~], (noun[maj])*.


/******* construction en 'de' *****\
/l'Union de défense des commerçants et artisans\
20> noun[org=+, maj=+, sg=+, mwe=+] @= |?[def];punct| noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (noun[maj, person:~])*, (adj[sg];noun[postprof])*, (coord, adj[sg]);(coord, noun[postprof]),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person])+, (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]))*,
	prep[form:fde, pl], (adj;noun[postprof])*, (noun[person:~])+, coord, noun, (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person:~])+, (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]))*.

/la même en début de phrase\
20> noun[org=+, maj=+, sg=+, mwe=+] @= noun[org1, start, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (noun[maj, person:~])*, (adj[sg];noun[postprof])*, (coord, adj[sg]);(coord, noun[postprof]),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person:~])+, (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]))+,
	(coord, (prep[form:fde], (det[def])), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), noun+[person:~, prof:~], (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof])),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person:~])+, (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]))*.

/règle-test\
/20> noun[org=+, maj=+, sg=+, mwe=+] @= |?[def];punct| noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (noun[maj, person:~])*, (adj[sg];noun[postprof])*,
	(prep[form:fde], (noun[person:~])+, (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof])).
\

/l'Association des Aveugles de France\
20> noun[org=+, maj=+, sg=+, mwe=+] @= |?[def];punct| noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (noun[maj, person:~])*, (adj[sg];noun[postprof])*, (coord, adj[sg]);(coord, noun[postprof]),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person:~, date:~])+, (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]))+,
	(coord, (prep[form:fde], (det[def])), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), noun+[person:~, prof:~, date:~], (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof])),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person:~, date:~])+, (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]))*.

/la même en début de phrase\
20> noun[org=+, maj=+, sg=+, mwe=+] @= noun[org1, start, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (noun[maj, person:~])*, (adj[sg];noun[postprof])*, (coord, adj[sg]);(coord, noun[postprof]),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person:~, date:~])+, (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]))+,
	(coord, (prep[form:fde], (det[def])), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), noun+[person:~, date:~, prof:~], (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof])),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person:~, date:~])+, (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]))*.

/les Forces de défense israéliennes\
20> noun[org=+, maj=+, pl=+, mwe=+] @= |?[def];punct| noun[org1pl, sansde:~, maj, phrasemaj:~, pl, !masc:!, !fem:!], (noun[maj, person:~])*, (adj[pl];noun[postprof])*, (coord, adj[pl]);(coord, noun[postprof]),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person:~])+, (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]))+,
	(coord, (prep[form:fde], (det[def])), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), noun+[person:~, prof:~], (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof])),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person:~])+, (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]))*.


/la "Fédération française d'athlétisme"\	
20> noun[org=+, maj=+, sg=+, mwe=+] @= |?[def], punct[form:fquotes]| noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (adj[sg];noun[postprof])*, (coord, adj[sg];noun[postprof]),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj;noun[postprof]), (noun[person:~])+, (adj;noun[postprof])*, (coord, adj;noun[postprof]))+,
	(coord, (prep[form:fde], (det[def])), (adj;noun[postprof])*, (coord, adj;noun[postprof]), noun+[person:~, prof:~], (adj;noun[postprof])*, (coord, adj;noun[postprof])),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj;noun[postprof]), (noun[person:~])+, (adj;noun[postprof])*, (coord, adj;noun[postprof]))* |punct[form:fquotes]|.

/un Office des Cartels pointilleux\
20> noun[org=+, maj=+, sg=+, mwe=+] @= noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (adj[sg];noun[postprof])*, (coord, adj[sg]);(coord, noun[postprof]),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person:~])+, (adj;noun[postprof]), (adj;noun[prof:~]))+,
	(coord, (prep[form:fde], (det[def])), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), noun+[person:~, prof:~], (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof])),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person:~])+, (adj[nat];adj[postprof];noun[postprof])*, (coord, adj[nat]);(coord, adj[postprof]);(coord, noun[prof:~]))+.

/
20> noun[org=+, maj=+, sg=+, mwe=+] @= noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (adj[sg];noun[postprof])*, (coord, adj[sg];noun[postprof]),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj;noun[postprof]), (noun[person:~])+, (adj;noun[postprof]), (adj;noun[prof:~]))+,
	(coord, (prep[form:fde]), (det[def]), (adj;noun[postprof])*, (coord, adj;noun[postprof]), noun+[person:~, prof:~], (adj[nat];adj[postprof];noun[postprof]), (coord, adj[nat];adj[postprof];noun[postprof]))+.
\
/Confédération africaine d'athlétisme amateur\
20> noun[org=+, maj=+, sg=+, mwe=+] @= noun[org1, sansde:~, maj, phrasemaj:~, sg, !masc:!, !fem:!], (adj[sg];noun[postprof])*, (coord, adj[sg]);(coord, noun[postprof]),
	(prep[form:fde], (det[def]), (adj;noun[postprof])*, (coord, adj);(coord, noun[postprof]), (noun[person:~])+, (adj[postprof];noun[postprof]), (adj[nat];adj[postprof];noun[prof:~]), (coord, adj[postprof]);(coord, noun[postprof]);(coord, adj[nat]))+.

/L'OFFICE NATIONAL DE L'IMMIGRATION\
20> noun[org=+, maj=+, sg=+, mwe=+] @= noun[org1, sansde:~, phrasemaj:+, sg, !masc:!, !fem:!], (adj[sg, nat];adj[postprof, sg];noun[postprof, sg])*, (coord, adj[postprof, sg];noun[postprof]),
	(prep[form:fde], (det[def]), (noun[person:~, org1:~])+, (adj[postprof];noun[postprof];adj[nat])*, (adj[postprof];noun[postprof];adj[nat]))+,
	(coord, (prep[form:fde]), (det[def]), noun+[person:~, prof:~, org1:~], (adj[nat];adj[postprof];noun[postprof]), (coord, adj[nat];adj[postprof];noun[postprof]))*.

/le ministère de l'Education nationale\
20> noun[org=+, mwe=+, sg=+] @= noun[org1, sansde:~, sansmaj, sg, !masc:!, !fem:!], (adj[sg, nat];adj[lemme:"international"];adj[lemme:"international"]), prep[form:fde], (det[def]), (noun[person:~, lemme:~base])+, (adj)*, (adj, coord, adj), (prep[form:fde], (det[def]), (noun)+, (adj)*, (adj)), (coord, prep[form:fde], (det[def]), (noun)+, (adj)*, (adj)), (adj).

/les ministères de la Justice et de l'Intérieur\
20> noun[org=+, mwe=+, pl=+] @= noun[org1, sansde:~, sansmaj, pl, !masc:!, !fem:!], prep[form:fde], (det[def]), (noun)+, (adj)*, coord, prep[form:fde], (det[def]), (noun)+, (adj)*, (adj).

/la Fédération française d'athlétisme\
//20> noun[org=+, maj=+, mwe=+, sg=+] @= noun[org1, sansde:~, maj, sg, !masc:!, !fem:!], (adj[sg])+, (coord, adj[sg]), prep[form:fde], (det), (adj)*, (coord, adj)*, noun[person:~], (adj)*, (adj)), (noun[person:~, org:~], (adj)*, (adj)).


/le parti des Travailleurs\
20> noun[org=+, mwe=+, masc=+, sg=+] @= |?[def], (adj[maj:~])| noun[parti, sg, !maj:!], prep[form:fde], (det[def]), (adj[maj]), noun[maj, nat:~, person:~], (adj), (coord, (prep), (det[def]), (adj[maj]), (noun[maj])*, noun[maj]).

/le mouvement des jeunes Giscardiens\
20> noun[org=+, mwe=+, masc=+, sg=+] @= |?[def], (adj[maj:~])| noun[lemme:mouvement, !maj:!, sg], prep[form:fde, pl], (adj), (noun), noun[maj, nat:~, person:~];adj[maj, nat:~, person:~], (adj), (coord), (prep, (det[def])), (adj[maj]), (noun[maj])*.

/Assurances Générales de France\
20> noun[org=+, mwe=+, masc=+, sg=+] @= noun[firme1, maj, phrasemaj:~], (?[maj])*, prep[form:fde], noun+[maj, person:~].

/la Générale Occidentale\
20> noun[org=+, mwe=+, masc=+, sg=+] @= noun[firme1, maj, phrasemaj:~], (?[maj])*, noun+[maj, person:~].

/ l'ambassade de France\
20> noun[org=+, mwe=+, masc=+, sg=+,pol=+] @= noun[lemme:"ambassade",sg], prep[form:fde], noun[pays]. 



/********** construction en 'pour' **********\
/le Comité pour la libération des prisonniers politiques arabes et du Proche-Orient\
20> noun[org=+, mwe=+, sg=+, maj=+] @= noun[org1, orgpour, org1de:~, sg,!masc:!, !fem:!], (adj;noun[maj]), prep[form:fpour];prep[form:fcontre], (det[def]), (noun)+, (adj)*, (prep[form:fde], (det[def]), (adj), (noun)+, (adj),  (adj)), (coord, (prep), (det[def]), (noun)+, (adj), (adj)), (prep[form:fen];prep[form:fa, det], noun[lieu]).

/la Fondation pour les Etudes de Défense Nationale\
20> noun[batimentorg=+, mwe=+, sg=+, maj=+] @= noun[batimentorg1, orgpour, org1de:~, sg, !masc:!, !fem:!], (adj;noun[maj]), prep[form:fpour];prep[form:fcontre], (det[def]), (noun)+, (adj)*, (prep[form:fde], (det[def]), (adj), (noun)+, (adj),  (adj)), (coord, (prep), (det[def]), (noun)+, (adj), (adj)).

/************* construction avec des adjectifs uniquement *********\
/le Parti fédéraliste flamand X Y\
20> noun[org=+, mwe=+] @= |noun[orgadj, maj], (adj)+| noun[maj, phrasemaj:~, person:~, lieu:~], noun[maj, person:~, lieu:~].

/le Parti démocrate français\
21> noun[org=+, maj=+, mwe=+, sg=+] @= noun[orgadj, maj, sg, !masc:!, !fem:!, nonorg:~], (adj[sg, postprof];adj[sg, nat];noun[postprof])+, (coord, adj[sg, postprof];adj[sg, nat]).

/les Forces armées angolaises\
20> noun[org=+, mwe=+] @= noun[org1pl, !maj:!, orgadj, !masc:!, !fem:!], (adj[pl, nat];adj[pl, postprof];noun[postprof])+, (coord, adj[pl, nat];adj[pl, postprof]).

/le parti Socialiste juif Bund\
20> noun[org=+, mwe=+] @= |noun[parti, !maj:!, !sg:!, !masc:!], (adj[masc, sg])*, (adj[maj, masc, sg])+, (adj[masc, sg])*| noun[maj, phrasemaj:~, person:~, lieu:~], noun[maj, phrasemaj:~, person:~, lieu:~].

/le parti intégriste Hezbe-i-Islami\
20> noun[org=+, mwe=+, nonorg2=+] @= |?[def], (adj[masc, sg])*, noun[parti, !maj:!, !sg:!, !masc:!], (adj[masc, sg])+| noun[maj, phrasemaj:~, person:~, lieu:~], (punct[hyph]), noun[maj, phrasemaj:~, person:~, lieu:~];noun[partic], ((punct[hyph]), noun[maj, phrasemaj:~, person:~, lieu:~]).


/le parti Socialiste juif\
21> noun[org=+, maj=+, mwe=+] @= |?[indef:~, adj:~]| noun[parti, nonorg:~, sg, !masc:!, !fem:!, orgadj], (adj)*, (adj[maj];adj[postprof])+, (adj[nat])*.

/les partis Socialistes flamand et belge\
21> noun[org=+, maj=+, mwe=+, pl] @= noun[parti, pl, !masc:!, !fem:!], (adj[pl])*, (adj[maj, pl];adj[postprof])+, adj[nat, sg], coord, adj[nat, sg].

/ le parti Baas\ 

21> noun[org=+, maj=+, mwe=+, pl] @= noun[parti, !masc:!, !fem:!], noun[maj,inconnu];noun[maj,pol,org].

/les Partis socialistes flamand et belge\
21> noun[org=+, maj=+, mwe=+, pl] @= noun[orgadj, maj, pl, !masc:!, !fem:!], (adj[pl])+, adj[nat, sg], coord, adj[nat, sg].

/le Karlsruher SC, le Hertha BSC Berlin\
25> noun[org=+, maj=+, mwe=+] @= |?[def]| noun[maj, nat:~, prof:~, tit:~, nonorg:~,toutmaj:~], noun[toutmaj, phrasemaj:~, nonorg:~, nonorg2:~], ((num), noun[maj, person:~]).

/le FC Cologne, le TSV Bayer 04 Leverkusen\
25> noun[org=+, maj=+, mwe=+] @= |?[def]| noun[toutmaj, phrasemaj:~, org:~, preorg:~, prefirme:~, firme:~, lettre:~], noun[maj, person:~, inconnu];noun[maj, person:~, ville];noun[maj, person:~, region];noun[maj, person:~, proper], ((num), noun[maj, person:~, inconnu];noun[maj, person:~, ville];noun[maj, person:~, region];noun[maj, person:~, org]).

/*********** contexte ***********\
/l'association X Y\
26> noun[org=+, maj=+, mwe=+] @= |?[def], (adj[maj:~]), noun[preorg, maj:~];noun[preorg, toutmaj], (adj[nat];adj[postprof])| (det), (adj[nat:~, postprof: ~])*, noun[maj, phrasemaj:~, org:~, persorg:~], (adj[guessed:~])*, ((prep), (adj), noun[maj])* .
/le magazine britannique, "The Tatler".\
26> noun[org=+, maj=+, mwe=+] @= |?[def], (adj[maj:~]), noun[preorg, maj:~, sg];noun[preorg, toutmaj], (adj[nat]), punct[form:fcm], (punct[form:fquotes])| (det), (adj[nat:~])*, noun[maj, phrasemaj:~, org:~, person:~], (adj)*, (prep)*, (noun[maj])* |(punct[form:fquotes]), punct|.


/l'association Clair de Terre\
26> noun[org=+, maj=+, mwe=+] @= |?[def], (adj[maj:~]), noun[preorg, maj:~], (adj[nat])| noun[maj, phrasemaj:~, org:~], ((prep), (adj)*, noun[maj])* .

/la ligue Vie et Santé\
26> noun[org=+, maj=+, mwe=+] @= |?[def], (adj[maj:~]), noun[preorg, maj:~], (adj[nat])| noun[maj, phrasemaj:~], (adj)*, coord, (adj)*, noun[maj] .


/le journal "Navarra Hoy"\
26> noun[org=+, orglieu=+, maj=+, mwe=+] @= |?[def], (adj[maj:~]), noun[preorg, maj:~], (adj[nat]), punct[form:fquotes]| (det), (adj)*, noun[maj, phrasemaj:~, org:~], (adj)*, (prep)*, (noun[maj])* |punct[form:fquotes]|.


/une association anglaise, la Cefaldi,\
26> noun[org=+, maj=+, mwe=+] @= |det[poss];det[indef], (adj[maj:~]), noun[preorg, maj:~], (adj[nat]), punct[form:fcm]| (det), noun[maj, phrasemaj:~, org:~, person:~, lieu:~], (noun[maj])*.


/la compagnie maritime britannique P & O\
26> noun[firme=+, maj=+, mwe=+, sg=+, orglieu=+] @= |?[def], (adj[maj:~]), noun[prefirme, sg, !masc:!, !fem:!], (adj), (adj[nat])| noun[maj, phrasemaj:~, firme:~, persorg:~,nat:~], (noun[maj])*, (coord;?[lemme:and], noun[maj])*, (noun[maj])*.

/le groupe Lafarge Coppée\
26> noun[firme=+, maj=+, mwe=+, sg=+] @= |?[def], (adj[maj:~]), noun[prefirme, sg, !masc:!, !fem:!], (adj), (adj[nat])| noun[maj, phrasemaj:~], (coord[lemme:"&"];?[lemme:and]), (noun[maj])+.


/le fabricant, "faux Systems",\
26> noun[firme=+, maj=+, mwe=+] @= |?[def], (adj[maj:~]), noun[prefirme, sansvirg:~, maj:~, !masc:!, !fem:!], (adj[nat]), punct[form:fcm], (punct[form:fquotes])| (det), (adj[nat:~])*, noun[maj, phrasemaj:~, firme:~, person:~];noun[maj, phrasemaj:~, firme:~, persorg:~], (adj)*, (prep)*, (noun[maj])* |(punct[form:fquotes]), punct|.

/l'OPA de X sur Y\
26> noun[firme=+, orgpers=+, maj=+, mwe=+] @= |noun[prefirmesur], (adj;verb[partpas]), prep[form:fde], (det, ?[nat])| (det), noun[person:~, org:~, maj, nat:~], (noun[maj])* |prep[form:fsur];coord|.
27> noun[firme=+, orgpers=+, maj=+, mwe=+] @= |noun[prefirmesur], (adj;verb[partpas]), prep[form:fde], (det, ?[nat]), noun[firme], coord, (prep[form:fde]), (det), (?[nat])| (det), noun[maj, nat:~, person:~, org:~], (noun[maj]) |prep[form:fsur]|.
//27> noun[firme=+, maj=+, mwe=+] @= |noun[prefirmesur], (adj;verb[partpas]), prep[form:fde], (det, ?[nat]), noun[firme], coord, (prep[form:fde]), (det), (?[nat])| (det), noun[person:~, org:~, maj, nat:~], (noun[maj])* |prep[form:fsur]|.

27> noun[firme=+, orgpers=+, maj=+, mwe=+] @= |noun[prefirmesur], (adj;verb[partpas]), (prep[form:fde], (noun[prefirme]), (det), (?[nat]), noun, adj*), prep[form:fsur], (det, noun[nat])| (det), noun[maj, phrasemaj:~, batimentorg:~, batimentorg1:~, lieu:~, nat:~], (coord, noun[maj]), (noun[maj])*.
27> noun[firme=+, orgpers=+, maj=+, mwe=+] @= |noun[prefirmesur], (adj;verb[partpas]), (prep[form:fde], (noun[prefirme]), (det), (?[nat]), noun, adj*), coord, (prep[form:fde]), (noun[prefirme]), (det), (?[nat]), noun, adj*, prep[form:fsur], (det, noun[nat])| (det), noun[maj, phrasemaj:~, batimentorg:~, batimentorg1:~, lieu:~, nat:~], (coord, noun[maj]), (noun[maj])*.

/les ONG X, (Y et Z)\
30> noun[org=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat])| (det), (noun[maj, phrasemaj:~, org:~])+, (adj)* |coord;punct[form:fcm]|.

/les ONG (X et) Y\
31> noun[org=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), noun[org], coord| (det), (noun[maj, phrasemaj:~])+, (adj)*.

/les ONG (X,) Y (et Z)\
32> noun[org=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), noun[org], punct[form:fcm]| (det), noun[maj, phrasemaj:~, org:~], (noun[maj, phrasemaj:~])*, (adj)* |coord;punct[form:fcm]|.

/les ONG (X,) Y (et Z)\
33> noun[org=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), noun[org], punct[form:fcm], noun[org], punct[form:fcm]| (det), (noun[maj, phrasemaj:~])+, (adj)* |coord;punct[form:fcm]|.

/les ONG (X, Y et) Z\
34> noun[org=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[preorg, pl], (adj), (adj[nat]), noun[org], (punct[form:fcm], noun[org])+, coord| (det), (noun[maj, phrasemaj:~])+, (adj)*.


/les compagnies Lausonia, La Renama, Latina Vita et Lausonia Vita\
/les compagnies X, (Y et Z)\
30> noun[firme=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[prefirme, pl], (adj), (adj[nat])| (det), (noun[maj, phrasemaj:~, org:~, firme:~])+, (adj)* |coord;punct[form:fcm]|.

/les compagnies (X et) Y\
31> noun[firme=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[prefirme, pl], (adj), (adj[nat]), noun[firme], coord| (det), (noun[maj, phrasemaj:~])+, (adj)*.

/les compagnies (X,) Y (et Z)\
32> noun[firme=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[prefirme, pl], (adj), (adj[nat]), noun[firme], punct[form:fcm]| (det), (noun[maj, phrasemaj:~])+, (adj)* |coord;punct[form:fcm]|.

/les compagnies (X,) Y (et Z)\
33> noun[firme=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[prefirme, pl], (adj), (adj[nat]), noun[firme], punct[form:fcm], noun[firme], punct[form:fcm]| (det), (noun[maj, phrasemaj:~])+, (adj)* |coord;punct[form:fcm]|.

/les compagnies (X, Y et) Z\
34> noun[firme=+, maj=+, mwe=+] @= |?[def, pl], (adj[maj:~, pl]), noun[prefirme, pl], (adj), (adj[nat]), noun[firme], (punct[form:fcm], noun[firme])+, coord| (det), (noun[maj, phrasemaj:~])+, (adj)*.

/ le 7 9 --> org.div \

34> noun[org=+,div=+,mwe=+] = | ?[def]| num[lemme:"7 9"].
34> noun[org=+,div=+,mwe=+] = | ?[def]| num[lemme:"18 20"].

marqueur:
/Bleu, le quotidien d'Agen\
45> ?[maj:~, det:~, adj:~], noun[maj, person:~, lieu:~, org=+], punct[form:fcm], (det), noun[preorg, sansvirg:~], ?[org:~].
45> punct[form:fquotes], noun[maj, org=+], punct[form:fquotes], punct[form:fcm], (det), noun[preorg].

45> ?[maj:~, det:~, adj:~], noun[maj, person:~, lieu:~, firme=+], punct[form:fcm], (det), noun[prefirme, postfirme:~-], ?[org:~, firme:~].


/filiale commune de Ford et de VW\
45> noun[prefirmede], (adj), prep[form:fde], (det[def], (adj[maj:~])), noun[maj, phrasemaj:~, firme:+], coord, prep[form:fde], noun[maj, preorg:~, prefirme:~, batimentorg1:~, firme=+].

/la filiale de la Socpress, Presse-Infor\
45> noun[prefirmede, prefirme], prep[form:fde], (det), (adj), noun, (noun), (adj), punct [form:fcm, nonorg=+], noun[maj, toutmaj:~, person:~, firme=+].

/************* autres **********\
/le KFLT\
//45> ?[def, sg], (adj[maj:~]), noun[toutmaj, phrasemaj:~, preorg:~, prefirme:~, lettre:~, nonorg:~, pays:~, org=+];noun[toutmajtiret, phrasemaj:~, preorg:~, prefirme:~, lettre:~, nonorg:~, pays:~, org=+].
//45> punct[form:fopar], noun[toutmaj, phrasemaj:~, preorg:~, prefirme:~, lieu:~, nonorg:~, org=+], punct[form:fcpar].
/(AMD-BA)\
//45> punct[form:fopar], noun[toutmajtiret, phrasemaj:~, preorg:~, prefirme:~, lieu:~, nonorg:~, org=+], punct[form:fcpar].

/les Verts --> org pol\

45> det[def], noun[maj,lemme:"vert",pl,org=+,entit=+,pol=+] , ?[maj:~]  .



45> noun[toutmaj, org], punct[form:fopar], noun[maj, org, nonorg=+], punct[form:fcpar].





séquence:
/le LTTE (Tigres pour la libération du Tamil Eelam)\
50> noun[org=+, maj=+] @= |noun[toutmaj, org], punct[form:fopar]| noun[maj, nonorg:~], (prep, (det[def])), (adj)*, (noun)*, (adj)*, (prep, (det[def])), (adj)*, (noun)*, (adj)* |punct[form:fcpar]|.

/Radio France\
50> noun[org=+, mwe=+] @= noun[org1, sansmaj, org1de:~, sansdef, !maj:!];noun[org1, org1de:~, sansdef, maj:+, !maj:!], (adj[maj]), noun[maj, phrasemaj:~, person:~];adj[maj, phrasemaj:~, person:~], (adj[maj]), (noun[maj])*.

/Le Club Med\
50> noun[org=+, mwe=+] @= |?[def]| noun[org1, sansmaj, org1de:~, !maj:!];noun[org1, org1de:~, maj:+, !maj:!], (adj),(adj[nat]), noun[maj, phrasemaj:~, person:~];adj[maj, phrasemaj:~, person:~], (adj[maj]), (noun[maj,person:~])*.

/le petit Bleu, le quotidien d'Agen\
50> noun [org=+, mwe=+] @= |(det[def])|  (adj)*, (noun[maj])*,(prep[form:fde], (det[def])), noun[maj, org:~, person:~, lieu:~] |punct[form:fcm], (det), noun[preorg, sansvirg:~,!firme:!,!edu:!,!non-profit:!,!gsp:!,!pol:!], ?[org:~]|.
/"le petit Bleu", le quotidien d'Agen\
50> noun [org=+, mwe=+] @= |punct[form:fquotes]| (det[def]), (adj)*, (noun[maj])*, noun[maj] |punct[form:fquotes], punct[form:fcm], (det), noun[preorg, sansvirg:~]|.

/la Cote Desfossés, le quotidien économique\
50> noun [org=+, mwe=+] @= (noun[maj])+, noun[maj, person:~, lieu:~] |punct[form:fcm], (det), noun[preorg], ?[org:~]|.

/le Standard de Meudon, club historique\
50> noun [org=+, mwe=+] @= (noun[maj])+, (prep[form:fde]), noun[maj, person:~, org:~] |punct[form:fcm], (det), noun[preorg], ?[org:~]|.

/Média-France International, la société chargée d'exporter le programme\
50> noun [firme=+, mwe=+] @= (det[def]), (adj)*, (noun[maj])*, noun[maj, org:~, firme:~, person:~, lieu:~] |punct[form:fcm], (det), noun[prefirme, postfirme:~-], ?[org:~, firme:~, verb:~];verb[partpas]|.


50> noun[org=+,div=+,entit=+,mwe=+] = noun[lieu,proper,maj] , noun[lemme:"matin"];noun[lemme:"dimanche"].

/ la radio (television) (nationale;regionale) congolaise\

50> noun[org=+,div=+,entit=+,mwe=+] = noun[lemme:"radio"], ( (punct[form:fhyph]), noun[lemme:"télévision"]) , (adj[nat:~]), adj[nat].

/l'armee de l'air / américaine

51> noun[org=+,mil=+,mwe=+]@= noun[lemme:"armée"], prep[form:fde], (det), noun[lemme:"air"];noun[lemme:"terre",oronyme=~] , (adj[nat]).

51> noun[org=+,mil=+,mwe=+]= ?[lemme:"armée"],?[nat].

/ le gouvernement de Paris | parisien\

51>  noun[org=+,pol=+,mwe=+] @= noun[lemme:gouvernement], (adj), adj[nat] .
51>  noun[org=+,pol=+,mwe=+] @= noun[lemme:gouvernement], (adj),  prep[form:fde] , noun[lieu] .

// Ajouts Ester pour les fonctions

// 1er secretaire du PC

51> noun[prof=+,pol=+,mwe=+] = noun[prof,pol] , (adj), prep[form:fde], (det[def]), noun[org,pol].

// chef d'etat major de l'armee yougoslave

51> noun[prof=+,grade=+,mwe=+] @= noun[grade] , prep[form:fde], (det[def]), noun[lemme:"armée"], (adj[nat]).





/************************* noms de personnes **********************\
Marqueur:

/l'Assemblée\
100> ?[def], noun[org1, maj, sg, phrasemaj:~, anton:~, nonorg:~, org=+], ?[maj:~].


/le PDG du Musée Grévin, Tititoto\
100> noun[prefirmede], prep[form:fde], (det), (adj), noun[org], punct[form:fcm], noun[maj, toutmaj:~, org:~, person=+].
100> noun[preorgde], prep[form:fde], (det), (adj), noun[org], punct[form:fcm], noun[maj, toutmaj:~, org:~, person=+].

/le leader X\
100> noun[preorg, preorgpers], noun[org, maj, person=+].


120> noun[org=~]{noun[org], punct[hyph], noun[org]}.

/lorsqu'un noeud noun a pour unique fils un noeud noun, faire passer le trait qui signale une entité nommée du père au fils (en prévision d'une élimination du noeud père dans deduction_entite.xip)\

140> noun[org]{noun[first, last, org=+]}.
140> noun[firme]{noun[first, last, firme=+]}.
140> noun[lieu, lieu=~]{noun[first, last, lieu=+]}.
140> noun[person, person=~]{noun[first, last, person=+]}.
//140> noun[date, date=~]{noun[first, last, date=+]}.
140> noun[periode, periode=~]{noun[first, last, periode=+]}.
140> noun[ville, ville=~]{noun[first, last, ville=+]}.
140> noun[pays, pays=~]{noun[first, last, pays=+]}.
140> noun[continent, continent=~]{noun[first, last, continent=+]}.
140> noun[quartier, quartier=~]{noun[first, last, quartier=+]}.
140> noun[batiment, batiment=~]{noun[first, last, batiment=+]}.
140> noun[batimentorg, batimentorg=~]{noun[first, last, batimentorg=+]}.

140> noun[lieupers]{noun[first, last, lieupers=+]}.
140> noun[perslieu]{noun[first, last, perslieu=+]}.
140> noun[lieupers1]{noun[first, last, lieupers1=+]}.
140> noun[perslieu1]{noun[first, last, perslieu1=+]}.
140> noun[orgpers]{noun[first, last, orgpers=+]}.
140> noun[persorg]{noun[first, last, persorg=+]}.
140> noun[orglieu]{noun[first, last, orglieu=+]}.


/effacement des traits en concurrence selon les traits de préférence\
150> noun[orgpers, org, person=~].
150> noun[orglieu, org, lieu=~, ville=~, pays=~, continent=~, region=~].
150> noun[orgpers, firme, person=~].
150> noun[orglieu, firme, lieu=~, ville=~, pays=~, continent=~, region=~].
150> noun[person:+, persorg:+, org=~, firme=~].
150> noun[person:+, persorg1:+, org=~, firme=~].

marqueur:
//acronymes entre parentheses

160> noun[org] , punct[form:fopar] , noun[org=+,toutmaj] , punct[form:fcpar] .
