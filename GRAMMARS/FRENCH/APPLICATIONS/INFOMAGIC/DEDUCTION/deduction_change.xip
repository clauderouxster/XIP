//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//! C. BRUN
//! ESTER2: modification des interprétations des ENs
//! tenant compte de la syntaxe
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!



/############################## DEDUCTION   ################################################\

Déduction:

if (^ORG[batiment,lieu,!traits:!](#1))
	LIEU[batiment=+,org=+,lieu=~](#1).

// *** Coordinations ad-hoc ***********************************

|PP{?*,NP{?*,Noun#1[last]}},COORD,PP{?*,NP{?*,Noun#2[last]}},punct;verb;sent|
	if (LIEU(#1) & LIEU(#2))
	COORDITEMS(#1,#2)

| NP{?*,NOUN#1[last]}, punct[form:fcm], NP{?*,NOUN#2[last]}, (punct[form:fcm]), coord,  NP{?*,NOUN#3[last]},punct;verb;sent|
	if (LIEU(#1) & LIEU(#2) & LIEU(#3))
	LINK-COORD(#1,#2)

| PP{?,NP{?*,NOUN#1[last]}}, punct[form:fcm], PP{?,NP{?*,NOUN#2[last]}}, (punct[form:fcm]), coord,  PP{?,NP{?*,NOUN#3[last]}},punct;verb;sent|
	if (LIEU(#1) & LIEU(#2) & LIEU(#3))
	LINK-COORD(#1,#2)

| NP{?*,NOUN#1[last]}, punct[form:fcm], NP{?*,NOUN#2[last]}, punct[form:fcm], NP{?*,NOUN#4[last]}, (punct[form:fcm]), coord,  NP{?*,NOUN#3[last]},punct;verb;sent|
	if (LIEU(#1) & LIEU(#2) & LIEU(#3) & LIEU(#4))
	LINK-COORD(#1,#2)

| PP{?,NP{?*,NOUN#1[last]}}, punct[form:fcm], PP{?,NP{?*,NOUN#2[last]}}, punct[form:fcm], PP{?,NP{?*,NOUN#4[last]}}, (punct[form:fcm]), coord,  PP{?,NP{?*,NOUN#3[last]}},punct;verb;sent |
	if (LIEU(#1) & LIEU(#2) & LIEU(#3) & LIEU(#4))
	LINK-COORD(#1,#2)

// *** TRAITEMENT DES ANTECEDENTS DES RELATIVES 

// qui 

	if (SUBJ(#1,#2) & COREF[rel](#3,#2[lemme:"qui"]))
		SUBJ(#1,#3)

// etre en train de : calcul sujet profond

if(SUBJ(#1[lemme:"être"],#2) & VMOD(#1,#3) & PREPOBJ(#3,#4[lemme:"en train de"]))
	SUBJ(#3,#2)

// ****  Utilisation de certains contextes syntaxiques pour tagger des mots inconnus

// mort à / né à --> lieu

if (VMOD(#1[verb,lemme:"mourir"],#2) & PREPOBJ(#2,#3[prep,form:fa])& ~LIEU(#2) )
	LIEU[ville=+](#2)

//*** VEHICULES

//*** PERSONNES
//Felix est un chat, emploi figuré problématique

if (^PERSONNE[hum](#1) & OBJ[spred](#2,#3[animal]) & SUBJ(#2,#1))
	PERSONNE[animal=+,hum=~](#1)

//*** VEHICULES
// Il conduit une 106


if (OBJ(#1[lemme:"conduire"],#2[num,chiffres,lettres:~]))
	PRODUIT[vehicule=+](#2)

// l'Erika est un pétrolier

if (SUBJ(#2,#1[proper]) & OBJ[spred](#2,#3[vehicule]))
		PRODUIT[vehicule=+](#1)

if (SUBJ(#2,#1[pron,rel]) & COREF[rel](#3[proper],#1) & OBJ[spred](#2,#4[vehicule]))
		PRODUIT[vehicule=+](#3)

// L'Erika, qui est un petrolier

//l'Erika, un pétrolier ...
 |NP[det]{?*,NOUN#2}|
if (SEQNP(#1[proper,org:~,person:~,pren:~,lieu:~],#2[vehicule]))
		PRODUIT[vehicule=+](#1)

// coordinations : les avions XXX et YYY

|noun#1{noun[pl,vehicule],?+}|
	if(PRODUIT[vehicule](#1) & COORDITEMS(#1,#2[maj,proper]))
	PRODUIT[vehicule=+](#2) 


// pilote de McLaren

if(NMOD[POSIT1](#1[lemme:"pilote"],#2[firme]) & ^ORG(#2))
		PRODUIT[vehicule=+](#2)


// à bord du de la ...


|PP{prep[form:fa],np{noun[lemme:"bord"]}},PP{PREP[form:fde],NP{?*,NOUN#2}}|
	if (^PERSONNE(#2) & ~PRODUIT(#2))
	PRODUIT[vehicule=+,hum=~](#2)


// age
// Il a (plus de/moins de) 10 ans
// attention: il a 10 ans d'ancienneté, il a 3 semaines pour en profiter
//(modif maud après chgment de l'arbre en amont)

// les enfants de 5 à 7 ans 



|NP{?*,NOUN[plusage]},PP#1{PREP[form:fde],NP{NUM#2,PREP[form:fa],NUM#2,noun[lemme:"an"]}},?[form:~fde]|
	AGE(#1)

|NP{?*,NOUN[plusage]},PP#1{PREP[form:fde],NP{NUM#2,COORD,NUM#2,noun[lemme:"an"]}},?[form:~fde]|
	AGE(#1)


|NP#1{DET{ADV[lemme:"moins"],?},NOUN#3[date:+,duree:+]{NUM,NOUN}},?[form:~fde]|
	if (OBJ(#2[lemme:"avoir"],#3))
	AGE(#3)
|NP#1{DET{ADV[lemme:"plus"],?},NOUN#3[date:+,duree:+]{NUM,NOUN}},?[form:~fde]| 
	if (OBJ(#2[lemme:"avoir"],#3))
	AGE(#3)

|NP#1{NOUN#3[date:+,duree:+]{NUM,NOUN}},?[prep:~]| 
	if (OBJ(#2[lemme:"avoir"],#3))
	AGE(#3)


|NP#1{det[poss:+],NOUN#3[date:+,duree:+]{NUM,NOUN}},?[prep:~]| 
	if (OBJ(#2[lemme:"fêter"],#3))
	AGE(#3)


// il fete ses 10 ans


// Pierre, 10 ans, 
|NP{?*,NOUN#1},PUNCT[form:fcm], NP#2{ADV*,NOUN#4{NUM,NOUN#3[timeduree,lemme:"an"]}},PUNCT[form:fcm];coord[coord]|
	if (PERSONNE(#1))
	AGE(#4)

|NP{?*,NOUN#1},PUNCT[form:fcm],NP#2{ADV*,NOUN#4{NUM,NOUN#3[timeduree,lemme:"an"]}},SENT|
	if (PERSONNE(#1))
	AGE(#4)

// âgé de 10 ans

|AP{ADV*,ADJ[lemme:"âgé"]},PP{?*,NP#1{ADV*,NOUN#2{NUM,NOUN#3[timeduree,lemme:"an"]}}}|
			AGE(#2)

// a l'age de 10 ans 

|PP{?*,NP{DET[def],NOUN[lemme:"âge"]}},PP{?*,NP{NOUN#2{NUM,NOUN#3[timeduree,lemme:"an"]}}}|
		AGE(#2)

// à 20 ans 

|PP{PREP[form:fa], NP#2{ADV*,NUM,NOUN#3[lemme:"an"]}},?[prep:~]|
		if (~AGE(#2))
		AGE(#2)


|PP[last]{PREP[form:fa], NP#2{ADV*,NUM,NOUN#3[lemme:"an"]}}|
	       if (~AGE(#2))
		AGE(#2)

|PP{PREP[form:fa], NP#2{ADV*,NUM,NOUN#3[lemme:"an"]}}|
	if(VMOD(?[lemme:"mourir"],#3) & ~AGE(#2))
		AGE(#2)

|PP{PREP[form:fa], NP#2{ADV*,NUM,NOUN#3[lemme:"an"]}}|
	if(NMOD(?[lemme:"mort"],#3) & ~AGE(#2))
		AGE(#2)

|PP{PREP[form:fa], NP#2{ADV*,NUM,NOUN#3[lemme:"an"]}}|
	if(VMOD(?[lemme:"décéder"],#3) & ~AGE(#2))
		AGE(#2)

|PP{PREP[form:fa], NP#2{ADV*,NUM,NOUN#3[lemme:"an"]}}|
	if(NMOD(?[lemme:"décès"],#3) & ~AGE(#2))
		AGE(#2)

|PP{PREP[form:fa], NP#2{ADV*,NUM,NOUN#3[lemme:"an"]}}|
	if(VMOD(#1[lemme:"éteindre"],#3) & ~AGE(#2) & REFLEX(#1,?))
		AGE(#2)

// un homme de trente ans
|NOUN#2{NUM,NOUN#3[timeduree,lemme:"an"]}|
if(NMOD[posit1](#1[plusage],#2))
	AGE(#2)


// la ménagère de moins de 50 ans

|NOUN#3{NUM,NOUN#4[timeduree,lemme:"an"]}|
if(NMOD[posit1](#1[plusage],#2[quant]) & (NMOD[posit1](#2,#3)))
	AGE(#3)



// traitement des coordinations type 3, 4 , 6 et 10 ans
// A SUPPRIMER POUR ESTER SELON LES GUIDELINES 

//|NP{NUM#2,COORD[lemme:"et"],NOUN#3}|
//	if (AGE(#3) & ~AGE(#2))
//	AGE(#2)


//|NP{NUM#1,PUNCT[form:fcm],NUM#2,COORD[lemme:"et"],NOUN#3}|
//	if (AGE(#3))
//	AGE(#1),AGE(#2).


|NOUN#1[ex_age:+]|
	AGE(#1)

// ajout Maud
// Il savait chanter dès 13 ans. Spectacle tout public dès 3 ans.
|PP#1{PREP[lemme:"dès"],NP{NOUN#2}}|
	if(^DUREE(#1))
	AGE[date=~](#2),
	~.





//*** METEO
//l'ouragan Katrina

|noun#1[proper,maj,meteo]|
	PHENOMENE-METEO(#1)

// Katrina est un ouragan

if (SUBJ(#2,#1[proper]) & OBJ[spred](#2,#3[meteo,proper:~]))
	PHENOMENE-METEO(#1)

//Katrina , qui est un ouragan

if (SUBJ(#2,#1[pron,rel]) & COREF[rel](#3[proper],#1) & OBJ[spred](#2,#4[meteo,proper:~]))
		PHENOMENE-METEO(#3)
//Katrina, l'ouragan ...

 |NP[det]{?*,NOUN#2}|
if (SEQNP(#1[proper,org:~,lieu:~,maj],#2[proper:~,meteo]))
			PHENOMENE-METEO(#1)


// equipes sportive : stade toulousain
// a verifier: c'est quand meme ambigu

|#1{#2,#3}|
if (^ORG[batiment,lieu](#1) & NMOD(#2,#3) & AGEO(#3,?))
	ORG[sport=+,div=+,batiment=~,lieu=~](#1)


// faire monter le trait du premodifieur sur
//les orgs pour avoir leurs types


if (NMOD[pol](#1,#2) & ^ORG(#2))
	ORG[pol=+](#2)

if (NMOD[posit1](#1[prefirme],#2) & ^ORG(#2))
	ORG[entreprise=+](#2)


// adresses versus line : elle habite rue des Petunias

if (OBJ(#1[lemme:"habiter"],#2) & ^LIEU[line](#2))
	LIEU[adresse=+,line=~](#2)
if (OBJ(#1[lemme:"demeurer"],#2) & ^LIEU[line](#2))
	LIEU[adresse=+,line=~](#2)
if (OBJ(#1[lemme:"vivre"],#2) & ^LIEU[line](#2))
	LIEU[adresse=+,line=~](#2)
if (OBJ(#1[lemme:"domicilier"],#2) & ^LIEU[line](#2))
	LIEU[adresse=+,line=~](#2)

// changement d'interpretation

//|NOUN#1{noun[lemme:"biennale"],?*}| 
//	if (^EVENEMENT(#1))
//	ORG[div=+](#1)

//if (^EVENEMENT[!traits:!](#1))
//	ORG(#1)

// Coordination, modifictaion des unites 

/8 et 9 degres\
|PP#1{prep,NP{num#3,coord,noun#2}}| 
	if ( ^AMOUNT[!unit:!](#2) & ~AMOUNT(#1))
	AMOUNT(#1)

|NP#1{num#3,coord,noun#2}| 
	if ( ^AMOUNT[!unit:!](#2) & ~AMOUNT(#1))
	AMOUNT(#1)

/8 à 9 degrés\

|NP#1{?*,num#3[last]},PP{prep[form:fa],NP{NOUN#2}}|
	if (^AMOUNT[!unit:!](#2))
	AMOUNT(#1)

/ de 8 à 9 degrés\
// ajout maud : pas de trait année sur le num
// de 60% du PIB en 2008 à 89% en ....

|PP{?*,NP{?*,num#1[last,annee:~]}},PP{prep[form:fa],NP{NOUN#2}}|
	if (AMOUNT[!unit:!](#2))
	AMOUNT(#1)


//**********METONYMIES*************************
// Changement loc.admi --> org.gsp
//*********************************************


/ if (NMOD(#1[lemme:"ville"],#2) & SUBJ(#3[metolocorgsubj],#1) &  LIEU(#2))
	METO-ORG(#2)

if ( (SUBJ(#1[metolocorgsubj],#2) | DEEPSUBJ(#1[metolocorgsubj],#2)) & LIEU(#2))
	METO-ORG(#2)

if ( (OBJ(#1[metolocorgobj],#2) | DEEPOBJ(#1[metolocorgobj],#2))  & LIEU(#2))
	METO-ORG(#2)

if (NMOD(#1[nmodlocorg_de],#2) & LIEU(#2) & PREPOBJ(#2,#3[form:fde]))
	METO-ORG(#2)

if (NMOD(#1[nmodlocorg_sur],#2) & LIEU(#2) & PREPOBJ(#2,#3[form:fsur]))
	METO-ORG(#2)

if (NMOD(#1[nmodlocorg_avec],#2) & LIEU(#2) & PREPOBJ(#2,#3[form:favec]))
	METO-ORG(#2)

if (NMOD(#1[nmodlocorg_aupresde],#2) & LIEU(#2) & PREPOBJ(#2,#3[lemme:"auprès de"]))
	METO-ORG(#2)

if (NMOD(#1[nmodlocorg_a],#2) & LIEU(#2) & PREPOBJ(#2,#3[form:fa]))
	METO-ORG(#2)

if (NMOD(#1[nmodlocorg_entre],#2) & LIEU(#2) & PREPOBJ(#2,#3[form:fentre]))
	METO-ORG(#2)

if (NMOD(#1[nmodlocorg_contre],#2) & LIEU(#2) & PREPOBJ(#2,#3[form:fcontre]))
	METO-ORG(#2)

if (  ADJMOD(#1[nmodlocorg_de],#2) &  LIEU(#2))
		METO-ORG(#2)


if (NMOD(#1,#2[nmodlocorg_de]) & LIEU(#1))
	METO-ORG(#1)



// *** ATTRIBUTS

if ( OBJ[spred](#1,#2[metospred]) & SUBJ(#1,#3) & LIEU(#3))
		METO-ORG(#3)

|#2{?*,#4[metospred]}|
if ( OBJ[spred](#1,#2) & SUBJ(#1,#3) & LIEU(#3))
		METO-ORG(#3)

// l'Irak vaincu

if (NMOD(#1,#2[metospred,adj]) &  LIEU(#1))
	METO-ORG(#1)

// **** VMODS

if (VMOD(#1[vmodlocorg_sur],#2) & LIEU(#2) &  PREPOBJ(#2,#3[lemme:"sur"]))
	METO-ORG(#2)

if (VMOD(#1[vmodlocorg_a],#2) & LIEU(#2) &  PREPOBJ(#2,#3[form:fa]))
	METO-ORG(#2)

if (VMOD(#1[vmodlocorg_vers],#2) & LIEU(#2) &  PREPOBJ(#2,#3[form:fvers]))
	METO-ORG(#2)

if (VMOD(#1[vmodlocorg_contre],#2) & LIEU(#2) &  PREPOBJ(#2,#3[form:fcontre]))
	METO-ORG(#2)

if (VMOD(#1[lemme:"dire"],#2) & LIEU(#2) & PREPOBJ(#2,#3[lemme:"du côté de"]))
	METO-ORG(#2)

if (VMOD(#1[lemme:"agir",ilse],#2)  & LIEU(#2) & PREPOBJ(#2,#3[form:fde]))
		METO-ORG(#2)


// la guerre à PAYS

|NP{?*,noun[lemme:"guerre"]}, PP{prep[form:fa],np{noun#1}}|
	if  (LIEU[admi](#1))
		METO-ORG(#1)

// la Syrie accusée de cacher ...

if (NMOD(#1,#2[metolocorgsubj,partpas]) & LIEU(#1))
	METO-ORG(#1)

//*** systematique : preposition induisant un glissement

if (PREPOBJ(#2,#3[prepmeto]) &  LIEU(#2))
		METO-ORG(#2)

// *** systematique : antecedent de relative /qui/

//if (COREF[rel](#1,#2[lemme:"qui"]) &   LIEU(#1))
//		METO-ORG(#1)


// cas a plusieurs argument

if ( SUBJ(#1[lemme:"avoir"],#2) & LIEU(#2) & OBJ(#1,#3[obj_avoir_metosubj]))
			METO-ORG(#2)

if ( OBJ(#1[lemme:"mettre"],#2[lemme:"pression"]) & VMOD(#1,#3) & LIEU(#3))
		METO-ORG(#3)

if ( OBJ(#1[lemme:"remettre"],#2[lemme:"pression"]) & VMOD(#1,#3) & LIEU(#3))
		METO-ORG(#3)

if ( SUBJ(#1[lemme:"être"],#2) & LIEU(#2) & VMOD(#1,#3) & PREPOBJ(#3,#4[lemme:"contre"]))
	METO-ORG(#2)

if ( SUBJ(#1[lemme:"être"],#2) & LIEU(#2) & VMOD(#1,#3) & PREPOBJ(#3,#4[lemme:"pour"]))
	METO-ORG(#2)
// *** Il lit Kant

if (^PERSONNE(#1) & OBJ(#2[lemme:"lire"],#1))
	PRODUIT[docu=+,hum=~](#1)	

// cas de coordinations : on considere que le glissement
//sémantique opere sur tous les coordonnés



if (METO-ORG(#1) & (LINK-COORD(#1,#2) | LINK-COORD(#2,#1)) &  LIEU(#2) & ~METO-ORG(#2))
	METO-ORG(#2)

if (METO-ORG(#1) & (COORDITEMS(#1,#2) | COORDITEMS(#2,#1)) &  LIEU(#2) & ~METO-ORG(#2))
	METO-ORG(#2)

// MMe Tathcher et la France
if (PERSONNE(#1) & (COORDITEMS(#1,#2) | COORDITEMS(#2,#1)) &  LIEU(#2))
	METO-ORG(#2)


//*** modif finale

if (METO-ORG(#1) & ^LIEU[admi](#1))
    ORG[gsp=+,admi=~](#1)

if (METO-ORG(#1) & ^LIEU[batiment,org,!pol:!](#1))
	ORG[lieu=~,batiment=~,org=~](#1)

// l'Europe:org.gsp par defaut

|NP{det[def],noun#1[lemme:"Europe"],?*}|
	if (^LIEU(#1))
	ORG[gsp=+,admi=~](#1)

// Union européenne gsp 

if (^LIEU(#1[lemme:"Union Européenne"]))
		ORG[gsp=+,admi=~](#1)

|PP{prep[form:fa],NP{NOUN#1}}|
	if(^ORG[batiment](#1))
	LIEU[batiment](#1) \

//PERSONNES

|NP#1{NOUN[tit],NOUN{NOUN#2}}|
	if (^PERSONNE(#2))
	PERSONNE(#1)


// Pour INA: attention

|NP{?*,noun#1},NP{NOUN#2[proper]}|
	if ( AGEO (#1,?) & ^ORG(#2) & PERSONNE(#2))
	~


/******* marquage des éléments à propager **********\
Script:

if (PHENOMENE-METEO(#1))
	{METEO##1=1;PERS##1=0;}

if (PRODUIT[VEHICULE](#1))
	{VEHIC##1=1;PERS##1=0;}

/******* fin marquage des éléments à propager **********\
Déduction:
|NOUN#1| 
	if (METEO##1:1)
	PHENOMENE-METEO(#1)

|NP{DET[def],NOUN#1[vehicule:~,maj]}| 
	if (VEHIC##1:1)
	PRODUIT[VEHICULE=+](#1)

|PP{PREP[def],NP{NOUN#1[vehicule:~,maj]}}| 
	if (VEHIC##1:1)
	PRODUIT[VEHICULE=+](#1)

/propagation d'un seul élément\

 
	

//if (^EVENEMENT[!traits:!](#1))
//	ORG(#1)

|np{noun[tit],noun#1}}| 
	if ( ^ORG(#1) & PERSONNE(#1))
	~

|np{noun[tit],noun#1}}}| 
	if ( ^LIEU(#1) & PERSONNE(#1))
	~


// effacements

|noun#1{?*,#2}|
	if (ORG(#1) & ^$1(#2))
	~


if (^PERSONNE(#1) & PRODUIT(#1))
	~

if (^PERSONNE(#1) & PHENOMENE-METEO(#1))
	~

|#1{#2,?*}| if (^DATE-ESTER(#2) & AMOUNT(#1)) ~

|NOUN#1{?#2,?+}|
	if (PERSONNE(#1) & ^DATE-ESTER(#2))
	~


|NOUN#1{?*,NOUN{NOUN#2,?}}|
	if (PERSONNE(#1) & ^DATE-ESTER(#2))
	~

//ajout maud
if (AGE(#1) & ^DUREE[date](#1))
   ~.

if (AGE(#1) & ^DUREE[date](#3) & MOTHER(#1,#3))
   ~.

if (AGE(#1) & ^DUREE[date](#3) & MOTHER(#1,#4) & MOTHER(#4,#3))
  ~.

if (AGE(#1) & ^DUREE-ESTER(#1))
   ~.

if (AGE(#1) & ^DUREE-ESTER(#3) & MOTHER(#1,#3))
   ~.

if (AGE(#1) & ^DUREE-ESTER(#3) & MOTHER(#1,#4) & MOTHER(#4,#3))
  ~.

if (AGE(#1) & ^DUREEMOD[date](#1))
   ~.

if (AGE(#1) & ^DUREEMOD[date](#3) & MOTHER(#1,#3))
   ~.

if (AGE(#1) & ^DUREEMOD[date](#3) & MOTHER(#1,#4) & MOTHER(#4,#3))
  ~.

|PP#1{?*,NP{?*,NOUN#2}}|
if (AGE(#2) & ^DATE(#1))
   ~



|NP#1{ADV,NOUN#2}|
if (AGE(#2) & ^DUREEMOD[date](#1))
   ~

