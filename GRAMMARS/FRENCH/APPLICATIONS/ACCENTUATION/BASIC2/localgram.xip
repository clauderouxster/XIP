//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//! ENCODING : UTF-8	[€àâáäã]
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


//Séquence:
Sequence:

/ this rule assigns starbis feature to any word after a punctuation at start of sentence\
/ notice:  ?[punct] covers both PUNCT and SGML categories \

1> ?[startbis=+] @= ?[start,punct], ?*[punct], ?.

1> ?[startbis=+] = ?[start:+].

/ this rule assigns startbis feature to any word after a word marked as strongbreak (typically open parenthesis or ; or :\
/ notice:  ?[punct] covers both PUNCT and SGML categories \
1> ? = ?[strongbreak], ?*[punct], ?[startbis=+].



//Désambiguïsation:
Tagging:

/ these desambiguation rules rewrite terminal nodes (change lexical category) in certain contexts\
/ correcting input from lexicon:  -le is a determiner if followed by hyphen and not a pronoun in Monsieur-le-Prince \

3> PRON[acc,hyph] %= DET[det=+,def=+,hyph=+]  |punct[Hyph]|.

/Montrevel-en-Bresse \
3> PRON[gen,hyph] %= PREP[form:fen, sfen=+, hyph=+]  |punct[Hyph]|.


/Montlouis-sur-Loire\
3> PREFIX[lemme:sur] %= |Punct[hyph]| PREP[form:fsur, sfsur=+, hyph=+].



/  ------------------     end desambiguation rules  ---------------- \ 


//Séquence:
Sequence:

/ le porte à porte\
6> NOUN[mwe=+]  =  |det;prep;num| noun#1[proper:~], prep, noun#2,
                             where (#1[lemme]:#2[lemme]).


/ jour après jour is an adverb \
7> ADV[mwe=+] =  |?[det:~,prep:~,num:~]| noun#1[proper:~], prep, noun#2,
                             where (#1[lemme]:#2[lemme]).

/ jour après jour is an adverb \
7> ADV[mwe=+] =   noun#1[startbis,proper:~], prep, noun#2,
                             where (#1[lemme]:#2[lemme]).





/---- rules for prefixed items ----\

/ a rule for coordinated prefixes: les pro et anti-Maastricht\
6> PREFIX = PREFIX, PUNCT[hyph]; COORD, PREFIX .


6>  VERB = |PRON[Nom];NEG, PRON*| PREFIX,   VERB[!masc:!,!fem:!,!sg:!,!pl:!,!p1:!,!p2:!,!p3:!,
                                     !ind:!,!imp:!,!subj:!,!cond:!,!inf:!,partpre:!,!partpas:!,
					pre:!,!pas:!,!fut:!,
					!subcatfeatures:!].


7> NOUN[P3=+] = |DET| PREFIX,   NOUN[p3, !masc:!,!fem:!,!sg:!,!pl:!,
					!subcatfeatures:!].

8>  VERB = |PRON; NOUN| PREFIX,   VERB[!masc:!,!fem:!,!sg:!,!pl:!,!p1:!,!p2:!,!p3:!,
                                     !ind:!,!imp:!,!subj:!,!cond:!,!inf:!,partpre:!,!partpas:!,
					!pre:!,!pas:!,!fut:!,
					!subcatfeatures:!].

8> VERB[inf=+] = |PREP[prepinf]| PREFIX,   VERB[inf,
                                     !ind:!,!imp:!,!subj:!,!cond:!,!inf:!,partpre:!,!partpas:!,
					!pre:!,!pas:!,!fut:!,
					!subcatfeatures:!].

8> VERB[partpas=+] = |NOUN| PREFIX,   VERB[partpas,!masc:!,!fem:!,!sg:!,!pl:!,
                                     !ind:!,!imp:!,!subj:!,!cond:!,!inf:!,partpre:!,!partpas:!,
					!pre:!,!pas:!,!fut:!,
					!subcatfeatures:!].

8> NOUN = PREFIX,   NOUN[!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,
					!subcatfeatures:!].

8> ADJ = |NOUN| PREFIX,   ADJ[!masc:!,!fem:!,!sg:!,!pl:!,
					!subcatfeatures:!].

8> VERB = |VERB| PREFIX,   VERB[!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,
                                     !ind:!,!imp:!,!subj:!,!cond:!,!inf:!,partpre:!,
					!partpas:!,!pre:!,!pas:!,!fut:!,
					!subcatfeatures:!].

/---- end of rules for prefixed  items ----\





/---- rules for hyphenated items ----\

/Garges-les-Oies\
7> Noun[mwe=+,maj=+] = noun[maj,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!,subcatfeatures:!], ?[hyph], 
				punct[hyph], ?[subcatfeatures:!, time:!].

/Bourg-de-Peage, Bourg-d'Oisans	(les villes sont déjà traitées dans localgram_entit1.xip)\
7> Noun[mwe=+,maj=+] = noun[maj,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!], punct[hyph], prep, 
			(punct[hyph]), noun[subcatfeatures:!, time:!].


/ FT: regroupe Pierre - Henry dans Pierre - Henry Michaud \
7> NOUN[proper=+] = NOUN[preN,!maj:!,!preN:!,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!, prenamb2:~],   PUNCT[form:fhyph],  NOUN[maj:+]. 

/AMD-BA\
9> NOUN[mwe=+,maj=+, toutmajtiret=+] @= NOUN[maj, toutmaj, !maj:!,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!],  PUNCT[hyph],  NOUN[maj, toutmaj], (PUNCT[hyph],  NOUN[toutmaj, maj]).

/Port-Moresby\
9> NOUN[mwe=+,maj=+] @= NOUN[maj,!maj:!,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!],  PUNCT[hyph],  NOUN[maj,pren:~], (PUNCT[hyph],  NOUN[maj]).

/ ce chien-ci \
9> NOUN = NOUN[!subcatfeatures:!], PUNCT[hyph],  ADV.


/ 35-40, 50 à 56 \
/9> NUM[mwe=+,num=+, time=-] = NUM, PUNCT[hyph];punct[form:fslash];prep[form:fa],  NUM.\
9> NUM[mwe=+,num=+] = NUM, PUNCT[hyph];punct[form:fslash],  NUM.


/ son/ses \
9> det[mwe=+,det=+,masc=+,fem=+,sg=+,pl=+] = det, PUNCT[hyph];punct[form:fslash],  det.

/ il/elle \
9> pron[mwe=+,pron=+,masc=+,fem=+,sg=+,pl=+] = pron[!poss:!,!dem:!,!nom:!,!acc:!,!p1:!,!p2:!,!p3:!], 
						PUNCT[hyph];punct[form:fslash], pron.



/---- end of rules for hyphenated  items ----\

/
10> NOUN[measure=+, mwe=+, time=-] = num[det:~], noun[measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!], num[det:~].

10> NOUN[measure=+, mwe=+, time=-] = noun[measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!], num[det:~].

10> NOUN[measure=+, mwe=+, time=-] = num[det:~], noun[measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!].
\

10> NOUN[measure=+, mwe=+] = num[det:~], noun[measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!], num[det:~].

10> NOUN[measure=+, mwe=+] = noun[measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!], num[det:~].

10> NOUN[measure=+, mwe=+] = num[det:~], noun[measure,!masc:!,!fem:!,!sg:!,!pl:!,!p3:!].


/--------------  fin grammaire de dates ---------------------\



//Séquence:
//Sequence:


/un jour, un beau jour\
//15> noun[mwe=+,time=+] =  det[indef, form:~fde], adj*, noun[time].


