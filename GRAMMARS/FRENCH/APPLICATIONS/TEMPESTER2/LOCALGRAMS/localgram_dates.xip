//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


//Aude Rebotier

// + modifs Maud 
// CH - depuis Mai 2011 Chronolines


Fragmenter:

// mi-juin
1>"mi-%1[[janv,févr,mars,avr,mai,juin,juill,ao,septe,oct,nov,déc]%c*]%2" : noun  = prefix%1[t_mod=mid,fstmod=+], noun%2[time=+,mois=+,fstmod=+].

// mi-été
1>"mi-%1[[printe,été,automn,hiver]%c*]%2" : noun  = prefix%1[t_mod=mid,fstmod=+], noun%2[time=+,saison=+,fstmod=+].



Marqueur:


//****************
// Marquage des nombres n'étant pas considérés comme des dates possibles
//****************


// un num supérieur à 4 chiffres n'est pas une date
2> num[lemme:"%d%d%d%d%d+",nondate=+,datepot=~].

// un num de trois à quatre chiffre commençant par un 0 n'est pas une date
2> num[lemme:"0[%d%d,%d%d%d]",nondate=+,datepot=~].

// un num de trois chiffres n'est pas une date s'il est suivi directement d'un nom
// règle à enelever si besoin
// cela evite de prendre "150 photos"; 
// pour ce qui est des années entre 0 et 1000, 
// on peut supposer que la date est suivie d'un determinant/prep/pronom mais pas 
// directement d'un nom
2> num[lemme:"%d%d%d",nondate=+,datepot=~], noun.

// *** !!!!! special ESTER 

// attention, règle à enlever peut-être
// un num commençant par un chiffre supérieur à 3 n'est pas une date
2> num[lemme:"[3,4,5,6,7,8,9][%d%d%d]",nondate=+,datepot=~].

// corpus oral : 600 mille
//2> num[nondate=+]{num[lemme:"%d%d%d",chiffres,nondate=+],num[lemme:"mille",nondate=+]}.

// 19 cent 97
//2> num[datepot=+,time=+]{num[lemme:"19",chiffres],num[lemme:"cent",lettres],num[chiffres]}.


// à commenter si besoin
// penser à gerer autrement les choses comme "la création de 1600 emplois"
// Tout ce n'est pas précédé de "en" ou "année" ou autre et qui ne commence pas par 19 ou 20 n'est pas une date
2> ?[predate:~],num[lemme:"[1,2][1,2,3,4,5,6,7,8][%d%d]",nondate=+,datepot=~].


//*** fin special ester

Séquence:

// "vingt et un" est un nombre
// Maud:
// - ajout négation de ou pour coord
// - "30 et 31" n'est pas un nombre

/un milliard 800 millions est un nombre


2> Num [gdnbre=+] = num[noun:+],(punct[form:fhyph]), coord[lemme:~"ou"], (punct[form:fhyph]),num[lemme:"1"].

/ traitement de "entre 18 mille et 50 mille francs" \

3> Num [gdnbre=+] = |prep[form:fentre]| num[noun:+,lemme:~"mi"], num[noun:+], (num[noun:+]) | coord | .

/deux noms de nombres font un seul nombre (quatre mille cinq-cent)\
// maud : ajout element final qui ne doit pas être un nom de mois --> à surveiller

//CB: le 18 20 continue --> num[18 20 continue]
//2> Num [gdnbre=+] @= num[noun:+,lemme:~"mi"], num[noun:+], (num[noun:+]), (punct[hyph:+], num[noun:+]), (c//oord, num),?[mois:~].


/les mois musulmans composés\
3> noun[time=+, mwe=+, sg=+, mois=+,month=+]= noun[mois1:+], (punct[lemme:']), (punct[form:fhyph]), noun[mois2:+].

/Jésus Christ = Jésus-Christ\
3> noun[form=fjc, masc=+, sg=+, proper=+,p3=+] = noun[lemme:Jésus], noun[lemme:Christ].



// MODIFS Maud :
// Sur reconnaissance des dates et traits assignés


//--------------------------------
//       DATES "CLASSIQUES" 
//--------------------------------


//******************************
//*** DATES ABSOLUES (avec annee spécifiée)

// Date absolue complète
// (lundi) 12 mars 1987
// AB 2012.04.12 prep, noun[form:fjc] was optional, created second rule with restriction on right context (no plural noun).
// This avoids "une bombe a tué noun{le 27 janvier 19} personnes" but allows "une bombe a tué noun{le 27 janvier 19} dix personnes"
// AB 2012.05.03 added [num], punct[form:fhyph] to grab first day in interval ex. 5-10 juin 2002
4> noun[date=+,time=+,mwe=+,complete=+,absolue=+] @=  (noun[jour];[num], punct[form:fhyph]), num[numjourpot];num[lemme:"premier"], noun[mois],  num[gdnbre:+,datepot=~,annee=+,year=+], prep, noun[form:fjc].
4> noun[date=+,time=+,mwe=+,complete=+,absolue=+] @=  (noun[jour];[num],(punct[form:fhyph])), num[numjourpot];num[lemme:"premier"], noun[mois],  num[gdnbre:+,datepot=~,annee=+,year=+] ~| noun[pl] | .

// Date absolue incomplète

// excep : interval - juillet-août 1990:
// AB 2011.03.15 ajout de date=+
4> NP[date=+,interval=+,time=+,mwe=+,relative=~] @=  noun[mois,date=+], punct[hyph:+], noun[mois,date=+], num[gdnbre,datepot=~,annee=+,year=+].

// mi-mars 1989
4> noun[date=+,time=+,mwe=+,absolue=+] @=  prefix[lemme:"mi-"], noun[mois],num[gdnbre,datepot=~,annee=+,year=+].


// mars 1987
4> noun[date=+,time=+,mwe=+,partial_date=+,absolue=+] @=  noun[mois],  num[shortyear:~,gdnbre,datepot=~,annee=+,year=+].

// CH - Jan 2012
// mars 10 (si pas suivi par nom pluriel non déterminé)
// e.g. ils ont expulsé en mars 10 agents russes
4> noun[date=+,time=+,mwe=+,partial_date=+,absolue=+] @=  noun[mois],  num[shortyear:+,gdnbre,datepot=~,annee=+,year=+] | ?[noun:~]; ?[pl:~] | .

// construire NP date avant chunking pour les années qui sont des num en coordination avec un noun
// ex. il était 3e en 2009 et vainqueur en 2011
4> NP = | prep[predate] | num[chiffres,gdnbre,noun] | coord, noun |.

///////////////////////
// DATES INCOMPLETES //
///////////////////////

// AB 2011.11.09 on distingue désormais les dates incomplètes des autres dates relatives (trait partial_date:+ au lieu de relative:+)
// ATTENTION: le trait partial_date apparaît sur des expressions avec une année comme "en 2002". Il désigne des dates qui manque un élément, que ce soit le jour, le mois ou l'année (donc le format complete est jour, mois et année).

// (lundi) 25 avril au matin
5> noun[mwe=+,tempexpr=+,date=+,daytime=+,partial_date=+,incomplete=+]@= (noun[jour]), num[numjourpot];num[lemme:"premier"], noun[mois], prep[form:fa],noun[daytime].

// (lundi) 15 mars dernier
5> noun[date=+,time=+,mwe=+,partial_date=+,incomplete=+] @= (noun[jour]), num[numjourpot];num[lemme:"premier"], noun[mois], adj[timemod:+].

//(lundi) 15 mars 
5> noun[date=+,time=+,mwe=+,partial_date=+,incomplete=+] @= (noun[jour]), num[numjourpot];num[lemme:"premier"], noun[mois].

// dimanche matin
5> noun[mwe=+,tempexpr=+,date=+,daytime=+,partial_date=+,incomplete=+]@= noun[jour],noun[daytime].

// (le) dimanche (num) au matin
5> noun[mwe=+,tempexpr=+,date=+,daytime=+,partial_date=+,incomplete=+]@= noun[jour], (num), prep[form:fa],noun[daytime].

// lundi 15 
5> noun[date=+,time=+,mwe=+,partial_date=+,incomplete=+] @= noun[jour,sg], num[numjourpot];num[lemme:"premier"]|?[nat:~,noun:~]|.

// lundis 15 
5> noun[date=+,time=+,mwe=+,partial_date=+,pl=+,incomplete=+] @= noun[jour,pl], num[numjourpot];num[lemme:"premier"]|?[nat:~,noun:~]|.


//******************************
//*** DATES RELATIVES

// au plus tard (ce/le) mardi
5> noun[date=+,time=+,mwe=+,relative=+] @=  adv[t_mod],(det), noun[jour,sg].

// mardi au plus tard
5> noun[date=+,time=+,mwe=+,relative=+] @=  noun[jour,sg], adv[t_mod].


// mi-mars
5> noun[date=+,time=+,mwe=+,relative=+] @=  prefix[lemme:"mi-"], noun[mois], (adj[timemod]).

//300 ap. JC
5> noun[mwe=+, date=+] = num[date=+], prep, noun[form:fjc].
5> noun[mwe=+, date=+] = num[date=+], prep, det[poss:+], noun[lemme:ère].


Marqueur:

// lundis: au pluriel et sans chiffre derriere ==> generique
//9> ?[setsignal:~],noun[jour,pl,generic=+],?[num:~,predate:~,timemod:~].

// la planète Mars n'est pas une date
7> noun[lemme:"planète"], noun[lemme:"Mars",mois=~,time=~].

// lundi,mars
7> ?[pren:~,num:~,mois:~,setsignal:~,pl:~], noun[jour,date=+,time=+,relative=+];noun[mois, date=+,time=+,relative=+,prenamb:~].

// AB 2011.11.29 lundi, mars précédé d'un verbe. certains verbes (ex. promis) ont le trait pl:+ qui empêchait la règle précédente de s'appliquer
7> ?[verb,noun:~,det:~,adj:~,prep:~], noun[jour,date=+,time=+,relative=+];noun[mois, date=+,time=+,relative=+,prenamb:~].

// idem en début de phrase
7> noun[start:+,jour,date=+,time=+,relative=+], ?[num:~].
7> noun[start:+,mois, date=+,time=+,relative=+], ?[num:~].



//------------------------------------
// ANNEES SEULES (non reconnues avant)
//------------------------------------

//**************
// Marquage en "datepot" des suites de 4 chiffres, desamb en contexte au fur et à mesure ensuite.
// à savoir : deux chiffres sont marqués comme "shortyear" (cf. plus haut)

// un nombre de 4 chiffres commençant par 19 ou 20 est potentiellemnt une date

10> num[lemme:"19%d%d", 20e=+,datepot=+,time=+,absolue=+,partial_date=+].
//,?[heure:~,periode:~,unit_monnaie:~,unit_len:~,percent:~,measure:~,nondate:~] .

10> num[lemme:"20%d%d", 21e=+,datepot=+,time=+,absolue=+,partial_date=+].
//,?[heure:~,periode:~,unit_monnaie:~,unit_len:~,percent:~,measure:~,nondate:~] .

// et même un nombre de 4 chiffres
// tout ceci reste en datepot sauf si moyen de désambiguiser plus loin
10>?[num:~], num[lemme:"%d%d%d%d",nondate:~,datepot=+,time=+,absolue=+,partial_date=+], ?[unit_monnaie:~,unit_len:~,num:~].




//*******************
//*** Avec Contexte

// Dates avec annee seule : absolue incomplète


// *** DATES à 4 chiffres


// une date seule précédée d'un événement :
// cette règle ne prend en compte que les evenements marqués ds multiwords.xip
// pour le reste c'est  a la fin localgram_event.
// ex : les Jeux Olympiques d'hiver 2004
10> noun[sport,mwe,event],(prep[form:fde]),num[gdnbre, date=+,datepot=~,annee=+,year=+].
    

// en 1989, vers 1989, dès 1989, printemps 1989: marquer comme date

// en 2004 et 2005
10> ?[predate], num[shortyear:~,gdnbre,nondate:~,time=+,datepot=~,date=+,annee=+,year=+,absolue=+,partial_date=+],  coord, num[shortyear:~,gdnbre,time=+,datepot=~,date=+,annee=+,year=+,absolue=+,partial_date=+].

// en 2004
10> ?[predate], num[shortyear:~,gdnbre,nondate:~,time=+,datepot,date=+,annee=+,year=+,absolue=+,partial_date=+], ?[heure:~,periode:~,unit_monnaie:~,unit_len:~,percent:~,measure:~,nondate:~] .

// AB 2012.03.23 depuis...1950.
10> ?[predate], punct[lemme:"..."], num[shortyear:~,gdnbre,nondate:~,time=+,datepot,date=+,annee=+,year=+,absolue=+,partial_date=+], sent[end].

10> ?[predate], num[shortyear:~,gdnbre,nondate:~,time=+,datepot=~,date=+,annee=+,year=+,absolue=+,partial_date=+], (punct[form:fcm]), (adv), ?[heure:~,periode:~,unit_monnaie:~,unit_len:~,percent:~,measure:~,num:~,noun] .


Séquence:

// à l'horizon 2008
11> adv[mwe=+,date=+,partial_date=+,absolue=+] @= adv[lemme:"à l'horizon"], num[shortyear:~,gdnbre,nondate:~,time=+,datepot=~,date=+,annee=+,year=+,absolue=+,partial_date=+].


Marqueur:

// idem mais en fin de phrase :  en 2004.
12> ?[predate], num[end:+,shortyear:~,gdnbre,nondate:~,time=+,datepot=~,date=+,annee=+,year=+,absolue=+,partial_date=+].



//*** DATES à 2 chiffres: shortyear


// pour les shortyears : contexte gauche plus restreint, ne pas prendre tous les [predate] mais seulement qlqs preps

//12> prep[predate], num[shortyear:+,gdnbre,time=+,datepot=~,date=+,annee=+,year=+,absolue=+,partial_date=+], ?[heure:~,periode:~,unit_monnaie:~,unit_len:~,percent:~,measure:~,nondate:~] .

// AB 2012.04.12 added time:+ feature to avoid "en 17 matches"
12>  prep[form:fen], num[shortyear:+,gdnbre,time=+,datepot=~,date=+,annee=+,year=+,absolue=+,partial_date=+], ?[heure:~,periode:~,unit_monnaie:~,unit_len:~,percent:~,measure:~,nondate:~,time] .

12>  prep[form:fdepuis], num[shortyear:+,gdnbre,time=+,datepot=~,date=+,annee=+,year=+,absolue=+,partial_date=+], ?[heure:~,periode:~,unit_monnaie:~,unit_len:~,percent:~,measure:~,nondate:~] .


// idem mais en fin de phrase
12> prep[form:fen], num[end,shortyear:+,gdnbre,time=+,datepot=~,date=+,annee=+,year=+,absolue=+,partial_date=+].


//*** AUTRES


// de 1998 à 2009 : config qui fait passer de datepot à date
12> prep, num[shortyear:~,gdnbre,nondate:~,time=+,datepot=~,date=+,annee=+,year=+,absolue=+,partial_date=+], prep[form:fa], num[shortyear:~,gdnbre,nondate:~,time=+,datepot=~,date=+,annee=+,year=+,absolue=+,partial_date=+].


// AB 2011.03.15 d'ici (à) 2012 for the time being gets same treatment as "jusqu'à 2012"
12> prep[sfde], adv[lemme:ici], (prep[sfa]), num[datepot=~,date=+,annee=+,year=+,gdnbre=+,absolue=+,tfin=+,partial_date=+].

// l'événement de 1989, la famine de 2006
// attention : le score de 7 6 , 6 2 = pas une date
// CH added a restriction: the number must be a 4 digit number. May be not enough
12> noun[nondate:~],prep[form:fde],num[shortyear:~,gdnbre,nondate:~,time=+,datepot=~,date=+,annee=+,year=+,absolue=+,partial_date=+, lemme:"%d%d%d%d"], ?[heure:~,periode:~,unit_monnaie:~,unit_len:~,percent:~,measure:~,noun:~] .

// dater de +annee:
12> verb [lemme:dater], prep[form:fde], num[gdnbre:+,date=+,absolue=+,partial_date=+,datepot=~,annee=+,year=+].
 
// 6-21 n'est pas une date:
12> adv[lemme:"%d-%d%d", date=~].



Séquence:

// mi-1989
13> noun[mwe=+,date=+,partial_date=+,absolue=+] @= prefix[lemme:"mi-"];?[lemme:"mi"],num[gdnbre1,date=+].
// pour les autres (mi-septembre): dans marqueur_temp.xip


//  les années 20 (et) 30 = DATE (relative pour shortyear)
13> noun[mwe=+,time=+, date=+,tempexpr=+,relative=+] @=  noun[lemme:année], num[shortyear:+], (coord), num[shortyear:+].

//  les années 1920 (et) 1921) = DATE (absolue sans shortyear)
13> noun[mwe=+,time=+, date=+,tempexpr=+,absolue=+] @=  noun[lemme:année], num[shortyear:~], (coord), num[shortyear:~].



// l'année (au singulier) 2004 - empty_introd sur le mwe pour calcul correct de la tete
13> noun[mwe=+,time=+, date=+,tempexpr=+,absolue=+,empty_introd=+] @=  noun[lemme:année,sg:+], num[shortyear:~].

// AB 2012.04.11 les années 1930-40
13>  noun[mwe=+,time=+, date=+,tempexpr=+,absolue=+] @=  noun[lemme:année,pl:+], num, punct[form:fhyph], num[shortyear].

// rectif CH
// les années 1920  = DATE (absolue sans shortyear) ou les années 20
13>  noun[mwe=+,time=+, date=+,tempexpr=+,absolue=+] @=  noun[lemme:année,pl:+], num .


// un jour en huit : en huit = adjectif 
13> adj[timemod=+,temp_ref=s] @= |noun[jour]| prep[form:fen], num[lemme:"huit"].


// XVIIIe siècle ou dix-huitième siècle
16> noun[mwe=+,date=+,absolue=+,tempexpr=+] @= adj[ord:+],noun[lemme:"siècle"].



/*********Fin modifs maud*****\



/le 9 thermidor an II\
14> noun[mwe=+,time=+, date=+] = noun[date:+], noun[lemme:an], num.


/**************** PERIODES *********************\

Marqueur:

15> noun[lemme:renaissance, maj:+, sg:+, periode=+].
15> noun[lemme:antiquité, maj:+, sg:+, periode=+].

//s. n'est l'abréviation de siècle que s'il est en minuscule
15> noun[lemme:"s.", maj:+, siecle=~].

Séquence:

//le IVe siècle (avant JC) et début IIIe
16> noun[tempexpr=+,date=+,absolue=+,mwe=+,time=+, periode=+] @= adj[ord:+];num[rom:+], noun[siecle:+], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]), coord, (det[def:+];prep[def:+]), (noun[predate]), adj[ord:+];num[rom:+], (noun[siecle:+]), (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).

//le IVe (et le IIIe) siècle (av. JC)
16> noun[tempexpr=+,date=+,absolue=+,mwe=+,time=+, periode=+] @= adj[ord:+];num[rom:+], (coord, (det[def:+];prep[def:+]), (noun[predate]), (det[def:+];prep[def:+]), adj[ord:+];num[rom:+]), noun[siecle:+], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).

//15> noun[mwe=+,time=+, periode=+] @= num, noun[siecle:+, sg], (prep, noun[form:fjc]), (prep, det[poss:+], noun[lemme:ère]).

//l'âge du fer
16> noun[mwe=+,time=+, periode=+] @= |det[def:+]| noun[lemme:âge], prep[form:fde], (det[def:+]), noun[age:+].

//période néolithique
16> noun[mwe=+, time=+, periode=+] = noun[ere], adj[periode:+];adj[periode2:+].

//l'Ancien Empire
16> noun[mwe=+, time=+, periode=+] @= ?[empire1, maj], noun[lemme:empire, maj], (adj[empire2]).




Marqueur:


// maud : pour enlever le trait datepot au élément ayant été reconnus comme des dates :
17> ?[date, datepot=~].




// CH rajout pour prendre en compte des coordinations restreintes
// eg ceux nés en 84 et 85 (seul 84 est marqué)
20> num[shortyear:+, time:+, date:+], coord, num[shortyear:+, date=+, time=+, annee=+,year=+, absolue=+, partial_date=+] .


Séquence:

// AB 2012.05.11 some date NPs need to be built before the chunker
// e.g. en NP{2011 et 2012}
21> NP[tempexpr=+,date=+,absolue=+,partial_date=+] @= | ?[predate] | num[partial_date,absolue,annee,date], coord, num[partial_date,absolue,annee,date] | noun[guessed,sg] |.

// AB 2012.05.11 build date NP before chunker when second potential date after coordination is followed by a bare plural noun
// e.g. en 2011 et 2013 pommes
21> NP[tempexpr=+,date=+,absolue=+,partial_date=+] @= | ?[predate] | num[partial_date,absolue,annee,date] | coord, num[partial_date,absolue,annee,date], noun[pl] | .

