//! Xerox Research Centre Europe - Grenoble Laboratory
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!
//! Copyright (c) 2000 - 2011 Xerox Corporation.  All Rights Reserved.
//!
//! André Bittar 2011.11.25 : file copied from TEMPESTER/CHUNKING/chunker_temp.xip
//! Modified.
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Séquence:

///////////////
// INTERVALS //
///////////////

// AB 2012.05.11 on ne met plus les adjectifs vu les problèmes
// maintenant traité comme AP voir marqueur_temp.xip l.53   
// du 4 au 6 janvier
//1> PP[tempexpr=+,interval=+,date=+] @= PP[form:fde], PP[tempexpr,form:fa,date,!start:!], (AP).
//1> PP[tempexpr=+,interval=+,heure=+] @= PP[form:fde], PP[tempexpr,form:fa,date,start:!], (AP).


// Pour les PP contenant explicitement une tempxr et donc déjà marqués avec cette
// info par marqueur_temp.xip
// i.e. "du 3 janvier au 5 janvier"
1> PP[tempexpr=+,interval=+,date=+] = PP[tempexpr,form:fde,date],PP[tempexpr,form:fa,date]. 
1> PP[tempexpr=+,interval=+,heure=+] = PP[tempexpr,form:fde,heure],PP[tempexpr,form:fa,heure]. 

//des....à
1> PP[tempexpr=+,interval=+,date=+] = PP[tempexpr,form:fdesaccent,date],PP[tempexpr,form:fa,date]. 


//entre...et
1> PP[tempexpr=+,interval=+,date=+] = PP[tempexpr,form:fentre],COORD[form:fet],NP[tempexpr,date]. 
1> PP[tempexpr=+,interval=+,heure=+] = PP[tempexpr,form:fentre],COORD[form:fet],NP[tempexpr,heure]. 

// entre minuit et 1h heure locale
1> ADV[tempexpr=+,interval=+,heure=+] = ADV[interval], NP[heure_extens].

// Avec des ellipses : 

// entre le 3 et le 5 janvier
1> PP[tempexpr=+,interval=+,date=+] @= PP[form:fentre],COORD,NP[tempexpr]. 

// traité plus haut non?
//du 15 au 25 janvier
1> PP[tempexpr=+,interval=+,relative=+,date=+] @= PP[form:fde,tempexpr],PP[tempexpr,date,relative]. 

//du 15 au 25 janvier 2008
1> PP[tempexpr=+,interval=+,absolue=+,date=+] @= PP[form:fde,tempexpr],PP[tempexpr,date,absolue]. 

///////////
// DATES //
///////////

// le premier dimanche du printemps
1> NP[tempexpr=+,date=+,relative=+] @= NP[date,relative], PP[form:fde,tempexpr].
// le premier dimanche du mois
1> NP[tempexpr=+,date=+,relative=+] @= NP[date,relative], PP[form:fde,time].

// pour demain, à partir de demain [prep+adv]
1> PP[tempexpr=+,date=+,relative=+]@= prep,noun[relative].
//1> PP[tempexpr=+,date=+,relative=+]@= prep,adv[tempexpr, relative].

// pendant les mois d'été
1> PP[tempexpr=+,date=+] @= PP[date,ily:~], PP[form:fde,sg,tempexpr].

// la fin de la journée
1> NP[tempexpr=+,date=+,heure=+] @= NP[t_mod], PP[tempexpr,daytime,date,heure].
// avant la fin de la journée
1> PP[tempexpr=+,date=+,heure=+] @= PP[t_mod], PP[tempexpr,daytime,date,heure].

// quelques heures plus tôt
1> NP[tempexpr=+,heure=+,time=+] @= NP[tempexpr,heure,time], ADV[postdate].

// peu avant 1h
1> PP[tempexpr=+,heure=+] @= adv[lemme:peu], PP[tempexpr,heure] .

// peu après une heure (peu après and peu avant are not segmented the same way)
1> PP[tempexpr=+,heure=+] @= adv[lemme:"peu après"], NP[tempexpr,heure] .

// la nuit de la Saint-Sylvestre
1> NP[daytime=+,date=+,tempexpr=+,relative=+] @= NP[daytime,date,tempexpr], PP[date,relative,tempexpr,form:fde] .

// le 14 janvier de l'année dernière
1> NP[tempexpr=+,relative=+,date=+] @= NP[partial_date,incomplete,tempexpr,date], PP[relative,date,tempexpr,form:fde].

// le 14 janvier de l'année 2002
1> NP[tempexpr=+,absolue=+,date=+] @= NP[partial_date,incomplete,tempexpr,date], PP[absolue,date,tempexpr,form:fde].

// les dernières secondes de 2003
1> NP[tempexpr=+,absolue=+,date=+] @= NP[tempexpr,heure], PP[tempexpr,date,absolue,partial_date,form:fde].

// les dernières secondes de l'année dernière
1> NP[tempexpr=+,relative=+,date=+] @= NP[tempexpr,heure], PP[tempexpr,date,relative,form:fde].

// AB 2012.05.11 on ne met plus les adjectifs vu les problèmes
// une année 2003 tumultueuse - WARNING: having the AP in the NP affects the calculation of other dependencies
//1> NP[tempexpr=+,absolue=+,date=+] @= NP[tempexpr,date,absolue], AP .

// AB 2012.05.11 on ne met plus les adjectifs vu les problèmes
// après une année 2003 tumultueuse - WARNING: see above, although perhaps ok for PP
//1> PP[tempexpr=+,date=+,absolue=+] @= PP[tempexpr,date,absolue], AP .

// 12h30 (11h30 GMT)
1> PP[relative=+,tempexpr=+,heure=+,date=+] @= PP[relative,tempexpr,heure], INS[timezone] .

// le début de cette merveilleuse année 2003
1> NP[tempexpr=+,date=+,absolue=+,t_mod=+] @= NP[t_mod], PP[tempexpr,date,absolue,form:fde] .

// jusqu'au début de cette merveilleuse année 2003
1> PP[tempexpr=+,date=+,absolue=+,t_mod=+] @= PP[t_mod], PP[tempexpr,date,absolue,form:fde] . 

1> PP[tempexpr=+,date=+,partial_date=+,absolue=+] @= PP[partial_date,absolue], punct[form:fcm], NP[partial_date,absolue], coord, NP[partial_date,absolue] | sent | .

///////////////
// DURATIONS //
///////////////

// trois mois de prison
1> NP[duree=+,tempexpr=+,date=+,time=+] @= NP[duree,tempexpr,date], PP[form:fde,tempexpr:~] .

// AB 2012.05.11 on ne met plus les adjectifs vu les problèmes
// include adjective
//1> PP[tempexpr=+,duree=+,date=+,time=+] @= PP[duree,date,time], AP .

// AB 2012.05.11 on ne met plus les adjectifs vu les problèmes
// AB 2012.04.11 on peut pas admettre un truc aussi radical, mais comment traiter "3 longs mois de négociations secrètes" (un seul TIMEX3 selon schéma d'annotation)? ex. il se limite à faire pendant trois mois des dons aux associations - ça va prendre "trois mois des dons" comme durée
//1> NP[tempexpr=+,duree=+,date=+,time=+] @= NP[duree], PP[form:fde], (AP) .
//1> PP[tempexpr=+,duree=+,date=+,time=+] @= PP[duree], PP[form:fde], (AP) .

////////////////
// AGGREGATES //
////////////////

1> PP[tempexpr=+,date=+] @= PP[date], coord, NP[date] | sent | .


////*************ESTER ESTER*****************
// expressions complexes à rassembler

// en septembre et octobre prochains
2> PP[tempexpr=+,date=+,relative=+] @= PP[tempexpr,date,relative],COORD,NP[tempexpr,date,relative]. 

// elle chante 18 heures par jour
2> ADV[tempexpr=+,duree=+]@= NP[duree],NP[tempexpr,set].

// AB 2012.04.11 too general
//fin décembre à début janvier: NP + PP
//2> ADV[tempexpr=+,date=+,relative=+]@= NP[date,relative],PP[date,relative,heure:~].

// CH - juin 2009
// Groupement SET + DATE[relative,de]
// eg. tous les matins de mai
5> NP[set=+,complex_et=+,tempexpr=+] @= NP[set:+], PP[date,relative,form:fde].




// CH (janvier 2012)
// rajout necessaire pour ne pas perdre le trait start lors des recompositions de chunks temporels complexes


Déduction:

// e.g. du 23 au 25 juin
| PP[tempexpr, start=+]{PP[start:+];NP[start:+]} |
~

// rajouts du trait start suite aux regles de splitting et recomposition
| ?[start:~,start=+]{?[fstmod,start:+,first:+]} |
~
