//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

 
Marqueur:
1> num,punct[form:fcpar], ?[debut=+].

/l'Ancien Empire Egyptien\
1> adj[empire1:+, aadj=+], noun[lemme:empire, maj].
2> noun[lemme:empire, maj], adj[maj, aadj=+].

/le parti Socialiste\
2> ?[def], (adj), noun[parti];noun[orgadj], adj[maj, aadj=+].
2> det, (adj), noun[parti];noun[orgadj], adj[maj, aadj=+].

Désambiguïsation:
// Festival International --> International=adj

3> noun<lemme:"international">, adj = | noun | adj.

3> verb<inf>, noun = |verb[lemme:venir], prep[form:fde]| verb.

/neuf est un chiffre devant un nom de mois\
3> (adj,num) %= num |noun[mois:+]|.

/L'Eglise Réformée de France : réformée est un adjectif\
5> (adj<postprof>, verb) = |noun[org1];noun[prof]| adj.

/un mot avec majuscule qui a été reconnu comme un verbe est un nom de personne s'il suit un nom de type Monsieur ou un prénom\
5> verb[maj:+, phrasemaj:~, noun:~] += |?[tit:+];noun[pren:+], (?[partic:+])| noun[proper=+,person:+, masc=+,fem=+, sg=+,pl=+,p3=+,maj=+, CR:~].
5> adj[maj:+, phrasemaj:~, noun:~] += |?[tit:+];noun[pren:+], (?[partic:+])| noun[proper=+,person:+, masc=+,fem=+, sg=+,pl=+,p3=+,maj=+, CR:~].


//seconde dans seconde guerre mondiale

5> noun<lemme:"seconde">,adj = adj[ord=+] | noun[guerre] |.

/un mot qui peut être verbe/adj ou nom avec maj. est un nom qd il commence par une majuscule hors début de ligne\

//7> verb<surface:~"DU">, noun <maj:+, phrasemaj:~, start:~, CR:~> = noun.
//7> verb[maj:+, toutmaj:~,phrasemaj:~, start:~, CR:~, debut:~] %= |?[org1:~]| noun[proper=+, maj=+].

7> verb[maj:+, toutmaj:+,phrasemaj:~, start:~, CR:~, debut:~,prep:~] %= |?[org1:~]| noun[proper=+, maj=+,toutmaj=+].
7> (adj, noun) [aadj:~, maj:+, phrasemaj:~, start:~, CR:~,prep:~] = ?[noun:+].
/un mot qui peut être verbe ou noun(prénom) avec maj. est un nom qd il précède un mot avec majuscule\
7> verb, noun <maj:+, phrasemaj:~, pren:+> = noun |?[maj]|.

/le conseiller belge, le reporter Untel\
7> verb, noun = |det| noun |noun;adj[nat]|.

/les 16-18 ans ; vers 10-11 heures ne sont pas des dates\
7> adv<date:+> %= adv[date=-] |noun[lemme:an];noun[lemme:heure]|.

/choisit l'interprétation prénom d'un mot ambigu suivi d'un mot inconnu avec majuscule\
8> noun<pren:+, prenamb:~,maj:+,proper>, noun<proper:~> = noun <person=+,proper> |noun[maj:+, guessed:+]|.

/Mars est nom de mois dans 'en Mars 1986'\
8> (noun<lemme:mars>, noun<lemme:Mars>) [maj, toutmaj:~] = |?[lemme:en]| noun[lemme:mars].

/Paris est une ville dans 'de Paris'\
8> (noun<lemme:"%c+">, noun<lemme:"%C%c+">) [maj, toutmaj:~] = |prep[def:~]| noun [lemme:"%C%c+"].

/Olympia n'est pas une ville avec l'article\
8> (noun<ville>, noun<org>) [maj, toutmaj:~] = |det;prep[def]| noun[org].
/Olympia n'est pas un prénom avec l'article\
9> (noun<pren>, noun<org>) [maj, toutmaj:~] = |det;prep[def]| noun[org].

/Libération des otages\
8> (noun<lemme:libération>, noun<lemme:Libération>) [maj] = noun[lemme:libération] |prep[form:fde]|.

/coordination de noms avec and\
8> noun[maj] = | noun[verb:~],adj*, ?[lemme:and], det*, adj*| noun.

/de l'image et du son\
8> noun, det = |?[def]| noun.

/rue est un nom entre un chiffre et un mot majuscule\
8> noun<lemme:rue>, verb = |num| ?[verb:~] |?[maj, CR:~]|.

/dernier roman --> roman=noun\

8> noun<lemme:roman>, adj = | adj | noun.

/producteur de\

8>noun<lemme:producteur>,adj = noun | prep[sfde]|.

/tchao Pantin\

noun<lemme:"Pantin">, noun<lemme:"pantin"> = | noun[lemme:"tchao"];noun[lemme:"TCHAO"] | noun[lemme:"pantin"]  .

/un | le César | Oscar\

8>noun<lemme:"César"> = | det | noun[pren=~].
8>noun<lemme:"Oscar"> = | det | noun[pren=~].

/ Jean Bernard POMMIER (cf INA)\
/8> noun<toutmaj> = | noun[pren] | noun[proper=+].\


8>noun<lemme:"Ivry"> =  noun[pren=+,ville=~] | noun[lemme:"Gitlis"] |.


/en France Jean --> France ~pren \
8>noun<pren,pays> = | prep[lemme:"en"] | noun[pren=~].

/Salvador Dali\

8>noun<lemme:"Salvador"> = noun[pren=+,pays=~] | noun[lemme:"Dali"] | .

/ Sydney Bechet\ 

8>noun<lemme:"Sydney"> = noun[pren=+,pays=~] | noun[lemme:"Bechet"] | .

/Zizi Jeanmaire\

8>noun<lemme:"zizi">  = noun[pren=+] | noun[lemme:"Jeanmaire"] | .

/L'Aube enchante\

8>noun<lemme:"Aube">, noun<lemme:"aube"> = noun[lemme:"aube",region=~,lieu=~] | adj |.


/ Aime \

8> noun<pren,prenamb,pays:~> = |?[maj:~]|  noun[prenamb=~] | noun[guessed];noun[proper] | .


/ la piece de theatre de Moliere: theatre de Moliere n'est pas un batiment\

9> noun<lemme:"théâtre"> = |noun[lemme:"pièce"], prep[form:fde] | noun[batimentorg1=~,pretitre=+].
 
Marqueur: 
10> Noun[lemme:"%c", maj:+, lettre=+].
10> Noun[lemme:"%C%d+", nonorg=+].
10> Noun[lemme:"RN%d+", nonorg=+].

/de Polytechnique\
10> prep, noun[lemme:polytechnique, maj:+, org=+].


