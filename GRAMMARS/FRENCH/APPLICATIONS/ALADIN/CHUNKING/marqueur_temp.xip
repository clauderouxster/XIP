//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

// EN COURS


Déduction:

//Preposition: form de PREP sur PP
| PP[form=fde]{prep[lemme:de]} | 
~

| PP[form=fa]{prep[lemme:à]} | 
~

| PP[form=fa]{prep[lemme:au]} | 
~

| PP[form=fentre]{prep[lemme:entre]} | 
~

| PP[form=fet]{prep[lemme:et]} | 
~

| PP[form=fdepuis]{prep[lemme:depuis]} | 
~

| PP[form=fjusque]{prep[form:fjusque]} | 
~

| PP[form=fde]{prep[form:fde]} | 
~

| PP[form=fdesaccent]{prep[form:fdesaccent]} | 
~

Marqueur:

//**********NOMINAL CHUNKS******************x**

// Pour marquer certains NP avec des traits temp.
// Ces NP sont déjà construits par le chunker.
// Pour le chunking d'autres expressions temporelles
// non gérées par chunker.xip, voir plus loin chunker_temp.xip
// ou plus haut localgram_temp.xip


// DATES ABSOLUES
// Bcp de distinctions, pour faire plus simple:
// absolue vs. relative seulement.

// absolue complete
1> NP[date=+,absolue=+,complete=+,!tempexpr=+]{?*,noun[mwe:+,date:+,absolue:+,complete:+,last:+,datecoord:~]}.

// absolue incomplete
1> NP[date=+,absolue=+,incomplete=+,!tempexpr=+]{?*,noun[mwe:+,date:+,absolue:+,incomplete:+,last:+,datecoord:~]}.

1> NP[date=+,absolue=+,incomplete=+,!tempexpr=+]{num[date:+,absolue:+,incomplete:+,last:+,datecoord:~]}.

1> NP[datepot=+,absolue=+,incomplete=+,!tempexpr=+]{num[datepot:+,absolue:+,incomplete:+,last:+,datecoord:~]}.

// ni l'un ni l'autre
1> NP[date=+,absolue=+,!tempexpr=+]{?*,noun[mwe:+,date:+,absolue:+,incomplete:~,complete:~,last:+,datecoord:~]}.

// absolue coord
1> NP[date=+,absolue=+,datecoord=+,!tempexpr=+]{?*,noun[mwe:+,date:+,absolue:+,incomplete:~,complete:~,last:+,datecoord:+]}.


// DATES RELATIVES

1> NP[date=+,relative=+,!tempexpr=+]{?*,noun[mwe:+,date:+,relative:+,last:+,datecoord:~,date_event:~]}.

1> NP[date=+,relative=+,!tempexpr=+]{?+,noun[mwe:~,date:+,relative:+,last:+,datecoord:~,date_event:~]}.

1> NP[date=+,relative=+,!tempexpr=+]{noun[date:+,relative:+,first:+,last:+,datecoord:~,date_event:~]}.

1> PP[date=+,relative=+,tempexpr=+]{prep,?*,np{?*,noun[date:+,relative:+,last:+,datecoord:~,date_event:~]}}.
//1> PP[date=+,relative=+]{prep,np{noun[date:+,relative:+,first:+,last:+,datecoord:~,date_event:~]}}.

// relative coord
1> NP[date=+,relative=+,datecoord=+,!tempexpr=+]{?*,noun[mwe:+,date:+,relative:+,last:+,datecoord:+]}.

// avec des adverbes seulement : le lendemain, pour aujourd'hui etc.
1> NP[date=+,relative=+,!tempexpr=+]{?*,noun[tempexpr:+,relative:+,last:+,datecoord:~,date_event:~]}.


// HEURES
// daytime

//modif special ESTER qui considère "matin,soir,etc." comme des dates et non comme des heures
//1> NP[heure=+,daytime=+,!tempexpr=+] {?*,noun[daytime,heure:+]}.
//1> NP[date=+,relative=+,!tempexpr=+] {?*,noun[daytime,heure:+]}.

//enlever daytime:~ : spécial ester
1> NP[interval:~,heure=+,!tempexpr=+] {?*,noun[mwe:+,heure:+,duree:~,last:+,daytime:~]}.

1> NP[heure=+,!tempexpr=+] {?*,noun[heure:+,duree:~,periode:~,last:+,daytime:~]}.

//intervalles

1> NP[interval=+,!tempexpr=+,date=+] {num[interval:+,date]}.


// DUREE-HEURE

1> NP[heure=+,duree=+,!tempexpr=+] {?*,noun[mwe:+,heure:+,duree:+,last:+]}.


// DUREE-DATE

1> NP[date=+,duree=+,!tempexpr=+] {?*,noun[mwe:+,date:+,duree:+,last:+]}.

1> NP[date=+,duree=+,!tempexpr=+] {?*,noun[date:+,duree:+,last:+]}.

//1> PP[date=+,duree=+] {PP{PREP,NP{?*,noun[mwe:+,date:+,duree:+,last:+]}}}.


// DATES ABSOLUES ET  RELATIVES

1> PP[date=+, absolue=+, t_mod=approx]{prep[first,predate:+,t_mod:approx],NP[date:+,absolue:+,last]}.

1> PP[date=+, relative=+, t_mod=approx]{prep[first,predate:+,t_mod:approx],NP[date:+,relative:+,last]}.


1> PP[date=+]{prep[t_mod:~approx,orig:~,tfin:~],NP[date:+,duree:~]}.

1> PP[date=+,absolue=+]{prep[t_mod:~approx,orig:~,tfin:~],NP[date:+,absolue:+,duree:~]}.

1> PP[date=+,absolue=+,incomplete=+]{prep,num[date:+,absolue:+,incomplete:+]}.

1> PP[date=+,absolue=+,incomplete=+,tempexpr=+]{PREP,NP{num[date:+,absolue:+,incomplete:+]}}.

1> PP[datepot=+,absolue=+,incomplete=+,tempexpr=+]{PREP,NP{num[datepot:+,absolue:+,incomplete:+]}}.

1> PP[date=+,absolue=+,incomplete=+]{PREP,PREP,NP{num[date:+,absolue:+,incomplete:+]}}.


// a enlever
/////// DATES qui deviennent des DUREE en fonction de la nature de la prep :

//1> PP[duree=+,date=+, orig=+]{prep[first,predate:+,orig:+],NP[date:+,absolue:+,last]}.
//1> PP[duree=+,date=+, orig=+]{prep[first,predate:+,orig:+],NP[date:+,relative:+,last]}.

//1> PP[duree=+,date=+, tfin=+]{prep[first,predate:+,tfin:+],NP[date:+,absolue:+,last]}.
//1> PP[duree=+,date=+, tfin=+]{prep[first,predate:+,tfin:+],NP[date:+,relative:+,last]}.


1> PP[date=+,absolue=+, orig=+]{prep[first,predate:+,orig:+],NP[date:+,absolue:+,last]}.
1> PP[date=+,relative=+,orig=+]{prep[first,predate:+,orig:+],NP[date:+,relative:+,last]}.

1> PP[date=+,absolue=+, tfin=+]{prep[first,predate:+,tfin:+],NP[date:+,absolue:+,last]}.
1> PP[date=+,relative=+, tfin=+]{prep[first,predate:+,tfin:+],NP[date:+,relative:+,last]}.


// Intervalles

1> PP[tempexpr=+,interval=+]{PREP,NP[interval:+]}.


// HEURES

1> PP[heure=+,t_mod=approx] {prep[first,predate:+,t_mod:approx],NP[heure:+,duree:~]}.

1> PP[heure=+,orig=+] {prep[first,predate:+,orig:+],NP[heure:+,duree:~]}.

1> PP[heure=+,tfin=+] {prep[first,predate:+,tfin:+],NP[heure:+,duree:~]}.

1> PP[heure=+] {prep,?*,NP[heure:+,duree:~,daytime:~,dureemod:~]}.

1> PP[heure=+,daytime=+] {prep,?*,NP[heure:+,duree:~,daytime:+]}.


// HEUREMOD

//§!!!special ESTER qui veut des date pour les moments du jour
1> NP[heuremod=+,!tempexpr=+]{?*,noun[heuremod:+,daytime:~]}.

1> NP[date=+,relative=+,tempexpr=+]{?*,noun[heuremod:+,daytime]}.


1> PP[heuremod=+] {prep,?*,NP[heuremod:+]}.


// DUREE-HEURE

1> PP[heure=+,duree=+] {?*,NP[heure:+,duree:+]}.

// DUREE-DATE

1> PP[date=+,duree=+] {?*,NP[date:+,duree:+]}.


// DUREEMOD-HEURE

1> PP[heure=+,dureemod=+] {?*,NP[heure:+,dureemod:+]}.

// DUREEMOD-DATE

1> PP[date=+,dureemod=+] {?*,NP[date:+,dureemod:+]}.

//DATEMOD

1> NP[datemod=+,relative=+]{?*,NOUN[datemod:+, relative:+]}.

1> NP[datemod=+,absolue=+]{?*,NOUN[datemod:+, absolue:+]}.

1> PP[datemod=+,relative=+]{prep,NP[datemod:+, relative:+]}.

1> PP[datemod=+,absolue=+]{prep,NP[datemod:+, absolue:+]}.


/***idem sur PP\
//Marquage expressions temporelles avec modifieurs
//ex: depuis/dès/

2> PP[tempexpr=+,t_mod=approx]{prep[first,predate:+,t_mod:approx],NP[tempexpr:+,last]}.

2> PP[tempexpr=+,orig=+]{prep[first,predate:+,orig:+],NP[tempexpr:+,last]}.

//2> PP[tempexpr=+,duree=+,date=+]{prep[first,predate:+,duree:+],NP[tempexpr:+,last,date:+]}.

//2> PP[tempexpr=+,duree=+,heure=+]{prep[first,predate:+,duree:+],NP[tempexpr:+,last,heure:+]}.

2> PP[tempexpr=+,tfin=+]{prep[first,predate:+,tfin:+],NP[tempexpr:+,last]}.

3> PP[tempexpr=+]{prep[first:+], NP[tempexpr:+,last]}.

3> PP[postjour=+]{prep[form:fa],NP{noun[last,postjour]}}.




// DATES COORDONNEES.

// il est parti les 30 et 31 mai
4> NP[datecoord=+,relative=+]{DET,NUM,COORD,NOUN[date,relative]}.

// il est parti les 29, 30 et 31 mai
4> NP[datecoord=+,relative=+]{DET,NUM,PUNCT,NUM,COORD,NOUN[date,relative]}.

// il est parti les 30 et 31 mai 2008
4> NP[datecoord=+,absolue=+]{DET,NUM,COORD,NOUN[date,absolue]}.

// il est parti les 29, 30 et 31 mai 2008
4> NP[datecoord=+,absolue=+]{DET,NUM,PUNCT,NUM,COORD,NOUN[date,absolue]}.

4> NP[datecoord=+,relative=+,date=+]{det,noun[datecoord,relative]}.

4> NP[datecoord=+,absolue=+,date=+]{det,noun[datecoord,absolue]}.


Déduction: 

// Enlever les expressions de durées qui sont en fait des ages
// utilisation de contexte
// partiellement traites dans la grammaire plus en amont. 
// Le probleme est que l'expression garde des traits tempexpr associes 
// ce qui a des consequences sur TEMPLINK
// agés de (moins de| plus de) N ans

| ap[lemme:"âgé"], pp[tempexpr=~, date=~, time=~, duree=~]{prep[lemme:de], np[duree=~,date=~,tempexpr=~,time=~]{ (det), (adv), noun[tempexpr=~,duree=~,date=~]{num, noun[lemme:an]}}} |
~


Marqueur:

//Marquage des éléments dans les intervalles
8> NP[interval:+]{PP[date:+,in-interval=+]{?*,NP[date:+,in-interval=+]},PP[date:+,in-interval=+]{?*,NP[date:+,in-interval=+]}}.

8> NP[interval:+]{NOUN[date:+,duree:~,in-interval=+],PUNCT,NOUN[date:+,duree:~,in-interval=+]}.



