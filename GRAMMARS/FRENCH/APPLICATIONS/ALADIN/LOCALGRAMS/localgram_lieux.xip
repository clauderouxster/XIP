//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2000 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Marqueur:

/ les iles de XXB et (de) UUU --> iles n'est pas un lieu1 \ 

/ le carembolage de l'A10\

2> noun[lemme:carambolage] , prep[lemme:de];prep[lemme:sur], det[def], noun[lemme:"A%d(%d)",lieu=+,line=+,entit=+].

2> noun[lemme:accident] , prep[lemme:de];prep[lemme:sur], det[def], noun[lemme:"A%d%d",lieu=+,line=+,entit=+].

/ le cinéma ZZZ\

2> ?[def] , noun[lemme:cinéma,batimentorg1=+], noun[proper].

/le Mont-Blanc\

2> ?[def], noun[lemme:"mont-blanc",lieu=+,geog=+,oronyme=+].

/ les Invalides\

2> det[def,pl], noun[lemme:"invalide",pl,maj,batimentorg=+].

/le Magic Hotel\
2> noun[batimentorg2maj, maj, phrasemaj:~, start:~, batimentorg2=+].

/la Fédération française d'athlétisme n'est pas un pays\
2> noun [pays1=~+, org1:+], adj[nat], prep[form:fde], noun.

/le Golfe\
2> ?[def], noun[regionmaj, maj, region=+, paysde=+].

/Eglise avec majuscule signale une institution et non un bâtiment\
2> noun[maj, start:~, toutmaj:~, org1:+, batiment1=~].

/Chambre de filles\
2> noun[lemme:chambre, maj, start, org1=~].

/l'Hexagone\
2> ?[def], noun[paysmaj, maj, phrasemaj:~, pays=+].

/le Pentagone\
2> ?[def], noun[lemme:"pentagone", maj, phrasemaj:~, batimentorg=+].

/Mars n'est la planète que dans certains contextes\
2> ?[form:~fsur, lemme:~planète], noun[lemme:Mars, maj, lieu=~, geo=~].
2> noun[lemme:Mars, maj, lieu=~, geo=~start].

/Luxembourg ou Israël est une ville et non un pays dans certains expressions\
2> prep[form:fa, def:~], noun[ville, pays=~].
2> noun[preville], prep[form:fde, def:~], noun[ville, pays=~].
/sinon c'est un pays\
3> noun[pays, ville=~].

/Suez n'est pas une compagnie dans certains contextes\
2> noun[ville1], noun[lemme:Suez, firme=~].
2> noun[lieu1], noun[lemme:Suez, firme=~].


/diplômé de l'Ecole d'agronomie de Rennes et de l'université du Maryland \
2> prep[form:fde], (det), noun[batimentorg1, sansde:~, preregionville:~, sg, maj, phrasemaj:~];noun[org1, sansde:~], (adj[postprof, sg]), prep[form:fde], (det), noun[pays:~, continent:~], (adj), (prep, (det), noun, adj*), coord, prep[form:fde], (det), noun[org1, horsorg=~];noun[batimentorg1, horsorg=+].

/la décrue de du Vidourle\

2>  noun[lemme:"décrue"], prep, (det), noun[maj, inconnu,geog=+,hydronyme=+,lieu=+].


Séquence:
/*******groupes qui ne sont pas des entités ************\

/jeu de paume\
10> noun[masc=+] = noun[lemme:jeu, !maj:!, !sg:!, !pl:!], prep[form:fde], noun[lemme:paume].

/hôtel de ville\
10> noun[mwe=+, masc=+, batimentorg1=+] = noun[lemme:hôtel, !maj:!, !sg:!, !pl:!], prep[form:fde], noun[lemme:ville].

/palais des sports\
/changement CB batimentorg1 --> batimentorg\
10> noun[mwe=+, masc=+, batimentorg=+] = noun[lemme:"palais", !maj:!, !sg:!, !pl:!], prep[form:fde], noun[palais2].

/coup de théâtre\
10> noun[mwe=+, masc=+] = noun[lemme:coup, !sg:!, !pl:!, !maj:!], prep[form:fde], noun[lemme:théâtre, batimentorg1=~].

/******** orhtographes approximatives******\
/Hong-kong (sans majuscule)\
10> noun[mwe=+, ville=+] = noun[lemme:Hong, person=~], (punct[form:fhyph]), noun [lemme:kong].

/les Etats-unis\
10> noun[mwe, lieu=+, masc=+, pl=+, maj] @= noun[maj, lemme:état], punct[form:fhyph], ?[lemme:uni].


/les Pays- Bas (avec des espaces ou une minuscule)\
10> noun[mwe, pays=+, masc=+, pl=+] @= noun[!maj:!, lemme:pays], punct[form:fhyph], ?[lemme:bas].

/************* création de chunks généraux *****************\

/Saint-Jean-l'Evangéliste, Saint-Jean-le-Vinoux\
10> noun[mwe, lieu=+, person=+] @= noun[saintTiret, !maj:!, !masc:!, !fem:!, !sg:!], pron[form:fhyph], (punct[form:fhyph]), noun[maj].

/Saint-Jean-De-Braye\
10> noun[mwe, lieu=+] @= noun[saintTiret, !maj:!, !masc:!, !fem:!, !sg:!], punct[form:fhyph], prep[form:fde], punct[form:fhyph], noun[maj].
10> noun[mwe, lieu=+] @= noun[saintTiret, !maj:!, !masc:!, !fem:!, !sg:!], punct[form:fhyph], prefix[lemme:"dé-"], noun[maj].

/le Massif Central\
10> noun[mwe, lieu=+, region=+] = noun[lemme:massif, maj], ?[lemme:central, maj].

/les Indes, la Dominique\
12> noun[pays=+, lieu=+, min=+] = det;prep[def], noun[pays, lieudef:+].

/ la City\
12> noun[quartier=+, lieu=+, min=+] = det[def, fem, sg], noun[quartierdef, fem, sg].

/la Confédération helvétique\
12> noun[pays=+, lieu=+, min=+] = det;prep[def], noun[pays, lieudef:+].
12> noun[pays=+, lieu=+, min=+] = det;prep[def], noun[pays, lieudef:+].

/Villeneuve-Saint-Martin\
15> noun[mwe=+, ville=+] = noun[ville:+], punct[form:fhyph], noun[saintTiret].

/République Tchèque\
15> noun[mwe=+, pays=+, maj=+, sg=+, masc=+] = noun[maj, pays1:+, sg, masc], (adj[adjPays]), ?[nat:+, sg, masc].
15> noun[mwe=+, pays=+, maj=+, sg=+, fem=+] = noun[maj, pays1:+, sg, fem, lemme:~"fédération"], (adj[adjPays]), ?[nat:+, sg, fem].
15> noun[mwe=+, pays=+, maj=+, sg=+, fem=+] = noun[maj, pays1:+, sg, fem], ?[lemme:russe, sg] |?[form:~fde]|.

/le Brabant Wallon, la Polynésie française\
15> noun[mwe, lieu=+, region=+] = |?[def]| noun[region], adj[nat];noun[nat].

/République Fédérale du Congo\
15> noun[mwe=+, pays=+, maj=+] = noun[maj, pays1:+], (adj[adjPays]), prep[form:fde], (det), noun[maj, pays].
15> noun[mwe=+, pays=+, maj=+] = noun[maj, pays1:+], adj[adjPays], prep[form:fde], (det), noun[maj].
15> noun[mwe=+, pays=+] = noun[pays1:+], (adj[adjPays]), prep[form:fde], (det), noun[maj, pays].

/Côte Ouest des Etats-Unis\
15> noun[mwe=+, region=+] @= noun[lemme:côte, maj], ?[ptCard], (prep[form:fde], noun[pays]).

/l'Afrique de l'Ouest\
15> noun[mwe=+, region=+] @= |?[def], (adj)| noun[continent];noun[region];noun[pays], prep[form:fde], (det[def]), ?[ptCard].

/l'ouest américain\
/attention : le sud africain\
//15> noun[mwe=+, region=+] @= |?[def], (adj)| ?[ptCard], ?[nat,pl:~].

/le pays basque, la région parisienne\ 
15> noun[mwe=+, region=+] = |?[def];prep[form:fen]| noun[region1:+, nonadj:~, sg], adj[nat:+, sg] |?[form:~fde, prep:~]|.
15> noun[mwe=+, region=+] = |?[def];prep[form:fen]| noun[region1:+, nonadj:~, sg], adj[sg, nat:+, end].

/les pays du golfe, les pays de l'Est\
15> noun[mwe=+, pays=+] = noun[lemme:pays, pl], prep[form:fde], (det[def]), ?[paysde];?[ptCard].

/la vallée du Rhône\
15> noun[mwe=+, region=+] @= |?[def]| noun[lemme:vallée], prep[form:fde], (det[def]), noun[inconnu, maj];noun[maj, lieu:+, pays:~, continent:~].

/le Val d'Aoste\
15> noun[mwe=+, region=+] = noun[region1:+, sg, maj], prep[form:fde], (det[def]), noun[maj:+, phrasemaj:~];noun[maj, inconnu];noun[pays];noun[region];noun[lieu].

/les îles Majnoon\
15> noun[mwe=+, region=+,geog:!] = noun[region1:+,!geog:!,!hydronyme:!,!oronyme:!], noun[maj:+, phrasemaj:~,person:~];noun[maj, inconnu,person:~];noun[region];noun[lieu].

/l'île principale de Honshu\
15> noun[mwe=+, lieu=+, perslieu1=+,geog:!] @= |?[def,pl:~], noun[region1:+, sg,!geog:!,!hydronyme:!,!oronyme:!], (adj), prep[form:fde]| noun[maj, ville:~, pays:~, person:~, continent:~], (noun[maj, inconnu]).

/la région Centre\
15> noun[mwe=+, region=+] = noun[lemme:région], noun[maj].

/la Mer Rouge\
15> noun[mwe=+, lieu=+,geog:!] = |?[det,pl:~]| noun[lieu1:+, sg, adjmaj:~, nonadj:~,!geog:!,!hydronyme:!,!oronyme:!], adj[nat:+];adj[maj:+, phrasemaj:~];?[maj, inconnu].
15> noun[mwe=+, lieu=+] @= noun[lieu1:+, sg,!geog:!,!hydronyme:!,!oronyme:!], adj[maj:+, phrasemaj:~];?+[maj, inconnu].

/la Mer de Glace, le lac de Genève, la vallée de l'Emblève\
15> noun[mwe=+, lieu=+,geog:!] @= noun[lieu1:+, !maj:!, !fem:!, !masc:!, !sg:!, !pl:!,!geog:!,!hydronyme:!,!oronyme:!],(adj),  prep[form:fde], (det[def]), noun+[maj:+, phrasemaj:~,date:~,org:~,prof:~,person:~];noun[maj, inconnu];noun[pays]; noun[region];noun[lieu].


/lac Titicaca\
15> noun[mwe=+, lieu=+,geog:!] @= noun[lieu1:+,!geog:!,!hydronyme:!,!oronyme:!],(noun[maj:+, phrasemaj:~,pren:~];noun[maj, inconnu]),  noun[maj:+, phrasemaj:~,pren:~];noun[maj, inconnu];noun[pays];noun[region], (adj).



/L'Afrique subsaharienne, le Sahara occidental\
18>  noun[mwe=+, region=+, maj=+] = noun[maj, pays:+];noun[maj, region:+];noun[maj, continent:+], adj[adjLieu:+].

/L'Afrique du Nord\
18>  noun[mwe=+, region=+, maj=+] = noun[maj, pays:+];noun[maj, region:+];noun[maj, continent:+], prep[form:fde], noun[ptCard:+].

/Port Elisabeth, Port-Saïd\
15> noun[mwe=+, ville=+, maj=+] = noun[maj, lemme:port], (punct[hyph]), noun[maj,sport:~].

/Parris Island\
15> noun[mwe=+, lieu=+] = noun[maj, phrasemaj:~], noun[lieu2].

/George Street, Alexander Platz\
15> noun[mwe=+, lieu=+, quartier=+] = (num), (punct), noun[maj, phrasemaj:~], (noun[maj, phrasemaj:~]), noun[quartier2].

/Liban Sud\
15> noun[mwe=+, Lieu=+, region=+, maj=+] = noun[maj, phrasemaj:~,org:~];noun[maj, pays];noun[maj, lieu],?[ptCard].

/Berlin ouest\
15> noun[mwe=+, Lieu=+, region=+, maj=+] = noun[maj, ville];noun[maj, quartier],?[ptCard].

/La Défense\
15> noun[quartier=+, lieu=+, min=+] = |~noun[lemme: "ministre"], prep[form:fde]| det [def, fem], noun[maj, phrasemaj:~, lemme:"défense"] |?[noun:~, adj:~]|.

/le département 77\

15>noun[mwe=+, Lieu=+, region=+] = noun[lemme:département], num .

marqueur:
/les rives du Saint-Laurent\

25> noun[lemme:rive], prep[form:fde], noun[guessed,maj,lieu=+,hydronyme=+];noun[proper,lieu=+,hydronyme=+,ville=~,org=~,person=~,firme=~].

25> noun[lemme:rivage], prep[form:fde], noun[guessed,maj,lieu=+,hydronyme=+];noun[proper,lieu=+,hydronyme=+,ville=~,org=~,person=~,firme=~].

/l'Ecole des femmes n'est pas une école\
25> noun[lemme:école, batimentorg1=~], prep[form:fde], noun[lemme:femme, pl].

/X, la ville de Paris\
28> noun[maj, nonville=+], punct[form:fcm], (det[def]), noun[preville:+, sg], (adj), prep[form:fde], noun[ville:+].

/Paris, la ville de X\
29> noun[ville:+], punct[form:fcm], (det[def]), noun[preville:+, sg], (adj), prep[form:fde], noun[maj, nonville=+].

/les villages sunnites de Chebaa, Kfar et Habbariyé\
30> det[def], noun[preville:+, pl], (adj[nat]), prep[form:fde], noun[maj, ville=+], (punct[form:fcm], noun[maj, ville=+]), (punct[form:fcm], noun[maj, ville=+]), (punct[form:fcm], noun[maj, ville=+]), coord, noun[maj, ville=+].

30> noun[lemme:conseil], adj[lemme:municipal, deville=+].
30> noun[lemme:conseiller], adj[lemme:municipal, deville=+].

30> noun[lemme:bastille, maj:+, batiment=+].

/Champfleuri\
30> noun[lieu=+, perslieu1=+, lemme:"Champ%c+"].

/les rues de Nice\
30> noun[previllepl, pl], prep[form:fde], (adj), (det), noun[maj, phrasemaj:~, ville=+].

/la Tour (Eiffel)\
30> noun[lemme:tour, fem, masc:~, batiment1=+].

/la ville de Blois\
30> noun[preville, sg], (adj, (coord, adj)), prep[form:fde], noun[maj:+, personne:~+, org:~, ville=+, PersLieu1=+, pren:~, region:~, pays:~, nonville:~].

/la prefecture de Lille\

30> noun[lemme:"préfecture"], (adj, (coord, adj)), prep[form:fde], noun[maj:+, personne:~+, org:~, admi=+,lieu=+,  PersLieu1=+, pren:~, region:~, pays:~].

/la province éthiopienne de l'Ogaden\
30> noun[preregion, sg], (adj[nat];adj[postProf]), prep[form:fde], (det), noun[maj:+, inconnu:+, personne:~+, org:~, region=+, PersLieu1=+, villereg=+].
30> noun[preregion, sg], (adj[nat];adj[postProf]), prep[form:fde], (det), noun[maj:+, ville:~, pays:~, personne:~+, org:~, region=+], prep[form:fa];prep[form:fen], noun[region];noun[pays].

/les provinces de l'Ogaden et du X\
32> noun[preregion, pl], (adj[nat];adj[postProf]), prep[form:fde], (det), noun[region], coord, prep[form:fde], noun[maj:+, inconnu:+, personne:~+, org:~, region=+, PersLieu1=+, villereg=+].

/une province éthiopienne, l'Ogaden\
30> det[indef], noun[preregion, sg], (adj[nat];adj[postProf]), punct[form:fcm], det, noun[maj:+, inconnu:+, personne:~+, org:~, region=+, PersLieu1=+, villereg=+].

/le chef-lieu (de la commune) (,) X ; la capitale de l'Erythrée, Asmara\
30> noun[cheflieu], (prep[form:fde], (det), noun), (punct[form:fcm]), noun[maj:+, inconnu:+, personne:~+, org:~, ville=+, PersLieu1=+].

/le chef-lieu /la capitale de Z \
32> noun[cheflieu], (prep[form:fde], det), (prep[form:fde, def]), noun[maj:+, inconnu:+, personne:~+, org:~, region=+].

/X, chef-lieu (de Y)\
33> noun[maj, phrasemaj:~, ville=+], punct[form:fcm], (det[def]), noun[cheflieu], prep[form:fde].

/Castellet, un hameau (du Luberon)\
32> noun[maj:+, inconnu:+, personne:~+, org:~, ville=+, nonville:~, PersLieu1=+], punct[form:fcm], (det), adj*, noun[preville, sg].
32> noun[maj:+, compville:+, personne:~+, org:~, ville=+, PersLieu1=+, nonville:~], punct[form:fcm], (det), adj*, noun[preville, sg].

/l'Ogaden, province (éthiopienne)\
32> noun[maj:+, inconnu:+, personne:~+, org:~, region=+, PersLieu1=+], punct[form:fcm], (det), adj*, noun[preregion, sg].

/le quartier de la Croix-Rousse\
33> noun[prequartier], (prep[form:fde]), (det), noun[maj, phrasemaj:~, lieu=+].

//33> noun[batiment1], (det), noun[maj, phrasemaj:~, quartier=+].
//33> noun[batiment1], (prep[form:fde]), (det), noun[maj, phrasemaj:~, perslieu=+, lieu=+].

/la station balnéaire de Dahab, le site touristique de Dahab\
33> ?[def], (adj), noun[prelieu, sg], (adj[adjprelieu]), prep[form:fde], noun[maj, lieu=+].

/autres contextes indicateurs de lieu\
35> adj[lemme:originaire], prep[form:fde], noun[maj:+, inconnu:+, lieu=+, lieupers=+];noun[maj:+, pren:+, lieu=+, lieupers=+].
35> ?[deville], (noun[org]), prep, noun[inconnu, maj, phrasemaj:~, lieuPers=+, ville=+, vville=+];noun[compville, maj, phrasemaj:~, lieuPers=+, ville=+, vville=+].
35> prep[preplieu:+], noun[inconnu, maj, phrasemaj:~, person:~, org:~, lieu=+, perslieu1=+].
35> prep[form:fdans], det[pl, def], noun[lemme:environs], prep[form:fde], noun[inconnu, maj, phrasemaj:~, person:~, org:~, lieu=+];noun[compville, maj, phrasemaj:~, person:~, org:~, lieu=+].
35> det, noun[ptcard:+], prep[form:fde], noun[inconnu, maj, person:~, org:~, lieu=+];noun[compville, maj, person:~, org:~, lieu=+].
35> prep[def], noun[ptcard:+], prep[form:fde], noun[inconnu, maj, person:~, org:~, lieu=+];noun[compville, maj, person:~, org:~, lieu=+].
35> noun[lemme:habitant], (adj), prep[form:fde];prep[form:fa], noun[inconnu, maj, person:~, org:~, lieu=+];noun[compville, maj, person:~, org:~, lieu=+].
35> noun[lemme:résident], (adj), prep[form:fde];prep[form:fa], noun[inconnu, maj, person:~, org:~, lieu=+];noun[compville, maj, person:~, org:~, lieu=+].
35> ?[lemme:naître], (prep[form:fen], num[date=+]), (det, noun[date:+]), prep[form:fa], noun[inconnu, maj, person:~, org:~, lieu=+, ville=+];noun[compville, maj, person:~, org:~, lieu=+, ville=+];noun[maj, phrasemaj:~, lieu=+, ville=+].
35> ?[lemme:mourir], (prep[form:fen], num[date=+]), (det, noun[date:+]), prep[form:fa], noun[inconnu, maj, person:~, org:~, lieu=+, ville=+];noun[compville, maj, person:~, org:~, lieu=+, ville=+];noun[maj, phrasemaj:~, lieu=+, ville=+].
35> ?[lemme:naître], (prep[form:fen], num[date=+]), (det, noun[date:+]), prep[form:fen], noun[inconnu, maj, person:~, org:~, lieu=+, region=+].
35> ?[lemme:mourir], (prep[form:fen], num[date=+]), (det, noun[date:+]), prep[form:fen], noun[inconnu, maj, person:~, org:~, lieu=+, region=+].
35> adj[lemme:né], (det, noun[date:+]), prep[form:fa];prep[form:fen], noun[inconnu, maj, person:~, org:~, lieu=+, ville=+];noun[compville, maj, person:~, org:~, lieu=+, ville=+].
35> adj[lemme:né], (det, noun[date:+]), prep[form:fen], noun[inconnu, maj, person:~, org:~, lieu=+, region=+].
35> ?[lemme:résider], (prep[form:fen], num[date=+]), (det, noun[date:+]), prep[form:fa], noun[pays:~, inconnu, maj, person:~, org:~, lieu=+];noun[pays:~, phrasemaj:~, maj, person:~, org:~, lieu=+, ville=+].
35> ?[lemme:résider], (prep[form:fen], num[date=+]), (det, noun[date:+]), prep[form:fen], noun[pays:~, inconnu, maj, person:~, org:~, lieu=+, region=+];noun[pays:~, phrasemaj:~, maj, person:~, org:~, lieu=+].
35> ?[lemme:habiter], (prep[form:fen], num[date=+]), (det, noun[date:+]), prep[form:fa], noun[inconnu, maj, person:~, org:~, lieu=+, pays:~];noun[phrasemaj:~, maj, person:~, org:~, lieu=+, ville=+, pays:~].
35> ?[lemme:débarquer], (prep[form:fen], num[date=+]), (det, noun[date:+]), prep[form:fa], noun[inconnu, maj, person:~, org:~, lieu=+, pays:~];noun[phrasemaj:~, maj, person:~, org:~, lieu=+, ville=+, pays:~].
35> ?[lemme:habiter], (prep[form:fen], num[date=+]), (det, noun[date:+]), prep[form:fen], noun[inconnu, maj, person:~, org:~, lieu=+, pays:~];noun[phrasemaj:~, maj, person:~, org:~, lieu=+, region=+, pays:~].

35> prep[form:fa], noun[maj, phrasemaj:~, ville=+], prep[form:fen], noun[pays];noun[region].

38> noun[lieu=~, ville=~, lieupers:~], punct[form:fcm], (det), (adj)*, noun[preville, sg], prep[form:fde], noun[ville].

séquence:

/le village de Beit Wazan\
40> noun[mwe=+, ville=+, PersLieu1=+, maj=+] = |noun[preville], (adj[postprof];adj[nat]), prep[form:fde]| noun[maj:+, inconnu:+, personne:~+, org:~, nonville:~], noun[maj:+, personne:~+].

40> noun[mwe=+, ville=+, PersLieu1=+] = |noun[preville], (adj[postprof];adj[nat]), prep| noun[maj:+, personne:~+, org:~, pren:~, nonville:~, org1:~, firme1pl:~, batimentorg1:~,person:~], noun[maj:+, personne:~+, org:~,person:~].

/les villages sunnites de Chebaa, Kfar Chouba et Habbariyé\
//42> noun[mwe=+, ville=+, PersLieu1=+, maj=+] = |noun[preville:+, pl], (adj), prep[form:fde], noun[maj, ville], coord| (noun[maj]), noun[maj].

42> noun[mwe=+, ville=+, PersLieu1=+, maj=+] = |det[def], noun[preville:+, pl], (adj[postprof, pl];adj[nat, pl]), prep[form:fde], noun[maj, ville], coord| (noun[maj]), noun[maj].
42> noun[mwe=+, ville=+, PersLieu1=+, maj=+] = |det[def], noun[preville:+, pl], (adj[postprof, pl];adj[nat, pl]), prep[form:fde], noun[maj, ville], punct[form:fcm]| (noun[maj]), noun[maj] |coord|.
43> noun[mwe=+, ville=+, PersLieu1=+, maj=+] = |det[def], noun[preville:+, pl], (adj[postprof, pl];adj[nat, pl]), prep[form:fde], noun[maj, ville], punct[form:fcm], noun[ville] , coord | (noun[maj]), noun[maj].

/la province éthiopienne de Z X\
40> noun[mwe=+, region=+, PersLieu1=+] = |noun[preregion, sg], (adj[nat]), prep[form:fde], (det)| noun[maj:+, inconnu:+, personne:~+, org:~], noun[maj:+, inconnu:+, personne:~+, org:~].

/l'Académie de Grenoble\
40> noun[mwe=+, region=+] = noun[preregionville], prep[form:fde, def:~], noun[ville, maj].

/une région belge, le Z X \
40> noun[mwe=+, region=+, persLieu1=+] = |det[indef], noun[preregion, sg], (adj[nat];adj[postProf]), punct[form:fcm]| det, noun[maj:+, inconnu:+, personne:~+, org:~], noun[maj:+, inconnu:+, personne:~+, org:~].

/le chef-lieu (de la commune) (,) Z X\
40> noun[mwe=+, ville=+] = |noun[cheflieu], (prep[form:fde], (det), noun), (punct[form:fcm])| noun[maj:+, inconnu:+, personne:~+, org:~], noun[maj:+, inconnu:+, personne:~+, org:~].

/le chef-lieu /la capitale de Z X\
41> noun[mwe=+, region=+] @= |noun[cheflieu], (prep[form:fde], det), (prep[form:fde, def]) | noun[maj:+, inconnu:+, personne:~+, org:~], noun[maj:+, inconnu:+, personne:~+, org:~].

/XA, chef-lieu (de Y)\
42> noun[mwe=+, ville=+] = noun[maj, phrasemaj:~], noun[maj, phrasemaj:~] |punct[form:fcm], noun[cheflieu], prep[form:fde]|.


/Beit Shéan, une ville israélienne\
40> noun[mwe=+, ville=+, PersLieu1=+] = noun[maj:+, inconnu:+, personne:~+, org:~], noun[maj:+, inconnu:+, personne:~+, org:~, nonville:~] |punct[form:fcm], (det), (adj)*, noun[preville, sg]|.

/l'Ogaden, province (éthiopienne)\
40> noun[mwe=+, region=+, PersLieu1=+] = noun[maj:+, inconnu:+, personne:~+, org:~], noun[maj:+, inconnu:+, personne:~+, org:~] |punct[form:fcm], (det), (adj)*, noun[preregion, sg]|.

/le 13e arrondissement\
//40> noun[mwe=+, Lieu=+, quartier=+] = |?[def]| num[ord];num[rom];adj[ord], noun [lemme:arrondissement].
40> noun[mwe=+, Lieu=+, admi=+] = |?[def]| num[ord];num[rom];adj[ord], noun [lemme:arrondissement].
40> noun[mwe=+, Lieu=+, admi=+] = |?[def]| num[ord];num[rom];adj[ord], noun [lemme:avenue].

40> noun[mwe=+, Lieu=+, quartier=+] = |?[def]| num[ord];num[rom];adj[ord], ? [lemme:parisien].

/la station balnéaire de Dahab Dihab\
40> noun[mwe=+, Lieu=+] @= |?[def], (adj), noun[prelieu, sg], (adj[adjprelieu]), prep[form:fde]| noun[maj], (noun[maj])+.


/autres contextes qui indiquent un nom de lieu\
40> noun[mwe=+, Lieu=+, lieupers=+] = |adj[lemme:originaire], prep[form:fde]| noun[maj], (punct[form:fhyph]), noun[maj, phrasemaj:~].
40> noun[mwe=+, ville=+, vville=+, lieuPers=+] @= |noun[deville];adj[deville], prep| noun[inconnu, maj, phrasemaj:~], (punct[form:fhyph]), noun[inconnu, maj], (noun[inconnu, maj]).
40> noun[mwe=+, Lieu=+] = |prep[preplieu:+]| noun[inconnu, maj, phrasemaj:~, person:~, org:~], noun[inconnu, maj, person:~, org:~].
40> noun[mwe=+, Lieu=+] = |prep[form:fdans], det[pl, def], noun[lemme:environs], prep[form:fde]| noun[inconnu, maj, phrasemaj:~, person:~, org:~], noun[inconnu, maj, person:~, org:~].
40> noun[mwe=+, Lieu=+] = |det;prep[def], noun[ptcard:+], prep[form:fde]| noun[inconnu, maj, person:~, org:~];noun[compville, maj, person:~, org:~], noun[inconnu, maj, person:~, org:~].
40> noun[mwe=+, Lieu=+] = |noun[lemme:habitant], prep[form:fde]| noun[inconnu, maj, pren:~, org:~, lieu=+], noun[inconnu, maj, person:~, org:~, lieu=+].
40> noun[mwe=+, Lieu=+, ville=+] = |?[lemme:naître];adj[lemme:né], (det, noun[date:+]), (prep[form:fen], num[date=+]), prep[form:fa]| noun[maj, pren:~, org:~], noun[inconnu, maj, person:~, org:~].
40> noun[mwe=+, Lieu=+] = |?[lemme:naître];adj[lemme:né], (det, noun[date:+]), (prep[form:fen], num[date=+]), prep[form:fen]| noun[maj, pren:~, org:~], noun[inconnu, maj, person:~, org:~].
40> noun[mwe=+, Lieu=+, ville=+] = |?[lemme:résider];?[lemme:habiter], (prep[form:fa], num[date=+]), (det, noun[date:+]), prep[form:fa]| noun[maj, person:~, org:~], noun[phrasemaj:~, maj, person:~, org:~].
40> noun[mwe=+, Lieu=+] = |?[lemme:résider];?[lemme:habiter], (prep[form:fen], num[date=+]), (det, noun[date:+]), prep[form:fen]| noun[maj, person:~, org:~], noun[phrasemaj:~, maj, person:~, org:~].


60> noun[mwe=+, ville=+, fem=+, sg=+] = det[fem, sg], ?[lemme:sérénissime, maj] |?[noun:~]|.
60> noun[mwe=+, ville=+, maj=+] = noun[maj, phrasemaj:~, start:~,lemme:~"inter"], (noun[maj, start:~]), noun[lemme:City].

60> noun[quartier=+, perslieu1=+] @= |det[def, sg]; prep[def, sg]| noun[prequartier], (prep[form:fde]), (det), noun[maj, phrasemaj:~], noun[maj].
60> noun[lieu=+] @= noun[prequartier], (prep[form:fde]), (det), noun[maj, inconnu:~], noun[maj, inconnu:~].

/la Prison/prison Saint-Jean\
60> noun[batiment=+, mwe=+] @= noun[batiment1, sg, org1:~, !fem:!, !masc:!, !maj:!], (adj[postprof, sg]), (det), noun[maj, phrasemaj:~];noun[saintsTiret];noun[saintTiret], (noun[maj])*, (prep[form:fde], noun[maj]).

/le Palais présidentiel d'Abidjan\
/CB: retriat de maj sur le 1er mot\
60> noun[batiment=+, mwe=+, maj=+] @= noun[batiment1, sg, org1:~, !fem:!, !masc:!], (adj[postprof, sg]), (det), prep[form:fde], noun[maj, lieu].

/l'église Saint-Jean\
60> noun[batiment=+, sg=+, mwe=+] @= noun[batiment1, sg, maj:~, !fem:!, !masc:!], (adj[postprof, sg]), (det), noun[maj, phrasemaj:~];noun[saintsTiret];noun[saintTiret], (noun[maj])*, (prep[form:fde], noun[maj]).

60> noun[perslieu1=+, batiment=+, lieu=+, sg=+, mwe=+] @= noun[batiment1, sansde:~, sg, !fem:!, !masc:!, !maj:!], (adj[postprof, sg];adj[nat, sg]), prep[form:fde], (det), noun[maj, phrasemaj:~], (prep[form:fde]), (det), noun[maj].

/la station X\
60> noun[batiment=+, sg=+, mwe=+] @= noun[station, sg], (adj[postprof, sg]), (det), (noun[org1:~, maj, phrasemaj:~];noun[saintsTiret];noun[saintTiret])+, (prep[form:fde], noun[maj]).
60> noun[batiment=+, sg=+, mwe=+] @= noun[station, sg], (adj[postprof, sg]), (det), noun[org1:~, maj, phrasemaj:~];noun[saintsTiret];noun[saintTiret], (noun[maj])*, noun[maj, org2:~], (prep[form:fde], noun[maj]).


/l'université Paris XIII \
60> noun[batimentorg=+, fem=+, mwe=+] @= noun[lemme:université, !sg:!, !pl:!], (noun[maj, phrasemaj:~])*, noun[ville, maj], num, (noun[maj, inconnu])*.
60> noun[batimentorg=+, fem=+, mwe=+] @= noun[lemme:université, !sg:!, !pl:!], (noun[maj])*, noun[ville, maj], num, (noun[maj])*.

/le centre Pompidou\
60> noun[batimentorg=+, sg=+, mwe=+] @= noun[batimentorg1, nonorg:~, sg, !maj:!, !fem:!, !masc:!], (adj[postprof, sg]), (det;noun[partic]), noun[maj, phrasemaj:~,sport:~], (adj), (noun[maj])*.


/La respectable fondation "Alcor Life Extension"\
60> noun[batimentorg=+] @= |noun[batimentorg1, nonorg:~, sg], punct[form:fquotes]| (adj[postprof, sg]), (det), noun[maj, phrasemaj:~], (adj), (noun[maj])* |punct[form:fquotes]|.

/le camp de Satory\
59> noun[perslieu1=+, batimentorg=+] @= noun[batimentorg1,pl:~, sansde:~, sg, demaj:~], (adj[postprof, sg]), (noun[maj];adj),  prep[form:fde], (det), noun[maj, phrasemaj:~],   (adj[ sg]), ((coord), (prep[form:fde]), ?[maj,adv:~,verb:~]), ((prep[form:fde]), (det), noun[maj]).
 
/le Centre de X (majuscule obligatoire)\
60> noun[perslieu1=+, batimentorg=+] @= noun[batimentorg1, sansde:~, sg, demaj:+, maj, phrasemaj:~], (adj[postprof, sg]), prep[form:fde], (det), noun[maj, phrasemaj:~, pays:~, continent:~], ((coord), ?[maj,adv:~,verb:~]), ((prep[form:fde]), (det), noun[maj]).

/le Centre de documentation\
60> noun[perslieu1=+, batimentorg=+] @= noun[batimentorg1, sansde:~, preregionville:~, sg, maj,demaj, phrasemaj:~];noun[batimentorg1, sansde:~, preregionville:~, sg, maj,demaj:~, phrasemaj:~], (adj[postprof, sg]), (adj), prep[form:fde], (det), noun[pays:~, continent:~,batimentorg1:~,batimentorg2:~], (adj), (prep, (det), noun[batimentorg1:~,batimentorg2:~], adj*), (coord, prep[form:fde], (det), noun[horsorg:~,batimentorg1:~,batimentorg2:~]), (adj), (prep, (det), noun[batimentorg2:~], adj*)*.

60> noun[perslieu1=+, batimentorg=+] @= |?[def]| noun[batimentorg1, sansde:~, preregionville:~, sg,demaj, phrasemaj:~];noun[batimentorg1, sansde:~, preregionville:~, sg,demaj:~, phrasemaj:~], adj[postprof, sg] , prep[form:fde], (det), noun[pays:~, continent:~,batimentorg1:~,batimentorg2:~], prep[form:fde], noun[horsorg:~,batimentorg1:~,batimentorg2:~,lieu=+] .

60> noun[perslieu1=+, batimentorg=+] @= |?[def]| noun[batimentorg1, sansde:~, preregionville:~, sg,demaj, phrasemaj:~,lemme:~"centre"];noun[batimentorg1, sansde:~, preregionville:~, sg,demaj:~, phrasemaj:~,lemme:~"centre"], (adj[postprof, sg]) , prep[form:fde], ((det), noun[pays:~, continent:~,batimentorg1:~,batimentorg2:~]), prep[form:fde], noun[horsorg:~,batimentorg1:~,batimentorg2:~,lieu=+] .


/l'Académie des Sciences\
60> noun[perslieu1=+, batimentorg=+] @= noun[batimentorg1, sansde:~, sg, maj, phrasemaj:~], (adj[postprof, sg]), prep[form:fde], (det), noun[pays:~, continent:~, ville:~], (adj), (coord, prep[form:fde], (det), noun), (prep, (det), noun).


/le camp de réfugiés de X\
60> noun[perslieu1=+, lieu=+] @= |noun[batimentorg1, sansde:+, sg], (prep[form:fde], noun[lemme:"réfugié"]), (adj[postprof, sg]), prep[form:fde]| (det), noun[maj, phrasemaj:~].

/le Centre dramatique national espagnol, l'Institut sud-africain des affaires étrangères\
60> noun[batimentorg=+] @= |?[indef:~]| noun[batimentorg1, maj, sg, sansde:~], adj+, (prep[form:fde], (det[def]), (noun[person:~];adj)+, (adj)), (prep[form:fde], (det[def]), noun;adj), (coord, (prep[form:fde]), (det[def]), noun;adj+).

/la Rand Afrikaans University\
//60> noun[batimentorg=+, maj=+, mwe=+] @= noun[maj, phrasemaj:~], noun[maj]*, noun[batimentorg2, maj, sg].
60> noun[batimentorg=+, maj=+, mwe=+] @= (?[maj, phrasemaj:~])+, noun[batimentorg2], (prep[form:fde];?[of], (noun[maj])+, (?[ang]), noun[maj]).


/le boulevard Saint-Germain\
/61> noun[quartier=+] @= |?[def];num;punct| noun[quartier1, sg], (prep[form:fde], (num)), (det[def]), (adj), (punct[hyph]), noun[maj, phrasemaj:~,prof:~], (punct[form:fhyph]), (noun[maj];adj[maj]), (punct[hyph]), (noun[maj];adj[maj]).\

61>  noun[quartier=+,adresse=+] @=  num, prep[form:fde], det[def] , noun[quartier1, sg,!line:!], (prep[form:fde], (num)), (det[def]), (adj), (punct[hyph]), noun[maj, phrasemaj:~, prof:~], (punct[form:fhyph]), (noun[maj, prof:~];adj[maj]), (punct[hyph]), (noun[maj,prof:~];adj[maj]).


61>  noun[quartier=+,adresse=+] @=  num, (punct) , noun[quartier1, sg,!line:!], (prep[form:fde]), (num), (det[def]), (adj), (punct[hyph]), noun[maj, phrasemaj:~], (punct[form:fhyph]), (noun[maj,prof:~];adj[maj]), (punct[hyph]), (noun[maj,prof:~];adj[maj];noun[proper]).



/boulevard Saint-Germain\
61> noun[quartier=+] @= noun[quartier1, sg,!line:!], (prep[form:fde], (num)), (det[def]), (adj), (punct[hyph]), noun[maj, phrasemaj:~,prof:~], (punct[form:fhyph]), (noun[maj,prof:~];adj[maj]), (punct[form:fhyph]), (noun[maj,prof:~];adj[maj]).

/la cité des 4000\
61> noun[quartier=+] @= |?[def];num;punct| noun[quartier1, sg], prep[form:fde], num.

/les quartiers de New Delhi\
62> noun[ville=+] = |noun[previllepl, pl], (adj), prep[form:fde]| (det[def]), noun[maj, phrasemaj:~], (det), noun[maj].

/le café Chez Mina, un café du boulevard Jean-Jaurès, Chez Mina\
63> noun[batimentorg=+] @= |det, (adj)*, noun[batimentorg1], (prep[form:fde], (det), noun[quartier];noun[ville], punct[form:fcm])| prep[form:fchez];prep[form:fa, maj], (adj), (noun[maj, phrasemaj:~, ville:~]), noun[maj, ville:~].

/Notre-Dame de la Salette\
63> noun[mwe=+, batiment=+] = noun[lemme:"notre-dame", maj], (punct[form:fhyph]), prep[form:fde], (punct[form:fhyph]), (det), (punct[form:fhyph]), noun[maj].

/ Le Zénith de VILLE\

63> noun[mwe=+, batiment=+] = noun[lemme:"zénith"], prep[form:fde], noun[ville,maj].

/le Grand Paris\

63> noun[mwe=+,lieu=+,ville=+] = |?[def]| ?[lemme:"grand"], noun[ville].

63> noun[mwe=+,lieu=+,batimentorg=+] = |?[def] | noun[lemme:mairie],  prep[form:fde], noun[admi=+];noun[ord].

/liaison Paris Nantes\

63>  noun[mwe=+,lieu=+,line=+] = noun[line,mwe:~];noun[lemme:liaison];noun[lemme:"axe"], noun[ville], (punct[form:fhyph]), noun[ville].

63>  noun[mwe=+,lieu=+,admi=+] = noun[lemme:station], adj[lemme:"balnéaire"], prep[form:fde], noun[ville];noun[guessed].

//École du Service de Santé des Armées de Lyon-Bron"

63> noun[mwe=+,batimentorg=+] = noun[batimentorg], prep[form:fde],(det[def]),  noun[ville].

Désambiguïsation:
/un village du Luberon\
65> noun[ville:+, region:+] = |noun[preville, sg], prep| noun[ville=~].
65> noun[lemme:Dominique] = |det[def, fem]| noun[ville:+].

marqueur:
80> prep[form:fa], noun[inconnu, maj, person:~, ville=+], punct[form:fopar], noun[region];noun[pays], punct[form:fcpar].
80> prep[form:fa], noun[inconnu, maj, person:~, ville=+], punct[form:fcm], prep[form:fa];prep[form:fen], noun[region];noun[pays], punct.

/selon Dainik Bangla, la ville de Rajshani est à court de vaccins\
85> noun[lieu=~, ville=~, lieupers:~], punct[form:fcm], (det), (adj)*, noun[preville, sg], prep[form:fde], noun[ville].

/aux environs de Nigashima\

85> prep[lemme:"aux environs de"], noun[proper,maj,inconnu,lieu=+].


100> noun[villeamb:+, ville=~, maj:~].
100> noun[villeamb:+, ville=~, phrasemaj:+].
100> noun[villeamb:+, ville=~, start:+].

100> noun[inconnu, maj, pays:~, lieu:~, geo:~, quartier:~, region:~, person:~, lemme:"%C%c+b(o)urg", ville=+].
100> noun[inconnu, maj, pays:~, lieu:~, geo:~, quartier:~, region:~, person:~, lemme:"%C%c+stadt", ville=+].
100> noun[inconnu, maj, pays:~, lieu:~, geo:~, quartier:~, region:~, person:~, lemme:"%C%c+ville", ville=+].
100> noun[inconnu, maj, pays:~, lieu:~, geo:~, quartier:~, region:~, person:~, lemme:"Ville%c+", ville=+].
100> noun[inconnu, maj, pays:~, lieu:~, geo:~, quartier:~, region:~, person:~, lemme:"%C%c+grad", ville=+].

100> noun[inconnu, maj, pays:~, lieu:~, geo:~, quartier:~, region:~, person:~, lemme:"%C%c+kirche", batiment=+].

105> noun[lieupers1:+, perslieu:~, lieupers=+].
105> noun[perslieu1:+, perslieu=+, lieupers:~].
106> noun[perslieu:+, lieupers=~].
106> noun[perslieu=~, lieupers:+].

110> noun[ville:+, lieu=+].
110> noun[pays:+, lieu=+, lieudef:~].
110> noun[region:+, lieu=+].
110> noun[continent:+, lieu=+].
110> noun[geo:+, lieu=+].
110> noun[quartier:+, lieu=+].

120> noun[perslieu:+, person:+, lieu=~, ville=~, quartier=~, region=~, pays=~, batiment=~, batimentorg=~].
120> noun[villereg, ville, region=~].

120> noun[line],(punct), num[lemme:"%d%d%d%d%d"], noun[ville=+,guessed].


Séquence:
// CB : un essai pour les adresses 



121> noun[mwe=+,adresse=+] @= num, punct[form:fcm], noun[quartier].

121>  noun[mwe=+,adresse=+] @= noun[adresse],(punct), num[lemme:"%d%d%d%d%d"], noun[ville], (noun[lemme:"cedex"], (num)).

121>  noun[mwe=+,adresse=+] @=  noun[line],(punct), num[lemme:"%d%d%d%d%d"], noun[ville], (noun[lemme:"cedex"], (num)).

121>  noun[mwe=+,adresse=+] @= noun[adresse],(punct),  noun[ville], adj[ord].


121>  noun[mwe=+,adresse=+] @=  noun[line],(punct), noun[ville], adj[ord].
