Désambiguïsation:





//######################################\\
//				PERSONNE				\\
//######################################\\


1> ?<guessed:+> %= | noun[tit_med:+] | noun[proper:+,person:+,guessed:~,inconnu:+,medecin:+] . // Dr über n'est pas un verbe du 1er groupe 
1> ?<guessed:+> %= | noun[tit:+,tit_med:~] | noun[proper:+,person:+,guessed:~,inconnu:+]. // Mr uber
1> ?<maj:+,noun:~> %= | noun[tit_med:+] | noun[proper:+,person:+,medecin:+] . // Dr Toussait
1> ?<maj:+,noun:~> %= | noun[tit:+,tit_med:~] | noun[proper:+,person:+] . // Mr Toussait
1> ?<maj:+,region:+> %= | noun[tit_med:+] | noun[proper:+,person:+,region:~,medecin:+] . // Dr Aube
1> ?<maj:+,region:+> %= | noun[tit:+,tit_med:~] | noun[proper:+,person:+,region:~] . // Mr Aube


// pour rajouter certaines lectures nom propre pour des suites reconnues de maniere erronee
1> noun[lemme:fleur,maj:+, toutmaj:~] %= | ?[det:~] | noun[proper=+,pren=+]  .
1> noun[lemme:fleur,maj:+, toutmaj:~] %= | | noun[proper=+,pren=+] | ?[maj:+] |.

// PH praticien hospitalier
1> noun[surface:PH] %= | ?[det:~] | noun[masc=+,fem=+,sg=+,p3=+,prof=+]  .
1> noun[surface:PH] %= | | noun[masc=+,fem=+,sg=+,p3=+,prof=+] | ?[num:~] |.

// stade IV, stade II ne sont pas des lieux...
1> noun[rom:+] %= |noun[lemme:stade]| num[rom=+].

// CH - enlever l'interpretation d'en a Cher quand precede "confrère"
1> noun[surface:"Cher"] %= adj[masc:+,sg:+] | noun[lemme:"confrère"] | .

// CH - interne avec maj et pas toutmaj est un nom et pas un adj ni un verbe
1> adj[surface:"Interne"] %= noun[sg=+,tit_med=+,tit=+] .
1> adj[surface:"Internes"] %= noun[sg=+,tit_med=+,tit=+,pl=+] .


Séquence:

2> ? = | ?[det] | noun[lemme:Candida,pren=~,proper=~] |?[maj:~]|. // parfois Candida n'est pas un prénom
2> ? = | ?[det] | noun[lemme:Pam,pren=~,proper=~] |?[maj:~]|. // parfois Pam n'est pas un prénom
2> ? = |noun[lemme:clou, sg:+],prep[lemme:de]| noun[surface:Seidel,proper=~,celeb=~]. //clou de Seidel
2> ? = |noun[lemme:drain],prep[lemme:de]| noun[lemme:Redon,proper=~,celeb=~]. // drain de Redon
2> ? = |noun[lemme:ostéotomie], (noun;punct;adj)*, prep[form:FDE] | noun[lemme:Weil,proper=~,celeb=~]. // Weil

2> ?= noun[lemme:Scarpa,proper=~,celeb=~] |adj|.
2> ?= |noun[lemme:triangle], prep[form:FDE]| noun[lemme:Scarpa,proper=~,celeb=~]. // triangle de Scarpa

// Forrest I(II) classification pour hémorragies gastro-intestinales
2> noun = noun[lemme:Forrest, proper=~], num[card:+] .



// A verifier: prendre ou non le titre dans le noeud noun de l'EN
//5> noun[person=+,proper=+,mwe=+] = noun[tit:+], noun[proper:+] |noun[org:+];noun[prof:+]|.
//5> noun[mwe=+] = noun[lemme:nom];noun[lemme:prénom], punct[form:F2PTS], noun[maj,proper=+,person=+].

// Dr Jean Plouf
10> noun[person=+,proper=+,mwe=+] = |noun[tit:+]| noun[pren:+],?[toutmaj:+].

// responsable : Jean PLOUF
10> noun[person=+,proper=+,mwe=+] = |punct| noun[pren:+],?[toutmaj:+].





//######################################\\
//					LIEU			    \\
//######################################\\

Désambiguïsation:

1> adj[lemme:centré,toutmaj:+] %= noun[masc=+,sg=+,p3=+] |adj[lemme:hospitalier]|. // à gauche de "hospitalier" le mot "centre" en maj est un nom
1> verb[lemme:centrer] %= noun[batimentorg1=+,demaj=+,masc=+,sg=+,p3=+] |prep,noun|. // Centre des Maladies du Foie et de l'Appareil Digestif (ne marche pas si je mets || en contexte gauche)

//Dafalgan : 2 gél 3 fois par jour PO (PO = per os = par voie orale)
1> noun[surface:"PO"] %= adv .

Séquence:

2> ? = |noun[lemme:score],(prep[form:FDE])| noun[lemme:Glasgow,ville=~,proper=~,lieu=~].
2> ? = noun[lemme:Glasgow,ville=~,proper=~,lieu=~] |num|. // parfois Glasgow n'est pas une ville
// CH
2> ? = noun[lemme:Glasgow, ville=~, proper=~, lieu=~] | punct[lemme:"="];punct[lemme:":"] | .
// CH - un Glasgow de NU
2> ? = | det | noun[lemme:Glasgow, ville=~, proper=~, lieu=~] | prep[lemme:de] | .
// CH le Glasgow est a 5 
2> ? = | det | noun[lemme:Glasgow, ville=~, proper=~, lieu=~] | verb[lemme:"être"] | .
// de maniere generale apres un article defini masc (le) Glasgow n'est pas une ville
2> ? = | det[def:+,masc:+] | noun[lemme:Glasgow, ville=~, proper=~, lieu=~] .
// Glasgow precede par parenthese ouvrante n'est pas une ville
2> ? = | punct[form:fopar] | noun[lemme:Glasgow, ville=~, proper=~, lieu=~] .
// Glasgow suivi de coma n'est pas ville
2> ? = noun[lemme:Glasgow, ville=~, proper=~, lieu=~] | noun[lemme:coma] | .


2> ? = |noun[guessed:+];noun[measure:+];noun[unit_weight:+]|noun[lemme:Pô,ville=~,proper=~,lieu=~].
2> ? = noun[lemme:Pô,ville=~,proper=~,lieu=~] |noun[guessed:+]|. // parfois Pô n'est pas une ville

2> ? = noun[lemme:côte,maj:~,geog=~,oronyme=~,lieu1=~].
2> ? = noun[lemme:côte,geog=~,oronyme=~,lieu1=~] |~prep[form:FDE]|. // il y a côtes et côtes...


// CH - pic de <toutmaj> n'est pas un lieu
2> ? = noun[lemme:pic,geog=~,oronyme=~,lieu1=~] | prep[lemme:de] | .


//Centre des Maladies du Foie et de l'Appareil Digestif
2> noun[perslieu1=+, batimentorg=+] @=	noun[batimentorg1, sansde:~, sg, demaj:+, maj, phrasemaj:~],
										(adj[postprof, sg]),
										prep[form:fde],
										(det), 
										noun[maj, phrasemaj:~, pays:~, continent:~], 
										(
											(coord),
											?[maj,adv:~,verb:~]
										), 
										(
											(coord;punct[form:FCM]),
											(prep[form:fde]), 
											(det),
											noun[maj]
										)*.
										
										
										
//Service de Pneumologie; Service des Maladies infectieuses
2> noun[org=+] @=	noun[lemme:service],
					prep[form:fde],
					noun[maj, phrasemaj:~, pays:~, continent:~, lieu:~,tit:~], 
					(adj[lemme:infectieux]),
					(
					(coord;punct[form:FCM]),
					(prep[form:fde]), 
					(det),
					noun[maj,lieu:~,tit:~],
					adj[lemme:infectieux]
					)*.
										


Reconstructeur:
// corrige : DATE [CR] NUM pris comme gd nombre
3> num{num#1,num#2[cr:+]} = #1,#2.							
