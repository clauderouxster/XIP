// deduction_sentiments_1.xip
// dépendances de sentiments, 1ère partie (de deduction_sentiments.xip), extraction des sentiments
// positifs et négatifs
// créé: 25.6.2007 (Sigrid Maurel)
// modifié: 23.1.2009 (Sigrid Maurel)


Déduction:

//----------------------------------------------------------------------------------------------------
// sentiments 


//-------------------- Appreciation


//**************************************** CONSEIL et INCITATION *************************************

//====================================================================================================
//====================================================================================================

// éviter : Max conseille Marie l'objet ne doit pas être humain

// Je te conseille Paris.

if( 	OBJ(#1[conseil : +], #3[noun : +, humain : ~]) 
	& SUBJ(#1, #2)
)
SENTIMENT[appreciation = +, positive = +](#2, #1, #3, ##SPEAKER). 


if( 	OBJ(#1[conseil : -], #3[noun : +, humain : ~]) 
	& SUBJ(#1, #2)
)
SENTIMENT[appreciation = +, negative = +](#2, #1, #3, ##SPEAKER). 
//====================================================================================================
//====================================================================================================




//====================================================================================================
//====================================================================================================

// Je recommande de voir Paris.

if( 	VMOD(#1[conseil : +, positive = +], #3[verb : +, inf : +]) 
	& SUBJ(#1, #2) 
	& PREPOBJ(#3, #5[lemme : de])
)
SENTIMENT[ appreciation = +, positive = +](#2, #1, #3, ##SPEAKER).


if( 	VMOD(#1[conseil : -], #3[verb : +, inf : +]) 
	& SUBJ(#1, #2) 
	& PREPOBJ(#3, #5[lemme : de])
)
SENTIMENT[ appreciation = +, negative = +](#2, #1, #3, ##SPEAKER).
//====================================================================================================
//====================================================================================================




//====================================================================================================
//====================================================================================================
// arg : + pour éviter "Il était clair hier , à voir Besson ."

// Ce jeu est  à conseiller.
// La côte est à visiter.

if ( 	PREPOBJ(#2[incitation: +,  averb: +, inf: +], #1[lemme:à]) 
	& SUBJ(#3[ lemme : être], #4[p3 : +]) 
	& VMOD(#3,#2) 	
)
SENTIMENT[ appreciation = +, positive = +](##EXPERIENCER, #2, #4, ##SPEAKER).



if ( 	PREPOBJ(#2[incitation: -,  averb: +, inf: +], #1[lemme:à]) 
	& SUBJ(#3[ lemme : être], #4[p3 : +]) 
	& VMOD(#3,#2) 	
)
SENTIMENT[ appreciation = +, negative = +](##EXPERIENCER, #2, #4, ##SPEAKER).

//====================================================================================================
//====================================================================================================




//************************************************** Fin CONSEIL *************************************
//****************************************************************************************************





//***********************************************  Nsent **********************************************


//====================================================================================================
//====================================================================================================


// Max éprouve du bonheur.

if(  	( OBJ(#1, #3[Nsent : +, !sentiment:!]) | VMOD(#1, #3[Nsent : +, !sentiment:!])  )
	& ( SUBJ(#1[verb : +, support : +], #2[humain : +]) | SUBJ(#1[verb : +, support : +], #2[pron : +, nom : +]) ) 
	& ( PREPOBJ(#3, #4[lemme : de]) | DETERM(#3, #4[lemme : du]) | ( DETERM(#3, #5[lemme : de]) & DETERM(#3, #6[lemme : le]) )
	   )
)
SENTIMENT[appreciation = +, numRegle = 8](#2, #3, ##CAUSE, ##SPEAKER).

if(^SENTIMENT(#2, #3[Nsent : +], #7, #8) & VMOD(#1[support : +], #4[verb : +, inf : +]))
	SENTIMENT(#2, #3, #4, #8)

//====================================================================================================
//====================================================================================================



//====================================================================================================
//====================================================================================================
// verb de support avoir 

// Max en a raz le bol.
// j'ai honte.

if(  	(OBJ(#1, #3[Nsent : +, support_avoir : +, !sentiment:!]) | VMOD(#1, #3[Nsent : +, support_avoir : +, !sentiment:!]))  
	& ( SUBJ(#1[verb : +, support : +], #2[humain : +]) | SUBJ(#1[verb : +, support : +], #2[pron : +, nom : +]) ) 
)
SENTIMENT[appreciation = +](#2, #3, ##CAUSE, ##SPEAKER).


if(^SENTIMENT[appreciation = +](#2, #3[Nsent : +], #7, #8) & VMOD(#1[support : +], #4[verb : +, inf : +]))
	SENTIMENT[appreciation = +](#2, #3, #4, #8)
//====================================================================================================
//====================================================================================================




//************************************************** Fin Nsent ***************************************
//****************************************************************************************************


//***********************************************  Préference ****************************************


//====================================================================================================
//====================================================================================================
// j'ai un faible pour cette voiture.

if( 	OBJ(#1, #3[ noun: +, preference : +, !sentiment:!]) 
	& SUBJ(#1[lemme : avoir], #2[verb : ~]) 
	& NMOD(#3,#4) 
	& PREPOBJ(#4, #5[lemme : pour])
)
SENTIMENT[appreciation = +](#2, #3, #4, ##SPEAKER).


//====================================================================================================
//====================================================================================================





//====================================================================================================
//====================================================================================================

// La région est une de mes préférées.
if ( 	NMOD(#2, #3[preference: +, !sentiment:!])
	& SUBJ(#1, #4) 
	& OBJ(#1[aux: +], #2[adj : ~,verb : ~, adv: ~]) 
)
SENTIMENT[appreciation = +](##EXPERIENCER, #3, #4, ##SPEAKER).

//====================================================================================================
//====================================================================================================



//====================================================================================================
//====================================================================================================

// #2[verb: ~, adj : ~, adv : ~ ] éviter : Mon préféré c'est possible.

// Mon préféré est Chambord.
if ( 	OBJ(#1[lemme : être], #2[verb: ~, adj : ~, adv : ~, conjque : ~ ]) 
	& SUBJ(#1, #3[preference: +, !sentiment:!, verb: ~]) 

 )
SENTIMENT[appreciation = +](##EXPERIENCER, #3, #2, ##SPEAKER).


//====================================================================================================
//====================================================================================================



//******************************************* Fin PREFERENCE *****************************************
//****************************************************************************************************



//*********************************************** Sents **********************************************




//====================================================================================================
//====================================================================================================

//Le parc est réussi. 

if(	SUBJ[passive : +](#1[verb: +, sents : +,  !sentiment:!],#2)	
)
SENTIMENT[appreciation = +](##EXPERIENCER, #1, #2, ##SPEAKER).


//====================================================================================================
//====================================================================================================





//====================================================================================================
//====================================================================================================

// Quel plaisir d'avoir pris un appartement. On a eu plaisir à vous voir.
//  Une des réussites[desn : +] de Barcelone

if (	NMOD(#1[ sents : +,  !sentiment:!, desn : ~], #2[adj : ~, adv : ~, no-sents : ~]) 
 )
SENTIMENT[appreciation = +](##EXPERIENCER, #1, #2, ##SPEAKER).


//====================================================================================================
//====================================================================================================




//====================================================================================================
//====================================================================================================
// posit2 : Bon, ...
// Paris est une belle ville. C'est du matériel de bonne qualité. 

if (	NMOD(#3[no-sents : ~], #4[adj : +,  sents : +, !sentiment:!])
)
SENTIMENT[appreciation = +](##EXPERIENCER, #4, #3, ##SPEAKER).



//====================================================================================================
//====================================================================================================



//====================================================================================================
//====================================================================================================

// Dans ce cas le sujet est la cause du sentiment : c'est malheureux
//Il est bon. (sents)  


if (	OBJ[spred: +](#2[lemme : être], #1[ sents : +, !sentiment:!]) 
		| VMOD[posit1  : +](#2[lemme : être], #1[ sents : +,  !sentiment:!])
	& SUBJ(#2, #3)
	
)
SENTIMENT[appreciation = +,  numRegle = 2](##EXPERIENCER,  #1, #3, ##SPEAKER).

// Dans ce cas le sujet ressent le sentiment : il est malheureux

// Il est content (emotion)

if(^SENTIMENT[appreciation = +,  numRegle : 2](#7,  #1[emotion : +], #2[dem : ~], #8) )
	SENTIMENT[appreciation = +,  numRegle = 2](#2,  #1, ##CAUSE, ##SPEAKER).


//Il est bon de voir ça.

if (	^SENTIMENT[numRegle : 2](#8,  #1, #3, #10) 
	& (ADJMOD(#1, #4[verb : +, inf : +]) | ADJMOD(#1, #4[noun : +])) // ADJMOD(bon, voir)
	& PREPOBJ(#4, #6) 		 // PREPOBJ(voir, de)
)
SENTIMENT(#8, #1,  #4, ##SPEAKER).



//Il est bon que tu viennes.

if (	^SENTIMENT[numRegle : 2](#8,  #1, #3, #10) 
	& DEEPSUBJ(#6, #4)
	& SUBJ(#6, #3)
)
SENTIMENT(#8, #1,  #4, ##SPEAKER).


//c'est un plaisir de manger.

if (	^SENTIMENT[numRegle : 2](#8,  #1, #3, #10) 
	& NMOD(#1, #4[verb : +, inf : +]) // NMOD(plaisir, manger)
	& PREPOBJ(#4, #6) 		 // PREPOBJ(voir, de)
)
SENTIMENT(#8, #1,  #4, ##SPEAKER).



//====================================================================================================
//====================================================================================================









//====================================================================================================
//====================================================================================================


// Luc chante merveilleusement.


if (	VMOD(#1[verb : +, aux: ~, no-sents : ~, sentiment : ~], #2[ sents : +, !sentiment:!, adv: +]) 
)
SENTIMENT[appreciation = +](##EXPERIENCER, #2, #1, ##SPEAKER).



//====================================================================================================
//====================================================================================================




//*******************************************Fin sents ****************************************
//****************************************************************************************************




//******************************************* Topic_subj Topic_obj ************************************

//--------------------------- sentiment intern : le sujet ressent le sentiment



//====================================================================================================
//====================================================================================================
//j'adore 



if (	(SUBJ(#1[last: +, verb: +, topic_subj : +, !sentiment:!], #2[humain : +]) 
	 | SUBJ(#1[last: +, verb: +, topic_subj : +,exp_subj : +, topic_obj : +,bj : +, !sentiment:!], #2[pron : +, nom : +]))

 )
SENTIMENT[ appreciation = +, numRegle = 4](#2, #1, ##CAUSE, ##SPEAKER).

 

// j'adore le film.

if (	^SENTIMENT[numRegle : 4,  !sentiment:!](#2, #1, #8, #10) 
	& ( OBJ(#1[last: +, verb: +], #3[quant : ~, ton : ~]) | VMOD(#1[last: +, verb: +], #3[quant : ~, adv : ~]))
) 
SENTIMENT(#2, #1, #3, ##SPEAKER).

//nous nous inquiétons de voir ...

if (	^SENTIMENT[numRegle : 4,  !sentiment:!](#2, #1, #8, #10) 
	& REFLEX(#1, #3) & VMOD(#1[last: +, verb: +], #4[quant : ~, adv : ~])
) 
SENTIMENT(#2, #1, #4, ##SPEAKER).

//====================================================================================================
//====================================================================================================






//--------------------------- sentiment extern : l'objet ressent le sentiment


//====================================================================================================
//====================================================================================================

//Le film émoustille Marie.

if (	OBJ(#1[last: +, verb: +, !sentiment:!, topic_obj : +], #2[humain : + ]) 		
	& SUBJ(#1, #3) 
)
SENTIMENT[appreciation = +](#2, #1, #3, ##SPEAKER).


//====================================================================================================
//====================================================================================================





//====================================================================================================
//====================================================================================================

//Le film plait à Marie.

//#VMOD-PP posit1 : PP postposé : rattachement au  verbe le plus proche 

if(	VMOD[posit1 : +](#1[averb : +, topic_obj : +,  !sentiment:!], #2) 
	& SUBJ(#1, #3) 
)
SENTIMENT[appreciation = +](#2, #1, #3, ##SPEAKER).



//====================================================================================================
//====================================================================================================




//====================================================================================================
//====================================================================================================
// #3 pronom accusatif (COD) me, te 
// Le film me plait. 


if(	SUBJ(#1[verb : +,  topic_obj : +, !sentiment:!], #2) 
	& ( OBJ(#1, #3[pron : +, acc : +]) | VMOD(#1, #3[pron : +, dat : +]))
)
SENTIMENT[appreciation = +](#3, #1, #2, ##SPEAKER).




//====================================================================================================
//====================================================================================================




//====================================================================================================
//====================================================================================================
// #2 humain ou pronom nominatif je, tu, ...
//	DEEPSUBJ(#1,#6) : ému "par cette nouvelle"
//	VMOD(#1, #8[psneg : ~]) ému "de voir"	


//je suis ému.
if(	(SUBJ[passive : +](#1[ topic_obj : +, !sentiment:!], #2[humain : +] )
	| SUBJ[passive : +](#1[ topic_obj : +, !sentiment:!], #2[pron : +, nom : +] )
	)  
)
SENTIMENT[ appreciation = +, numRegle = 5](#2, #1, ##CAUSE, ##SPEAKER).




//je suis ému par cette nouvelle. 
// Je serai très intéressé d' entendre vos points de vue.

if(	^SENTIMENT[numRegle : 5](#2, #1, #8, #10) & ( VMOD(#1, #6[adv : ~]) | DEEPSUBJ(#1,#6)   )	)
	SENTIMENT(#2, #1, #6, ##SPEAKER)

//====================================================================================================
//====================================================================================================


//*******************************************Fin topic_subj_obj ***************************************
//*****************************************************************************************************





// ********************************************   TRAIT   *************************************


// elle a un charme fou == à reconnaitre ! fou, énorme, incroyable .... mériteraient bien un trait



//====================================================================================================
//====================================================================================================

// trait  ex: Marie a du charme. ; il a du caractère.

// det[lemme : du] pour éviter "un caractère sociale".
// Elle a du charme.
// Elle beaucoup de courage.

//if ( 	(OBJ(#1[lemme : avoir ], #2[ noun: +, trait: +, !sentiment:!]) | VMOD(#1[lemme : avoir], #2[ noun: +, trait: +, !sentiment:!]))
//	& SUBJ(#1, #3) 
//	& ( DETERM(#2, #4[lemme : du]) | DETERM(#2, #4[lemme : de]) | PREPOBJ(#2, #4[lemme : de]) )
// )
//SENTIMENT[appreciation = +](##EXPERIENCER, #2, #3, ##SPEAKER).




//====================================================================================================
//====================================================================================================






// ****************************************** Fin TRAIT ***********************************************
// ****************************************************************************************************




//-----------------------------------------------------------------------------------------------------
//-----------------------------------------------------------------------------------------------------



//------------------------------------OPINION 



// ******************************************* ADJOP *************************************************


//====================================================================================================
//====================================================================================================

// il est évident que Luc deteste le film .


if(	^SENTIMENT(#8, #1, #2, #10) 
	& DEEPSUBJ(#4[lemme : être ], #1)
	& OBJ[spred : +](#4, #3[adjop : +, !typesent:!])
) 
SENTIMENT[appreciation = ~](#8, #1, #2, #10). 

// je suis sûr que Luc deteste le film.

if(	^SENTIMENT(#8, #1, #2, #10) 
	& ADJMOD(#3[adjop : +, !typesent:!], #1)
) 
SENTIMENT[appreciation = ~](#8, #1, #2, #10). 


//====================================================================================================
//====================================================================================================


//====================================================================================================
//====================================================================================================

// Je suis certain que la ville est belle .

if(	^SENTIMENT(#8, #1, #2, #10) 
	& (OBJ (#5, #1) | VMOD(#5, #1) )
	& ADJMOD(#3[adjop : +, !typesent:!], #5)
) 
SENTIMENT[appreciation = ~](#8, #1, #2, #10). 


//====================================================================================================
//====================================================================================================

// ******************************************* FIN ADJOP **********************************************
// ****************************************************************************************************




// ******************************************* ADVOP *************************************************


//====================================================================================================
//====================================================================================================

// apparemment le parc est réussi.
if( 	^SENTIMENT(#8, #1, #2, #10) 
	& VMOD(#1, #3[advop : +, !typesent:!])
) 
SENTIMENT[appreciation = ~](#8, #1, #2, #10). 


//====================================================================================================
//====================================================================================================

//====================================================================================================
//====================================================================================================

// le film est peut-être bon.

if(	^SENTIMENT(#8, #1, #2, #10) 
	& ADJMOD(#1, #5[advop : +, !typesent:!])
) 
SENTIMENT[appreciation = ~](#8, #1, #2, #10). 


//====================================================================================================
//====================================================================================================

// ******************************************* Fin ADVOP **********************************************
// ****************************************************************************************************







// ******************************************* NOUNOP *************************************************

//====================================================================================================
//====================================================================================================

// j'ai la certitude que Luc chante merveilleusement.
// j'ai la certitude que la précision est bonne.

if(	^SENTIMENT(#8, #1, #2, #10) 
          & (OBJ (#5, #1) | VMOD(#5, #1) )
	& NMOD[propque : +](#3[nounop : +, !typesent:!], #5)
) 
SENTIMENT[appreciation = ~](#8, #1, #2, ##SPEAKER). 


//====================================================================================================
//====================================================================================================



//====================================================================================================
//====================================================================================================

//j'ai la certitude que le parc est réussi.

if(	^SENTIMENT(#8, #1, #2, #10) 
	& NMOD[propque : +](#3[nounop : +, !typesent:!], #1)
) 
SENTIMENT[appreciation = ~](#8, #1, #2, #10). 


//====================================================================================================
//====================================================================================================


// **************************************** FIN NOUNOP ************************************************
// ****************************************************************************************************







// ******************************************* PREPOP *************************************************


//====================================================================================================
//====================================================================================================

//  selon lui Luc deteste le film .
// selon moi on a eu plaisir à vous voir .

|PP[prepop : +]{prep, NP{#3[ pron : +, ton :+ ]}}|
if(	^SENTIMENT(#8, #1, #2, #10) 
	& ( VMOD(#1, #3) | VMOD(#2, #3) | (OBJ(#4, #1) & VMOD(#4, #3) ) | (OBJ(#4, #2) & VMOD(#4, #3) ))
) 
SENTIMENT[opinion = +, appreciation = ~](#8, #1, #2, #10). 


//====================================================================================================
//====================================================================================================



// ******************************************* PREPOP *************************************************
// ****************************************************************************************************





//***************************************** VERBOP ****************************************************


//====================================================================================================
//====================================================================================================

 //le film semble intéressant.
 //les tomates ont l'air bonne.

if(      OBJ(#1[verbop : +, semble : +], #2[sents : +, !sentiment:!]) 	
	& SUBJ(#1, #3)
) 
SENTIMENT[opinion = +](##EXPERIENCER, #2, #3, ##SPEAKER). 

//====================================================================================================
//====================================================================================================


//====================================================================================================
//====================================================================================================

 //je pense qu'il est bon de voir ça .
// je pense qu' elle a du charme .
// je pense que Luc chante merveilleusement.


if( 	^SENTIMENT(#8, #1, #2, #10) 
	& (OBJ(#5, #1) | VMOD(#5, #1))	
	& (OBJ(#3[verbop : +, !typesent:!], #5) | VMOD(#3[verbop : +, !typesent:!], #5))
) 
SENTIMENT[appreciation = ~](#8, #1, #2, #10). 

//====================================================================================================
//====================================================================================================



//====================================================================================================
//====================================================================================================

// Je pense que Paris est une belle ville .
if( 	^SENTIMENT(#8, #1, #2, #10) 
	& (OBJ(#5, #2) | VMOD(#5, #2))	
	& OBJ(#3[verbop : +, !typesent:!], #5)
) 
SENTIMENT[appreciation = ~](#8, #1, #2, #10). 

//====================================================================================================
//====================================================================================================

//====================================================================================================
//====================================================================================================

// je pense que le parc est réussi.
if( 	^SENTIMENT(#8, #1, #2, #10) 
	& (OBJ[propque : +](#3[verbop : +, !typesent:!], #1) | VMOD[subord : +](#3[verbop : +, !typesent:!], #1) )
) 
SENTIMENT[appreciation = ~](#8, #1, #2, ##SPEAKER). 


//====================================================================================================
//====================================================================================================

//----------------- verbop2 : verbe qui doit être précedé par l'auxiliaire être

//====================================================================================================
//====================================================================================================

 //je suis persuadé qu'il est bon de voir ça .
// je suis convainqu qu' elle a du charme .


if( 	^SENTIMENT(#8, #1, #2, #10) 
	& (OBJ(#5, #1) | VMOD(#5, #1))	
	& (OBJ(#3[verbop2 : +, !typesent:!], #5) | VMOD(#3[verbop2 : +, !typesent:!], #5))
	& AUXIL(#3, #6[lemme : être])
) 
SENTIMENT[appreciation = ~](#8, #1, #2, #10). 

//====================================================================================================
//====================================================================================================

//====================================================================================================
//====================================================================================================

// Je suis persuadé que Paris est une belle ville .

if( 	^SENTIMENT(#8, #1, #2, #10) 
	& (OBJ(#5, #2) | VMOD(#5, #2))	
	& VMOD[subord : +](#3[verbop2 : +, !typesent:!], #5)
	& AUXIL(#3, #6[lemme : être])
) 
SENTIMENT[appreciation = ~](#8, #1, #2, #10). 

//====================================================================================================
//====================================================================================================



//====================================================================================================
//====================================================================================================

// je convaincu que le parc est réussi.

if( 	^SENTIMENT(#8, #1, #2, #10) 
	& (OBJ[propque : +](#3[verbop2 : +, !typesent:!], #1) 
		| VMOD[subord : +](#3[verbop2 : +, !typesent:!], #1) )
	& AUXIL(#3, #6[lemme : être])
) 
SENTIMENT[appreciation = ~](#8, #1, #2, ##SPEAKER). 


//====================================================================================================
//====================================================================================================

// *************************************** FIN VERBOP *************************************************
// ****************************************************************************************************

//***********************************************************


//====================================================================================================
//====================================================================================================

// Ce film est moche sans être ennuyant.

if (    OBJ[spred: +](#1, #2[ sents : +, !typesent:!, positive : +]) 
	& PREPOBJ(#1, #3[sfsans: +])
	& SUBJ(#4, #5) 
	& VMOD(#4, #1)
)
SENTIMENT[ negative : +](##EXPERIENCER, #2, #5, ##SPEAKER).

// Ce film est moche sans être ennuyant.
if (    OBJ[spred: +](#1, #2[ sents : +, !typesent:!, negative : +]) 
	& PREPOBJ(#1, #3[sfsans: +])
	& SUBJ(#4, #5) 
	& VMOD(#4, #1)
)
SENTIMENT[ positive : +](##EXPERIENCER, #2, #5, ##SPEAKER).
//====================================================================================================
//====================================================================================================


//==================================================================================================
//==================================================================================================

// Il est sans charme.

if ( PREPOBJ(#1[trait : +, !typesent:!, positive : +], #3[sfsans: +]) 
	& SUBJ(#4, #5)
	& OBJ(#4, #1)
)
SENTIMENT[appreciation = +, negative = +, positive = ~](##UNKNOWN, #1, #5, ##UNKNOWN).


//====================================================================================================
//====================================================================================================

