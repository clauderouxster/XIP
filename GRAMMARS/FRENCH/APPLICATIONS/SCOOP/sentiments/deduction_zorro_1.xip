// deduction_zorro_1.xip
// dépendances de ZORRO
// créé: 20.1.2009 (Sigrid Maurel)
// modifié: 2.2.2009 (Sigrid Maurel)


Déduction:

// J'aime beaucoup l'Allemagne et la France.
// inclure les verbes à la 3e personne "mon bébé aime ..." dans ZORRO
// exclure les verbes à la 1ère personne pour éviter les doublons
if ( OBJ(#1[last:+, verb:+, cond:~, fut:~, sents:+, positif:+, val:~, inf:~, p2:~, p1:~], #2[no-sents:~])
 & ~AUXIL(#1, #3[cond:+]) & ~AUXIL(#1, #3[fut:+]) & ~AUXIL(#1, #3[p3:+]) )
SENTIMENT[positif=+, confiance=2](#1, #2).

if ( OBJ(#1[last:+, verb:+, cond:~, fut:~, sents:+, negatif:+, val:~, inf:~, p2:~, p1:~], #2[no-sents:~])
 & ~AUXIL(#1, #3[cond:+]) & ~AUXIL(#1, #3[fut:+]) & ~AUXIL(#1, #3[p3:+]) )
SENTIMENT[negatif=+, confiance=2](#1, #2).


// Mon fils est allergique au plv.
if ( ADJMOD(#1, #2[allergene:+, negatif:+]) | NMOD(#1, #2[allergene:+, negatif:+]) )
SENTIMENT[negatif=+, confiance=2](#1, #2).

// Mon fils est allergique au plv et au gluten.
if ( SENTIMENT(#1, #2[allergene:+, negatif:+]) & COORDITEMS(#2, #3[allergene:+, negatif:+]) )
SENTIMENT[negatif=+, confiance=2](#1, #3).


// facile à digérer
if ( ADJMOD(#1[sents:+, positif:+], #2[verb:+, inf:+]) )
SENTIMENT[positif=+, confiance=3](#1, #2).

// difficile à digérer
if ( ADJMOD(#1[sents:+, negatif:+], #2[verb:+, inf:+]) )
SENTIMENT[negatif=+, confiance=3](#1, #2).
