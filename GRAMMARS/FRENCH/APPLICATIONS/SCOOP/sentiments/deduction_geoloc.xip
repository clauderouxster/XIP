// deduction_geoloc.xip
// grammaire de géolocalisation
// créé: 27.3.2009 (Sigrid Maurel, CELI France)
// modifié: 2.4.2009 (Sigrid Maurel, CELI France)


Marqueur:

1> noun[batiment:+, geoent=+].
1> noun[batiment1:+, geoent=+].
1> noun[batimentorg:+, geoent=+].
1> noun[batimentorg1:+, geoent=+].
1> noun[lieu:+, geoent=+].
1> noun[lieu1:+, geoent=+].
1> noun[quartier:+, geoent=+].
1> noun[quartier1:+, geoent=+].



//----------------------------------------------------------------------------------------------------
Déduction:

// interpretation sémantique constante: DIRECTION, LOCALIZE', REFERENCE
// avec la dépendance LIEU:

// L'église St Jean se trouve à gauche sur la place Victor Hugo.
50> if ( SUBJ(#1, #2) & VMOD(#1, #3[geoloc:+]) & VMOD(#1, #4) & LIEU(#2) & LIEU(#4) )
GEOLOC(#3, #2, #4).

// L'église St Jean se trouve derrière la place Victor Hugo.
// attention: pas de lien entre #2 et #4
50> if ( SUBJ(#1, #2) & PREPOBJ(#4, #3[geoloc:+]) & VMOD(#1, #4) & LIEU(#2) & LIEU(#4) )
GEOLOC(#3, #2, #4).

// L'église St Jean est proche de la place Victor Hugo.
50> if ( SUBJ(#1, #2) & OBJ(#1, #3[geoloc:+]) & ADJMOD(#3, #4) & LIEU(#2) & LIEU(#4) )
GEOLOC(#3, #2, #4).

// L'église St Jean est à gauche.
50> if ( SUBJ(#1, #2) & VMOD(#1, #3[geoloc:+]) & LIEU(#2) )
GEOLOC(#3, #2, ##EMPTY).

// L'église St Jean est proche.
50> if ( SUBJ(#1, #2) & OBJ(#1, #3[geoloc:+]) & LIEU(#2) )
GEOLOC(#3, #2, ##EMPTY).

// L'église St Jean est près.
50> | SC{?*, NP{?*, #1}, ?*, FV{?*, #2}}, prep#3[geoloc:+] |
if ( SUBJ(#2, #1) & LIEU(#1) )
GEOLOC(#3, #1, ##EMPTY).



//----------------------------------------------------------------------------------------------------
// sans la dépendance LIEU, utilisation du trait "geoent":

// Pont au-dessus d'un déversoir
50> if ( NMOD(#1[geoent:+], #2) & PREPOBJ(#2, #3[geoloc:+]) )
GEOLOC(#3, #1, #2).

// Le lac au centre du Parc
50> if ( NMOD(#1[geoent:+], #2[geoloc:+]) & NMOD(#2, #3[geoent:+]) )
GEOLOC(#2, #1, #3).

// Le Pont des Soupirs est à gauche du palais des Doges.
50> if ( SUBJ(#1, #2[geoent:+]) & OBJ(#1, #3) & VMOD(#1, #4[geoloc:+]) )
GEOLOC(#4, #2, #3).

// Le château est sur la gauche en face des pompiers à l'entrée de Bazas.
50> if ( SUBJ(#1, #2[geoent:+]) & OBJ(#1, #3) & PREPOBJ(#3, #4[geoloc:+]) )
GEOLOC(#4, #2, #3).

// L'église St Jean est au nord de la capitale.
50> if ( SUBJ(#1, #2[geoent:+]) & VMOD(#1, #3) & PREPOBJ(#3, #4[geoloc:+]) & ~GEOLOC(#4, #2, #10) )
GEOLOC(#4, #2, #3).

// L'hôtel est à 300 mètres à gauche sur la place Richelieu.
50> if ( SUBJ(#1, #2[geoent:+]) & OBJ(#1, #3) & VMOD(#1, #4[geoloc:+]) & GEOLOC(#4, #2, #3)
 & VMOD(#1, #5[geoent:+]) )
GEOLOC(#4, #2, #5).

// Le Pont des Soupirs est à gauche.
50> if ( SUBJ(#1, #2[geoent:+]) & VMOD(#1, #3[geoloc:+]) & ~GEOLOC(#3, #2, #10) )
GEOLOC(#3, #2, ##EMPTY).

// La mer à main gauche jusqu'à Gibraltar.
50> if ( NMOD(#1, #2[geoent:+]) & NMOD(#2, #3[geoloc:+]) )
GEOLOC(#3, #1, ##EMPTY).

// Le Pont des Soupirs est proche.
50> if ( SUBJ(#1, #2[geoent:+]) & OBJ(#1, #3[geoloc:+]) & ~GEOLOC(#3, #2, #10) )
GEOLOC(#3, #2, ##EMPTY).

// Le Pont des Soupirs est près.
// attention: pas de lien entre #2 et #3
50> | SC{?*, NP{?*, #2}, ?*, FV{?*, #1}}, prep#3[geoloc:+] |
if ( SUBJ(#1, #2[geoent:+]) )
GEOLOC(#3, #2, ##EMPTY).



//----------------------------------------------------------------------------------------------------
// direction sans but

// prendre à gauche...
50> if ( VMOD(#1, #2[geoloc:+]) & ~GEOLOC(#2, #10, #11) )
GEOLOC(#2, #1, ##EMPTY).



//----------------------------------------------------------------------------------------------------
// adresses

// A Grenoble au 12 rue Claude Genin.
60> if ( LIEU[quartier:+](#1) & DETERM(#1, #2[num:+]) )
ADD-NUM(#1, #2).

// A Grenoble au 12 rue Claude Genin.
60> | noun#1[quartier:+]{noun#2[quartier1:+], noun#3} |
if ( LIEU(#1) & NMOD(#2, #3) )
ADD-STREET(#1, #3).

// A Grenoble au 12 rue Claude Genin.
60> | noun#1[quartier:+]{noun#2[quartier1:+], noun#3} |
if ( LIEU[quartier:+](#1) & LIEU[ville:+](#4) & (NMOD(#4, #1) | NMOD(#1, #4)) )
ADD-TOWN(#1, #4).

// Au 12 rue Claude Genin et au 14 cours Jean Jaurès à Grenoble.
60> | noun#1[quartier:+]{noun#2[quartier1:+], noun#3} |
if ( LIEU[quartier:+](#1) & LIEU[quartier:+](#4) & ADD-TOWN(#4, #5) & COORDITEMS(#1, #4) )
ADD-TOWN(#1, #5).

