// localgram_sentiments.xip
// grammaire locale pour les mots multiples des sentiments
// créé: 1.3.2007 (Sigrid Maurel)
// modifié: 2.2.2009 (Sigrid Maurel)


Séquence:

// pas mal
// 21.6.2007: transféré dans split_sentiments.xip
//1> adj[sents=+, positif=+, enchante=+] = adv[lemme:"pas mal"].

// interressant (sans accent é, avec 2 r)
1> adj[sents=+, positif=+, enchante=+] = prefix[lemme:inter], prefix[lemme:re], ?[surface:ssant].
1> adj[sents=+, positif=+, enchante=+] = prefix[lemme:inter], prefix[lemme:re], ?[surface:ssante].
1> adj[sents=+, positif=+, enchante=+] = prefix[lemme:inter], prefix[lemme:re], ?[surface:ssants].
1> adj[sents=+, positif=+, enchante=+] = prefix[lemme:inter], prefix[lemme:re], ?[surface:ssantes].
// intéréssant (avec 2e accent é)
1> adj[sents=+, positif=+, enchante=+] = adj[surface:intéréssant].
1> adj[sents=+, positif=+, enchante=+] = adj[surface:intéréssante].
1> adj[sents=+, positif=+, enchante=+] = adj[surface:intéréssants].
1> adj[sents=+, positif=+, enchante=+] = adj[surface:intéréssantes].

// bien sur/biensur (sans accent û, et/ou en un mot)
// éventuellement trop d'effets de bord?
1> adv = adj[lemme:bien], prep[lemme:sur].
1> adv = prefix[lemme:bi], ?[surface:ensur].
1> adv = prefix[lemme:bi], ?[surface:ensûr].

// bin (=ben)
1> adv = prefix[lemme:bi], ?[surface:n].

// est ce que
1> adv[sque=+] = verb[lemme:être, p3:+, pre:+, sg:+], pron[lemme:ce], conjque[lemme:que].

// 6.12.2007: -> lexique_corrections.xip avec règles ::
// trés (accent à l'envers)
//1> adv = ?[surface:trés]; ?[surface:Trés].
// aprés (accent à l'envers)
//1> prep = ?[surface:aprés]; ?[surface:Aprés].

// tb (très bien)
1> adj[sents=+, positif=+] = noun[surface:tb]; noun[surface:TB].

// à coté
1> prep = prep[lemme:à], ?[surface:coté].

// ? (trois points (...) en un seule caractère), semble se comporter comme le oe, est remplacé par un ?
// PB: règle n'a pas d'effet???
//1> punct[sent=+, noun=~] = noun[surface:\?].

// être classe
1> adj[sents=+, positif=+] = | verb[lemme:être], (?[verb:~, noun:~])* | ?[surface:classe].

// peut-etre (sans accent ê)
1> adv = verb[surface:peut], (punct[hyph:+]), verb[surface:etre].

// pire en pire
1> noun[sents=+, negatif=+, mwe=+] = noun[lemme:pire], prep[lemme:en], noun[lemme:pire].

// mieux en mieux
1> noun[sents=+, positif=+, mwe=+] = noun[lemme:mieux], prep[lemme:en], noun[lemme:mieux].

// pas ... du tout
1> adv = | adv[lemme:pas], verb | prep[lemme:de], pron[lemme:tout].

// un p'tit peu
1> adv = det[lemme:un], noun[lemme:p], punct[lemme:\'], noun[surface:tit], pron[lemme:peu].
2> adj = noun[lemme:p], punct[lemme:\'], noun[surface:tit].

// ultra (rapide) (=très), super rapide, super bien, hyper bien
5> adv[toto=+] = adj[lemme:hyper]; adj[lemme:super, sents=~, positif=~, excite=~]; ?[surface:ultra];
 adj[lemme:bien, sents=~, positif=~, enchante=~] | (adv), adj |. //[verb:~]

// mais bon, après bon, bon après, bon voilà
5> adv = coord[lemme:mais]; prep[lemme:après]; noun[lemme:oui]; adv[lemme:donc],
 adj[lemme:bon, sents=~, positif=~, enchante=~].
5> adv = adj[lemme:bon, sents=~, positif=~, enchante=~], prep[lemme:après]; adv[lemme:voilà];
 ?[surface:voila]; noun[lemme:aller]; adv[lemme:certes].

// code html
10> punct[html=+] = coord[lemme:\&], punct[lemme:\#], num, punct[lemme:\;].



//----------------------------------------------------------------------------------------------------
Désambiguïsation:

// super galère
45> verb, adj = | adv[toto:+] | adj.

// un clic
50> adv[lemme:clic] %= | det | noun[noun=+, masc=+, sg=+].

// adverbes qui devraient être taggué comme adjectifs
// pas trop mal, pas trop bien
55> adv[lemme:"mal"] %= | adv[lemme:"pas"], adv[lemme:"trop"] | adj[sents=+, negatif=+].
55> adv[lemme:"bien"] %= | adv[lemme:"pas"], adv[lemme:"trop"] | adj[sents=+, positif=+].

// en avoir marre
60> verb[surface:marre] %= | verb[lemme:avoir], (adv) | noun[sents=+, negatif=+, positif=~].

// par défaut -> pas négatif
65> noun[lemme:défaut] = | prep[lemme:par] | noun[sents=~, negatif=~].

// enfin bon, alors bon -> pas positif
65> adj[lemme:bon] = | adv[lemme:alors]; adv[lemme:enfin] | adj[sents=~, positif=~, enchante=~].

// propre à
65> adj[lemme:propre] = adj[sents=~, positif=~] | prep[lemme:à] |.

// pas mal de ...
// Après pas mal de temps passé à mettre au point une idée qui me trottait dans la tête, ...
// C'est pas mal d'aller au cinéma.
// Globalement je me suis pas mal investi sur ce jeu.
65> adj[lemme:mal] = | adv[lemme:pas] | adj[sents=~, negatif=~] | prep[lemme:de], noun[verb:~] |.
65> adj[lemme:mal] = | adv[lemme:pas] | adj[sents=~, negatif=~] | verb[partpas:+] |.

// lettre recommandée
65> ?[surface:recommandée] = | noun[lemme:lettre] | ?[sents=~, positif=~, conseil=~, averb=~].

