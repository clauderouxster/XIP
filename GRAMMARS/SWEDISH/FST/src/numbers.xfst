# ============================================================================
# CONTENT:      written numbers
# 
# LANGUAGE:     SWEDISH
#
# AUTHOR:       A. Schiller
#
# CREATED:      26-Aug-2010
# ============================================================================
# USAGE:        xfst 	-l [ThisFile]
# ============================================================================

define NUMCARD	[ %+Num:0 %+Card:0 ];
define NUMORD	[ %+Adj:0 %+Ord:0 ];
define NUMORDM	[ %+Adj:0 %+Ord:0 %+Masc:0 ];

define Card0	{noll} ;
define Ord0	{nollte}  NUMORD ;

define Card1	[{en}|{ett}];	
define CardN	[{två}|{tre}|{fyra}|{fem}
       		|{sex}|{sju}|{åtta}|{nio}];
define OrdN	[{första}|{andra}|{tredje}|{fjärde}|{femte}
       		|{sjätte}|{sjunde}|{åttonde}|{nionde}] NUMORD ;

define OrdMasc	[ {först} a:e | {andr} a:e ] NUMORDM;


define Card1N	[{tio}|{elva}|{tolv}|{tretton}|{fjorton}|{femton}
       		|{sexton}|{jsutton}|{arton}|{nitton}];
define Ord1N	[{tionde}|{elfte}|{tolfte}|{trettonde}|{fjortonde}
       		|{femtonde}|{sextonde}|{jsuttonde}|{artonde}|{nittonde}] NUMORD ;

define CardN0	[{tjugo}|{trettio}|{fyrtio}|{femtio}
       		|{sextio}|{sjutio}|{åttio}|{nittio}];

define OrdN0	CardN0 {nde}  NUMORD ;

define Card100	{hundra} ;
define Ord100	Card100 {de} NUMORD ;

define Card1000 {tusen};
define Ord1000	Card1000 {de} NUMORD ;


# combinations
define Card2to99	[ CardN | Card1N | CardN0 (CardN|Card1) ];
define Card1to99	Card1 | Card2to99 ;

define Card2to999 	[ (( {ett} | CardN ) Card100) Card2to99 ] ;

define CARD		[ [ ( ({ett} | Card2to999) Card1000 ) [Card1 | Card2to999] ] 
       			| Card1000 
			| Card100 
			| Card0 ] NUMCARD ;


define Ord1to99		OrdN | Ord1N | OrdN0 | CardN0 OrdN ;

define Ord1to999	(( {ett} | CardN ) Card100) Ord1to99 ;

define ORD 		[ ( ({ett} | Card2to999) Card1000 )  Ord1to999 ] 
       			| Ord1000 
			| Ord100
			| Ord0 ;

read regex [
	CARD 
|	ORD 
];