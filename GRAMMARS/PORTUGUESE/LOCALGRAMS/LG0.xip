//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : LG0.xip                                                     !
//!   Date       : 2007/Maio                                                   !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!  Dealing with verbs that have different properties if they are used as     !
//!  base form or with a refl pronoun.                                         !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!



Sequence:

////////////////////////////////////////////////////////////////////////////////
// Measure
////////////////////////////////////////////////////////////////////////////////
1> symbol[time=+, meas=+] = |num| ?[lemma:"'"], ?[lemma:"'"].


////////////////////////////////////////////////////////////////////////////////
// verbs
////////////////////////////////////////////////////////////////////////////////

// TORNAR-SE
// TORNAR-SE has to bear the feature cop:+, when TORNAR alone has not.
// We also have to deal the cases where the reflex is on the left of the verb
//
// Example: Ele tornou-se presidente.
//
1> verb[cop=+] = ?[lemma:tornar], ?[lemma:se]. 
1> verb[cop=+] = ?[lemma:se], ?[lemma:tornar] .

// FAZER-SE
// FAZER-SE before an adjective has to be taken as a copula.
// Example: Ela fez-se bonita
//
1> verb[cop=+] = ?[lemma:fazer], ?[lemma:se] | (pron[quant];adv), adj | .
1> verb[cop=+] = ?[lemma:se], ?[lemma:fazer] | (pron[quant];adv), adj | .


// DEITAR-SE A + INF
// Deitar-se is an aspectual form and has to bear feature aInfInc:+
//
// Example: Ele deitou-se a comer.
//
1> verb[aInfInc=+] = ?[lemma:deitar], ?[lemma:se] | ?[lemma:a], verb[inf:+] | .
1> verb[aInfInc=+] = ?[lemma:se], ?[lemma:deitar] | ?[lemma:a], verb[inf:+] | .


// POR-SE A + INF
// pôr-se is has to bear the feature aInfInc:+ 
//
// Examples: Eu pus-me a chorar 
// Examples: Eu pus-me chorando
//
1> verb[aInfInc=+,noGerInc=+] = ?[lemma:pôr], ?[lemma:se] | (?[lemma:a]), verb[gerund:+];verb[inf:+] | .
1> verb[aInfInc=+,noGerInc=+] = ?[lemma:se], ?[lemma:pôr] | (?[lemma:a]), verb[gerund:+];verb[inf:+] | .


// A GENTE is personal pronoun that accords inflexion pers=3, Gender=f, Number=sing
// but it is interpreted as pers=1, Number=plural (the same as "nós")
//
// This rule should be coded in RuDriCo as soon as negation is available there!!!!!!
//
// Example: A falar é que a gente se entende
// Example: Se a gente vier a esta hora
// Example: A gente nova não sabe viver (it is not a pronoun)
// Example: A gente do teatro ... (it is not a pronoun)
//
2> pron[pers=+,fem=+,sg=+] = ?[surface:a], ?[lemma:gente] | ?[adj:~,surface:~"de"] | .


