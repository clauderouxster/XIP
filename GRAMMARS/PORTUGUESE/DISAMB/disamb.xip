//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2003 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : Portuguese XIP grammar
//!   filename   : disamb.xip
//!   Date       : March. 2005
//!   Author     : Caroline Hagege
//!   Description: General POS disambiguation rules
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


// POS disambiguation


Tagging:


// -----------------------------------------------------------------------------
// upper case words (other than at the beginning of a sentence) must be
// * a  proper name


1> ?<maj:+,start:~> = ?<proper:+> .

// CH June 2007
// If a token is starting by an uppercase and is preceeded by "a" in lower case
// and followed by something in lower case, then we consider it as a proper noun
// e.g. a Vigor , nome de empresa ?

1> ?[maj:+,toutmaj:~,proper:~,time:~] %= | art[lemma:"o",fem:+,maj:~] | noun[proper=+] . 


// -----------------------------------------------------------------------------

// Deleting tag ADJ for past-participle
2> adj, pastpart = ?[adj=~] .


// -----------------------------------------------------------------------------

// FRACTION-ADJ-NOUN ambiguity
// e.g meia
// 2 canecas e meia (um quarto)
2> num,adj,noun = | num, adj;noun, ?[lemma:e], (art) | num .



// -----------------------------------------------------------------------------

// Se houver uma preposicao por perto a esquerda e uma forma ambigua verbal conjuntivo, infinitivo
// entao escolha-se a forma infinitiva
// para conseguir
2> verb<inf:+>, verb<subj:+,fut:+> = | prep, (art), (adj), (noun), (adv) | verb[inf:+] .

// -----------------------------------------------------------------------------

// NOUN-ADJ ambiguity


10> noun, adj = adj | noun[verb:~,proper:~,adj:~] | .


// if there is an interrogative followed by ambiguous noun/adj followed by non-ambiguous verb
// then the ambiguous noun/adj is a noun
// Que general venceu ?
10> noun, adj = | pron[lemma:que] | noun | verb[noun:~,adj:~], ?*, punct[lemma:?] | .


// -----------------------------------------------------------------------------

// INFINITIVE vs NOUN ambiguity
// After an aspectual verb followed by a prep, then it is an non-flexioned infinitive

10> noun, verb<inf:+> = | verb[vasp:+], (adv), prep |  verb[inf:+] .


// If we have an ambiguous verb[inf],noun followed by an infinitive verb
// And if this verb is also preceeded by a preposition
// Then it is an infinitive and not a noun
// e.g. Vim para o Pedro poder comer o bolo.
11> noun, verb<inf:+> = | prep, (art;pron[quant:+]), (pron[poss:+]), (adj), noun | verb[inf:+] | (prep), verb[inf:+] | .


// -----------------------------------------------------------------------------
// PRON-ADV ambiguity
//

//

15> pron<lemma:"muito",sg:+>    %= | verb | adv | prep | .
15> pron<lemma:"pouco",sg:+>    %= | verb | adv | prep | .

15> pron<lemma:"nada">     %= | verb | adv | prep | .



// -----------------------------------------------------------------------------

// PRON PREP+DETPL ambiguity (e.g. nos)
30> pron[surface:"nos"] %= prep | noun | .
30> noun[surface:"nos"] %= prep | noun | .

// -----------------------------------------------------------------------------

// ADJ Verb first person sigular
// if we have a ambiguous verb[1st person]/adj that just follows a singular noun,
// then it is an adj and not a verb
// e.g. financiamento oculto

40> adj<sg:+>, verb<pres:+,1p:+> = | noun[sg:+] | adj .

// -----------------------------------------------------------------------------

// NOUN Verb first person sigular
// if we have a ambiguous verb[1st person]/noun that just follows determiners/adj
// then it is an noun and not a verb
// e.g. Os seu belo pupilo

40> noun<sg:+>, verb<pres:+,1p:+> = | art;pron[dem:+], (pron[poss:+];num), adj[sg:+] | noun .


// --------------------------------------------------------------------------

// NOUN/VERB ambiguity
// If we have the lemma "que" followed possibly by an adverb and then by an ambiguous 
// noun/verb 
// Then we chose the reading verb and not noun
// e.g. diz que ja veio[VERB] na regiao ha 18 anos.
40> noun<sg:+>, verb = | ?[verb], ?*, ?[lemma:que], (pron), (adv) | verb .

// NOUN/VERB ambiguity preceeded by "que" in case of interrogative clauses
// e.g. Em que data foi ?
40> noun<sg:+>, verb = | (prep), ?[lemma:que] | noun | (adj), verb | .

// --------------------------------------------------------------------------

// VERB[fut:+,subj:+]/VERB[inf] ambiguity
// If we have on the left of an ambiguous verb[fut,subj]/infinitive verb
// AND if on the left of this verb we have another verb bearing feature
// s_np_inf, then we chose the infinitive form
40> verb<inf:+>,verb<subj:+,fut:+> = | verb[s_np_inf:+], ?* | verb[inf:+] .

// -----------------------------------------------------------------------------
// for the rest: take preferred readings from HMM tagger

90> ? = ?[hmmselection].


// -----------------------------------------------------------------------------
// Regras apos a seleccao do HMM

// VINF apanhado como Verbo finito
// Vim para poder o Pedro COMER[vinf] o bolo

100> verb[fut:+,subj:+,1p:+] %= | verb[noinfdeo:+], (art;pron[quant]), (pron[poss:+]), (adj), noun | verb[verb:+,inf:+] .


// VINF taken as masculine noun. This is only done for very constrained constructions.
//
// Example: Para ele, o PCD é o CONTINUAR(as noun) da esperança de os cabo-verdianos 
// poderem resolver problemas.
// check if not introducing problems.
100> verb[inf:+] %= | verb[lemma:ser], art[lemma:o,sg:+,masc:+] | noun[masc:+,sg:+,inf:+] .
// Same rule as above when the article is the first element of the sentence
//
// Example : O continuar da esperança de os cabo-verdianos poderem resolver problemas...
100> verb[inf:+] %= | art[lemma:o,sg:+,masc:+,start:+] | noun[masc:+,sg:+,inf:+] .

