!# ============================================================================
!# TYPE:	lexc
!# ENCODING:	UTF-8	[€äàáâ]
!# CONTENT:     function words 
!# 		(pronouns, determiners, prepositiosn, conjunctions)
!#
!# LANGUAGE:    PORTUGUESE
!#
!# CREATED:	12-Aug-2008
!#
!# ============================================================================
!# NOTE: 
!# - added features "+Neg" and "+Coord" to some conjunctions
!# ============================================================================

Multichar_Symbols
+Adv
+Art
+Conj
+Det
+Prep
+Pron


+Comp
+Coord 
+Def 
+Dem
+Excl
+Indef
+Interrog
+Neg
+Pers
+Pop
+Poss
+Postprep
+Refl
+Rel 

+Sg +Pl +SP
+Masc +Fem +MF
+1P +2P +3P
+Nom +Acc +Dat +AccDat

+Acron +Abbr


! special markers
^=	! "compound" boundary

(.)	! optional dot
(-)	! optional hyphen
[-]	! hyphen or space

^]	! lemma boundary (for inflecting proper names)



!# ------------------------------------------------------------------------
!# Continuation classes

LEXICON	MSG	+Masc+Sg	#;
LEXICON	FSG	+Fem+Sg		#;
LEXICON	MPL	+Masc+Pl	#;
LEXICON	FPL	+Fem+Pl		#;

LEXICON MSGPL	MSG; MPL;
LEXICON FSGPL	FSG; FPL;

LEXICON MFSGPL	MSGPL; FSGPL;

!# plural = singular
LEXICON MSP	+Masc+SP	#;
LEXICON FSP	+Fem+SP		#;
LEXICON MFSP	MSP; FSP;

!# masc = fem
LEXICON XSG	+MF+Sg		#;
LEXICON XPL	+MF+Pl		#;
LEXICON XSGPL	XSG; XPL;

!# masc = fem; plural = singular
LEXICON XSP	+MF+SP		#;


!# =============================================================================
!# MAIN LEXICON
!# =============================================================================

LEXICON ROOT

!# -----------------------------------------------------------------------------
!# articles
!# -----------------------------------------------------------------------------
o+Art+Def:a			FSG;
o+Art+Def:as			FPL;
o+Art+Def			MSG;
o+Art+Def:os			MPL;

um+Art+Indef			MSG;
um+Art+Indef:uma		FSG;
um+Art+Indef:umas		FPL;
um+Art+Indef:uns		MPL;

!# -----------------------------------------------------------------------------
!# determiners
!# -----------------------------------------------------------------------------
cada+Det+Indef			XSG;
cujo+Det+Rel:cuja		FSG;
cujo+Det+Rel:cujas		FPL;
cujo+Det+Rel:cujos		MPL;
cujo+Det+Rel			MSG;
outro+Det+Indef			MSG;
outro+Det+Indef:outra		FSG;
outro+Det+Indef:outras		FPL;
outro+Det+Indef:outros		MPL;
qual+Det+Interrog:quais		XPL;
qual+Det+Interrog		XSG;
qualquer+Det+Indef:quaisquer	XPL;
qualquer+Det+Indef		XSG;
quanto+Det+Interrog		MSG;
quanto+Det+Interrog:quanta	FSG;
quanto+Det+Interrog:quantas	FPL;
quanto+Det+Interrog:quantos	MPL;
que% de+Det+Excl		XPL;
que+Det+Interrog		XSP;

!# -----------------------------------------------------------------------------
!# personal pronouns
!# -----------------------------------------------------------------------------
! The case tag may be +Nom (nominative), +Acc (accusative), +Dat (dative) 
! or +Postprep (when preceded by a preposition). 

eu+Pron+Pers+Acc+1P+Sg:me	      	#;
eu+Pron+Pers+Nom+1P+Sg			#;
eu+Pron+Pers+Dat+1P+Sg:me		#;
eu+Pron+Pers+Dat+1P+Sg:m		ContPron3;
eu+Pron+Pers+Postprep+1P+Sg:mim		#;

tu+Pron+Pers+Nom+2P+Sg			#;
tu+Pron+Pers+Acc+2P+Sg:te		#;
tu+Pron+Pers+Dat+2P+Sg:te		#;
tu+Pron+Pers+Dat+2P+Sg:t		ContPron3;
tu+Pron+Pers+Postprep+2P+Sg:ti		#;

ele+Pron+Pers+Nom+3P+Masc+Sg		#;
ele+Pron+Pers+Acc+3P+Masc+Sg:o		#;
ele+Pron+Pers+Dat+3P+Masc+Sg:lhe	#;
ele+Pron+Pers+Dat+3P+Masc+Sg:lh		ContPron3;
ele+Pron+Pers+Postprep+3P+Masc+Sg	#;

ela+Pron+Pers+Nom+3P+Fem+Sg		#;
ela+Pron+Pers+Acc+3P+Fem+Sg:a		#;
ela+Pron+Pers+Dat+3P+Fem+Sg:lhe		#;
ela+Pron+Pers+Dat+3P+Fem+Sg:lh		ContPron3;
ela+Pron+Pers+Postprep+3P+Fem+Sg	#;

nós+Pron+Pers+Nom+1P+Pl			#;
nós+Pron+Pers+Acc+1P+Pl:nos		#;
nós+Pron+Pers+Dat+1P+Pl:nos		#;
nós+Pron+Pers+Dat+1P+Pl:no-l		ContPron3;
nós+Pron+Pers+Postprep+1P+Pl		#;

vós+Pron+Pers+Nom+2P+Pl			#;
vós+Pron+Pers+Acc+2P+Pl:vos		#;
vós+Pron+Pers+Dat+2P+Pl:vos		#;
vós+Pron+Pers+Dat+2P+Pl:vo-l		ContPron3;
vós+Pron+Pers+Postprep+2P+Pl		#;

eles+Pron+Pers+Nom+3P+Masc+Pl		#;
eles+Pron+Pers+Acc+3P+Masc+Pl:os	#;
eles+Pron+Pers+Dat+3P+Masc+Pl:lhes	#;
eles+Pron+Pers+Dat+3P+Masc+Pl:lh	ContPron3;
eles+Pron+Pers+Postprep+3P+Masc+Pl	#;

elas+Pron+Pers+Nom+3P+Fem+Pl		#;
elas+Pron+Pers+Acc+3P+Fem+Pl:as		#;
elas+Pron+Pers+Dat+3P+Fem+Pl:lhes	#;
elas+Pron+Pers+Dat+3P+Fem+Pl:lh		ContPron3;
elas+Pron+Pers+Postprep+3P+Fem+Pl	#;

! reflexive
se+Pron+Refl+AccDat+3P			XSP;
se+Pron+Refl+Postprep+3P:si		XSP;


! with adverb
eis+Adv^=ela+Pron+Acc+3P+Fem+Sg:ei-la		#;
eis+Adv^=elas+Pron+Acc+3P+Fem+Pl:ei-las		#;
eis+Adv^=ele+Pron+Acc+3P+Masc+Sg:ei-lo		#;
eis+Adv^=eles+Pron+Acc+3P+Masc+Pl:ei-los	#;

!# -----------------------------------------------------------------------------
!# possessive pronouns
!# -----------------------------------------------------------------------------
meu+Pron+Poss			MSG;
meu+Pron+Poss:meus		MPL;
meu+Pron+Poss:minha		FSG;
meu+Pron+Poss:minhas		FPL;

teu+Pron+Poss			MSG;
teu+Pron+Poss:teus		MPL;
teu+Pron+Poss:tua		FSG;
teu+Pron+Poss:tuas		FPL;

seu+Pron+Poss			MSG;
seu+Pron+Poss:seus		MPL;
seu+Pron+Poss:sua		FSG;
seu+Pron+Poss:suas		FPL;

nosso+Pron+Poss			MSG;
nosso+Pron+Poss:nossos		MPL;
nosso+Pron+Poss:nossa		FSG;
nosso+Pron+Poss:nossas		FPL;

vosso+Pron+Poss			MSG;
vosso+Pron+Poss:vossos		MPL;
vosso+Pron+Poss:vossa		FSG;
vosso+Pron+Poss:vossas		FPL;

!# -----------------------------------------------------------------------------
!# indefinite pronouns
!# -----------------------------------------------------------------------------
algo+Pron+Indef			MSG;
alguém+Pron+Indef		MSG;
alguma% coisa+Pron+Indef	FSG;
algum+Pron+Indef:alguma		FSG;
algum+Pron+Indef:algumas	FPL;
algum+Pron+Indef:alguns		MPL;
algum+Pron+Indef		MSG;
algures+Pron+Indef		MSG;
ambos+Pron+Indef:ambas		FPL;
ambos+Pron+Indef		MPL;
cada+Pron+Indef			XSG;
cada% um+Pron+Indef:cada% uma	FSG;
cada% um+Pron+Indef		MSG;
certo+Pron+Indef:certa	  	FSG;
certo+Pron+Indef:certas		FPL;
certo+Pron+Indef:certos		MPL;
certo+Pron+Indef		MSG;
coisíssima% nenhuma+Pron+Indef	FSG;
muito+Pron+Indef		MSG;
muito+Pron+Indef:muita		FSG;
muito+Pron+Indef:muitas		FPL;
muito+Pron+Indef:muitos		MPL;
nada+Pron+Indef			MSG;
nenhum+Pron+Indef		MSG;
nenhum+Pron+Indef:nenhuma	FSG;
nenhum+Pron+Indef:nenhumas	FPL;
nenhum+Pron+Indef:nenhuns	MPL;
ninguém+Pron+Indef		MSG;
o% que+Pron+Indef		XSG;
outrem+Pron+Indef		MSG;
outro+Pron+Indef		MSG;
outro+Pron+Indef:outra		FSG;
outro+Pron+Indef:outras		FPL;
outro+Pron+Indef:outros		MPL;
porquê+Pron+Indef		XSG;
pouco+Pron+Indef		MSG;
pouco+Pron+Indef:pouca		FSG;
pouco+Pron+Indef:poucas		FPL;
pouco+Pron+Indef:poucos		MPL;
qualquer+Pron+Indef:quaisquer	XPL;
qualquer+Pron+Indef		XSG;
qualquer% um+Pron+Indef		MSG;
qualquer% um+Pron+Indef:quaisquer% umas	FPL;
qualquer% um+Pron+Indef:quaisquer% uns	MPL;
qualquer% um+Pron+Indef:qualquer% uma	FSG;
quanto+Pron+Indef		MSG;
quanto+Pron+Indef:quanta	FSG;
quanto+Pron+Indef:quantas	FPL;
quanto+Pron+Indef:quantos	MPL;
quejando+Pron+Indef		MSG;
quejando+Pron+Indef:quejandos	MPL;
tanto+Pron+Indef		MSG;
tanto+Pron+Indef:tanta		FSG;
tanto+Pron+Indef:tantas		FPL;
tanto+Pron+Indef:tantos		MPL;
toda% a% gente+Pron+Indef	FSG;
todo+Pron+Indef			MSG;
todo+Pron+Indef:toda		FSG;
todo+Pron+Indef:todas		FPL;
todo+Pron+Indef:todos		MPL;
tudo+Pron+Indef			MSG;
um+Pron+Indef			MSG;
um+Pron+Indef:uma		FSG;
um+Pron+Indef:umas		FPL;
um+Pron+Indef:uns		MPL;
vários+Pron+Indef		MPL;
vários+Pron+Indef:várias	FPL;



!# -----------------------------------------------------------------------------
!# demonstrative pronouns
!# -----------------------------------------------------------------------------
aquele+Pron+Dem:aquela		FSG;
aquele+Pron+Dem:aquelas		FPL;
aquele+Pron+Dem:aqueles		MPL;
aquele+Pron+Dem			MSG;
aqueloutro+Pron+Dem:aqueloutra	FSG;
aqueloutro+Pron+Dem:aqueloutras	FPL;
aqueloutro+Pron+Dem:aqueloutros	MPL;
aqueloutro+Pron+Dem		MSG;
aquilo+Pron+Dem			MSG;
esse+Pron+Dem:essa		FSG;
esse+Pron+Dem:essas		FPL;
esse+Pron+Dem:esses		MPL;
esse+Pron+Dem			MSG;
essoutro+Pron+Dem:essoutra	FSG;
essoutro+Pron+Dem:essoutras	FPL;
essoutro+Pron+Dem:essoutros	MPL;
essoutro+Pron+Dem		MSG;
este+Pron+Dem:esta		FSG;
este+Pron+Dem:estas		FPL;
este+Pron+Dem:estes		MPL;
este+Pron+Dem			MSG;
estoutro+Pron+Dem:estoutra	FSG;
estoutro+Pron+Dem:estoutras	FPL;
estoutro+Pron+Dem:estoutros	MPL;
estoutro+Pron+Dem		MSG;
isso+Pron+Dem			MSG;
isto+Pron+Dem			MSG;

!# -----------------------------------------------------------------------------
!# relative pronouns
!# -----------------------------------------------------------------------------
como+Pron+Rel				XSP;
onde+Pron+Rel				XSP;
o% qual+Pron+Rel:a% qual		FSG;
o% qual+Pron+Rel:as% quais		FPL;
o% qual+Pron+Rel			MSG;
o% qual+Pron+Rel:os% quais		MPL;
por+Prep^=o% qual+Pron+Rel:pela% qual	FSG;
por+Prep^=o% qual+Pron+Rel:pelas% quais	FPL;
por+Prep^=o% qual+Pron+Rel:pelo% qual	MSG;
por+Prep^=o% qual+Pron+Rel:pelos% quais	MPL;
quando+Pron+Rel			  	XSP;
quanto+Pron+Rel				MSG;
quanto+Pron+Rel:quanta			FSG;
quanto+Pron+Rel:quantas			FPL;
quanto+Pron+Rel:quantos			MPL;
quem+Pron+Rel				XSP;
que+Pron+Rel				XSP;


!# -----------------------------------------------------------------------------
!# interrogative pronouns
!# -----------------------------------------------------------------------------
como+Pron+Interrog			XSG;
o% que+Pron+Interrog			MSG;
o% quê+Pron+Interrog			MSG;
qual+Pron+Interrog:quais		XPL;
qual+Pron+Interrog			XSG;
quanto+Pron+Interrog			MSG;
quanto+Pron+Interrog:quanta		FSG;
quanto+Pron+Interrog:quantas		FPL;
quanto+Pron+Interrog:quantos		MPL;
quem+Pron+Interrog			XSG;
que+Pron+Interrog			XSG;
quê+Pron+Interrog			XSG;

!# -----------------------------------------------------------------------------
!# other pronouns (personal pronouns ???)
!# -----------------------------------------------------------------------------
como% tal+Pron				XSG;	!E
como% tal+Pron:como% tais		XPL;	!E

a% senhora+Pron			FSG;
as% senhoras+Pron		FPL;
o% senhor+Pron			MSG;
os% senhores+Pron		MPL;

você+Pron			XSG;
você+Pron+Nom+3P+Pop:ocê	XSG;	!??

vocês+Pron			XPL;

vosmecê+Pron+Nom+3P		XSG;
vossemecê+Pron+Nom+3P		XSG;

Vossa% Excelência+Pron		FSG;
Vossa% Mercê+Pron		FSG;
Vossa% Senhoria+Pron		FSG;

a% gente+Pron+Nom+3P		XSG;


!# -----------------------------------------------------------------------------
!#  prepositions (including forms with article or adverb)
!# -----------------------------------------------------------------------------

abaixo% 				PrepDE;
acerca% 				PrepDE;
acima% 					PrepDE;		
a% despeito% 				PrepDE;
adiante% 				PrepDE;
a% favor% 				PrepDE;
a% fim% 				PrepDE;
à% frente% 				PrepDE;
além% 					PrepDE;
ante+Prep				#;
antes% 					PrepDE;
ao% lado% 				PrepDE;
ao% longo% 				PrepDE;
ao% pé% 				PrepDE;
ao% redor% 				PrepDE;
a% par% 				PrepDE;
a% partir% 				PrepDE;
apesar% 				PrepDE;
após+Prep				#;
					PrepA;		! preposition "a" without any prefix
a+Prep^=onde+Adv+Interrog:aonde		#;
a% respeito% 				PrepDE;
até+Prep				#;
até% 					PrepA;
atrás% 					PrepDE;
através% 				PrepDE;
cerca% 					PrepDE;
					PrepCOM;	! preposition "com" (incl. with personal pronouns)
conforme+Prep				#;
consoante+Prep				#;
contra+Prep				#;
da% frente% 				PrepDE;
de% acordo% 				PrepCOM;
de% baixo% 				PrepDE;
debaixo% 				PrepDE;
de% cima% 				PrepDE;
de% dentro% 				PrepDE;
de% diante% 				PrepDE;
de% fora% 				PrepDE;
defronte% 				PrepA;
defronte% 				PrepDE;
dentre+Prep				#;
dentro% 				PrepDE;
depois% 				PrepDE;		! preposition "de" without any prefix
					PrepDE;
de+Prep^=além+Adv:dalém			#;
de+Prep^=aquém+Adv:daquém		#;
de+Prep^=onde+Adv+Interrog:donde	#;
desde+Prep				#;
dês+Prep				#;
de% trás% 				PrepDE;
detrás% 				PrepDE;
devido% 				PrepA;
diante% 				PrepDE;
do% lado% 				PrepDE;
do% pé% 				PrepDE;
durante+Prep				#;
					PrepEM;		! preposition "em" (without prefix)
em% atenção% 				PrepA;
em% baixo% 				PrepDE;
embaixo% 				PrepDE;
em% cima% 				PrepDE;
em% face% 				PrepA;
em% face% 				PrepDE;
em% frente% 				PrepA;
em% frente% 				PrepDE;
em% torno% 				PrepA;
em% torno% 				PrepDE;
em% vez% 				PrepDE;
em% virtude% 				PrepDE;
entre+Prep				#;
excepto+Prep				#;
exceto+Prep				#;
face% a+Prep				#;
face% 					PrepA;
fora% 					PrepDE;
frente% 				PrepA;
graças% 				PrepA;
inclusive+Prep				#;
junto% 					PrepCOM;	!??
junto% 					PrepA;
junto% 					PrepDE;
longe% 					PrepDE;
mediante+Prep				#;
na% frente% 				PrepDE;
não% obstante+Prep			#;
no% meio% 				PrepDE;
para% a% frente% 			PrepDE;
para% baixo% 				PrepDE;
para% cima% 				PrepDE;
para% 					PrepCOM;
para% dentro% 				PrepDE;
para% detrás% 				PrepDE;
para% diante% 				PrepDE;
para% fora% 				PrepDE;
para% frente% 				PrepDE;
para% o% lado% 				PrepDE;
para% o% pé% 				PrepDE;
para+Prep				#;
para% trás% 				PrepDE;
pela% frente% 				PrepDE;
perante+Prep				#;
perto% 					PrepDE;
por% baixo% 				PrepDE;
por% causa% 				PrepDE;
por% cima% 				PrepDE;
por% dentro% 				PrepDE;
por% detrás% 				PrepDE;
por% diante% 				PrepDE;
por% fora% 				PrepDE;
por+Prep				#;
por+Prep^=o+Art+Def:pela		FSG;
por+Prep^=o+Art+Def:pelas		FPL;
por+Prep^=o+Art+Def:pelo		MSG;
por+Prep^=o+Art+Def:pelos		MPL;
por% trás% 				PrepDE;
p'ra+Prep				#;
pra+Prep				#;
prá+Prep				#;
pra+Prep^=o+Art+Def:pra			FSG;
pra+Prep^=o+Art+Def:pro			MSG;
qual+Prep				#;
que% nem+Prep				#;
salvo+Prep				#;
segundo+Prep				#;
sem+Prep				#;
sob% pena% 				PrepDE;
sob+Prep				#;
sobre+Prep				#;
tirante+Prep				#;
trás+Prep				#;
via+Prep				#;

!# -----------------------------------------------------------------------------
!# conjunctions
!# -----------------------------------------------------------------------------
a% fim% de% que+Conj			#;
ainda% que+Conj				#;
à% medida% que+Conj			#;
a% menos% que+Conj			#;
a% não% ser% que+Conj			#;
antes% de+Conj				#;
antes% que+Conj				#;
ao% passo% que+Conj			#;
apenas+Conj				#;
apesar% de+Conj				#;
apesar% de% que+Conj			#;
após+Conj				#;
à% proporção% que+Conj			#;
aquando+Conj				#;
assim% como+Conj			#;
assim+Conj				#;
assim% que+Conj				#;
até% que+Conj				#;
bem% como+Conj				#;
bem% que+Conj				#;
cada% vez% que+Conj			#;
caso+Conj				#;
claro% que+Conj				#;
como+Conj				#;
como% se+Conj				#;
com% que+Conj				#;
conforme+Conj				#;
conquanto+Conj				#;
contanto% que+Conj			#;
contudo+Conj				#;
dado% que+Conj				#;
de% forma% que+Conj			#;
de% maneira% que+Conj			#;
de% modo% que+Conj			#;
depois% que+Conj			#;
desde% que+Conj				#;
de% sorte% que+Conj			#;
do% que+Conj+Comp	      		#;	!E
e+Conj+Coord	   			#;	!AS18/08/2008
embora+Conj				#;
enquanto+Conj				#;
entanto+Conj				#;
entrementes% que+Conj			#;
entretanto+Conj				#;
e/ou+Conj+Coord				#;	!AS18/08/2008
exceto% se+Conj				#;
isto% é+Conj+Abbr:i.e			#;
já+Conj					#;
já% que+Conj				#;
logo+Conj				#;
logo% que+Conj				#;
mal+Conj				#;
mas+Conj				#;
mesmo% que+Conj				#;
nem+Conj+Neg				#;	!AS18/08/2008
nem% que+Conj				#;
no% entanto+Conj			#;
ora+Conj				#;
ou+Conj+Coord				#;	!AS18/08/2008
para% que+Conj				#;
pois+Conj				#;
pois% que+Conj				#;
por% conseguinte+Conj			#;
porém+Conj				#;
por% isso+Conj				#;
por% mais% que+Conj			#;
por% menos% que+Conj			#;
porquanto+Conj				#;
porque+Conj				#;
portanto+Conj				#;
posto% que+Conj				#;
quando+Conj				#;
quase+Conj				#;
que+Conj				#;
que% nem+Conj				#;
quer+Conj				#;
salvo% se+Conj				#;
se% bem% que+Conj			#;
se+Conj	 				#;
segundo+Conj				#;
seja+Conj				#;
sempre% que+Conj			#;
sem% que+Conj				#;
senão+Conj				#;
suposto+Conj				#;
suposto% que+Conj			#;
tais% como+Conj				#;
tal% como+Conj				#;
todas% as% vezes% que+Conj		#;
toda% vez% que+Conj			#;
todavia+Conj				#;
uma% vez% que+Conj			#;
visto% como+Conj			#;
visto% que+Conj				#;

!# =============================================================================
!# prepositional continuations
!# =============================================================================
LEXICON PrepDE
de+Prep				#;	! simple preposition

de+Prep^=aí+Adv:daí		#;	! preposition with adverb
de+Prep^=ali+Adv:dali		#;
de+Prep^=aqui+Adv:daqui		#;
de+Prep^=outrora+Adv:doutrora	#;

de+Prep^=o+Art+Def:da		FSG;	! preposition with article
de+Prep^=o+Art+Def:das		FPL;
de+Prep^=o+Art+Def:do		MSG;
de+Prep^=o+Art+Def:dos		MPL;
de+Prep^=um+Art+Indef:duma	FSG;
de+Prep^=um+Art+Indef:dumas	FPL;
de+Prep^=um+Art+Indef:dum	MSG;
de+Prep^=um+Art+Indef:duns	MPL;

de+Prep^=algum+Pron+Indef:dalguma		FSG;	! preposition with pronoun
de+Prep^=algum+Pron+Indef:dalgumas		FPL;
de+Prep^=algum+Pron+Indef:dalgum		MSG;
de+Prep^=algum+Pron+Indef:dalguns		MPL;
de+Prep^=o% que+Pron+Indef:do% que		XSG;
de+Prep^=outro+Pron+Indef:doutra		FSG;
de+Prep^=outro+Pron+Indef:doutras		FPL;
de+Prep^=outro+Pron+Indef:doutro		MSG;
de+Prep^=outro+Pron+Indef:doutros		MPL;
de+Prep^=um+Pron+Indef:duma			FSG;
de+Prep^=um+Pron+Indef:dumas			FPL;
de+Prep^=um+Pron+Indef:dum			MSG;
de+Prep^=um+Pron+Indef:duns			MPL;

de+Prep^=aquele+Pron+Dem:daquela		FSG;
de+Prep^=aquele+Pron+Dem:daquelas		FPL;
de+Prep^=aquele+Pron+Dem:daquele		MSG;
de+Prep^=aquele+Pron+Dem:daqueles		MPL;
de+Prep^=aqueloutro+Pron+Dem:daqueloutra	FSG;
de+Prep^=aqueloutro+Pron+Dem:daqueloutras	FPL;
de+Prep^=aqueloutro+Pron+Dem:daqueloutro	MSG;
de+Prep^=aqueloutro+Pron+Dem:daqueloutros	MPL;
de+Prep^=aquilo+Pron+Dem:daquilo		MSP;
de+Prep^=esse+Pron+Dem:dessa			FSG;
de+Prep^=esse+Pron+Dem:dessas			FPL;
de+Prep^=esse+Pron+Dem:desse			MSG;
de+Prep^=esse+Pron+Dem:desses			MPL;
de+Prep^=essoutro+Pron+Dem:dessoutra		FSG;
de+Prep^=essoutro+Pron+Dem:dessoutras		FPL;
de+Prep^=essoutro+Pron+Dem:dessoutro		MSG;
de+Prep^=essoutro+Pron+Dem:dessoutros		MPL;
de+Prep^=este+Pron+Dem:desta			FSG;
de+Prep^=este+Pron+Dem:destas			FPL;
de+Prep^=este+Pron+Dem:deste			MSG;
de+Prep^=este+Pron+Dem:destes			MPL;
de+Prep^=estoutro+Pron+Dem:destoutra		FSG;
de+Prep^=estoutro+Pron+Dem:destoutras		FPL;
de+Prep^=estoutro+Pron+Dem:destoutro		MSG;
de+Prep^=estoutro+Pron+Dem:destoutros		MPL;
de+Prep^=isso+Pron+Dem:disso			MSP;
de+Prep^=isto+Pron+Dem:disto			MSP;

de+Prep^=ela+Pron+Pers+3P:dela			FSG;	! with personal pronoun
de+Prep^=elas+Pron+Pers+3P:delas		FPL;
de+Prep^=ele+Pron+Pers+3P:dele			MSG;
de+Prep^=eles+Pron+Pers+3P:deles		MPL;

de+Prep^=onde+Pron+Rel:donde			XSP;	! with realtibe pronoun
de+Prep^=o% qual+Pron+Rel:da% qual		FSG;
de+Prep^=o% qual+Pron+Rel:das% quais		FPL;
de+Prep^=o% qual+Pron+Rel:do% qual		MSG;
de+Prep^=o% qual+Pron+Rel:dos% quais		MPL;


LEXICON PrepA
a+Prep						#;
a+Prep^=o+Art+Def:à				FSG;
a+Prep^=o+Art+Def:ao				MSG;
a+Prep^=o+Art+Def:aos				MPL;
a+Prep^=o+Art+Def:às				FPL;

a+Prep^=aquele+Pron+Dem:àquela			FSG;
a+Prep^=aquele+Pron+Dem:àquelas			FPL;
a+Prep^=aquele+Pron+Dem:àquele			MSG;
a+Prep^=aquele+Pron+Dem:àqueles			MPL;
a+Prep^=aqueloutro+Pron+Dem:àqueloutra		FSG;
a+Prep^=aqueloutro+Pron+Dem:àqueloutras		FPL;
a+Prep^=aqueloutro+Pron+Dem:àqueloutro		MSG;
a+Prep^=aqueloutro+Pron+Dem:àqueloutros		MPL;
a+Prep^=aquilo+Pron+Dem:àquilo			MSP;
a+Prep^=onde+Pron+Rel:aonde			XSP;
a+Prep^=o% qual+Pron+Rel:ao% qual		MSG;
a+Prep^=o% qual+Pron+Rel:aos% quais		MPL;
a+Prep^=o% qual+Pron+Rel:à% qual		FSG;
a+Prep^=o% qual+Pron+Rel:às% quais		FPL;
a+Prep^=o% que+Pron+Indef:ao% que		XSG;


LEXICON PrepEM
em+Prep						#;
em+Prep^=o+Art+Def:na				FSG;
em+Prep^=o+Art+Def:nas				FPL;
em+Prep^=o+Art+Def:no				MSG;
em+Prep^=o+Art+Def:nos				MPL;
em+Prep^=um+Art+Indef:numa			FSG;
em+Prep^=um+Art+Indef:numas			FPL;
em+Prep^=um+Art+Indef:num			MSG;
em+Prep^=um+Art+Indef:nuns			MPL;
em+Prep^=algum+Pron+Indef:nalguma		FSG;
em+Prep^=algum+Pron+Indef:nalgumas		FPL;
em+Prep^=algum+Pron+Indef:nalgum		MSG;
em+Prep^=algum+Pron+Indef:nalguns		MPL;
em+Prep^=aquele+Pron+Dem:naquela		FSG;
em+Prep^=aquele+Pron+Dem:naquelas		FPL;
em+Prep^=aquele+Pron+Dem:naquele		MSG;
em+Prep^=aquele+Pron+Dem:naqueles		MPL;
em+Prep^=aqueloutro+Pron+Dem:naqueloutra	FSG;
em+Prep^=aqueloutro+Pron+Dem:naqueloutras	FPL;
em+Prep^=aqueloutro+Pron+Dem:naqueloutro	MSG;
em+Prep^=aqueloutro+Pron+Dem:naqueloutros	MPL;
em+Prep^=aquilo+Pron+Dem:naquilo		MSP;
em+Prep^=ela+Pron+Pers+3P:nela			FSG;
em+Prep^=elas+Pron+Pers+3P:nelas		FPL;
em+Prep^=ele+Pron+Pers+3P:nele			MSG;		
em+Prep^=eles+Pron+Pers+3P:neles		MPL;
em+Prep^=esse+Pron+Dem:nessa			FSG;
em+Prep^=esse+Pron+Dem:nessas			FPL;
em+Prep^=esse+Pron+Dem:nesse			MSG;
em+Prep^=esse+Pron+Dem:nesses			MPL;
em+Prep^=essoutro+Pron+Dem:nessoutra		FSG;
em+Prep^=essoutro+Pron+Dem:nessoutras		FPL;
em+Prep^=essoutro+Pron+Dem:nessoutro		MSG;
em+Prep^=essoutro+Pron+Dem:nessoutros		MPL;
em+Prep^=este+Pron+Dem:nesta			FSG;
em+Prep^=este+Pron+Dem:nestas			FPL;
em+Prep^=este+Pron+Dem:neste			MSG;
em+Prep^=este+Pron+Dem:nestes			MPL;
em+Prep^=estoutro+Pron+Dem:nestoutra		FSG;
em+Prep^=estoutro+Pron+Dem:nestoutras		FPL;
em+Prep^=estoutro+Pron+Dem:nestoutro		MSG;
em+Prep^=estoutro+Pron+Dem:nestoutros		MPL;
em+Prep^=isso+Pron+Dem:nisso			MSP;
em+Prep^=isto+Pron+Dem:nisto			MSP;
em+Prep^=o% qual+Pron+Rel:na% qual		FSG;
em+Prep^=o% qual+Pron+Rel:nas% quais		FPL;
em+Prep^=o% qual+Pron+Rel:no% qual		MSG;
em+Prep^=o% qual+Pron+Rel:nos% quais		MPL;
em+Prep^=o% que+Pron+Indef:no% que		XSG;
em+Prep^=outro+Pron+Indef:noutra		FSG;
em+Prep^=outro+Pron+Indef:noutras		FPL;
em+Prep^=outro+Pron+Indef:noutro		MSG;
em+Prep^=outro+Pron+Indef:noutros		MPL;
em+Prep^=um+Pron+Indef:numa			FSG;
em+Prep^=um+Pron+Indef:numas			FPL;
em+Prep^=um+Pron+Indef:num			MSG;
em+Prep^=um+Pron+Indef:nuns			MPL;



LEXICON PrepCOM
com+Prep					#;

com+Prep^=eu+Pron+Pers+1P+Sg:comigo		#;
com+Prep^=tu+Pron+Pers+2P+Sg:contigo		#;
com+Prep^=nós+Pron+Pers+1P+Pl:connosco		#;
com+Prep^=nós+Pron+Pers+1P+Pl:conosco		#;
com+Prep^=vós+Pron+Pers+2P+Pl:convosco		#;
com+Prep^=se+Refl+Pers+3P:consigo		#;

LEXICON ContPron3
^=ela+Pron+Acc+3P+Fem+Sg:a		#;
^=elas+Pron+Acc+3P+Fem+Pl:as		#;
^=ele+Pron+Acc+3P+Masc+Sg:o		#;
^=eles+Pron+Acc+3P+Masc+Pl:os		#;