//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : LGTimeNoun.xip                                              !
//!   Date       : 2008/July                                                   !
//!   Author     : Nuno Mamede & João Loureiro                                 !
//!   Rebuilt by : Andreia Maurício (2010-2011)                                !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!   COMPOUND TIME NOUNS                                                      !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Sequence:


////////////////////////////////////////////////////////////////////////////////
// Marking nums with the feature of the measure to enable its normalization   //
////////////////////////////////////////////////////////////////////////////////

1> ? @= num[t-milisecond=+];?[lemma:um,t-milisecond=+],  noun[lemma:milissegundo,masc].
1> ? @= num[t-second=+];?[lemma:um,t-second=+],  noun[lemma:segundo,masc].
1> ? @= num[t-minute=+];?[lemma:um,t-minute=+],     ?[lemma:minuto].
1> ? @= num[t-hour=+];?[lemma:um,t-hour=+],         ?[lemma:hora].
1> ? @= num[t-monthday=+];?[lemma:um,t-monthday=+], ?[lemma:dia,sg].
1> ? @= num[t-week=+];?[lemma:um,t-week=+],         ?[lemma:semana].
1> ? @= num[t-month=+];?[lemma:um,t-month=+],       ?[lemma:mês].
1> ? @= num[t-year=+];?[lemma:um,t-year=+],         ?[lemma:ano].

1> ? @= num[t-year=+,t-hora-string2=+];?[lemma:um,t-year=+,t-hora-string2=+],         ?[lemma:década].
1> ? @= num[t-year=+,t-hora-string2=+];?[lemma:um,t-year=+,t-hora-string2=+],         ?[lemma:século].
1> ? @= num[t-year=+,t-hora-string2=+];?[lemma:um,t-year=+,t-hora-string2=+],         ?[lemma:milénio].
1> ? @= num[t-month=+,t-hora-string2=+];?[lemma:um,t-month=+,t-hora-string2=+],       ?[lemma:trimestre].
1> ? @= num[t-month=+,t-hora-string2=+];?[lemma:um,t-month=+,t-hora-string2=+],       ?[lemma:semestre].
1> ? @= num[t-week=+,t-hora-string2=+];?[lemma:um,t-week=+,t-hora-string2=+],         ?[lemma:quinzena].
1> ? @= num[t-monthday=+,t-hora-string2=+];?[lemma:um,t-monthday=+,t-hora-string2=+], ?[lemma:fim-de-semana].

1> ? @=   ?[lemma:século],  num[t-year=+,t-hora-string2=+].

1> ? @= ?[lemma:minuto],   conj[lemma:e], ?[lemma:meio,t-hora-string2=+,t-second=+].
1> ? @= ?[lemma:hora],     conj[lemma:e], ?[lemma:meio,t-hora-string2=+,t-minute=+].
1> ? @= ?[lemma:dia],      conj[lemma:e], ?[lemma:meio,t-hora-string2=+,t-hour=+].
1> ? @= ?[lemma:semana],   conj[lemma:e], ?[lemma:meio,t-hora-string2=+,t-year=+].
1> ? @= ?[lemma:mês],      conj[lemma:e], ?[lemma:meio,t-hora-string2=+,t-monthday=+].
1> ? @= ?[lemma:ano],      conj[lemma:e], ?[lemma:meio,t-hora-string2=+,t-month=+].
1> ? @= ?[lemma:década],   conj[lemma:e], ?[lemma:meio,t-hora-string2=+,t-year=+].
1> ? @= ?[lemma:século],   conj[lemma:e], ?[lemma:meio,t-hora-string2=+,t-year=+].
1> ? @= ?[lemma:milénio],  conj[lemma:e], ?[lemma:meio,t-hora-string2=+,t-year=+].

1> ? @= ?[lemma:década],   ?[lemma:de], num[t-year=+].

1> ? @= num[lemma:"[12]%d%d%d-[12]%d%d%d", time=+, t-interval=+, t-tempref=absolut, t-tempref2=absolut, t-data-string=+].

1> ? @= prep[lemma:"no decurso de"], num[t-year=+].
1> ? @= ?[lemma:"remontar"], prep[lemma:"a"], num[time=+,t-year=+].

1> ? @= noun[t-one-day], ?[lemma:de], num[t-year=+].


////////////////////////////////////////////////////////////////////////////////
// Compound nouns                                                             //
////////////////////////////////////////////////////////////////////////////////

// Example: dia de são nunca (à tarde)            adv += [advtimefreq:+].
2> noun[time=+] @= ?[lemma:"dia de são nunca"], (prep[lemma:a], art[def], ?[t-part-of-day]).
2> noun[time=+] @= ?[lemma:"dia de são nunca"], (prep[lemma:de], ?[t-part-of-day]).

// Example: turno da manhã
2> noun[time=+,t-part-of-day=+] = ?[surface:turno];?[surface:período], ?[lemma:de], ?[surface:a], ?[lemma:manhã];?[lemma:tarde];?[lemma:noite].

// Example: anos 30
2> noun[time=+, t-date=+, t-tempref=absolut, t-year=+] = ?[lemma:ano, pl], num[lemma:"%d0",dig, frac:~, t-year=+].

////////////////////////////////////////////////////////////////////////////////
// SÉCULOS                                                                    //
////////////////////////////////////////////////////////////////////////////////

// Example: século XX
2> noun[time=+, t-date=+, t-tempref=absolut, t-century=+] @= ?[lemma:século], num[rom];num[card, frac:~], (?[lemma:"antes de Cristo"];?[lemma:"depois de Cristo"];?[lemma:"ano do senhor"]).

// Example: II milénio a.C
2> noun[time=+, t-date=+, t-tempref=absolut] = num[rom];num[card, frac:~], ?[lemma:milénio], prep[lemma:"antes de"];prep[lemma:"depois de"], art, ?[lemma:"era cristã"].
2> noun[time=+, t-date=+, t-tempref=absolut] = num[rom];num[card, frac:~], ?[lemma:milénio], prep[lemma:"antes de"];prep[lemma:"depois de"], art, ?[surface:nossa], ?[lemma:era].
2> noun[time=+, t-date=+, t-tempref=absolut] @= num[rom];num[card, frac:~], ?[lemma:milénio], (?[lemma:"antes de Cristo"];?[lemma:"depois de Cristo"];?[lemma:"ano do Senhor"]).

// Example: a primeira metade do século XV
2> noun[time=+,t-date=+,t-tempref=absolut,t-no-normalization=+]  = ?[def], ?[lemma:primeiro];?[lemma:último];?[lemma:segundo], ?[lemma:metade];?[lemma:década], prep[lemma:de], ?[def], ?[t-century].
2> noun[time=+,t-date=+,t-tempref=absolut,t-no-normalization=+]  = ?[lemma:primeiro];?[lemma:último];?[lemma:segundo], ?[lemma:metade];?[lemma:década], prep[lemma:de], ?[def], ?[t-century].
2> noun[time=+,t-date=+,t-tempref=absolut,t-no-normalization=+]  = ?[surface:meados], prep[lemma:de], ?[def], ?[t-century].

//NJM+JB, 2012/05/08
// Example: os anos 80 do século XX
3> noun[time=+,t-date=+,t-tempref=absolut,t-no-normalization=+]  @= (?[def]), noun[t-tempref:absolut,t-year], prep[lemma:de], ?[def], ?[t-century].

//NJM+JB, 2012/05/08
// Example: década de 80 do século XX
3> noun[time=+, t-date=+, t-tempref=enunc, t-decade=+] @= ?[lemma:década], prep[lemma:de], 
                                                          num[lemma:"%d0",dig, frac:~, t-year=+];num[lemma:"%d%d%d0",dig, frac:~, t-year=+],
                                                          prep[lemma:de], ?[def], ?[t-century].

// Example: o 1º lustro da década de 1930 do século XV
3> noun[time=+,t-date=+,t-tempref=absolut,t-no-normalization=+]  @= (?[def]), ?[lemma:primeiro];?[lemma:último];?[lemma:segundo];num[ord], ?[surface:lustro], (prep[lemma:de], ?[def], ?[lemma:década]), prep[lemma:de], ?[num], (prep[lemma:de], (art[def]), ?[t-century]).
3> noun[time=+,t-date=+,t-tempref=absolut,t-no-normalization=+]  @= (?[def]), ?[lemma:primeiro];?[lemma:último];?[lemma:segundo];num[ord], ?[surface:lustro], (prep[lemma:de], ?[def], ?[lemma:década]), prep[lemma:de], ?[num], (prep[lemma:de], (art[def]), ?[t-century]).

// Example: o 1º quartel do século XX
3> noun[time=+,t-date=+,t-tempref=absolut,t-no-normalization=+]  @= (?[def]), ?[lemma:primeiro];?[lemma:último];?[lemma:segundo];num[ord], ?[lemma:quartel], prep[lemma:de], ?[def], ?[t-century].

// Example: 700 a.C
3> noun[time=+,t-date=+,t-tempref=absolut]  =  num[lemma:"2%d%d%d",t-year=+];num[lemma:"1%d%d%d",t-year=+];
												num[lemma:"%d%d%d",t-year=+];num[lemma:"%d%d",t-year=+];
												num[lemma:"%d",t-year=+],
												?[lemma:"antes de Cristo", time]; ?[lemma:"depois de Cristo", time] .


// Example: início da manhã
4> noun[time=+,t-part-of-day=+]   = | conj | ?[t-focus], prep[lemma:de], (?[lemma:o]), ?[t-meas];?[t-part-of-day].



//deslocado de AdvTime
/////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////
//MOVED FROM THE CHUNKER ON 2008/02/01
/////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////

// compound mesasure (adds feature: measother)
// Example: Um dia por mês
//
8> noun[time=+, t-frequency=+] @= ?[t-meas], ?[lemma:por], ?[t-meas].
8> noun[time=+, t-frequency=+] @= ?[t-meas], ?[lemma:a], art[lemma:o], ?[t-meas].
8> noun[time=+, t-frequency=+] @= ?[t-meas], ?[lemma:"a o dia"].


// Example: euros à hora
//
8> noun[SEM-measother=+] @= ?[SEM-unitype,time:~], ?[lemma:por], ?[SEM-unitype].
8> noun[SEM-measother=+] @= ?[SEM-unitype,time:~], ?[lemma:a], art[lemma:o], ?[SEM-unitype].
8> noun[SEM-measother=+] @= ?[SEM-unitype,time:~], adv[lemma:"a o dia"].


////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
// Time and Dates                                                             //
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

// Dias da Semana : segunda feira (sem hífen)
// Used by XEROX (do not erase)
// 2ª feira
10> noun[time=+,t-weekday=2] = noun[t-weekday:2], ?[lemma:feira].
10> noun[time=+,t-weekday=3] = noun[t-weekday:3], ?[lemma:feira].
10> noun[time=+,t-weekday=4] = noun[t-weekday:4], ?[lemma:feira].
10> noun[time=+,t-weekday=5] = noun[t-weekday:5], ?[lemma:feira].
10> noun[time=+,t-weekday=6] = noun[t-weekday:6], ?[lemma:feira].

10> noun[time=+,t-weekday=2] = ?[surface:2ª], ?[lemma:feira].
10> noun[time=+,t-weekday=3] = ?[surface:3ª], ?[lemma:feira].
10> noun[time=+,t-weekday=4] = ?[surface:4ª], ?[lemma:feira].
10> noun[time=+,t-weekday=5] = ?[surface:5ª], ?[lemma:feira].
10> noun[time=+,t-weekday=6] = ?[surface:6ª], ?[lemma:feira].


////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
// Relative Dates                                                             //
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

// MW expressions (XEROX ONLY)
11> noun[time=+,t-monthday=+,t-uncertain=+, t-sentido=after,t-val=2, t-tempref=enunc] = prep[lemma:"depois_de"], ?[lemma:amanhã].
11> noun[time=+,t-monthday=+,t-uncertain=+,t-sentido=before,t-val=2, t-tempref=enunc] = prep[lemma:"antes_de"],  ?[lemma:ontem].


////////////////////////////////////////////////////////////////////////////////
// Dates                                                                      //
////////////////////////////////////////////////////////////////////////////////

//CH modification. Catch the longest expression first
// dias 4, 5 e 6 de janeiro de 1995
14> noun[t-date=+,t-monthday=+] @= ?[lemma:dia], num[dig];num[card, frac:~], (punct[comma], num[dig];num[card, frac:~])+, (?[lemma:e], num[dig];num[card, frac:~]) .


//CH transformed the following rules in order to avoid the structure noun{num}
15> ? @= |prep[lemma:de]| num[dig,time=+,t-monthday=+];num[card, frac:~,time=+,t-monthday=+] |?[surface:a], num[dig];num[card, frac:~], prep[lemma:de], ?[t-month]|.


////////////////////////////////////////////////////////////////////////////////
// acrescentar o traço uncertain às regras em baixo com o traço timeref qd a sua normalização estiver a ser feita
////////////////////////////////////////////////////////////////////////////////

//CH TO MOVE  uncertain after having calculated t-date e.g. no passado dia 7 de dezembro de 2003, was badly tokenized
15> noun[time=+,t-monthday=+] = ?[lemma:dia], num[dig, t-monthday=+];num[card, frac:~];?[ord].

15> noun[time=+,t-monthday=+] = |?[timeref]| num[ord, t-monthday=+] |?[lemma:de], ?[t-month]|.

15> noun[time=+,t-monthday=+] = ?[lemma:dia], num[dig, t-monthday=+];num[card, frac:~];?[ord].

15> ? @= num[ord,time=+,t-monthday=+,timeref=+] |?[lemma:de], ?[t-month]|.

15> noun[time=+,t-monthday=+] = |?[timeref]| num[dig];num[card, frac:~] |?[lemma:de], ?[t-month]|.

//DCO, 2010/09/20
// Adicionei o traço virtual:~, para que esta regra não interfira com os Decretos-Lei (e.g. Decreto-Lei n.º "35/95 de 31 de maio")
15> ? @= num[dig,time=+,t-monthday=+,virtual:~];num[card,frac:~,time=+,t-monthday=+,virtual:~] | ?*[lemma:de], ?[t-month] |.

15> noun[time=+,t-monthday=+] = |?[timeref]| num[dig];num[card, frac:~] |?[lemma:de], ?[surface:o], num[dig];num[card, frac:~]|.

15> ? @= num[dig,time=+,t-monthday=+];num[card, frac:~,time=+,t-monthday=+] |?[lemma:de], ?[surface:o], num[dig];num[card, frac:~]|.

15> noun[time=+,t-monthday=+] = |?[timeref]| num[dig];num[card, frac:~] |?[lemma:de], ?[surface:o], (?[timeref]), ?[lemma:mês]|.

15> ? @= num[dig,time=+,t-monthday=+];num[card, frac:~,time=+,t-monthday=+] |?[lemma:de], ?[surface:o], (?[timeref]), ?[lemma:mês]|.

15> ?[time=+,t-monthday=+] = |prep[lemma:de]| num[dig];num[card, frac:~] |?[surface:a], num[dig];num[card, frac:~], prep[lemma:de], ?[t-month]|.


// próximo mês de Maio
// mês de Maio
// (próximo) mês 12
// próximo mês
// mês anterior
// primeiro mês
// (2 do) 3 (de 2002)
// (2 do) 3 (do [próximo] ano)
// (2 do) 3
// 3 (de 2002)
// 3 (do [próximo] 2002)

//15> noun[time=+,t-month=+,t-uncertain=+] = ?[timeref], ?[lemma:mês], num[dig];num[card, frac:~].

15> noun[time=+,t-month=+,t-uncertain=+] = ?[timeref], ?[lemma:mês], ?[lemma:de], ?[t-month].
15> noun[time=+,t-month=+] = ?[lemma:mês], ?[lemma:de], ?[t-month].
15> noun[time=+,t-month=+] = ?[lemma:mês], num[dig];num[card, frac:~].
15> ?[time=+,t-month=+] = |num[dig];num[card, frac:~], ?[lemma:de], ?[surface:o]| num[dig];num[card, frac:~] |?[lemma:de], num[dig];num[card, frac:~]|.
15> ?[time=+,t-month=+] = |num[dig];num[card, frac:~], ?[lemma:de], ?[surface:o]| num[dig];num[card, frac:~] |?[lemma:de], ?[surface:o], ?[timeref], ?[lemma:ano]|.
15> ?[time=+,t-month=+] = |num[dig];num[card, frac:~], ?[lemma:de], ?[surface:o]| num[dig];num[card, frac:~].
15> ?[time=+,t-month=+] = num[dig];num[card, frac:~]  |?[lemma:de], num[dig];num[card, frac:~]|.
15> ?[time=+,t-month=+] = num[dig];num[card, frac:~]  |?[lemma:de], ?[surface:o], ?[timeref], ?[lemma:ano]|.

// 1999
// (em...) 1999
// <evento> de 1999
// (próximo) ano (de) 2001
// próximo ano
// ano seguinte
15> ?[time=+,t-year=+] = |?[t-month];?[lemma:mês];?[t-season], (?[lemma:de];punct[comma])| num[dig];num[card, frac:~].
15> ?[time=+,t-year=+] = |num[dig];num[card, frac:~], ?[lemma:de]| num[dig];num[card, frac:~].
15> ?[time=+,t-year=+] = |?[lemma:em]| num[dig,t-monthday:~] |?[noun:~, num:~,adj:~]|.
15> ?[time=+,t-year=+] = |?[lemma:desde]| num[dig] |?[noun:~, num:~]|.
15> ?[time=+,t-year=+] = |?[lemma:"depois de"]| num[dig] |?[noun:~, num:~]|.
15> ?[time=+,t-year=+] = |?[event], ?[lemma:de]| num[dig].
15> ?[time=+,t-year=+] = |?[t-meas, sem-period], ?[lemma:de]| num[dig];num[card, frac:~].

15> noun[time=+,t-year=+] = ?[lemma:ano], ?[lemma:de], num[dig];num[card, frac:~].



// if we have a possible t-date (4 digits starting by 1[09] or 20 between parenthesis,
// we mark it as a t-date:+,t-year:+
15> ? = | punct[left,paren] | num[lemma:"1%d%d%d",time=+,t-year=+] | punct[right,paren] |.
15> ? = | punct[left,paren] | num[lemma:"20%d%d", time=+,t-year=+] | punct[right,paren] |.

//DCO, 2010/09/20
// Esta regra estava a interferir com os Decretos-Lei. Adicionei virtual:~
15> ? = num[lemma:"%d%dd%d/%d%d%d%d",time=+,t-year=+,t-no-normalization=+,virtual:~];
        num[lemma:"%d%d/%d%d",time=+,t-year=+,t-no-normalization=+,virtual:~];
        num[lemma:"%d%d/%d%d",time=+,t-year=+,t-no-normalization=+,virtual:~].


//ATENÇÂO ESTAS REGRAS TÊM DE SER CORRIGIDAS
// depois de + digit
// antes de + digit
15> ? = | prep[lemma:"a meio de"]    | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].
15> ? = | prep[lemma:"antes de"]     | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].
15> ? = | prep[lemma:"ao fim de"]    | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].
15> ? = | prep[lemma:"ao final de"]  | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].
15> ? = | prep[lemma:"ao longo de"]  | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].
15> ? = | prep[lemma:"depois de"]    | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].
15> ? = | prep[lemma:"desde"]        | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].
15> ? = | prep[lemma:"durante"]      | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].
15> ? = | prep[lemma:"em finais de"] | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].
15> ? = | prep[lemma:"em meados de"] | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].
15> ? = | prep[lemma:"no começo de"] | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].  //<-------------------------------- ALTERAR
15> ? = | prep[lemma:"no fim de"]    | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].
15> ? = | prep[lemma:"no final de"]  | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].

15> ? = | prep[lemma:"por volta de"]    | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].
15> ? = | ?[lemma:"em"], ?[lemma:"cerca de"]    | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+].

15> ? = | prep[lemma:"até"]          | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+] | ?[noun:~,adj:~] |.
15> ? = | prep[lemma:"de"]           | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+] | ?[noun:~,adj:~] |.
15> ? = | prep[lemma:"desde"]        | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+] | ?[noun:~,adj:~] |.
15> ? = | prep[lemma:"em"]           | num[lemma:"1%d%d%d",time=+,t-year=+];num[lemma:"20%d%d",time=+,t-year=+] | ?[noun:~,adj:~] |.


15> ? = num[dig,rom:~,time=+,t-year=+] | ?[t-era] |.


////////////////////////////////////////////////////////////////////////////////
// Regras para os períodos e eras que não constam do léxico                   //
////////////////////////////////////////////////////////////////////////////////
// era Elizabetana
16> noun[time=+, t-date=+, t-tempref=absolut] = ?[lemma:era], noun[proper].

// anos Regan
16> noun[time=+, t-date=+, t-tempref=absolut] = ?[lemma:anos], noun[SEM-Hpeople].

// período
16> noun[time=+, t-date=+, t-tempref=absolut] = ?[lemma:período], noun.

// dia internacional/mundial/nacional dos surfitas
16> noun[time=+, t-date=+, t-tempref=absolut] @= (art), ?[lemma:dia], ?[lemma:internacional];?[lemma:nacional];?[lemma:mundial], prep, (art), noun.
16> noun[time=+, t-date=+, t-tempref=absolut] @= (art), ?[lemma:ano], ?[lemma:internacional];?[lemma:nacional];?[lemma:mundial], prep, (art), noun.

// noite de Santo António
16> noun[time=+,t-date=+,t-tempref=enunc, t-one-day=+] = ?[t-part-of-day], prep, noun[proper].

//tempo dos descobrimentos
16> noun[time=+, t-date=+, t-absolut=+] @= (art), noun[lemma:tempo], prep, (art), noun.

////////////////////////////////////////////////////////////////////////////////
// Other kind of dates                                                        //
////////////////////////////////////////////////////////////////////////////////

// 04/02
// 02/Janeiro
// Jan/02
// 4/1/1920
// 4/Jan/1920
16> noun[time=+,t-data-string=+] = num[dig], ?[slash], num[dig];?[t-month], ?[slash], num[dig].
16> noun[time=+,t-data-string=+] = num[dig], ?[slash], ?[t-month].
16> noun[time=+,t-data-string=+] = ?[t-month], ?[slash], num[dig].


//17> noun[time=+,t-date=+] @= ?[t-monthday], (?[lemma:de], (?[lemma:o])), ?[t-month], (?[lemma:de], (?[lemma:o]), ?[t-year]), (?[timeref]).
//17> noun[time=+,t-date=+] @= ?[t-monthday], ?[lemma:de], (?[lemma:o]), ?[timeref], ?[lemma:mês].
//17> noun[time=+,t-date=+] @= ?[t-monthday], ?[lemma:de], (?[lemma:o]), ?[lemma:mês], ?[timeref].
//17> noun[time=+,t-date=+] @= ?[t-month], ?[lemma:de], (?[lemma:o]), ?[t-year], (?[timeref]).

