//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : LGTimeAdv.xip                                               !
//!   Date       : 2006/March                                                  !
//!   Author     : Nuno Mamede & João Loureiro                                 !
//!   Rebuilt by : Andreia Maurício (2010-2011)                                !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!   COMPOUND TIME ADVERBS                                                    !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Sequence:

////////////////////////////////////////////////////////////////////////////////
// FREQUÊNCIA                                                                 //
////////////////////////////////////////////////////////////////////////////////

// Example: vezes sem conta/fim
1> adv[advtimefreq=+] @= (?[lemma:um,fem], ?[surface:e]), ?[surface:outra], ?[surface:vez].

// Example: umas poucas de vezes
1> adv[advtimefreq=+]  @= ?[lemma:um,fem,pl], ?[surface:poucas], prep[lemma:de], ?[lemma:vez,pl].


// Example: de vez em quando/vez
1> adv[advtimefreq=+] @= prep[lemma:de], ?[surface:vez], ?[lemma:em], ?[lemma:quando];?[lemma:vez].

// Example: (por) uma vez sem exemplo
1> adv[advtimefreq=+] @= ?[lemma:por], ?[lemma:um], ?[surface:vez], ?[lemma:sem], ?[lemma:exemplo].
1> adv[advtimefreq=+] @= ?[lemma:um], ?[surface:vez], ?[lemma:sem], ?[lemma:exemplo].

// Example: segundo a segundo
// Example: de segundo a segundo
// Example: de segundo em segundo
// Example: de momento em momento
1> adv[advtimefreq=+] @= (?[lemma:de]),
                          #1[t-meas];#1[lemma:momento];#1[t-season],
                          ?[surface:a];?[surface:em],
                          #2[t-meas];#2[lemma:momento];#2[t-season],
                          where(#1[lemma]:#2[lemma]).

// Example: numa ou noutra ocasião
// Example: numa ocasião ou noutra
1> adv[advtimefreq=+] @= prep[lemma:em], art[indef], conj, (prep[lemma:em]), ?[lemma:outro], ?[lemma:altura];?[lemma:momento];?[lemma:instante];?[lemma:ocasião];?[t-part-of-day];?[t-weekday].
1> adv[advtimefreq=+] @= prep[lemma:em], art[indef], ?[lemma:altura];?[lemma:momento];?[lemma:instante];?[lemma:ocasião];?[t-part-of-day];?[t-weekday], conj, (prep[lemma:em]), ?[lemma:outro].

// Example: todas as meias-horas
// Example: às meias/quartos (de) horas
1> adv[advtimefreq=+] = prep[lemma:a], ?[surface:as], ?[surface:meias], ?[surface:horas].
1> adv[advtimefreq=+] = prep[lemma:a], ?[surface:as], ?[surface:meias-horas].
1> adv[advtimefreq=+] = prep[lemma:a], ?[surface:os], ?[surface:quartos], ?[lemma:de], ?[surface:hora].

1> adv[advtimefreq=+] @= (?[surface:a]), ?[surface:todas], ?[surface:as], ?[surface:meias], ?[surface:horas].
1> adv[advtimefreq=+] @= (?[surface:a]), ?[surface:todas], ?[surface:as], ?[surface:meias-horas].
1> adv[advtimefreq=+] @= (?[surface:a]), ?[surface:todas], ?[surface:os], ?[surface:quartos], ?[lemma:de], ?[surface:hora].
1> adv[advtimefreq=+] @= (?[surface:a]), ?[surface:todas], ?[surface:as], ?[surface:horas].
1> adv[advtimefreq=+] @= (?[surface:a]), ?[surface:toda], ?[surface:a], ?[surface:hora].

// Example: vez sim vez não
// Example: momento sim momento não
// Example: milénio sim milénio não
1> adv[advtimefreq=+] = #1[t-meas];#1[lemma:momento];#1[t-season];#1[lemma:vez,sg];#1[lemma:momento],
                        ?[lemma:sim],
                        #2[t-meas];#2[lemma:momento];#2[t-season];#2[lemma:vez,sg];#2[lemma:momento],
                        ?[lemma:não],
                        where(#1[lemma]:#2[lemma]).



////////////////////////////////////////////////////////////////////////////////
// HORAS                                                                      //
////////////////////////////////////////////////////////////////////////////////

// estes advérbios devem ser "advtimeasp" ou "advtimedate"

// Example: à hora prevista/combinada/anunciada/marcada
3> adv[time=+,t-hora=+] = prep[lemma:a], ?[surface:a], ?[surface:hora],
                        ?[surface:prevista];?[surface:combinada];?[surface:anunciada];?[surface:marcada].

// Example: às tantas da manhã
3> adv[time=+,t-hora=+] @= prep[lemma:a], ?[surface:as], ?[surface:tantas], ?[lemma:de], art[def], ?[t-part-of-day].

////////////////////////////////////////////////////////////////////////////////
// DURAÇÃO                                                                    //
////////////////////////////////////////////////////////////////////////////////

// Example: durante muitos e belos anos
// Example: durante muitos e bons anos
// Example: durante muitos e longos anos
// Example: durante muitos e muitos dias
6> adv[advtimedur=+] @= (prep[lemma:durante];prep[lemma:por]),
                        ?[lemma:"muitos e muitos"];?[lemma:"muitos e bons"];?[lemma:"muitos e longos"];?[lemma:"muitos e belos"],
                        ?[t-meas];?[t-season].

// Example: dias e dias
6> adv[advtimedur=+] = #1[t-meas,pl];#1[t-season,pl],
                       ?[surface:e],
                       #2[t-meas,pl];#2[t-season,pl],
                       where(#1[lemma]:#2[lemma]).

// Example: durante/por dias e dias
6> pp[time=+,t-duration=+]  = prep[lemma:durante];prep[lemma:por],
                            adv[lemma:"dias e dias"];adv[lemma:"semanas e semanas"];adv[lemma:"meses e meses"];
                            adv[lemma:"anos e anos"];adv[lemma:"décadas e décadas"];adv[lemma:"séculos e séculos"].

// Example: segundo após segundo
6> adv[advtimedur=+] = #1[t-meas];#1[lemma:momento];#1[lemma:época];#1[lemma:era];#1[t-season], ?[surface:após],
                       #2[t-meas];#2[lemma:momento];#2[lemma:época];#2[lemma:era];#2[t-season],
                       where(#1[lemma]:#2[lemma]).

// Example: em pouco tempo
6> adv[advtimedur=+] = prep[lemma:em], ?[lemma:pouco], ?[lemma:tempo].


////////////////////////////////////////////////////////////////////////////////
// DATE (tipo_tempref=enunc)                                                   //
////////////////////////////////////////////////////////////////////////////////

// Example: uma vez
7> adv[advtimefreq=+] = ?[lemma:um,fem,start], ?[surface:vez] | punct[comma]|.
7> adv[advtimefreq=+] = ?[lemma:um,fem],       ?[surface:vez].

// Example: mais semana menos semana
7> adv[time=+, t-date=+, t-date-aux=+, t-uncertain=+, t-tempref=enunc, t-ref-after=+] @=
               (?[advnum]), (?[lemma:mais]), #1[t-meas];#1[t-season], ?[lemma:menos], #2[t-meas];#2[t-season],
               where(#1[lemma]:#2[lemma]).


// Example: no futuro imediato/próximo/longínquo
7> adv[time=+, t-date=+, t-date-aux=+, t-uncertain=+, t-tempref=enunc, t-ref-after=+] @=
                prep[lemma:em], art[def];art[indef], ?[lemma:futuro], ?[lemma:imediato];?[lemma:próximo];?[lemma:longínquo].


// Example: desde o princípio dos tempos.
7> adv[advtimedate=+,t-ref-simult=+,t-ref-before=+] @= prep[lemma:desde], ?[def],
                                                       ?[lemma:princípio];?[lemma:início];?[lemma:começo],
                                                       prep[lemma:de], art[def],
                                                       ?[lemma:tempo];?[lemma:universo].

// Example: até ao princípio dos tempos.
7> adv[advtimedate=+,t-ref-simult=+,t-ref-after=+] @= prep[lemma:"até a"], ?[def],
                                                      ?[lemma:princípio];?[lemma:início];?[lemma:começo],
                                                      prep[lemma:de], art[def],
                                                      ?[lemma:tempo];?[lemma:universo].


// Example: até esta manhã.
7> adv[advtimedate=+,t-ref-simult=+,t-ref-after=+] @= prep[lemma:"até"], ?[proxdem], (adj),
                                                       ?[lemma:data];?[t-part-of-day];?[lemma:tempo];?[t-season].


// Example: no tempo da outra senhora
7> adv[advtimedate=+,t-tempref=enunc] @= prep[lemma:em], ?[def];?[proxdem],
                                         ?[lemma:tempo], ?[lemma:de], ?[lemma:o], ?[lemma:outro], ?[lemma:senhor].


// Example: no meio tempo
// Example: nesse meio tempo
7> adv[advtimedate=+,t-ref-simult=+,t-tempref=enunc] @= prep[lemma:em], ?[def];?[proxdem], ?[lemma:meio], ?[lemma:tempo].



////////////////////////////////////////////////////////////////////////////////
// DATE TIPO RESIDUAL 	                                                      //
////////////////////////////////////////////////////////////////////////////////

// Example: desde (a mais) tenra idade
7> adv[advtimedate=+,t-ref-after=+,t-ref-simult=+,t-tempref=text] = prep[lemma:desde], (?[surface:a], ?[lemma:mais]), ?[lemma:tenra], ?[lemma:idade].

// Example: desde menino/criança/adulto
7> adv[advtimedate=+,t-ref-after=+,t-ref-simult=+,t-tempref=text] = prep[lemma:desde],
                            ?[lemma:bebé];?[lemma:jovem];?[lemma:menino];?[lemma:criança];?[lemma:adolescente];?[lemma:adulto].

// Example: desde a puberdade/adolescência
7> adv[advtimedate=+,t-ref-after=+,t-ref-simult=+,t-tempref=text] = prep[lemma:desde], ?[surface:a], ?[lemma:puberdade];?[lemma:adolescência].

// Example: desde a idade adulta
7> adv[advtimedate=+,t-ref-after=+,t-ref-simult=+,t-tempref=text] = prep[lemma:desde], ?[surface:a], ?[lemma:idade], ?[lemma:adulto].

// Example: desde muito novo
7> adv[advtimedate=+,t-ref-after=+,t-ref-simult=+,t-tempref=text] = prep[lemma:desde], ?[surface:muito], ?[lemma:novo].

// Example: até à puberdade":
7> adv[advtimedate=+,t-ref-before=+,t-ref-simult=+,t-tempref=text] = prep[lemma:até], ?[surface:a], ?[surface:a], ?[lemma:puberdade];?[lemma:adolescência].

// Example: até à idade adulta
7> adv[advtimedate=+,t-ref-before=+,t-ref-simult=+,t-tempref=text] = prep[lemma:até], ?[surface:a], ?[surface:a], ?[lemma:idade], ?[lemma:adulto].

// Example: até uma idade avançada
7> adv[advtimedate=+,t-ref-before=+,t-ref-simult=+,t-tempref=text] = prep[lemma:até], ?[lemma:um], ?[lemma:idade], ?[surface:avançada].



////////////////////////////////////////////////////////////////////////////////
// ENUNCIAÇÃO                                                                 //
////////////////////////////////////////////////////////////////////////////////


// Example: dentro em pouco
9> adv[advtimedate=+,t-ref-after=+,t-tempref=enunc] @= ?[lemma:dentro], ?[lemma:"em pouco"].


////////////////////////////////////////////////////////////////////////////////
// advtimedate                                                                //
////////////////////////////////////////////////////////////////////////////////

// Example: de tarde, ....
10> adv[advtimedate=+,t-part-of-day=+] = prep[lemma:de], ?[lemma:madrugada];?[lemma:manhã];?[lemma:manhãzinha];?[lemma:tarde];?[lemma:noite].

// Example: à tarde, ....
10> adv[advtimedate=+,t-part-of-day=+] = prep[lemma:a], ?[lemma:o], ?[lemma:tarde];?[lemma:tardinha];?[lemma:noite];?[lemma:noitinha].


////////////////////////////////////////////////////////////////////////////////
// ADVERBIAL PHRASES: Todo+det+ano
////////////////////////////////////////////////////////////////////////////////

// Example: de momento
12> adv[time=+,t-tempref=enunc,t-ref-simult=+]  @= ?[lemma:de], ?[surface:momento].

// Example: em cima da hora
12> adv[time=+,t-tempref=text]  @= ?[lemma:"em cima"], ?[lemma:de], ?[surface:a], ?[surface:hora].

// Example: (muito) fora de horas/tempo
12> adv[time=+,t-tempref=text]  @= (?[lemma:muito]), ?[lemma:"fora de"], ?[surface:horas];?[surface:tempo].


// Example: no presente/passado (recente/imediato)
12> adv[time=+,t-tempref=enunc]  @= ?[lemma:em], art[def], ?[surface:"passado recente"].
12> adv[time=+,t-tempref=enunc]  @= ?[lemma:em], art[def], ?[surface:presente].


// Example: Um dia destes
// Example: Um dia, ...
// Example: um dia.
12> adv[time=+,t-tempref=enunc]  @= ?[lemma:um], ?[lemma:dia], ?[lemma:de], ?[proxdem].
12> adv[time=+,t-tempref=enunc]  @= ?[surface:Um], ?[lemma:dia] |punct|.
12> adv[time=+,t-tempref=enunc]  @= ?[surface:um], ?[lemma:dia,last].


////////////////////////////////////////////////////////////////////////////////
// ADVERBIAL PHRASES: DE+Há+anos+para+cá                                      //
////////////////////////////////////////////////////////////////////////////////

// Example: de há anos e anos a esta parte
15> adv[time=+, advtimedur=+] = ?[lemma:de], ?[surface:há], adv[advtimedur],
                  ?[surface:a], ?[surface:esta], ?[surface:parte].				

// Example: de há anos e anos para cá
15> adv[time=+, advtimedur=+] = ?[lemma:de], ?[surface:há], adv[advtimedur], ?[surface:para], ?[surface:cá].

// Example: de há uns/alguns/5... anos para cá
15> adv[time=+,t-ref-before=+,t-ref-simult=+, t-complex=+, t-tempref=enunc] = ?[lemma:de], ?[surface:há],
                  (?[surface:alguns];?[surface:bastantes];?[surface:demasiados];?[surface:imensos];?[surface:poucos];?[surface:muitos];num;?[surface:tantos];?[surface:uns];?[surface:vários]),
                  ?[t-meas];?[t-season], ?[lemma:para], ?[surface:cá].

// Example: de há cinco anos a esta parte
15> adv[time=+,t-ref-before=+] = ?[lemma:de], ?[surface:há],  num, ?[t-meas];?[t-season], ?[lemma:a], ?[lemma:esta], ?[lemma:parte].


// Example: de há (uns/alguns/...) anos a esta parte
15> adv[time=+,t-ref-before=+,t-ref-simult=+] = ?[lemma:de], ?[surface:há],
                  (?[surface:alguns];?[surface:bastantes];?[surface:demasiados];?[surface:imensos];?[surface:poucos];?[surface:muitos];?[surface:tantos];?[surface:uns];?[surface:vários]),
                  ?[t-meas];?[t-season], ?[surface:a], ?[surface:esta], ?[surface:parte].


// Example: de há uns bons/poucos anos para cá
15> adv[time=+,t-ref-before=+,t-ref-simult=+] = ?[lemma:de], ?[surface:há], ?[surface:uns],?[surface:bons];?[surface:poucos],
                  ?[t-meas];?[t-season], ?[lemma:para], ?[surface:cá].

// Example: de há uns bons/poucos anos a esta parte
15> adv[time=+,t-ref-before=+] = ?[lemma:de],  ?[surface:há], ?[surface:uns],?[surface:bons];?[surface:poucos],
                  ?[t-meas];?[t-season], ?[surface:a], ?[surface:esta], ?[surface:parte].


// Example: de há uns tantos anos para cá
15> adv[time=+,t-ref-before=+,t-ref-simult=+] = ?[lemma:de], ?[surface:há], ?[surface:uns],?[surface:tantos],
                  ?[t-meas];?[t-season], ?[lemma:para], ?[surface:cá].

// Example: de há uns tantos anos a esta parte
15> adv[time=+,t-ref-before=+] = ?[lemma:de], ?[surface:há], ?[surface:uns],?[surface:tantos], ?[t-meas];?[t-season],
                  ?[surface:a], ?[surface:esta], ?[surface:parte].


// Example: de há 2 dias a esta parte
// retirei o t-date-aux
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= ?[lemma:de], verb[lemma:haver], (?[advnum]), ?[lemma:um];num[frac:~, ord:~], (prep[lemma:de]), ?[t-meas];?[t-season], ?[lemma:e], num[lemma:"um quarto"];?[lemma:meio], ?[surface:a],?[surface:esta],?[surface:parte].
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= ?[lemma:de], verb[lemma:haver], (?[advnum]), ?[lemma:um];num[frac:~, ord:~], (prep[lemma:de]), ?[t-meas];?[t-season], ?[surface:a],?[surface:esta],?[surface:parte].

// Example: (desde/até) há 2 dias e meio (atrás/ a esta parte
// ANDREIA:alterei para duracao no entanto pode ter valor de data
// adicionar traço para dizer que pode tb ser data
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= (?[lemma:desde];?[lemma:até]), verb[lemma:haver], (?[advnum]), ?[lemma:um];num[frac:~, ord:~], (prep[lemma:de]), ?[t-meas];?[t-season], ?[lemma:e], num[lemma:"um quarto"];?[lemma:meio], ?[surface:atrás].
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= (?[lemma:desde];?[lemma:até]), verb[lemma:haver], (?[advnum]), ?[lemma:um];num[frac:~, ord:~], (prep[lemma:de]), ?[t-meas];?[t-season], ?[lemma:e], num[lemma:"um quarto"];?[lemma:meio], ?[surface:a],?[surface:esta],?[surface:parte].
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= (?[lemma:desde];?[lemma:até]), verb[lemma:haver], (?[advnum]), ?[lemma:um];num[frac:~, ord:~], (prep[lemma:de]), ?[t-meas];?[t-season], ?[lemma:e], num[lemma:"um quarto"];?[lemma:meio].

// Example: (desde/até) há 2 dias (atrás/ a esta parte)
// ATENÇÃO:alterei para duracao no entanto pode ter valor de data
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= (?[lemma:desde];?[lemma:até]), verb[lemma:haver], (?[advnum]), (?[lemma:um];num[frac:~, ord:~]), (prep[lemma:de]), ?[t-meas];?[t-season], ?[surface:atrás].
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= (?[lemma:desde];?[lemma:até]), verb[lemma:haver], (?[advnum]), (?[lemma:um];num[frac:~, ord:~]), (prep[lemma:de]), ?[t-meas];?[t-season], ?[surface:a],?[surface:esta],?[surface:parte].

16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= (?[lemma:desde];?[lemma:até]), verb[lemma:haver], (?[advnum]), (?[lemma:um];num[frac:~, ord:~]), (prep[lemma:de]), ?[t-meas,last];?[t-season,last].
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= (?[lemma:desde];?[lemma:até]), verb[lemma:haver], (?[advnum]), (?[lemma:um];num[frac:~, ord:~]), (prep[lemma:de]), ?[t-meas];?[t-season] |punct|.
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= (?[lemma:desde];?[lemma:até]), verb[lemma:haver], (?[advnum]), (?[lemma:um];num[frac:~, ord:~]), (prep[lemma:de]), ?[t-meas];?[t-season] | ?[lemma:~em], ?[lemma:~que] |.


//há 110 milhões de anos
16> adv[time=+, t-date=+, t-date-aux=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= verb[lemma:haver], (?[advnum]), (num[frac:~, ord:~])+, prep[lemma:de], ?[t-meas], (?[lemma:atrás]).

//muito mais tarde
16> adv[time=+, t-date=+, t-tempref=text] = adv[advsupra], adv[lemma:mais], adv[advtimedate].


////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
// ADVERBIAL PHRASES:     Fazer+num+time                                      //
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

// Example: faz 15 dias na sexta-feira
16> adv[time=+, t-complex=+, t-date-aux=+, t-uncertain=+,t-tempref=text] @= verb[lemma:fazer], (?[advnum]), (?[advtimedate]), ?[lemma:um];num[frac:~, ord:~], ?[t-meas];?[t-as-a-meas],
                                                                            prep[lemma:em], ?[lemma:o], ?[t-meas];?[t-weekday].

// Example: faz 15 dias ontem
16> adv[time=+, t-complex=+, t-date-aux=+, t-uncertain=+,t-tempref=text] @= verb[lemma:fazer], (?[advnum]), (?[advtimedate]), ?[lemma:um];num[frac:~, ord:~], ?[t-meas];?[t-as-a-meas],
                                                                            adv[advtimedate].

// Example: faz 2 dias daqui a 5 anos
16> adv[time=+, t-complex=+, t-date-aux=+, t-uncertain=+,t-tempref=text] @= verb[lemma:fazer], (?[advnum]), (?[advtimedate]), ?[lemma:um];num[frac:~, ord:~], ?[t-meas];?[t-as-a-meas], prep[lemma:de], ?[lemma:aqui], ?[surface:a], ?[lemma:um];num[frac:~, ord:~], ?[t-meas].





//NJM+JB, 2012/03/30
// Example: Faz 2 anos [tem de ser em início de frase]
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= verb[lemma:fazer,start], (?[advnum]), ?[lemma:um];num[frac:~,ord:~], (prep[lemma:de]), ?[lemma:ano], ?[lemma:e], num[lemma:"um quarto"];?[lemma:meio].
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= verb[lemma:fazer,start], (?[advnum]), ?[lemma:um];num[frac:~,ord:~], (prep[lemma:de]), ?[lemma:ano].

//NJM+JB, 2012/03/30
// Example: Faz 2 semanas [Não tem de ser em início de frase]
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= verb[lemma:fazer], (?[advnum]), ?[lemma:um];num[frac:~,ord:~], (prep[lemma:de]), ?[t-meas,lemma:~ano];?[t-season],
                                                                                 ?[lemma:e], num[lemma:"um quarto"];?[lemma:meio].
16> adv[time=+, t-duration=+, t-uncertain=+, t-tempref=enunc, t-ref-before=+] @= verb[lemma:fazer], (?[advnum]), ?[lemma:um];num[frac:~,ord:~], (prep[lemma:de]), ?[t-meas,lemma:~ano];?[t-season].





// Example: faz dia 20 quinze dias
16> adv[time=+, t-complex=+, t-date-aux=+, t-uncertain=+,t-tempref=text] @= verb[lemma:fazer], (?[advnum]), noun[t-monthday], num[frac:~, ord:~], ?[t-meas];?[t-as-a-meas], (?[lemma:atrás];?[t-sentido:after]).


////////////////////////////////////////////////////////////////////////////////
// CH - Complex temporal triggers that are not tokenized as a whole           //
////////////////////////////////////////////////////////////////////////////////
// a seguir a...
16> prep[t-ref-after=+] = ?[lemma:a], ?[lemma:seguir], prep[lemma:a].
16> adv[t-ref-after=+]  = | ?[time,t-meas] | ?[lemma:a], ?[lemma:seguir].
