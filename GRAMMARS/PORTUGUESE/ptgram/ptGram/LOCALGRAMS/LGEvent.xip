//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : LGEvent.xip                                                 !
//!   Date       : 2007/March                                                  !
//!   Author     : Luís Romão                                                  !
//!   Revised by : Diogo Oliveira (2010/August, 2011/April)                    !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!  Event Name Rules                                                          !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


//DCO, 2010/08/18
// Changed all "org" occurrences to "SEM-Hcollective", in order to comply with the new set of directives.

//DCO, 2011/04/01
// The new EVENT category is as follows:
//========================================================
// EVENT CATEGORY       (EVENTO)
// + ORGANIZED          (ORGANIZADO)
//   + + POLITICAL      (POLITICO)
//   + + SPORTS         (DESPORTIVO)
//   + + SCIENTIFIC     (CIENTIFICO)
//   + + ARTISTIC       (ARTISTICO)
//   + + OTHER          (OUTRO)
// + NATURAL            (NATURAL)

Sequence:

//Festa da Cerveja, Festa da Música
1> NOUN[event=+,organized=+,event-other=+] @= (num[rom] ; num[ord]), ?[lemma:festa], prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*, (prep, (art));(punct), (num[lemma:"1%d%d%d"];num[lemma:"20%d%d"]).

//Semana da Música, Semana da Informática, Mês da Juventude
1> NOUN[event=+,organized=+,event-other=+] @= (num[rom] ; num[ord]), ?[lemma:semana,sg];?[lemma:mês,sg], (adj), (prep[lemma:de]), (art), ?+[maj], (prep, (art), ?+[maj])*.

//Bênção das Fitas (de Coimbra)
1> NOUN[event=+,organized=+,event-other=+] @= (num[rom] ; num[ord]), ?[lemma:bênção], prep[lemma:de], art, ?[lemma:fita], (prep[lemma:de];prep[lemma:em], (art), noun[location]).

//Queima das fitas (de Coimbra)
1> NOUN[event=+,organized=+,event-other=+] @= (num[rom] ; num[ord]), ?[lemma:"queima das fitas"], (num[lemma:"1%d%d%d"];num[lemma:"20%d%d"]), (prep[lemma:de];prep[lemma:em], (art), noun[location]).

// DCO, 2011/02/01
// Example: Receção ao Caloiro 2010
1> NOUN[event=+,organized=+,event-other=+] @= ?[lemma:receção], prep[lemma:a], art, ?[lemma:caloiro], ((prep[lemma:de];prep[lemma:em]), (art), noun[location]), (num[lemma:"1%d%d%d"];num[lemma:"20%d%d"]).

// DCO, 2011/02/01
// Example: Março Jovem 2006
1> NOUN[event=+,organized=+,event-other=+] @= noun[t-month,maj], ?[lemma:jovem,maj], num[lemma:"1%d%d%d"];num[lemma:"20%d%d"].

// DCO, 2011/02/01
// Example: Vans/Eastpak Club Tour 2010
1> NOUN[event=+,organized=+,event-other=+] @= noun[SEM-Hcollective,SEM-Hinstitution], ?[lemma:tour], (num[lemma:"1%d%d%d"];num[lemma:"20%d%d"]).

// CH added "Concilio de XXX"
1> noun[event=+,organized=+,event-other=+] @= (num[rom] ; num[ord]), ?[lemma:concílio], prep[lemma:de], ?[maj], (prep[lemma:de];art;?[maj])*.

//Cerimónia/Sessão de abertura/inauguração/encerramento
1> NOUN[event=+,organized=+,event-other=+] @= ?[lemma:cerimónia];?[lemma:cerimônia];?[lemma:sessão], prep[lemma:de],
?[lemma:abertura];?[lemma:inauguração];?[lemma:encerramento], (prep[lemma:de], (art), ?+[maj])*.

//Jornadas da Juventude, da Inovação
1> NOUN[event=+,organized=+,event-other=+] @= ?[lemma:jornada], prep[lemma:de], art, ?+[maj], (prep, (art), ?+[maj])*.

//Semana Santa
1> ? @= ?[lemma:"semana santa",event=+,organized=+,event-other=+].

//Party
1> NOUN[event=+,organized=+,event-other=+] @= noun[maj], (prep; art; ?[maj])*, ?[lemma:party], (num) .
//1> ? @= ?[lemma:party,maj,event=+,eventac=+] .

//Excursão Serra da Estrela
1> NOUN[event=+,organized=+,event-other=+] @= ?[lemma:excursão], prep, (art), ?[location].

// event (sigla) -> sigla = event tb
//3> NOUN[SEM-Hcollective=+, organized=+] @= |?[event], punct[paren]| noun[toutmaj] |punct[paren]|.
1> ? @= ?[event], punct[paren], noun[toutmaj,event=+,organized=+], punct[paren] .

//jantar oficial na Casa Branca
1> NOUN[event=+,organized=+,event-other=+] @= ?[lemma:"jantar oficial"], prep[lemma:em], (art), ?[location].

//casamento real espanhol
1> NOUN[event=+,organized=+,event-other=+] @= ?[lemma:casamento], ?[lemma:real], ((prep), (art), ?[location];?[gentcountry]).

//(cerimónia de) casamento do príncipe William com Kate Middleton.
1> NOUN[event=+,organized=+,event-other=+] @= (?[lemma:cerimónia], prep[lemma:de], (art)), ?[lemma:casamento], prep[lemma:de], (art), ((?[SEM-Hcargo];?[SEM-Hprofession]), ?[proper])*, (prep, (art), ?[location];?), conj;prep, (prep), (art), ((?[SEM-Hcargo];?[SEM-Hprofession]), ?[proper])*, (prep, (art), ?[location];?).

//(cerimónia de) baptismo/baptizado dos filhos de David e Victoria Beckham
1> NOUN[event=+,organized=+,event-other=+] @= (?[lemma:cerimónia], prep[lemma:de], (art)), ?[lemma:batismo];?[lemma:batizado], prep[lemma:de], (art), (noun, prep, (art)), ((?[SEM-Hcargo];?[SEM-Hprofession]), ?[proper])*, (prep, (art), ?[location];?), conj;prep, (prep), (art), ((?[SEM-Hcargo];?[SEM-Hprofession]), ?[proper])*, (prep, (art), ?[location];?).

//funeral de Artur Agostinho
1> NOUN[event=+,organized=+,event-other=+] @= ?[lemma:funeral];?[lemma:enterro];?[lemma:"cerimónia fúnebre"], prep[lemma:de], (art), ((?[SEM-Hcargo];?[SEM-Hprofession]), ?[proper])*, (num).

//Procissão do Senhor dos Passos
//Procissão em honra de Nossa Senhora de Fátima
1> NOUN[event=+,organized=+,event-other=+] @= ?[lemma:procissão], (prep, (art), ?[maj])*.

//Gala de Natal da TVI
1> NOUN[event=+,organized=+,event-other=+] @= (num[rom];num[ord]), ?[lemma:gala], ((prep), (art), ?[maj])*.

//desfile das Marchas Populares de Lisboa
1> NOUN[event=+,organized=+,event-other=+] @= (num[rom];num[ord]), (?[lemma:desfile]), (prep[lemma:de], (art)), ?[lemma:"marcha popular"];?[lemma:marcha];?[lemma:carnaval], (prep, (art), ?[location]).


////////////////////////////////////////////////////////
//
// FROM THIS POINT ON, ONLY POLITICAL (ORGANIZED) EVENTS
//
///////////////////////////////////////////////////////

//DCO, 2011/05/09
//VERA, 2011/05/26
//pré-campanha das eleições presidenciais
//campanha para as eleições legislativas de 2009
1> NOUN[event=+,organized=+,political=+] @= ?[organized,political], (prep[lemma:de];prep[lemma:para], (art[def]), ?[lemma:"eleições%*"]), (prep, (art[def])), num[lemma:"1%d%d%d"];num[lemma:"20%d%d"].

//DCO, 2011/02/05
//VERA, 2011/05/26
//campanha de Francisco Lopes
//comício da CDU no Campo Pequeno
//comício de encerramento da campanha no Porto
1> NOUN[event=+, organized=+, political=+] @= (num[rom];num[ord]), ?[organized,political], (noun;adj), ((prep[lemma:de]), (art), ?[maj])*, (prep, (art), ?[organized,political]), (prep, (art), ?+[maj];?+[SEM-Hpeople];?[SEM-Hinstitution])*, (prep, (art), ?[location]).

//VERA, 2011/05/26
//24º Congresso de Viseu
1> NOUN[event=+, organized=+, political=+] @= (num[rom];num[ord]), ?[lemma:congresso,maj];?[lemma:"congresso ?+"], (noun;adj), prep[lemma:de], (art), ?[SEM-Hinstitution];?[location], (prep[lemma:em], (art), ?[location]).

//DCO, 2011/02/05
//VERA, 2011/05/26
//Conferência das Nações Unidas sobre Alterações Climáticas
1> NOUN[event=+, organized=+, political=+] @= (num[rom];num[ord]), ?[lemma:conferência], (prep), (art), ?+[SEM-Hcollective,SEM-Hadministration,maj,location:~];?+[SEM-Hpeople];?+[SEM-Hinstitution], (prep, (art), ?+[maj])*.

//VERA, 2011/05/26
//Reunião de embaixadores dos países da UE em Bruxelas
1> NOUN[event=+, organized=+, political=+] @= (num[rom];num[ord]), ?[lemma:reunião,maj], (prep), (art), (noun, prep, (art)), ?+[admin_area,location].

//VERA, 2011/05/26
//eleições regionais de 2007
//eleições autárquicas de Espinho
//eleições intercalares para a assembleia de freguesia de Figueiredo de Alva
//eleição de o presidente de a Associação de Estudantes de a Faculdade de Letras de a Universidade de Beja
1> NOUN[event=+, organized=+, political=+] @= ?[lemma:eleição];?[lemma:"eleição%*"];?[lemma:"eleições%*"], (noun;adj;num), (prep, (art[def];pron), ?[t-date,time]), (prep, (art[def]), ?[SEM-Hcargo,SEM-Hprofession]), (prep, (art), ?[SEM-Hinstitution];noun), (prep, (art[def]), ?[location];?[SEM-Hinstitution]).

//VERA, 2011/05/26
//Marcha Anti-Touradas e de Defesa Animal
//1.ª Marcha Nacional por a Igualdade
1> NOUN[event=+,organized=+,political=+] @= (num[rom];num[ord]), ?[lemma:marcha], (?+[maj]), (conj), prep, (art[def]), ?+[maj].

//DCO, 2011/05/09
//Jornadas Parlamentares do PS
1> NOUN[event=+,organized=+,political=+] @= noun[lemma:jornada], ?[lemma:parlamentar], prep[lemma:de], (art[def]), ?[maj,SEM-Hcollective].

// Batalha de...
1> NOUN[event=+,organized=+,political=+] @= ?[lemma:batalha], prep[lemma:de], (art[def]), ?+[maj], (prep, (art), ?+[maj])*.

// Revolução de...
1> NOUN[event=+,organized=+,political=+] @= ?[lemma:revolução], prep[lemma:de], (art[def]), ?+[maj];num;?+[t-date], (prep, (art), ?+[maj])*.
1> NOUN[event=+,organized=+,political=+] @= ?[lemma:revolução], (adj[maj]).

//CH added
// O Caso Whitaker
1> NOUN[event=+,organized=+,political=+] @= ?[lemma:caso], ?[maj], (prep[lemma:de]; art; ?[maj])* .

// 1ª/2ª Guerra Mundial (Grande Guerra) - também II Guerra Mundial
1> NOUN[event=+,organized=+,political=+] @= num[ord];num[rom];?[lemma:primeiro];?[lemma:segundo], ?[lemma:"guerra mundial"].
1> NOUN[event=+,organized=+,political=+] @= num[ord];num[rom];?[lemma:primeiro];?[lemma:segundo], ?[lemma:grande], ?[lemma:guerra].

//Guerra do Vietname, Guerra do Yom Kippur
1> NOUN[event=+,organized=+,political=+] @= (num[ord]), ?[lemma:guerra], prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*.

// guerra sangrenta do Afeganistão
1> NOUN[event=+,organized=+,political=+] @= ?[lemma:guerra], adj+, (prep[lemma:de], (art), noun[location]).

//Guerra Colonial
1> ? @= ?[surface:"Guerra %+",event=+,organized=+,political=+].

//Coroação da Rainha Isabel II, tomada de posse de Cavaco Silva
1> NOUN[event=+,organized=+,political=+] @= ?[lemma:coroação], prep[lemma:de], (art), ?+[maj], (num[rom]).
1> NOUN[event=+,organized=+,political=+] @= ?[lemma:tomada], prep[lemma:de], ?[lemma:posse], prep[lemma:de], (art), (noun), ?+[maj], (prep, (art), ?+[maj])*.


//////////////////////////////////////////////////
//
// FROM THIS POINT, ONLY EVENTS RELATED TO SPORTS
//
//////////////////////////////////////////////////

//22ª jornada (da II Divisão de Honra)
1> NOUN[event=+,organized=+,sports=+] @= num[ord], ?[lemma:jornada], prep[lemma:de], art, (num), ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2011/04/04
//Maratona de Lisboa
//Meeting de Chemnitz
//Meeting de Dudelange
//Meeting de Kawasaki
//Meia Maratona Cidade de Ayamonte
//Meia Maratona de Ovar
//Meia Maratona Manuela Machado
1> NOUN[event=+,organized=+,sports=+] @= (num[ord];num[rom]), ?[lemma:maratona,maj];?[lemma:"meia maratona",maj];?[lemma:meeting,maj], (prep[lemma:de]), noun+[proper];?[location].

//Jogos Municipais
//DCO, 2011/05/04 - Regra corrigida
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:jogo], adj*, (prep[lemma:de]), (art), ?+[maj], (prep ; art ; ?[maj])*.

//DCO, 2011/04/04
//Campeonato Nacional - II Divisão
//Campeonato Nacional - II Divisão de Júniores
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:campeonato,maj], adj;noun, (punct[lemma:"-"]), num[rom];num[ord], noun, (prep, (art[def]), ?+[maj])*.

//DCO, 2011/02/06
//Campeonato de Lisboa de ténis de mesa
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"campeonato"], prep[lemma:de], (art[def]), ?[location], prep[lemma:de], (art[def]), ?[discipline,sports-discipline].

//DCO, 2011/04/04
//Campeonato Nacional de 1974
//Campeonato Nacional de futebol
//Campeonato Nacional de basquetebol
//Campeonato de Portugal
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"campeonato"], (?[lemma:"nacional"]), prep[lemma:de], num[lemma:"1%d%d%d"];num[lemma:"20%d%d"];?[discipline,sports-discipline];?[location].

//DCO, 2011/04/04
//meia-final do Campeonato Nacional de Futebol
2> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"meia-final"];?[lemma:"quartos-de-final"];?[lemma:"oitavos-de-final"];?[lemma:"avos-de-final"], prep[lemma:de], (art[def]), ?[event,organized,sports].

//DCO, 2011/04/04
//Campeonato Nacional
//Campeonato Nacional da A1
//Campeonato Nacional da CNB1
//Campeonato Nacional de Carambola
//Campeonato Nacional de Clubes de Pista Coberta
//Campeonato Nacional de Estrada
//Campeonato Nacional de Veteranos de Pista Coberta
//Campeonato Nacional Juniores em Pista Coberta
//Campeonatos Nacionais de Clubes da 1ª Divisão
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"campeonato",maj], ?[lemma:"nacional",maj];?[lemma:"regional",maj], (prep[lemma:de]), (art[def]), (?+[maj]), (prep, (art[def]), (num[rom];num[ord]), ?+[maj])*.

//DCO, 2011/02/05
//Campeonato Sub-23
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"campeonato"], noun[lemma:"sub-%d+"].

//DCO, 2011/04/04
//15.ª Corrida de Lisboa
1> NOUN[event=+,organized=+,sports=+] @= (num[ord] ; num[rom]), ?[lemma:corrida], prep[lemma:de], (art), ?[location].

//DCO, 2011/04/04
//Corrida de São Silvestre -- TODO!!!!!!
//Corrida de São Silvestre da Amadora
//Corrida de São Silvestre de Lisboa
//Corrida de São Silvestre do Funchal
//Corrida de São Silvestre dos Olivais
//Corrida de São Silvestre da Cidade do Porto
1> NOUN[event=+,organized=+,sports=+] @= (num[ord] ; num[rom]), ?[lemma:"Corrida de São Silvestre"], (prep[lemma:de]), (art), (?[location]).

//DCO, 2011/04/04
//São Silvestre de Lisboa
1> NOUN[event=+,organized=+,sports=+] @= (num[ord];num[rom]), ?[lemma:"São Silvestre"], prep[lemma:de], (art), ?[location].

//Euro 2004
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:euro, maj], num.

// Grande Prémio de X...
1> NOUN[event=+,organized=+,sports=+] @= (num[rom];num[ord]), ?[lemma:"grande prémio"], (prep[lemma:de], noun, (num)), (prep[lemma:de], (?[lemma:o]), noun[location]).

// Jogos Olímpicos (de Inverno) (de 1994)
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"jogos olímpicos"], (prep[lemma:de], ?[lemma:inverno];?[lemma:verão]), (prep[lemma:de], noun[location]), (prep[lemma:de], ?[num]).

// Madeira Cup, X Cup
1> NOUN[event=+,organized=+,sports=+] @= (num[rom];num[ord]), ?[maj], ?[lemma:cup].

//DCO, 2011/04/04
//Liga Europa
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"liga"], ?[surface:"Europa"].

//DCO, 2011/04/04
//Liga dos Campeões
//Liga dos Campeões Europeus
//Liga dos Campeões Europeus de Basquetebol
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"liga"], prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*.

//Mundial de Sub-20
//a Copa, o Mundial, o Europeu (de futebol)
1> NOUN[event=+,organized=+,sports=+] @= | art[def];pron[dem] | (num[rom,start_event=+,event=+,organized=+]), ?[lemma:copa, maj,start_event=+,organized=+,event=+];?[lemma:mundial,maj,start_event=+,organized=+,event=+];?[lemma:europeu,maj,start_event=+,organized=+,event=+], ((prep[lemma:de]), ?+[maj,end_event=+], (num)).

//DCO, 2011/02/05
//Torneio Cidade da Guarda
//Torneio Cidade de Guimarães
//Torneio Cidade de Leiria
//Torneio Cidade de Loures
//Torneio Cidade do Sabugal
//Torneio Cidade de Viana do Castelo
//Torneio da Cidade de Florença
//Torneio de Paris
//Torneio do Guadiana
//Torneio de Futsal de Montalegre
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"torneio",maj], prep[lemma:de], (art[def]), ?[discipline,sports-discipline], (prep[lemma:de], (art[def]), ?[location]).

//DCO, 2011/02/05
//Torneio de Inglaterra
//Torneio do Sport Lisboa e Benfica
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"torneio",maj], (prep[lemma:de], (art[def])), ?[location];?[SEM-Hcollective,SEM-Hinstitution,SEM-sport-club].

//DCO, 2011/02/05
//Torneio Internacional de Inglaterra
//Torneio Internacional do Sport Lisboa e Benfica
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"torneio internacional"], (prep[lemma:de], (art[def])), ?[location];?[SEM-Hcollective,SEM-Hinstitution,SEM-sport-club].

//DCO, 2011/02/05
//Torneio Internacional de Futebol Sub-15
//Torneio Internacional da Feira de S. Mateus
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"torneio internacional"], (prep[lemma:de], (art[def])), ?+[maj], (prep, (art[def]), ?+[maj])*.

//DCO, 2011/02/05
//Torneio das Três Nações
1> NOUN[event=+,organized=+,sports=+] @= ?[lemma:"torneio",maj], prep[lemma:de], (art[def]), num[rom:~,ord:~,time:~,t-meas:~,curr:~,sports_results:~], ?[surface:"Nações"].

// Volta a Portugal, Volta a França, Tour de France
1> NOUN[event=+, organized=+,sports=+] @= (num[rom];num[ord]), ?[lemma:volta,maj], ?[lemma:a], (?[lemma:o]),  noun[location, country], (prep[lemma:em], noun).

1> NOUN[event=+, organized=+,sports=+] @= (num[rom];num[ord]), ?[lemma:tour, maj], ?[lemma:de], ?[lemma:France, maj].

//Rali de Monte Carlo
1> NOUN[event=+,organized=+,sports=+] @= (num[rom];num[ord]), (?[lemma:edição], prep, (art)), ?[lemma:"rali",maj], (prep[lemma:de], (art[def])), ?[location], (num[lemma:"1%d%d%d"];num[lemma:"20%d%d"]).


////////////////////////////////////////////////////////
//
// FROM THIS POINT ON, ONLY SCIENTIFIC (ORGANIZED) EVENTS
//
///////////////////////////////////////////////////////

//11ª Conferência Nacional de Economia da Saúde
1> NOUN[event=+,organized=+,scientific=+] @= (num[rom];num[ord]),
                                              ?[lemma:congresso,maj];?[lemma:"congresso ?+",maj];?[lemma:conferência,maj];?[lemma:"conferência ?+",maj],
                                              (?[maj]), ((prep), (art[def]), ?[maj])*.

//Seminário Literatura e Sociedade
//Seminário sobre Ciências de a Comunicação em Ponta Delgada
1> NOUN[event=+,organized=+,scientific=+] @= (num[rom];num[ord]), ?[organized,scientific,maj], (prep), (art), ?[maj];?[SEM-Hinstitution], ((conj);(prep), (art), ?[maj])*.


////////////////////////////////////////////////////////
//
// FROM THIS POINT ON, ONLY ARTISTIC (ORGANIZED) EVENTS
//
///////////////////////////////////////////////////////

//Exposição de Pintura Berardo
1> NOUN[event=+,organized=+,artistic=+] @= (num[ord] ; num[rom]), ?[lemma:exposição], (prep[lemma:de], noun+), (prep[lemma:de]), ?+[maj], (prep, (art), ?+[maj])*.
1> NOUN[event=+,organized=+,artistic=+] @= |?[lemma:exposição]| ?+[maj];num, ((?[lemma:e]),prep*, (art), ?+[maj])*.
1> NOUN[event=+,organized=+,artistic=+] @= (num[ord] ; num[rom]), ?[lemma:"exposição de pintura"], (prep[lemma:de]), ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2011/04/15
// (2.ª) (grande) exposição «O mundo dos dinossauros»
1> NOUN[event=+,organized=+,artistic=+] @= (num[ord] ; num[rom]), (adj), ?[organized,artistic], (?), punct[left,bracket];punct[quote], ?[maj], ?*, punct[right,bracket];punct[quote].

//DCO, 2011/04/18
// 53ª cerimónia dos Grammy, cerimónia de entrega dos Óscares
2> NOUN[event=+,organized=+,artistic=+] @= (num[ord] ; num[rom]), (adj), ?[lemma:cerimónia], prep[lemma:de], (art[def]), (noun, prep, (art)), ?[maj] ; ?[event,organized,artistic].

//VERA, 2011/05/09
//II Espectáculo de Variedades
//Espectáculo de teatro musical O Quebra-Nozes
//espectáculo «Fantasy»
2> NOUN[event=+,organized=+,artistic=+] @= (num[ord] ; num[rom]), (adj);(noun) , (prep[lemma:de],art[def]), ?[lemma:"espetáculo de ?+"];?[lemma:espetáculo], (prep,noun), ((punct[left,bracket];punct[quote]),?+[maj],?*,(punct[right,bracket];punct[quote]))*.

//VERA, 2011/05/09
//Feira de Artesanato (e Pintura) de Lisboa
//Festival de Chocolate de Óbidos
//47ª edição do Festival RTP da Canção
1> NOUN[event=+,organized=+,artistic=+] @= (num[ord] ; num[rom]), (adj);(noun), (prep[lemma:de],art[def]), ?[lemma:"feira de ?+"];?[lemma:"feira do ?+"];?[lemma:festival,maj], (conj,noun);(?[maj]), prep[lemma:de], (art), ?+[maj], (prep, (art), ?[maj])*, (num[lemma:"1%d%d%d"];num[lemma:"20%d%d"]).

//VERA, 2011/05/23
//Feira de Março
//Feira de Inverno
//Festival de Vila Nova de Gaia
2> NOUN[event=+,organized=+,artistic=+] @= (num[ord] ; num[rom]), (adj);(noun), (prep[lemma:de],art[def]), ?[organized,artistic], prep[lemma:de], (art), ?+[maj].

//VERA, 2011/05/09
//XXXII edição da Fatacil – Feira de Artesanato, Turismo, Agricultura
1> NOUN[event=+,organized=+,artistic=+] @= (num[ord] ; num[rom]), (adj);(noun) , (prep[lemma:de],art[def]), ?[maj], punct, ?[lemma:"feira de ?+"], (punct;conj,?+[maj])*.

//Mostra de Vinhos do Cadaval
//26ª edição da Mostra de Artesanato e Cerâmica de Barcelos
1> NOUN[event=+,organized=+,artistic=+] @= (num[rom];num[ord]), (noun), (prep[lemma:de],art[def]), ?[surface:"Mostra"], (adj), prep[lemma:de], (art), ?+[maj], (conj,noun), (prep, (art), ?[location]).

//VERA, 2011/05/23
//3ª edição do Ciclo de Cinema Espanhol
//Ciclo de Cinema «Imagem e Memória»
1> NOUN[event=+,organized=+,artistic=+] @= (num[rom];num[ord]), (noun), (prep[lemma:de],art[def]), ?[lemma:"ciclo de ?+"], (?+[maj]), ((punct[left,bracket];punct[quote]),?+[maj],?*,(punct[right,bracket];punct[quote])).

//VERA, 2011/05/23
//Concerto dos Xutos e Pontapés (no Campo Pequeno)
1> NOUN[event=+, organized=+, artistic=+] @= (num[ord];num[rom]);(adj), ?[lemma:concerto], (adj), prep[lemma:de], (art), ?[SEM-Hgroup];?+[maj], (prep, (art), ?[location]).

//VERA, 2011/05/23
//Festival Doclisboa 2010.
//festival de cinema IndieLisboa
1> NOUN[event=+, organized=+, artistic=+] @= (num[ord];num[rom]);(adj), (?[lemma:edição], prep, (art)), ?[lemma:festival];?[lemma:"festival de ?+"];?[lemma:"festival ?+"], (adj), (prep[lemma:de], (art)), ?+[noun],(num[lemma:"1%d%d%d"];num[lemma:"20%d%d"]).

//VERA, 2011/05/23
//21ª edição do Amadora BD - Festival Internacional de Banda Desenhada da Amadora
1> NOUN[event=+,organized=+,artistic=+] @= (num[rom];num[ord]), (noun), (prep[lemma:de],art[def]), ?[maj], (?), (punct), ?[organized,artistic];?[lemma:"festival +?*"], prep, (art), noun, prep, (art), ?[location], (punct).

//DCO
//Feira Internacional de Lisboa, Feira do Móvel (foi para ORG)
1> NOUN[event=+,organized=+,artistic=+] @= (num[rom] ; num[ord]), ?[lemma:"feira internacional"], prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*.

//DCO
//Expo 98
1> NOUN[event=+,organized=+,artistic=+] @= ?[maj,lemma:Expo];?[lemma:EXPO], num[rom:~,lemma:"%d%d"].

1> ? = noun[lemma:"EXPO'%d%d",event=+,organized=+,artistic=+];noun[lemma:"Expo'%d%d",event=+,organized=+,artistic=+].

//Show
1> NOUN[event=+,organized=+,artistic=+] @= ?[lemma:show], ?[maj], (prep; art; ?[maj])*.

1> NOUN[event=+,organized=+,artistic=+] @= noun[maj], (prep; art; ?[maj])*, ?[lemma:show] .


//Lire le Portugal avec la Foundation Gulbenkian
1> ? @= ?[lemma:"Lire le Portugal avec la Foundation Gulbenkian",event=+,organized=+,artistic=+].

//////////////////////////////////////////////////
//
// FROM THIS POINT ON, ONLY NATURAL EVENTS
//
//////////////////////////////////////////////////

//DCO, 2011/04/05
// furacão Katrina, tempestade tropical Andrew, tempestade tropical "Andrew", furacão São Calisto II
1> NOUN[event=+,natural=+] @= ?[natural], (punct[quote]), noun+[maj,proper], (punct[quote]), (num[rom]).

//DCO, 2011/04/05
// o grande terramoto do Japão (de 2011)
1> NOUN[event=+,natural=+] @= (adj), ?[natural], prep[lemma:de], (art[def]), ?[location], (prep[lemma:de], ?[t-date]).

//DCO, 2011/04/05
//o (grande) tsunami de 2004
2> NOUN[event=+,natural=+] @= (adj), ?[natural], prep[lemma:de], ?[t-date].

//DCO, 2011/04/05
//praga de gafanhotos
1> NOUN[event=+,natural=+] @= ?[lemma:praga,natural], prep[lemma:de], (art[def]), noun.

//DCO, 2011/04/05
//nomes esquisitos, que não são apanhados com o traço proper
//ciclone de Bohla, ciclone Gafilo
1> NOUN[event=+,natural=+] @= (adj), ?[natural], (prep[lemma:de]), ?[maj], (prep[lemma:de], (art), ?[maj])*.

//DCO, 2011/04/05
//maré negra no Golfo do México
//erupção vulcânica nas Filipinas
1> NOUN[event=+,natural=+] @= (adj), ?[natural], prep[lemma:em], (art[def]), ?[location].
