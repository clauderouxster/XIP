//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : LGAward.xip                                                 !
//!   Date       : 2011/April                                                  !
//!   Author     : Diogo Oliveira                                              !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!  Awards                                                                    !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Sequence:

//DCO, 2011/04/04
//Supertaça AFL
//Supertaça Compal
//Supertaça Europeia
//Supertaça Intercontinental
//Supertaça Ibérica
//Supertaça Sul-americana
//Taça Ibérica
//Taça Intercontinental
//Taça Latina
//Taça Monumental
//Taça O Jogo
//Taça Caixa Geral de Depósitos
//Taça Câmara Municipal de Oeiras
1> NOUN[award=+,sports-award=+] @= ?[lemma:"Supertaça"];?[lemma:"taça",maj], ?[SEM-Hcollective,SEM-Hinstitution];?[SEM-Hcollective,SEM-Hadministration];?[proper,toutmaj];?[gentcontinent];?[gentregion];?[lemma:"intercontinental"];?[surface:"Latina"];?[surface:"Monumental"].

//DCO, 2011/04/04
//Taça 28 de Maio
1> NOUN[award=+,sports-award=+] @= ?[lemma:"taça",maj], ?[time,t-date].

//DCO, 2011/04/04
//Taça Lisboa, Taça «Lisboa»
1> NOUN[award=+,sports-award=+] @= ?[lemma:"taça",maj], (punct[comma:~,dots:~,quest:~,dash:~,quote:~,slash:~,etc:~]), noun[city], (punct[comma:~,dots:~,quest:~,dash:~,quote:~,slash:~,etc:~]).

//DCO, 2011/04/04
//Supertaça/Taça de Angola de basquetebol
1> NOUN[award=+,sports-award=+] @= ?[lemma:"Supertaça"];?[lemma:"taça",maj], prep[lemma:de], (art), ?[country], prep[lemma:de], (art), ?[discipline,sports-discipline].

//DCO, 2011/04/04
//Supertaça/Taça de futsal de Portugal
1> NOUN[award=+,sports-award=+] @= ?[lemma:"Supertaça"];?[lemma:"taça",maj], prep[lemma:de], (art), ?[discipline,sports-discipline], prep[lemma:de], (art), ?[country].

//DCO, 2011/04/04
//Taça de Portugal, Taça de Inglaterra, Taça do Rei, ...
1> NOUN[award=+,sports-award=+] @= ?[lemma:"taça",maj], prep[lemma:de], (art), ?[country];?[lemma:"rei"].

//DCO, 2011/04/04
//Taça UEFA, Taça FIFA, ...
1> NOUN[award=+,sports-award=+] @= ?[lemma:"taça",maj], ?[SEM-Hcollective,SEM-Hinstitution].

//DCO, 2011/04/04
//Taça da Liga
//Taça dos Libertadores
//Taça de Honra
//Taça do Mundo
//Taça dos Campeões Europeus
//Taça do Campeonato do Mundo
//Taça do Campeonato da Europa
//Taça da Independência de Angola
//Taça de Ouro da Imprensa
//Taça dos Campeões Europeus de Pista
//Taça dos Clubes Campeões Europeus de Corta-Mato
//Taça dos Clubes Vencedores de Taças
//Taça Centenário da Associação de Futebol de Lisboa
1> NOUN[award=+,sports-award=+] @= ?[lemma:"taça",maj], prep[lemma:de], (art[def]), ?+[maj], (prep, (art[def]), ?+[maj])*.

1> NOUN[award=+,sports-award=+] @= ?[lemma:"taça",maj], (?[lemma:"centenário",maj]), prep[lemma:de], (art[def]), ?[SEM-Hcollective,SEM-Hinstitution].

//DCO, 2011/04/04
//Taça Intercontinental de Futsal
1> NOUN[award=+,sports-award=+] @= ?[lemma:"taça intercontinental"], prep[lemma:de], (art[def]), ?[discipline,sports-discipline].

//DCO, 2011/04/04
//Copa do Brasil
1> NOUN[award=+,sports-award=+] @= ?[surface:"Copa"], prep[lemma:de], (art[def]), ?[country].

//DCO, 2011/04/04
//Copa Libertadores da América
1> NOUN[award=+,sports-award=+] @= ?[surface:"Copa"], ?+[maj], (prep, (art[def]), ?+[maj])*.
