//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : LGOrg.xip                                                   !
//!   Date       : 2007/April                                                  !
//!   Author     : Luís Romão                                                  !
//!   Revised by : Diogo Oliveira (2010/August-October)                        !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!  Organization Name Rules                                                   !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


//DCO, 2010/08/17
// Changed all "company" occurrences to "SEM-Hinstitution" in order to comply with the new set of directives.
// Changed all "start_org" and "end_org" occurrences to "start_collective" and "end_collective", respectively.
// Changed all "org" occurrences to "SEM-Hcollective", in order to comply with the new set of directives.

//DCO, 2010/08/22
// Changed all "local" occurrences to "location"

Sequence:

//Vera, 2012/11/05
//Estabelecimento Prisional Central de Santa Cruz do Bispo
//Estabelecimento Prisional Especial de Tires
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+,location=+] = ?[lemma:"estabelecimento prisional"], (adj), prep[lemma:de], (art), ?[location].

//Presídio Militar de Tomar
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+,location=+] = ?[lemma:"presídio militar"], prep[lemma:de], (art), ?[location].

//Prisão de Castelo Branco
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+,location=+] = ?[lemma:prisão];?[lemma:cadeia], prep[lemma:de], (art), ?[location].

//Vera, 2011/07/13
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] = ?[lemma:"bolsa de valores"];?[lemma:bolsa], prep[lemma:de], (art), ?[location].

//DCO, 2011/07/01
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] = noun[SEM-inst], ?[lemma:número];?[lemma:"nº"];?[lemma:"n.º"], num.

//DCO, 2011/04/26
1> ? = noun[proper,SEM-Hcollective], conj[lemma:e] ; conj[lemma:ou], noun[proper,toutmaj:~,SEM-Hcollective:~,location:~,SEM-Hcollective=+] .

//DCO, 2011/02/23
//A. F. A.
//S. S. U. Operária
1> noun[SEM-Hcollective=+] @= noun[lemma:"%C."], noun[lemma:"%C."], noun+[lemma:"%C."], (noun[maj]).

// Igreja Católica, Protestante, etc., são entidades da categoria HUMANO, tipo COLETIVO, subtipo INSTITUICAO
// problem because have been considered as CULTURE in LGCulture.xip

// Folha + de + location is a newspaper name.
// Example: Folha de S. Paulo/Pernambuco/Niteroi etc.
1> noun[SEM-Hjornal=+,SEM-Hcollective=+,SEM-Hinstitution=+] = noun[lemma:folha], prep[lemma:de], ?[location].

// Portugal X França (EVENTOS)
1> NOUN[event=+, eventac=+] = noun[maj], ?[lemma:X], noun[maj].

// Telecom/ Portugal, France Telecom
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= (?[maj, art:~]), ?[lemma:Telecom].

//DCO, 2011/07/14
// Regra corrigida
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:"junta de freguesia"], prep[lemma:de], (art[def]), ?[location].

//////////////////////////////////////////////////
//////////////////////////////////////////////////
//////////////////////////////////////////////////
//////////////////////////////////////////////////
//////////////////////////////////////////////////

// anúncio à/da
1> ?  @= ?[lemma:anúncio], prep[lemma:a];prep[lemma:de], (?[lemma:o]), ?[maj,start_collective=+,SEM-Hcollective=+,SEM-Hinstitution=+], (prep; art; ?[maj,end_collective=+,SEM-Hinstitution=+])*.

//DCO, 2010/10/03
// Cuidado com "Para que organização nos EUA trabalhava ...?" - adicionada restrição location:~
// organização...
1> ?  @= ?[lemma:organização,maj:~], (prep), (art), ?[maj,location:~,start_collective=+,SEM-Hcollective=+, SEM-Hadministration=+], (prep; art; ?[maj,location:~,end_collective=+,SEM-Hadministration=+])*.

//DCO, 2010/10/03
// Lemas adicionados e regra corrigida
// Organização Mundial da Saúde, Organização Nacional de Acreditação, Organização Administrativa do Município e do Orçamento Municipal
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:organização,maj];?[lemma:"organização mundial",maj];?[lemma:"organização nacional",maj];?[lemma:"organização administrativa",maj], (adj), (prep), (art), ?[maj], (prep ; art ; conj ; ?[maj])* .

// Corte (Internacional) de Justiça
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:corte,maj], (adj), prep[lemma:de], ?[maj], (prep;art;?[maj])* .

// a sede de...
2> ?  @= ?[lemma:sede], prep[lemma:de], (art[def]),  ?[maj,start_collective=+,SEM-Hcollective=+,SEM-Hinstitution=+], (prep; art; ?[maj,end_collective=+,SEM-Hinstitution=+])*.

// União X Mundial, União Europeia, União Africana
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:união, maj], ?[maj], (prep; art; ?[maj])*.

//DCO, 2011/07/14
// Comunidade Económica Europeia, Comunidade de Países de Língua Portuguesa
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:"comunidade económica",maj], (prep[lemma:de]), ?[maj], (prep; art; ?[maj])*.

// empresa/ONG... X
1> ? @= ?[lemma:empresa];?[lemma:ong],  ?[maj,start_collective=+,SEM-Hinstitution=+,SEM-Hcollective=+], (prep; art; ?[maj,end_collective=+,SEM-Hinstitution=+])*.


// Ministério das Obras Públicas, Ministério do Ambiente
// Junta de Freguesia da Luz
// Município de Setúbal, Autarquia, Concelho
//CH restriction to avoid to take "secretario de [Estado da Cultura]"
//DCO 2011/02/09 words no longer carry the "SEM-Hcollective" feature from the lexicon (in order to avoid, e.g., <EM>secretaria</EM>)
//Also, the word that introduces the entity, e.g., 'autarquia' de Barcelos, does NOT need to be capitalized.
2> ? @= | ?*[SEM-Hcargo:~] | noun[SEM-Hadministration,start_collective=+], prep[lemma:de,cont_collective=+], (art[def]), ?[maj,toutmaj:~,end_collective=+], (prep[lemma:de,cont_collective=+] ; art[def] ; ?[maj,end_collective=+])*.

// Câmara Municipal de Lisboa
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] = ?[lemma:câmara], (?[lemma:municipal]), prep[lemma:de], (art), ?[maj,location].

// ???
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:câmara], (?[lemma:municipal]), prep[lemma:de], (art), ?[maj,start_loc](prep;art;?[maj])*, ?[maj,end_loc].

//Câmara de Comércio (conflito com CM - CH conflito evitado especializando as regras de CM)
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:câmara, maj], (prep[lemma:de]), (art), ?+[maj], (?[lemma:e]), (prep*, (art), ?+[maj])*.

// Assembleia Nacional, Municipal, de Freguesia
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:assembleia], ?[lemma:nacional];?[lemma:municipal], (prep[lemma:de], ?[lemma:freguesia]), prep[lemma:de], (art), ?+[maj], (prep, art*, ?+[maj])*.
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:assembleia], ?[lemma:nacional];?[lemma:municipal], (adj).
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:assembleia],  prep[lemma:de], ?[lemma:freguesia], (adj).

//Direção-Geral de Viação
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:direção-geral],  prep[lemma:de], ?+[maj], (prep, art*, ?+[maj])*.

//DCO, 2010/09/23
// A segunda regra não estava a disparar porque "administração" não estava a ficar com os traços SEM-Hcollective e SEM-Hadministration
// Administração da SOMAGUE
//DCO, 2011/07/18 regras comentadas
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:administração, maj], prep[lemma:de], (art), ?+[maj], ((?[lemma:e]), prep*, art*, ?+[maj])*.
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:administração, maj:~], prep[lemma:de], (art), ?[maj,start_collective=+,SEM-Hcollective=+,SEM-Hinstitution=+], (?[lemma:e]; prep; art; ?[maj,end_collective=+,SEM-Hinstitution=+])*.

//DCO, 2010/09/23
// Lema corrigido
// Secretaria de Estado dos Transportes
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:"secretaria de estado"], prep[lemma:de], (art), ?+[maj], (prep[lemma:de], art*, ?+[maj])*.

//FC do Porto, Boavista FC
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:futebol], ?[lemma:clube], prep[lemma:de], (art), ?+[maj], (prep, art*, ?+[maj])*.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:FC], ?+[maj], (prep, art*, ?+[maj])*.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= (art), ?+[maj], (prep, art*, ?+[maj])*, ?[lemma:FC].

//Sport Lisboa e Benfica, Sporting Clube de Portugal
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:sport], ?[lemma:Lisboa], ?[lemma:e], ?[lemma:Benfica].
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:Sporting], ?[lemma:clube], prep[lemma:de], ?[maj].

//Faculdade de Letras, Faculdade de Ciências da Universidade de Lisboa, Departamento de Biologia
//DCO, 2011/05/02
//Removido "centro": dizer que um centro é uma universidade é demasiado arriscado
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Huniversity=+] @= ?[lemma:faculdade,maj];?[lemma:universidade,maj], prep[lemma:de], (art), (?[maj], (?[lemma:e]))+, prep[lemma:de], (art), (noun+), (prep[lemma:de]), (art), ?+[maj], (prep, art*, ?+[maj])*.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Huniversity=+] @= ?[lemma:faculdade,maj];?[lemma:universidade,maj], prep[lemma:de], (art), (?[maj], (?[lemma:e]))+, prep[lemma:de], (art), (noun+), (prep[lemma:de]), (art), ?[lemma:universidade], (adj), prep[lemma:de], (art), ?+[maj], (prep, art*, ?+[maj])*.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Huniversity=+] @= ?[lemma:faculdade,maj];?[lemma:universidade,maj], prep[lemma:de], (art), (?[maj], (?[lemma:e]))+, prep[lemma:de], (art), (noun+), (prep[lemma:de]), (art), ?[lemma:universidade,maj], adj.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Huniversity=+] @= ?[lemma:faculdade,maj];?[lemma:universidade,maj], prep[lemma:de], (art), (?[maj,sem-sport:~,sports-discipline:~], (?[lemma:e]))+.

//DCO, 2010/10/03
// Example: Centro Cultural de Belém
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"centro cultural"], prep, (art), ?[location,maj].

//DCO, 2010/10/03
// "Faculdade de Direito" tem que ser apanhada assim, porque, por algum motivo", "de direito" é um único token quando sai do RuDriCo ...
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Huniversity=+] @= ?[lemma:faculdade,maj], ?[lemma:de], ?[lemma:direito].

//Departamento de Biologia, Reprografia da LEIC, etc.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:departamento];?[lemma:reprografia];?[lemma:portaria];?[lemma:secção];?[lemma:acessoria];?[lemma:secretaria];?[lemma:unidade];?[lemma:setor];?[lemma:gabinete], prep[lemma:de], (art), (?[maj], (?[lemma:e]))+, prep[lemma:de], (?+[maj]), (prep[lemma:de]), (art), ?+[maj], (prep, art*, ?+[maj])*.

1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:departamento];?[lemma:reprografia];?[lemma:portaria];?[lemma:secção];?[lemma:acessoria];?[lemma:secretaria];?[lemma:unidade];?[lemma:setor];?[lemma:gabinete], prep[lemma:de], (art), (?[maj], (?[lemma:e]))+, prep[lemma:de], (?+[maj]), (prep[lemma:de]), (art), ?[lemma:universidade], (adj), prep[lemma:de], (art), ?+[maj], (prep, art*, ?+[maj])*.

1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:departamento];?[lemma:reprografia];?[lemma:portaria];?[lemma:secção];?[lemma:acessoria];?[lemma:secretaria];?[lemma:unidade];?[lemma:setor];?[lemma:gabinete], prep[lemma:de], (art), (?[maj], (?[lemma:e]))+, prep[lemma:de], (?+[maj]), (prep[lemma:de]), (art), ?[lemma:universidade,maj], adj.

//DCO, 2011/05/11
//Regra corrigida
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:departamento];?[lemma:reprografia];?[lemma:portaria];?[lemma:secção];?[lemma:acessoria];?[lemma:secretaria];?[lemma:unidade];?[lemma:setor];?[lemma:gabinete], (?[lemma:geral]), prep[lemma:de], (art[def]), ?[maj], (prep[lemma:de] ; art[def] ; conj[lemma:e] ; ?[maj])*, (punct[bracket,left], ?[toutmaj,SEM-Hcollective=+,SEM-Hadministration=+], punct[bracket,right]).

//DCO, 2011/05/11
//Sociedade Internacional de Óptica e Fotónica (SPIE)
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= noun[lemma:"Sociedade internacional"], prep[lemma:de], (art[def]), ?[maj], (prep[lemma:de] ; art[def] ; conj[lemma:e] ; ?[maj])*, (punct[bracket,left], ?[toutmaj,SEM-Hcollective=+,SEM-Hinstitution=+], punct[bracket,right]).

//Partido Comunista Português, Partido Social Democrata (siglas no léxico), Partido Nacional Escocês
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+, SEM-Hpartido=+] @= ?[lemma:SEM-Hpartido, maj];?[lemma:"partido nacional", maj], (adj+[maj]), ((?[lemma:e]),prep*, art*, ?[maj])*.

//o candidato do PSDB, a campanha, o deputado (presidencial, autárquica...) do PSDB
1> ? @= ?[lemma:candidato];?[lemma:deputado];?[lemma:parlamentar], prep[lemma:de], art, ?[maj,SEM-Hcollective=+,SEM-Hinstitution=+,start_collective=+], (prep; art; ?[maj,end_collective=+,SEM-Hinstitution=+])*.

//CH commented can be campanha de Cavaco Silva
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= |?[lemma:campanha], adj, prep[lemma:de], art| ?+[maj], (prep, art*, ?+[maj])*.

//DCO, 2011/07/14
// Cruz Vermelha Portuguesa, Francesa, etc. [o gentílico não precisa de estar em maiúsculas]
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] = noun[lemma:"Cruz Vermelha"], ?[gentcountry].

//DCO, 2011/07/18
// Instituto Português
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] = noun[lemma:instituto,maj], ?[gentcountry,maj].

//DCO, 2011/07/18
// Instituto Superior Bissaya Barreto
1> ? @= noun[SEM-school,start_collective=+], (adj[maj,end_collective=+]), noun[SEM-Hpeople,SEM-Hindividual,end_collective=+].

//DCO, 2011/07/14
// Regra para apanhar escolas, liceus, colégios, escolas superiores, etc. Consultar entradas lexicais com o traço SEM-school em LEXICONS/lexNounSem.xip.
2> ? @= noun[SEM-school,start_collective=+], (adj[maj,end_collective=+]), (prep[lemma:de,cont_collective=+], (art[def])), ?[maj,end_collective=+], (prep[lemma:de,cont_collective=+] ; art[def] ; conj[lemma:e,cont_collective=+] ; noun[location,end_collective=+] ; noun[SEM-Hcollective,SEM-Hinstitution,end_collective=+] ; noun[SEM-Hpeople,SEM-Hindividual,end_collective=+] ; ?[maj,end_collective=+])*.

//DCO, 2011/07/15
// <SEM-SCHOOL> de <COLLECTIVE, ADMINISTRATION> (Academia da Força Aérea)
2> ? @= noun[SEM-school,start_collective=+], (adj[maj,end_collective=+]), (prep[lemma:de,cont_collective=+], (art[def])), noun[SEM-Hcollective,SEM-Hadministration,end_collective=+].

//DCO, 2011/07/18
// <INSTITUTION>
2> ? @= noun[SEM-Hcollective,SEM-Hinstitution,start_collective=+], (adj[maj,end_collective=+]), (prep[lemma:de,cont_collective=+], (art[def])), ?[maj,end_collective=+], (prep[lemma:de,cont_collective=+] ; art[def] ; conj[lemma:e,cont_collective=+] ; noun[location,end_collective=+] ; noun[SEM-Hcollective,SEM-Hinstitution,end_collective=+] ; noun[SEM-Hpeople,SEM-Hindividual,end_collective=+] ; ?[maj,end_collective=+])*.

//DCO, 2011/07/12
//Regra corrigida
//1> ? @= ?[lemma:instituto,start_collective=+,SEM-school=+];?[lemma:"instituto superior",start_collective=+,SEM-school=+], (?[lemma:politécnico,end_collective=+]), (prep[lemma:de]; art; ?[maj,end_collective=+])* .
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:instituto, maj];?[lemma:escola, maj].

//DCO, 2011/02/07
//II Divisão do Campeonato Nacional
// Tem que estar neste ficheiro (ou em qualquer outro que seja corrido antes do LGLast) devido a uma regra no LGLast que estraga tudo
// SOLUÇÃO TEMPORÁRIA!!!
1> NOUN[event=+,organized=+,sports=+] @= num[rom];num[ord], ?[lemma:"divisão"], prep[lemma:"de"], (art[def]), ?[lemma:"campeonato",maj], ?[lemma:"nacional",maj].

//DCO, 2010/09/23
// Adicionado novo lema... "associação desportiva"
// Associação (Liga, Grupo, Confederação, Conselho) de Amizade Portugal-Bulgária, Associação Desportiva de Silves, Instituição + Sindicato
//DCO, 2011/02/05
// Retirei "liga", agora está em LGSports
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:associação];?[lemma:"associação desportiva"];?[lemma:grupo];?[lemma:instituição];?[lemma:sindicato];?[lemma:confederação];?[lemma:federação];?[lemma:conselho];?[lemma:sociedade], (prep[lemma:de]), (art), (?[maj]), (prep), (art), ?+[maj], (punct*[comma], ?*[lemma:e], prep*, art*, ?+[maj])*.

//DCO, 2011/02/05
// Retirei "liga", agora está em LGSports
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:associação];?[lemma:"associação desportiva"];?[lemma:grupo, maj];?[lemma:instituição, maj];?[lemma:sindicato, maj];?[lemma:confederação], (adj), (prep[lemma:de]), (art), ?+[maj], (prep, art*, ?+[maj])*.

//DCO+Vera, 2011/02/05
//Conselho de Arbitragem da Federação Portuguesa de Futebol
//Conselho de Arbitragem da Liga
//Conselho de Disciplina da Federação Portuguesa de Futebol
//Comissão Disciplinar da Liga
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"conselho de arbitragem"];?[lemma:"conselho de disciplina"];?[lemma:"comissão disciplinar"], prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*.

//DCO+Vera, 2011/02/05
//Seleção Resto da Europa
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"seleção",maj], ?+[maj], (prep, (art), ?+[maj])*.

//DCO+Vera, 2011/02/05
//Seleção de Luanda de Futebol
//Seleção de Lourenço Marques
//Seleção de Portugal de futebol
//Seleção de Angola
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:seleção], prep[lemma:de], (art), ?[country];?[city], (prep[lemma:de], (art)), (?[discipline,sports-discipline]).

//DCO+Vera, 2011/02/05
//Seleção portuguesa de futebol
//Seleção de futebol
//Seleção nacional de rugby/râguebi
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:seleção], (?[gentcity];?[gentcountry];?[lemma:"nacional"]), prep[lemma:de], (art), ?[discipline,sports-discipline].

//DCO+Vera, 2011/02/05
//Benfica SAD
//Benfica - SAD
//Benfica, SAD
//Benfica Sociedade Anónima Desportiva
//Benfica - Sociedade Anónima Desportiva
//Benfica, Sociedade Anónima Desportiva
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[SEM-Hinstitution], (punct[lemma:"-"];punct[comma]), ?[lemma:SAD].

//DCO, 2011/02/05
//Federação Internacional de História e Estatística de Futebol
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"federação internacional"], prep[lemma:de], (art), ?+[maj], (conj*, prep*, (art), ?+[maj])*.

//DCO, 2010/10/03
// Associação Nacional ...
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"associação nacional"], prep[lemma:de], (art), ?+[maj], (prep ; art ; conj ; ?+[maj])*.

//DCO, 2011/07/15 Vamos obrigar que "grupo" venha em maiúsculas
//Grupo Sonae
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:grupo,maj], ?+[maj].

//DCO, 2010/09/23
// "clube de futebol" agora é um único lema...
// clube de futebol da Damaia, Club Med
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:clube];?[lemma:"clube de futebol"];?[lemma:club];?[lemma:aeroclube], (adj), prep[lemma:de], (art), ?+[maj], (prep, art*, ?+[maj])*.

//DCO, 2011/02/05
// Regra melhorada para suportar coisas como "Clubes Campeões Europeus de Corta-Mato"
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:clube];?[lemma:club];?[lemma:aeroclube], ?+[maj], (prep, (art[def]), ?+[maj])*.

1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?+[maj], (prep, art*, ?+[maj])*, ?[lemma:club];?[lemma:clube], prep[lemma:de], (art), ?+[maj], (prep, art*, ?+[maj])*.

//DCO, 2011/02/01
// Regra alterada para contemplar sinais de pontuação
// Example: Vans/Eastpak Club
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?+[maj], (punct*, prep*, art*, ?+[maj])*, ?[lemma:club];?[lemma:clube].

//DCO, 2011/02/05
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:clube, maj];?[lemma:aeroclube, maj].

//DCO, 2010/08/19
// Estas regras não funcionavam porque os lemas mudaram
// Comité do PCP, Comité Central do PCP
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:comité];?[lemma:"comité central"], (prep[lemma:de]), (art), ?[SEM-Hcollective,SEM-Hinstitution,SEM-Hpartido].

// Comité (central) do Partido Comunista Chinês
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:comité];?[lemma:"comité central"], (prep[lemma:de]), (art), ?[lemma:partido];?[lemma:"Partido%+"], (adj), ?[gentcountry].

//DCO, 2010/08/19
// Comissão de Defesa, Comissão Winograd são entidades HUMANO COLETIVO ADMINISTRACAO
//DCO, 2011/05/04
//Regra corrigida
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:comissão], (prep[lemma:de], (art[def])), ?+[maj], (prep[lemma:de] ; art[def] ; conj[lemma:e] ; ?[maj])*.

//DCO, 2010/08/19
// Retirados traços maj porque as entidades podem estar escritas com letra minúscula
// Departamento de Marketing da Xerox, Departamento de Vendas da IBM + Sucursal + Filial são entidades HUMANO COLETIVO ADMINISTRACAO
//DCO, 2011/02/03
// Regras corrigidas
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"departamento %+"];?[lemma:"recursos humanos"], prep[lemma:de], (art), ?+[maj,SEM-Hinstitution], (prep[lemma:de] ; art[def] ; ?[maj])*.
//1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:departamento];?[lemma:sucursal];?[lemma:filial], prep[lemma:de], (art), ?+[maj], (prep[lemma:de]), (art), ?+[maj], (prep, art*, ?+[maj])*.
//1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:departamento];?[lemma:sucursal];?[lemma:filial], prep[lemma:de], (art), ?+[maj].

//Não funciona
//Departamento de contabilidade da Câmara Municipal de Lisboa.
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:"departamento %+"], prep[lemma:de], (art), ?+[maj,SEM-Hadministration];?+[maj,admin_area], (prep[lemma:de] ; art[def] ; ?[maj])*.
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:"serviço municipalizado"], prep[lemma:de], (art), ?+[maj,SEM-Hadministration];?+[maj,admin_area], (prep[lemma:de] ; art[def] ; ?[maj])*.

//DCO, 2010/09/23
// Lema e regra corrigidos
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:"serviço administrativo",SEM-Hcollective=+,SEM-Hadministration=+], (conj[coord], adj[maj]), (prep[lemma:de], (art), ?[maj]).

//DCO, 2010/09/23
// livraria não precisa de ser maj para ser entidade mencionada
// Livraria Barata, Livraria Nacional (de França)/francesa
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:livraria], ?+[maj], (prep, (art), ?[maj])*;(?[gentcountry]).

//DCO, 2010/09/23
// As entidades não precisam de estar em maj
// Embaixada de Portugal (em Paris), Embaixada Portuguesa
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:embaixada], ?[gentcountry], prep[lemma:em], (art), ?+[maj], (prep, art*, ?+[maj])*.
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:embaixada], ?[gentcountry].
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:embaixada], ?[lemma:de], (art), ?+[maj], (prep, art*, ?+[maj])*.
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:embaixada], ?[lemma:de], (art), ?+[maj], (prep, art*, ?+[maj])*, prep[lemma:em], (art), ?+[maj], (prep, art*, ?+[maj])*.

//DCO, 2010/09/23
// Lemas corrigidos
// Arquivo Nacional/Municipal de...
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"arquivo municipal"];?[lemma:"arquivo nacional"], ?[lemma:de], (art), ?+[maj], (prep, art*, ?+[maj])*.
1> ? @= ?[lemma:"arquivo municipal",SEM-Hcollective=+,SEM-Hinstitution=+];?[lemma:"arquivo nacional",SEM-Hcollective=+,SEM-Hinstitution=+].

//DCO, 2010/09/23
// "hospital" não precisa de ser maj
// Hospital de Santa Maria, Hospital dos Capuchos, Hospital Júlio de Matos
//DCO, 2011/05/11
// Hospital Pediátrico de Coimbra
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:hospital], (adj), ?[lemma:de], (art), ?+[maj], (prep, art*, ?+[maj])*.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:hospital], ?+[maj], (prep, art*, ?+[maj])*.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"hospital pediátrico"], ?[lemma:de], (art), ?[maj], (prep[lemma:de] ; art[def] ; ?[maj])*.

//DCO, 2011/05/11
//Centro de saúde de Bragança, centro de saúde local
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"centro de saúde"], (adj), (?[lemma:de], (art), ?[maj]), (prep[lemma:de] ; art[def] ; ?[maj])*.

//DCO, 2010/09/23
// Lema corrigido
// Biblioteca Nacional/Municipal/Pública de...
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:biblioteca];?[lemma:"biblioteca municipal",SEM-Hcollective=+,SEM-Hinstitution=+];?[lemma:"biblioteca pública",SEM-Hcollective=+,SEM-Hinstitution=+], (adj), (?[lemma:de]), (art), ?+[maj], (prep, art*, ?+[maj])*.

//DCO, 2010/09/23
// Lema corrigido
// "bombeiros" não precisa de ser maj
// Bombeiros Voluntários (Sapadores) de...
//DCO, 2011/05/11
//Adicionado "bombeiro" (Bombeiros de Sapadores)
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= (?[lemma:corpo], prep[lemma:de], (art)), ?[lemma:"bombeiro voluntário"];?[lemma:bombeiro], ?[lemma:de], (art), ?+[maj], (prep, art*, ?+[maj])*.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= (?[lemma:corpo], prep[lemma:de], (art)), ?[lemma:"bombeiro voluntário"];?[lemma:bombeiro].

// Silva & Filhos, Lda. / Rubilex S.A.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?+[maj, lemma:~geral], (?[lemma:e];?[symbol]), (punct*[dash], prep*, art*, ?+[maj, lemma:~geral])*, (punct[comma]), ?[lemma:limitada];?[lemma:SA];?[lemma:"Sociedade Anónima"].

//DCO, 2010/09/23
// Aeroporto da Portela, Aeroporto Francisco Sá Carneiro não são entidades HUMANO COLETIVO INSTITUICAO, mas sim entidades do tipo LOCAL CRIADO CONSTRUCAO
// Passou para LGLocation.xip
//1> NOUN[location=+,admin_area=+,building=+] @= ?[lemma:aeroporto], (?[lemma:de]), (art), ?+[maj], (prep, art*, ?+[maj])*.

//DCO, 2010/09/23
// Lema corrigido
// Autoridade Nacional da Palestina
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:"autoridade nacional",SEM-Hcollective=+,SEM-Hadministration=+], prep[lemma:de], (art[def];prep;?[maj])* .

//DCO, 2010/09/23
// "congresso" não precisa de ser maj
// Congresso dos EUA, etc.
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:congresso], (prep), (art), ?+[maj, location, country];?+[gentcountry], (prep, (art), ?+[maj])*.

//DCO, 2010/09/23
// "jornal" e "diário" não precisam de ser maj
// Jornal de Notícías, Diário Económico
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+, SEM-Hjornal=+] @= ?[lemma:jornal], (prep[lemma:de]), (art), ?+[maj].
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+, SEM-Hjornal=+] @= ?[lemma:diário], (prep[lemma:de]), (art), ?+[maj].

// no jornal XXX
1> ? @= ?[lemma:jornal,maj:~], ?[maj,start_collective=+,SEM-Hcollective=+,SEM-Hjornal=+], (prep[lemma:de]; art; ?[maj,end_collective=+])* .

//DCO, 2010/09/23
// Primeira regra corrigida
// NY Times, The Times
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Hjornal=+] @= ?+[maj], ?[lemma:times, maj].
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Hjornal=+] @= ?+[lemma:the], ?[lemma:times, maj].

// Rádio Europa, TV Cultura
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:rádio];?[lemma:TV], (prep[lemma:de]), (art), ?[SEM-Hcollective];?[location] .
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:rádio];?[lemma:TV], (prep[lemma:de]), (art), ?+[maj], (prep, (art), ?+[maj])* .

//DCO, 2010/09/17
// Rádio Regional de Arouca
// Esta regra teve de ser escrita desta maneira, porque o RuDriCo junta "rádio" e "regional" num só token (faz o mesmo a "rádio" + "comercial")
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"rádio regional"], (prep[lemma:de]), (art), ?+[maj].

//DCO, 2010/09/17 (last update: 2010/09/24)
// Rádio Comercial, Rádio Comercial da Linha
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"rádio comercial",SEM-Hcollective=+,SEM-Hinstitution=+], (prep[lemma:de], (art), ?+[maj])*.

//DCO, 2010/09/17
// Antena 1
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:antena], num[dig,frac:~,time:~,t-meas:~].

//DCO, 2010/09/17
// Romântica FM, Cidade FM, Best Rock FM, Horizonte Açores FM
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[maj], ?*[maj], ?[surface:FM];?[surface:fm].

//DCO, 2010/09/17
// M80, M80 Leiria
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:M];?[lemma:m], ?[lemma:80], (?[maj]).

//DCO, 2010/09/17
// 97 FM Pombal
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= num[dig,frac:~,time:~,t-meas:~], ?[surface:FM];?[surface:fm], ?*[location,maj].

//DCO, 2010/09/24
// "hotel", "pousada", etc., não precisam de ser maj
// Example: hotel/Hotel Ritz (de Lisboa), pousada/Pousada do Gerês
// A regra de baixo representa um caso de metonímia (HUMANO COLETIVO INSTITUIÇÃO -> LOCAL) que já se encontra tratado no ficheiro dependency4entit.xip
//3> noun[location=+, extended=+] @= |prep[lemma:em], (art)| ?[lemma:hotel, maj];?[lemma:pousada, maj];?[lemma:residencial, maj];?[lemma:pensão, maj], ?+[maj], (prep, (art), ?+[maj])*.
3> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:hotel];?[lemma:pousada];?[lemma:residencial];?[lemma:pensão], (prep, (art)), ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/24
// Regra corrigida para englobar "restaurante", "bar" e "discoteca" na entidade mencionada
// Example: restaurante Gambrinus (de Lisboa)
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:restaurante];?[lemma:bar];?[lemma:discoteca], ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/24
// "prefeitura" não precisa de ser maj para ser entidade mencionada
// Example: prefeitura do Rio de Janeiro (Brasil), Prefeitura de Natal
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:prefeitura], prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/24
// "museu" não precisa de ser maj para ser entidade mencionada
// Example: Museu do Prado, museu dos Coches
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:museu], (prep[lemma:de]), (art), ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/24
// aqui já faz sentido que seja maj: "ontem fui visitar um museu" (nada); "ontem fui visitar o Museu" (HUMANO COLETIVO INSTITUICAO)
1> ? @= ?[lemma:museu,maj,SEM-Hcollective=+,SEM-Hinstitution=+].

//Vera, 2011/05/03
// Museu Etnográfico do Freixial
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"museu ?+"], prep[lemma:de], (art), ?+[maj].


//DCO, 2010/09/24
// Lema corrigido
// "manager" apagado, porque o chunker não consegue construir NOUN = FOREIGN + PREP + (ART) + ?[maj]
// Identificação a partir de cargos (serve para classificar pessoas)
// Example: diretor(-geral) da McKinsey, treinador do Benfica
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= | ?[lemma:diretor];?[lemma:"director-%+"];?[lemma:CEO];?[lemma:treinador], prep[lemma:de], (art) | ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/13 (last update: 2010/09/24)
// Agora o lema é assembleia-geral, não é preciso apanhar os tokens separados
// "assembleia geral" não precisa de ser maj para ser entidade mencionada
// Example: Assembleia Geral do Benfica
//DCO, 2011/02/23
// Changed ?[SEM-Hinstitution] to ?[SEM-Hcollective]
2> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:assembleia-geral], (prep[lemma:de]), (art), ?[SEM-Hcollective].
2> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:assembleia-geral], (prep[lemma:de]), (art), ?+[maj], (prep, art*, ?+[maj])*.

//DCO, 2010/09/13
// TAP (Air) Portugal, VW Portugal, GM Europa, TEXSA Portuguesa
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[toutmaj, art:~, interj:~], (?[maj]), ?[country];?[continent];?[gentilic, maj].

//DCO, 2010/09/24
// Regra corrigida
// Fábrica da Coca-Cola
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= | ?[lemma:fábrica], prep[lemma:de], (art) | ?+[maj,SEM-Hcollective=+,SEM-Hinstitution=+], (prep*, (?[slash]), (art), ?+[maj])*.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= |?[lemma:fábrica], prep[lemma:de], noun*, (prep[lemma:de], (art)) | ?+[maj,SEM-Hcollective=+,SEM-Hinstitution=+], (prep*, (?[slash]), (art), ?+[maj])*.

// a direção do FC Porto, os dirigentes da Sapatex
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= |?[lemma:direção];?[lemma:direção];?[lemma:dirigente];?[lemma:diretor];?[lemma:gerente], prep[lemma:de], (art)| ?+[maj], (prep, (art), ?+[maj,location:~,admin_area:~])*.

//DCO, 2010/09/24
// Lema e regra corrigidos
// Example: Agência (de Notícias) Lusa
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"agência de notícias",SEM-Hcollective=+,SEM-Hinstitution=+], ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2011/07/15
// aqui já faz sentido que seja maj
// Example: A Procuradoria.
1> ? = ?[lemma:procuradoria,maj,SEM-Hcollective=+,SEM-Hinstitution=+].

//DCO, 2010/09/24
// "fundação" não precisa de ser maj para ser entidade mencionada
// Example: Fundação Oriente, fundação Serralves
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:fundação], ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/24
// Lema corrigido
// Problemas com: "A quarta Feira Internacional de Lisboa" --> o TEMPO vai apanhar "quarta Feira" e pensa que é "quarta-feira" (dia de semana)
// Example: (VII/7.ª/sétima) Feira Internacional (de Lisboa), Feira do Móvel
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= (num[rom];num[ord];?[ord]), ?[lemma:"feira internacional",SEM-Hcollective=+,SEM-Hinstitution=+];?[lemma:feira], (prep[lemma:de], (art)), ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2011/07/15
// A Direcção
1> ? = ?[lemma:direção,maj,SEM-Hcollective=+,SEM-Hadministration=+].

//DCO, 2010/09/24
// Lema corrigido
// Example: Direção Regional de Saúde (do Ambiente)
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:"direção regional"], prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/24
// Lema corrigido e regra melhorada
// Example: Força Portugal, Força Aérea (Portuguesa), Força Aérea dos EUA
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:"Força Aérea",SEM-Hcollective=+,SEM-Hadministration=+], (?[gentcountry]);(prep[lemma:de], (art), ?[country]).

//DCO, 2011/07/15 traço corrigido
// Example: Forças de Defesa
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:força,maj], (prep[lemma:de]), ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/26
// O Estado (português)/(de Portugal)
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:estado,maj,location:~,admin_area:~,division:~,SEM-Hcollective=+,SEM-Hadministration=+], (?[gentcountry]);(prep[lemma:de], (art), ?[country]).

// devia estar no léxico (já está lexOrg.xip)
//1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= (?[maj,art:~]), ?[lemma:estado, maj];?[lemma:governo, maj];?[lemma:parlamento, maj];?[lemma:senado, maj];?[lemma:knesset, maj];
//?[lemma:duma, maj];?[lemma:congresso, maj];?[lemma:conselho, maj];?[lemma:instituição, maj];?[lemma:estados, maj];?[lemma:comissão, maj];?[lemma:pentágono, maj]
//, (adj;noun), (prep[lemma:de], ?[location]).

//DCO, 2010/09/24
// Regras corrigidas
// Example: fundador da Rubilex, A Rubilex foi fundada
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= |?[lemma:fundador], prep[lemma:de], (art)| ?+[maj,SEM-Hcollective=+,SEM-Hinstitution=+], (prep, (art), ?+[maj,location:~,admin_area:~])*.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?+[maj,art:~,SEM-Hcollective=+,SEM-Hinstitution=+], (prep, (art), ?+[maj])* | ?[lemma:ser], ?[lemma:fundar] |.

//DCO, 2011/05/11
// Governo (com maiúscula)
1> ? = noun[lemma:governo,maj,SEM-Hcollective=+,SEM-Hadministration=+].

//DCO, 2011/05/11
// Estado (com maiúscula)
//DCO, 2011/07/15 esta regra não está a ser corrida, não percebo porquê
//1> ? = noun[lemma:Estado,maj,SEM-Hcollective=+,SEM-Hadministration=+].

//DCO, 2010/08/19
// Governo de Portugal, coroa da Dinamarca, parlamento de Inglaterra, executivo de Angola
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:governo];?[lemma:coroa];?[lemma:parlamento];?[lemma:executivo], prep[lemma:de], (art), ?[location].

//DCO, 2010/09/21
// Governo português, coroa dinamarquesa, parlamento inglês, executivo angolano, ...
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:governo];?[lemma:coroa];?[lemma:parlamento];?[lemma:executivo], ?[gentcountry].

//DCO, 2010/09/21
// Poder executivo/legislativo/judicial de Portugal
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:"poder executivo"];?[lemma:"poder legislativo"];?[lemma:"poder judicial"], prep[lemma:de], (art), ?[location].

//DCO, 2010/09/21
// Poder executivo/legislativo/judicial português
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:"poder executivo"];?[lemma:"poder legislativo"];?[lemma:"poder judicial"], ?[gentcountry].

// Example: Companhia das Lezírias
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:companhia, maj], prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/10/01
// Example: Companhia Nacional de Bailado, Companhia Nacional de Canto e Dança de Moçambique
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"companhia nacional",maj], prep[lemma:de], (art), ?+[maj], (conj, ?+[maj], prep[lemma:de], (art), ?+[maj])*.

// Example: Armada Inglesa, Exército de Libertação Popular
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:exército, maj];?[lemma:armada], (prep[lemma:de]), (art), ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/24
// Example: A Presidência.
1> ? @= ?[lemma:presidência,maj,SEM-Hcollective=+,SEM-Hadministration=+].

//DCO, 2010/10/03
// Example: A presidência portuguesa
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:presidência], ?[gentcountry].

//DCO, 2010/09/13
// "Consultora X" é INSTITUIÇÃO e não ADMINISTRAÇÃO
// O lema para "Consultora" é "consultor", e não "consultora"
// Example: A Consultoria McKinsey
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:consultor];?[lemma:consultoria], ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/24
// Regra corrigida
// Example: PT de Portugal
//          Sporting da Covilhã
//DCO, 2011/03/20
// The preposition must be "de"
//DCO, 2011/07/15 regra comentada
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[SEM-Hinstitution], prep[lemma:"de"], (art), ?[location,maj].

//DCO, 2011/02/04
// Casa do Benfica da Beira Litoral
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:casa], prep[lemma:de], (art), ?[SEM-Hinstitution], (prep, (art), ?[location,maj])*.

// a A & B (quase sempre é collective)
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= |art[fem]| noun[maj], ?[symbol], noun[maj].

//DCO, 2010/09/24
// Verificado!
// o site da Amazon
1> ?  @= | ?[lemma:site], prep[lemma:de], (art) | ?+[maj,SEM-Hpeople:~,SEM-Hcollective=+,SEM-Hinstitution=+,start_collective=+], (prep, (art), ?+[maj,end_collective=+])*.

//DCO, 2010/09/24
// Regra corrigida
// editado, distribuido, comercializado pela X
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= | ?[lemma:editar,pastpart];?[lemma:distribuir,pastpart];?[lemma:comercializar,pastpart], prep[lemma:por], art[fem] | ?+[maj,SEM-Hcollective=+,SEM-Hinstitution=+,start_collective=+], (prep, (art), ?+[maj,end_collective=+])*.

//DCO, 2010/09/24
// Regras corrigidas
// Atentados do IRA, atentados reinvindicados pela Al-Qaeda
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= | ?[lemma:atentado], ?[lemma:];?[lemma:reinvindicar, pastpart], prep[lemma:por], (art)| ?+[maj,SEM-Hcollective=+,SEM-Hinstitution=+,start_collective=+], (prep, (art), ?+[maj,end_collective=+])*.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= | ?[lemma:atentado], prep[lemma:de], (art) | ?+[maj,SEM-Hcollective=+,SEM-Hinstitution=+,start_collective=+], (prep, (art), ?+[maj,end_collective=+])*.

//DCO, 2010/09/24
// "centro" não precisa de ser maj para que tudo seja uma entidade mencionada
// Centro Português de Cardiologia
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:centro], ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/24
// Regra corrigida
// "tribunal" e "supremo" não precisam de ser maj para que tudo seja uma entidade mencionada
// Tribunal da Boa Hora
// Tribunal Criminal de 1ª instância
// Tribunal Constitucional de Portugal
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:tribunal];?[lemma:"tribunal criminal"];?[lemma:"tribunal constitucional"], (prep[lemma:de]), (art), (num), ?[lemma:instância];?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/24
// e.g Supremo Tribunal de Justiça (de Portugal)
1> noun[SEM-Hcollective=+,SEM-Hadministration=+] @= ?[lemma:"Supremo Tribunal de Justiça",SEM-Hcollective=+,SEM-Hadministration=+], (prep[lemma:de], (art), ?[country]).

//DCO, 2010/09/24
// Regra corrigida - são entidades HUMANO COLECTIVO INSTITUICAO, e não HUMANO COLECTIVO ADMINISTRACAO
// Example: Polícia de Lisboa
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:polícia], prep[lemma:de], (art), ?+[maj], (prep, (art), ?+[maj])*.

//DCO, 2010/09/24
// Example: A Polícia (Estadual)
1> ? = ?[lemma:polícia,maj,SEM-Hcollective=+,SEM-Hinstitution=+];?[lemma:"polícia estadual",maj,SEM-Hcollective=+,SEM-Hinstitution=+].

//DCO, 2010/09/24
// Regra corrigida
// Movimento de Resistência Islâmica, Movimento Popular de Libertação de Angola
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:movimento,maj];?[lemma:"movimento popular",maj], (prep[lemma:de]), (art), (?+[maj]), ((?[lemma:e]), prep*, (art), ?+[maj])*.

//DCO, 2010/09/24
// "banco" não precisa de ser maj para que tudo seja entidade mencionada
// Example: Banco Popular, Banco Espírito Santo, Banco Bilbau e Viscaia
//DCO, 2011/05/10
//Adicionado adjectivo, e.g. "banco público"
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:banco], (adj), (prep[lemma:de]), (art), (?+[maj]), ((?[lemma:e]), prep*, (art), ?+[maj])*.

//DCO, 2011/05/11
//Banco Central da China
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"banco central"], (adj[gentcountry]), (prep[lemma:de]), (art), (?+[maj]), ((?[lemma:e]), prep*, (art), ?+[maj])*.


//DCO, 2010/09/24
// Example: O Banco.
1> ? = ?[lemma:banco,maj,SEM-Hcollective=+,SEM-Hinstitution=+].

//DCO, 2010/09/24
// Regra corrigida
// Example: A PT, empresa de comunicações; A PT, a maior empresa de comunicações de Portugal; etc.

//DCO, 2010/09/24
// Regra corrigida
// Example: um empregado/funcionário/técnico/trabalhador da...
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= | ?[lemma:empregado];?[lemma:funcionário];?[lemma:trabalhador];?[lemma:técnico], prep[lemma:de], (art) | ?+[maj,SEM-Hpeople:~,SEM-Hcollective=+,SEM-Hinstitution=+,start_collective=+], ((?[lemma:e]), prep*, (art), ?+[maj,end_collective=+,location:~,admin_area:~])*  .

//DCO, 2010/09/24
// Regra corrigida
// Example: licenciado/graduado/doutorado pela....
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= | ?[lemma:licenciado];?[lemma:doutorado];?[lemma:graduado], prep[lemma:por], (art) | ?+[maj,SEM-Hcollective=+,SEM-Hinstitution=+,start_collective=+], ((?[lemma:e]), prep*, (art), ?+[maj,end_collective=+])*  .

// Example: University of Harvard, Harvard University
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Huniversity=+] @= ?[lemma:university, maj], ?[lemma:of], ?+[maj], (prep*, (art), ?+[maj])*  .
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Huniversity=+] @=  ?+[maj], (prep*, (art), ?+[maj])* , ?[lemma:university] .

// Escuteiros, Corpo Nacional de Escutas
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:corpo], ?[lemma:nacional], prep[lemma:de], ?[lemma:escuta, maj].

//DCO, 2010/09/24
// Example: Os Escuteiros.
1> ? @= ?[lemma:escuteiro,maj,SEM-Hcollective=+,SEM-Hinstitution=+].

//DCO, 2010/09/24
// Seguradora X
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:seguradora,maj], ?+[maj], (prep*, (art), ?+[maj])*.
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= |?[lemma:seguradora]| ?+[maj], (prep*, (art), ?+[maj])*.

//DCO, 2011/04/04
// Regras de desambiguação INSTITUTION vs. LOCATION
//1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"A."], ?[lemma:"F."], (prep[lemma:de]), (art[def]), noun[location].

//DCO, 2011/04/04
//Casa do Benfica em Barcelos
//Casa do Benfica na Ilha do Faial
//Casa do Benfica no Bombarral
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"Casa do Benfica"], prep[lemma:"em"];prep[lemma:"de"], (art[def]), ?[location].

//DCO, 2011/04/04
//Filial Nº 00 - Sport Laulara e Benfica .
//Filial Nº 0  - Sport Macau e Benfica .
//Filial Nº 1  - Sport Faro e Benfica .
//Filial Nº 2  - Sport Abrantes e Benfica .
//Filial Nº 3  - Sport Viseu e Benfica .
//Filial Nº 4  - Sport Tortosendo e Benfica .
//Filial Nº 10 - Sport Castanheira de Pera e Benfica .
//Filial Nº 8  - Sport Lagos e Benfica .
//Filial Nº 9  - Sport Nisa e Benfica .
//Filial nº 12 - Sport Algoz e Benfica .
//Filial Nº 14 - Sport Arronches e Benfica .
//Filial Nº 16 - Sport Nelas e Benfica .
//Filial Nº 17 - Sport Alenquer e Benfica .
//Filial Nº 18 - Sport Fuseta e Benfica .
//Filial Nº 21 - Sport Newark e Benfica .
//Filial Nº 22 - Sport London e Benfica .
//Filial Nº 24 - Sport Vila Real e Benfica .
//Filial Nº 25 - Sport Montreal e Benfica .
//Filial Nº 26 - Sport Luxemburgo e Benfica .
//Filial Nº 27 - Sport Lausanne e Benfica .
//Filial Nº 28 - Sport Beauregard e Benfica .
//Filial Nº 29 - Sport Bissau e Benfica .

//Filial Nº 6  - Sport Lisboa e Évora .
//Filial Nº 11 - Sport Lisboa e Cartaxo .
//Filial Nº 23 - Sport Lisboa e Olivais .

//Filial nº 7 - Sport Benfica e Castelo Branco .
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[surface:"Sport"], ?[maj], (prep, (art[def]), ?[maj]), conj[lemma:"e"], ?+[maj].

//DCO, 2011/04/04
//assembleia do Benfica
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"assembleia"], prep[lemma:de], (art[def]), ?[SEM-Hcollective,SEM-Hinstitution].

//DCO, 2011/04/04
// S. C. Olhanense
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[surface:"S. C."];?[surface:"S.C."];?[surface:"SC"];?[surface:"A. C."];?[surface:"A.C."];?[surface:"AC"], ?[SEM-Hcollective,SEM-Hinstitution,SEM-sport-club].

//DCO, 2011/04/04
//Sporting C. Olhanense
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[lemma:"Sporting"], ?[lemma:"%C."], ?[SEM-Hcollective,SEM-Hinstitution,SEM-sport-club].

//DCO, 2011/04/04
// Torino A. C.
1> noun[SEM-Hcollective=+,SEM-Hinstitution=+] @= ?[SEM-Hcollective,SEM-Hinstitution,SEM-sport-club], ?[surface:"S. C."];?[surface:"S.C."];?[surface:"SC"];?[surface:"A. C."];?[surface:"A.C."];?[surface:"AC"].

//DCO, 2011/04/04
//"o" Vitória -> clube
//DCO, 2011/04/04
// Added "O" Vitória ...
1> ? @= art[surface:"o"];art[surface:"O"], noun[surface:"Vitória",SEM-Hcollective=+,SEM-Hinstitution=+,SEM-sport-club=+].

// ADDED CH
// XXX Airways, XWERWER Lda. are institution names
2> noun[SEM-Hcollective=+,SEM-Hinstitution=+] = ?[maj], (prep; art[def], ?[maj])*, ?[SEM-Hcollective,SEM-Hinstitution,sufixo_em].

// ADDED NJM
// XXX, Lda. are institution names, if XXX is a institution name
// (the LexOrg.xip file assumes this rule is working)
//
2> noun[SEM-Hcollective=+,SEM-Hinstitution=+] = ?[SEM-Hcollective,SEM-Hinstitution];?[brand], (Punct[comma])*,
                           noun[lemma:"Sociedade Anónima"];noun[lemma:"SA"];
                           adj[lemma:"limitado"];
                           adj[lemma:"incorporated"];
                           adj[lemma:"limitado"];
                           noun[lemma:"empresa pública"];
                           noun[lemma:"corporação"];
                           noun[lemma:"CRL"].

2> noun[SEM-Hcollective=+,SEM-Hinstitution=+] = ?[SEM-Hcollective,SEM-Hinstitution], (Punct[comma])*, ?[lemma:"S"], ?[lemma:"A"].


// ADDED CH
// political party followed by place
// PSD/Vila Real
2> noun[SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Hpartido=+] @= noun[lemma:"PS(D)%p%C%c+"], ?[maj] .
2> noun[SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Hpartido=+] @= noun[lemma:"PC(P)%p%C%c+"], ?[maj] .
2> noun[SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Hpartido=+] @= noun[lemma:"CD[SU]%p%C%c+"], ?[maj] .
// PSD-Lisboa
2> ? @= noun[lemma:"PS(D)%p%C%c+",SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Hpartido=+] .
2> ? @= noun[lemma:"PC(P)%p%C%c+",SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Hpartido=+] .
2> ? @= noun[lemma:"CD[SU]%p%C%c+",SEM-Hcollective=+,SEM-Hinstitution=+,SEM-Hpartido=+] .



