//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : dependency7.xip                                             !
//!   Date       : 2006/October                                                !
//!   Author     : Caroline Hagege, Nuno Mamede                                !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!   Dependency file treating and correcting some SUBJ-CDIR-MOD dependencies  !
//!                                                                            !
//!   Dependencies handled in this file:  SUBJ-OBJ Adjustements                !
//!                                                                            !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


DependencyRules:


////////////////////////////////////////////////////////////////////////////////
//
// VCOP tudo AP/VCPART
//
////////////////////////////////////////////////////////////////////////////////

//NJM+JB, 2011/01/19
// When Pron 'tudo' is an inverted subject of an adjectival clause (to avoid it to be considered CDIR of the VCOP)
//
// Example: O Pedro disse que estava tudo doente.
// Example: estava tudo doente.
//                        -> PREDSUBJ (estava,doente)
//                        -> SUBJ_POST(estava,tudo)
//                        -> ATTRIB(tudo,doente)
// ELIMINADA PELA REGRA   -> MOD(tudo,doente)
//
| #1{VERB#2[cop];PASTPART#2[cop], ?-};SC#1{?*,?{VERB#2[cop];PASTPART#2[cop]}}, (ADVP), NP#5, (ADVP), NP#3;AP#3 |
 if ( ^MOD(#6,#4) & HEAD(#4,#3) & HEAD(#6[lemma:tudo],#5) )
   ~


////////////////////////////////////////////////////////////////////////////////
//
// CONTROL VERBS and INFINITIVES
//
////////////////////////////////////////////////////////////////////////////////

// Verbs bearing feature s_np_inf followed by NP and by infinitive
// In this case NP is dir object of the verb and subject of the infinitive
// And the infinitive is a direct object of the main verb.
//
// Example: Deixou o Pedro comer a sopa.
//
| ?[verb]{?*,#1[last,s_np_inf]}, NP#3, VINF{?*,#2[last]} |
if ( CDIR[post](#1,#4) & ~SUBJ(#2,?) & ~PREDSUBJ(#1,#2) & HEAD(#4,#3) )
   SUBJ[pre=+](#2,#4),
   CDIR[post=+,inf=+](#1,#2).


////////////////////////////////////////////////////////////////////////////////
//
// CORRECT PROBLEMS WITH INFINITIVES
//
////////////////////////////////////////////////////////////////////////////////

// 1st part of the rule
// Correcting erroneous SUBJ[inf] when there is a bad chunking
// PARA O PEDRO is taken as a PP
//
// It was also generating the bad dependency SUBJ[post,inf](vim,comer)
// that this 2 rules are correcting.
//
// Example: Vim para o Pedro poder comer o bolo.
//
| PP#1 |
if ( SUBJ[pre](#3[inf],#2) & ^SUBJ[post,inf](#4,#3) & HEAD(#2,#1) )
MOD[features=~,post=+,inf=+](#4,#3)

// 2nd part of the rule
// We also have to delete the erroneous dependency MOD involving the bad constructed PP
//
// Example:  Vim para o Pedro comer o bolo
// we delete MOD(vim,Pedro)
//
| PP#1 |
if ( SUBJ[pre](#3[inf],#2) & MOD[post,inf](#4,#3) & ^MOD[post](#4,#2) & HEAD(#2,#1) )
~



////////////////////////////////////////////////////////////////////////////////
//
// ASSIGN ADVERBIAL MODIFIERS TO THE SENTENCE (TOP node)
//
////////////////////////////////////////////////////////////////////////////////

| #4[cat:0]{pp#2;advp#2, ?[comma]} |
 if ( HEAD(#3,#2[inquote:~]) & ~mod(#4,#3) & ~PREDSUBJ(#4,#3) & ~QUANTD(?,#3) & ~CLASSD(#3,?) )
   MOD(#4, #3)