//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//! --- CONTENTS ---                                                           !
//!   Project    : Portuguese XIP grammar                                      !
//!   filename   : dependencyFPhrase.xip                                       !
//!   Date       : 2010/July                                                   !
//!   Author     : Nuno Mamede                                                 !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!                                                                            !
//!   Dependencies handled in this file:  FIXED                                !
//!                                                                            !
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


DependencyRules:

////////////////////////////////////////////////////////////////////////////////
//
// Fixed phrases
//
////////////////////////////////////////////////////////////////////////////////


//========================================================
// cantar <E> vitória            ->  FIXED(cantar,vitória)
//========================================================

// Example: O Luís cantou vitória
//
if ( VDOMAIN(?,#2[lemma:cantar]) & CDIR[post](#2,#3[lemma:vitória]) )
  FIXED(#2,#3)

//because POS is not perfect :-)))))
if ( VDOMAIN(?,#2[lemma:cantar]) & MOD[post](#2,#3[lemma:vitória]) & PREPD(#3,?[lemma:a]) )
  FIXED(#2,#3)


//========================================================
// carregar <Poss> cruz           ->  FIXED(carregar,cruz)
//========================================================

// Example: Cada um carrega a sua cruz
//
if ( VDOMAIN(?,#2[lemma:carregar]) & MOD[post](#2,#3[lemma:cruz]) & POSS(#3,?) )
  FIXED(#2,#3)


//========================================================
// não valer um chavo           ->  FIXED(valer,chavo,não)
//========================================================

// Example: Tu não vales um chavo
//
if ( VDOMAIN(#1,#2[lemma:valer]) & CDIR[post](#2,#3[lemma:chavo]) & QUANTD(#3,?[lemma:um]) & MOD[neg,pre](#1,#5[lemma:não]) )
  FIXED(#2,#3,#5)


//========================================================
// correr as capelinhas       ->  FIXED(correr,capelinhas)
//========================================================

// Example: A Ana corre as capelinhas
//
if ( VDOMAIN(?,#2[lemma:correr]) & CDIR[post](#2,#3[surface:capelinhas]) & DETD(#3,?[lemma:o,pl]) & ~QUANTD[post](#3,#5[surface:todas]) )
  FIXED(#2,#3)


//========================================================
// correr as capelinhas todas    ->  FIXED(correr,capelinhas,todas)
//========================================================

// Example: O Paula corre as capelinhas todas
//
if ( VDOMAIN(?,#2[lemma:correr]) & CDIR[post](#2,#3[surface:capelinhas]) & DETD(#3,?[lemma:o,pl]) & QUANTD[post](#3,#5[surface:todas]) )
  FIXED(#2,#3,#5)


//========================================================
// Chorar <E> lágrimas de crocodilo  ->  FIXED(chorar,lágrimas de crocodilo)
//========================================================

// Example: O Pedrinho chora lágrimas de crocodilo
//
if ( VDOMAIN(?,#2[lemma:chorar]) & CDIR[post](#2,#3[lemma:lágrimas-de-crocodilo,pl]) )
  FIXED(#2,#3)


//========================================================
// passar as passas do Algarve    ->  FIXED(passar,passas,Algarve)
//========================================================

// Example: Ele passou as passas do Algarve
//
if ( VDOMAIN(?,#2[lemma:passar]) & CDIR[post](#2,#3[surface:passas]) & DETD(#3,?[lemma:o,pl]) &
     MOD[post](#3,#5[lemma:Algarve]) & PREPD(#5,?[lemma:de]) )
  FIXED(#2,#3,#5)


//========================================================
// passar as passinhas do Algarve   ->  FIXED(passar,passinhas,Algarve)
//========================================================

// Example: O Pedro passou as passinhas do Algarve
//
if ( VDOMAIN(?,#2[lemma:passar]) & CDIR[post](#2,#3[surface:passinhas]) & DETD(#3,?[lemma:o,pl]) &
     MOD[post](#3,#5[lemma:Algarve]) & PREPD(#5,?[lemma:de]) )
  FIXED(#2,#3,#5)


//========================================================
// comer sopas de cavalo cansado   ->  FIXED(comer,sopas,cavalo,cansado)
//========================================================

// Example: Comeste sopas de cavalo cansado
//
if ( VDOMAIN(?,#2[lemma:comer]) & CDIR[post](#2,#3[surface:sopas])  &
     MOD[post](#3,#5[lemma:cavalo]) & PREPD(#5,?[lemma:de]) &
     ( MOD[post](#5,#6[lemma:cansar,pastpart]) || MOD[post](#5,#6[lemma:cansado,adj]) ) )
  FIXED(#2,#3,#5,#6)


//========================================================
// dar-se ares de importância    ->  FIXED(dar,ares,importância)
//========================================================

// Example: Ela dá-se ares de importância
//
if ( VDOMAIN(?,#2[lemma:dar]) & CDIR[post](#2,#3[surface:ares])  & CLITIC[post](#2,?[ref,lemma:eu]) &
     MOD[post](#3,#5[lemma:importância]) & PREPD(#5,?[lemma:de]) )
  FIXED(#2,#3,#5)

// esta regra não funciona - o pronome reflexo sai como CDIR


//========================================================
// N-hum ganha corpo              ->  FIXED(ganhar,corpo)
//========================================================

// Example: O projeto ganhou corpo rapidamente
//
if ( VDOMAIN(?,#2[lemma:ganhar]) & CDIR[post](#2,#3[lemma:corpo]) & SUBJ(#2,#4[human:~]) )
  FIXED(#2,#3)


//========================================================
// abanar o esqueleto          ->  FIXED(abanar,esqueleto)
// abanar o capacete            ->  FIXED(abanar,capacete)
//========================================================

// Example: A Maria abanou o esqueleto a noite toda
// Example: O Paulo abana o capacete
//
if ( VDOMAIN(?,#2[lemma:abanar]) &
     ( CDIR[post](#2,#3[lemma:esqueleto]) || CDIR[post](#2,#3[lemma:capacete]) ) &
     DETD(#3,?[lemma:o])
   )
  FIXED(#2,#3)


//========================================================
// desanuviar o ambiente    ->  FIXED(desanuviar,ambiente)
// descontrair o ambiente   ->  FIXED(descontrair,ambiente)
//========================================================

// Example: A anedota do Pedro desanuviou o ambiente
// Example: A história da Paula descontraiu o ambiente
//
if ( (VDOMAIN(?,#2[lemma:desanuviar]) || VDOMAIN(?,#2[lemma:descontrair]) ) &
     CDIR[post](#2,#3[lemma:ambiente]) & DETD(#3,?[lemma:o]) )
  FIXED(#2,#3)


//========================================================
// arreganhar os dentes       ->  FIXED(arreganhar,dentes)
//========================================================

// Example: O Pedro arreganhou os dentes
//
if ( VDOMAIN(?,#2[lemma:arreganhar]) & CDIR[post](#2,#3[lemma:dente,pl]) & DETD(#3,?[lemma:o,pl]) )
  FIXED(#2,#3)


//========================================================
// arreganhar a tacha          ->  FIXED(arreganhar,tacha)
//========================================================

// Example: A Manuela arreganhou a tacha
//
if ( VDOMAIN(?,#2[lemma:arreganhar]) & CDIR[post](#2,#3[lemma:tacha]) & DETD(#3,?[lemma:o]) )
  FIXED(#2,#3)

//because POS is not perfect :-)))))
if ( VDOMAIN(?,#2[lemma:arreganhar]) & MOD[post](#2,#3[lemma:tacha]) & PREPD(#3,?[lemma:a]) )
  FIXED(#2,#3)


//========================================================
// dar o nó                    ->  FIXED(CASAR,dar,nó)
//========================================================

// Example: O Paulo e a Carina deram o nó.
// Example: O Paulo deu o nó com a Carina.
//
if ( VDOMAIN(?,#2[lemma:dar]) & CDIR[post](#2,#3[lemma:nó]) &
     (SUBJ(#2,?[human]) || (PREPD(#4[human],?[lemma:com]) & MOD[post](#2,#4[human])) )
   )
  FIXED[normalized=+](##verb[surface="casar",lemma="casar"],#2,#3)


//========================================================
// juntar os trapos            ->  FIXED(CASAR,juntar,trapos)
// juntar os trapinhos         ->  FIXED(CASAR,juntar,trapinhos)
//========================================================

// Example: O Paulo e a Carina juntaram os trapos.
// Example: O Paulo juntou os trapos com a Carina.
//
if ( VDOMAIN(?,#2[lemma:juntar]) & (CDIR[post](#2,#3[lemma:trapo]) || CDIR[post](#2,#3[surface:trapinhos])) &
     (SUBJ(#2,?[human]) || (PREPD(#4[human],?[lemma:com]) & MOD[post](#2,#4[human])) )
   )
    FIXED[normalized=+](##verb[surface="casar",lemma="casar"],#2,#3)


//========================================================
// enterrar a vida de solteiro  ->  FIXED(CASAR,enterrar,vida,solteiro)
//========================================================

// Example: O Paulo enterrou a vida de solteiro
//
if ( VDOMAIN(?,#2[lemma:enterrar]) & CDIR[post](#2,#3[lemma:vida]) & MOD[post](#3,#4[lemma:solteiro]) & PREPD(#4,?[lemma:de]) )
    FIXED[normalized=+](##verb[surface="casar",lemma="casar"],#2,#3,#4)


//========================================================
// entrar no clube dos casados      ->  FIXED(CASAR,entrar,clube,casados)
// passar para o clube dos casados  ->  FIXED(CASAR,passar,clube,casados)
//========================================================

// Example: O Paulo passou para o clube dos casados
//
if ( (VDOMAIN(?,#2[lemma:entrar]) || VDOMAIN(?,#2[lemma:passar]) ) & MOD[post](#2,#3[lemma:clube]) & MOD[post](#3,#4[lemma:casado]) & PREPD(#4,?[lemma:de]) )
    FIXED[normalized=+](##verb[surface="casar",lemma="casar"],#2,#3,#4)


//========================================================
// bater a bota                  ->  FIXED(MORRER,bater,bota)
// bater a caçoleta              ->  FIXED(MORRER,bater,caçoleta)
//========================================================

// Example: O trabalhador bateu a bota
//
if ( VDOMAIN(?,#2[lemma:bater]) &
     (CDIR[post](#2,#3[lemma:bota]) || CDIR[post](#2,#3[lemma:caçoleta]) ) &
     DETD(#3,?[lemma:o])
   )
    FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3)


//========================================================
// esticar o pernil              ->  FIXED(MORRER,esticar,pernil)
//========================================================

// Example: A Manuela esticou o pernil
//
if ( VDOMAIN(?,#2[lemma:esticar]) & CDIR[post](#2,#3[lemma:pernil]) & DETD(#3,?[lemma:o]) )
  FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3)


//========================================================
// ir desta para melhor         ->  FIXED(MORRER,ir,desta,melhor)
//========================================================

// Example: A Manuela foi desta para melhor
//
if ( VDOMAIN(?,#2[lemma:ir]) &
     MOD[post](#2,#3[lemma:este]) & PREPD(#3,?[lemma:de]) &
     (MOD[post](#3,#4[lemma:bem]) || MOD[post](#3,#4[lemma:bom])) & PREPD(#4,?[lemma:para])
   )
  FIXED(##verb[surface="morrer",lemma="morrer"],#2,#3,#4)

//because lemma disambiguation is not perfect :-)))))
if ( VDOMAIN(?,#2[lemma:ser]) &
     PREDSUBJ(#2,#3[lemma:este]) & PREPD(#3,?[lemma:de]) &
     (MOD[post](#3,#4[lemma:bem]) || MOD[post](#3,#4[lemma:bom])) & PREPD(#4,?[lemma:para])
   )
  FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3,#4)


//========================================================
// dar o (seu) último suspiro     ->  FIXED(MORRER,dar,último,suspiro)
// soltar o (seu) último suspiro  ->  FIXED(MORRER,soltar,último,suspiro)
//========================================================

// Example: A Manuela deu o (seu) último suspiro de noite
//
if ( (VDOMAIN(?,#2[lemma:dar]) || VDOMAIN(?,#2[lemma:soltar]) ) &
     CDIR[post](#2,#3[lemma:suspiro]) & DETD(#3,?[lemma:o]) &
     MOD[pre](#3,#4[lemma:último])
   )
  FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3,#4)


//========================================================
// dizer adeus ao mundo       ->  FIXED(MORRER,dizer,adeus,mundo)
//========================================================

// Example: Ontem, a Manuela disse adeus ao mundo
//
if ( VDOMAIN(?,#2[lemma:dizer]) &
     CDIR[post](#2,#3[lemma:adeus]) &
     MOD[post](#2,#4[lemma:mundo])
   )
  FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3,#4)


//========================================================
// entregar a alma ao Criador/Senhor   ->  FIXED(MORRER,entregar,alma,criador)
// dar a alma ao Criador/Senhor        ->  FIXED(MORRER,dar,alma,criador)
//========================================================

// Example: Ontem, a Manuela entregou a alma ao criador
//
if ( (VDOMAIN(?,#2[lemma:entregar]) || VDOMAIN(?,#2[lemma:dar]) || VDOMAIN(?,#2[lemma:encomendar])) &
     CDIR[post](#2,#3[lemma:alma]) &
     (MOD[post](#2,#4[lemma:criador]) || MOD[post](#2,#4[lemma:senhor]))
   )
  FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3,#4)

//========================================================
// ir para os anjinhos              ->  FIXED(MORRER,ir,anjinhos)
// ir para o céu                    ->  FIXED(MORRER,ir,céu)
// ir para as cucuias               ->  FIXED(MORRER,ir,cucuias)
//========================================================

// Example: A Manuela foi para os anjinhos
// Example: A Manuela foi para o céu
// Example: A Manuela foi para as cucuias
//
if ( VDOMAIN(?,#2[lemma:ir]) &
     ( MOD[post](#2,#3[surface:anjinhos]) || MOD[post](#2,#3[lemma:céu]) || MOD[post](#2,#3[lemma:cucuia]) ) &
     PREPD(#3,?[lemma:para])
   )
   FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3)

//because lemma disambiguation is not perfect :-)))))
if ( VDOMAIN(?,#2[lemma:ser]) &
     ( MOD[post](#2,#3[surface:anjinhos]) || MOD[post](#2,#3[lemma:céu]) || MOD[post](#2,#3[lemma:cucuia]) ) &
     PREPD(#3,?[lemma:para])
   )
   FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3)


//========================================================
// ir para o lado de lá (ambiguous)   ->  FIXED(MORRER,ir,lado,lá)
//========================================================

// Example: Ontem, a Manuela foi para o lado de lá
//
//if ( VDOMAIN(?,#2[lemma:ir]) & MOD[post](#2,#3[lemma:lado]) & DETD(#3,?[lemma:o]) & MOD[post](#3,#4[lemma:lá]) )
//    FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3,#4)

//because lemma disambiguation is not perfect :-)))))
//if ( VDOMAIN(?,#2[lemma:ser]) & MOD[post](#2,#3[lemma:lado]) & DETD(#3,?[lemma:o]) & MOD[post](#3,#4[lemma:lá]) )
//    FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3,#4)


//========================================================
// ir para o outro lado  (ambiguous)   ->  FIXED(MORRER,ir,outro,lado)
//========================================================

// Example: Ontem, a Manuela foi para o outro lado
//
//if ( VDOMAIN(?,#2[lemma:ir]) & MOD[post](#2,#3[lemma:lado]) & QUANTD(#3,?[lemma:outro]) )
//    FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3,#4)

//because lemma disambiguation is not perfect :-)))))
//if ( VDOMAIN(?,#2[lemma:ser]) & MOD[post](#2,#3[lemma:lado]) & QUANTD(#3,?[lemma:outro]) )
//    FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3,#4)


//========================================================
// ir para o outro mundo         ->  FIXED(MORRER,ir,outro mundo)
// passar para o outro mundo     ->  FIXED(MORRER,passar,outro mundo)
// partir para o outro mundo     ->  FIXED(MORRER,partir,outro mundo)
//========================================================

// Example: Ontem, a Manuela foi para o outro mundo
// Example: Ontem, a Manuela passou para o outro mundo
// Example: Ontem, a Manuela partiu para o outro mundo
//
if ( (VDOMAIN(?,#2[lemma:ir]) || VDOMAIN(?,#2[lemma:passar]) || VDOMAIN(?,#2[lemma:partir]) ) &
     MOD[post](#2,#3[lemma:"outro mundo"]) & PREPD(#3,?[lemma:para])
   )
    FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3)

//because lemma disambiguation is not perfect :-)))))
if ( VDOMAIN(?,#2[lemma:ser]) & MOD[post](#2,#3[lemma:"outro mundo"]) & PREPD(#3,?[lemma:para]) )
  FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3)


//========================================================
// ir para a quinta das tabuletas     ->  FIXED(MORRER,ir,quinta das tabuletas)
// ir para o jardim das tabuletas     ->  FIXED(MORRER,ir,jardim das tabuletas)
// ir para a cidade dos pés juntos    ->  FIXED(MORRER,ir,cidade dos pés juntos)
//========================================================

// Example: Ontem, a Manuela foi para a quinta das tabuletas
// Example: Ontem, a Manuela foi para o jardim das tabuletas
// Example: Ontem, a Manuela foi para a cidade dos pés juntos
//
if ( (VDOMAIN(?,#2[lemma:ser]) || VDOMAIN(?,#2[lemma:ir]) ) &
     (MOD[post](#2,#5[lemma:"quinta das tabuletas"]) || MOD[post](#2,#5[lemma:"jardim das tabuletas"]) || MOD[post](#2,#5[lemma:"cidade dos pés juntos"])) &
     PREPD(#5,?[lemma:para])
   )
  FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#5)


//========================================================
// descer à sepultura          ->  FIXED(MORRER,descer,sepultura)
// descer ao túmulo            ->  FIXED(MORRER,descer,túmulo)
//========================================================

// Example: Ontem, a Manuela desceu à sepultura
// Example: Ontem, a Manuela desceu ao túmulo

if ( VDOMAIN(?,#2[lemma:descer]) &
     (MOD[post](#2,#3[lemma:sepultura]) || MOD[post](#2,#3[lemma:túmulo]) ) &
     PREPD(#3,?[lemma:a])
   )
    FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3)


//========================================================
// adormecer na paz do Senhor   ->  FIXED(MORRER,adomecer,paz,senhor)
// descansar na paz do Senhor   ->  FIXED(MORRER,descansar,paz,senhor)
//========================================================

// Example: Ontem, a Manuela adormeceu na paz do Senhor
// Example: Ontem, a Manuela descansou na paz do Senhor
//
if ( (VDOMAIN(?,#2[lemma:adormecer]) || VDOMAIN(?,#2[lemma:descansar]) ) &
     MOD[post](#2,#3[lemma:paz]) & PREPD(#3,?[lemma:em]) &
     MOD[post](#3,#4[lemma:senhor]) & PREPD(#4,?[lemma:de])
   )
   FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3,#4)


//========================================================
// adormecer no Senhor          ->  FIXED(MORRER,adomecer,senhor)
//========================================================

// Example: Ontem, a Manuela adormeceu no Senhor
//
if ( VDOMAIN(?,#2[lemma:adormecer]) & MOD[post](#2,#3[lemma:senhor]) & PREPD(#3,?[lemma:em]) )
    FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3)


//========================================================
// dormir o sono eterno        ->  FIXED(MORRER,dormir,sono,eterno)
// dormir o derradeiro sono    ->  FIXED(MORRER,dormir,sono,derradeiro)
// dormir o último sono        ->  FIXED(MORRER,dormir,sono,último)
//========================================================

// Example: A Manuela dorme o sono eterno
// Example: A Manuela dorme o último sono
// Example: A Manuela dorme o derradeiro sono
//
if ( VDOMAIN(?,#2[lemma:dormir]) & CDIR[post](#2,#3[lemma:sono]) &
     ( MOD(#3,#4[lemma:último]) || MOD(#3,#4[lemma:eterno]) || MOD(#3,#4[lemma:derradeiro]) ) & DETD(#3,?[lemma:o])
   )
    FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3,#4)


//========================================================
// já não estar entre nós         ->  FIXED(MORRER,estar,nós,não)
//========================================================

// Example: A Manuela já não está entre nós
//
if ( VDOMAIN(#1,#2[lemma:estar]) &
     MOD(#2,#3[lemma:eu]) &
     PREPD(#3,#4[lemma:entre]) &
     MOD[neg,pre](#1,#5[lemma:não])
   )
    FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#3,#5)


//========================================================
// (estar a) fazer tijolo     ->  FIXED(MORRER,estar,fazer,tijolo)
//========================================================

// Example: A Manuela está a fazer tijolo desde ontem
//
if ( VDOMAIN(#1[lemma:estar],#2[lemma:fazer]) & CDIR(#2,#3[lemma:tijolo]) )
    FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#1,#2,#3)


//========================================================
// estar sob sete palmos de terra  ->  FIXED(MORRER,estar,palmos,terra)
//========================================================

// Example: A Manuela está sob sete palmos de terra
//
if ( VDOMAIN(?,#2[lemma:estar]) &
     PREPD(#5[lemma:palmo],?[lemma:sob]) &
     (QUANTD(#5,?[lemma:sete]) || QUANTD(#5,?[lemma:7])) &
     QUANTD(#4[lemma:terra],#5) &
     PREPD(#4,?[lemma:de]) &
     MOD[post](#2,#4)
   )
  FIXED[normalized=+](##verb[surface="morrer",lemma="morrer"],#2,#5,#4)


////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
// NEGATION FOR ALL FIXED PHRASES
///////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
if ( ^FIXED(#1,#2) & MOD[neg](#1,?) )
  FIXED[neg=+](#1,#2)

if ( ^FIXED(#1,#2,#3[lemma:~não]) & MOD[neg](#1,?) )
  FIXED[neg=+](#1,#2,#3)

if ( ^FIXED(#1,#2,#3,#4) & MOD[neg](#1,?) )
  FIXED[neg=+](#1,#2,#3,#4)

