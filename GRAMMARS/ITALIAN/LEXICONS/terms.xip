//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Copyright (c) 2006 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//! File:	terms.xip
//! Content:	NEs
//!
//! Language:	ITALIAN
//! ENCODING:	UTF-8	[€àâáäã]
//!
//! Author:	Anne Schiller
//!
//! Date: 	17-Jan-2006	:: initial version
//! Update:	08-Apr-2008	:: changed to UTF-8
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!



// ------------------------------------------------------------------------------------------------------------------------------

Sequence:

//10> noun[prop=+] = noun;?[src:guess], noun[postmod,featprop,!featprop:!].



// // // ORGANISATIONS


//La Confederazione[premod, org] Nazionale[adj, geo] dei Camionisti[noun, prof, maj] ha fatto sciopero.
8> noun[prop=+,org=+,mwe=+,featprop:!] = noun[premod,featprop,!gender:!,!number:!], (adj[geo:+])+, prep, (det[art]), noun[!prof:+!,!featprop:!].



//La Confederazione[premod org] Generale[adj] Italiana dei Lavoratori[noun, prof, maj] ha fatto sciopero.
8> noun[prop=+,org=+,mwe=+,featprop:!] = noun[premod,featprop,!gender:!,!number:!], (adj)+, adj[demonym], prep, (det[art]), noun[!prof:+!,!featprop:!].



//L'Associazione[premod org] (Nazionale) Italiana [adj,demonym:+] Biblioteche [noun,  maj] ha fatto sciopero.
8> noun[prop=+,org=+,mwe=+,featprop:!] = |det[art,def]| noun[maj:+,premod,featprop,org,!featprop:!], (adj[geo:+])*, adj[demonym:+], noun[maj:+,prof:+].	//L'Associazione Italiana Editori è nata nel 1989; L'Associazione Nazionale Italiana Editori è nata nel 1989;
8> noun[prop=+,org=+,mwe=+,featprop:!] = |det[art,def]| noun[maj:+,premod,featprop,org,!featprop:!], (adj[geo:+])*, adj[demonym:+], noun[maj:+].	//L'Associazione Italiana Biblioteche è nata nel 1989; L'Associazione Nazionale Italiana Biblioteche è nata nel 1989;
8> noun[prop=+,org=+,mwe=+,featprop:!] = |det[art,def]| noun[maj:+,premod,featprop,org,!featprop:!], adj[geo:+], noun[maj:+,prof:+].	//L'Associazione Nazionale Editori è nata nel 1989;
8> noun[prop=+,org=+,mwe=+,featprop:!] = |det[art,def]| noun[maj:+,premod,featprop,org,!featprop:!], adj[geo:+], noun[maj:+].	//L'Associazione Nazionale Biblioteche è nata nel 1989.




//L'As Roma SpA (société de football - ici en tant que sociétà per azioni) 
8> noun[prop=+,org=+,mwe=+,featprop:!] = (noun[sport,premod,featprop,!featprop:!, maj]), (det[art]), ?[prop:+,maj,!gender:!,!number:!], noun[postmod,company,featprop,!featprop:!].
8> noun[prop=+,org=+,mwe=+,featprop:!] = | noun[premod,org:+,featprop,!featprop:!, maj:~]| (det[art]), ?[prop:+,maj,!gender:!,!number:!,sport], noun[postmod,featprop,!featprop:!].	//la società Juventus Football Club


//il Milan Ac
8> noun[prop=+,org=+,mwe=+,featprop:!] = ?[prop:+,maj,!gender:!,!number:!, featgeo=~], noun[postmod,sport:+,!sport:!,company:~].



//FC Internazionale
8> noun[prop=+,org=+,mwe=+,featprop:!] = noun[premod,sport,!sport:!,company:~], ?[maj,!gender:!,!number:!,firstname=~,featgeo=~].





//La società Francesco (Aldo) Flumendosa (Murru) Srl è fallita
8> noun[prop=+,soc=+,mwe=+,featprop:!] @=| (noun[premod:+,org:+]) | noun+[firstname,featpeople=~], noun+[maj:+,dig:~,featgeo=~,featorg=~,company:~], (conj[coord]),(noun[lemma:figlio]),noun[postmod:+,company:+].
//where (soc##1=1).

//La società Bellighé Srl è fallita
8> noun[prop=+,soc=+,mwe=+,featprop:!] @=| (noun[premod:+,org:+]) | noun+#1[maj:+,dig:~,featgeo=~,featorg=~,company:~], (conj[coord]),(noun[lemma:figlio]),noun[postmod:+,company:+],
where (soc##1=1).


//La società/ la ditta Puddu
8> noun[prop=+,mwe=+,soc=+] @=| det*[art], noun[premod:+,org:+,fem:+,title:~,prof:~] | noun+[maj:+,dig:~,featgeo=~,featorg=~,company:~], (adj[geo:~,dem:~,poss:~]).


//La Regione (Autonoma) (della) Sardegna, il Comune di Cagliari.
9> noun[prop=+,inst=+,mwe=+,featprop:!] @= noun#1[!inst:+!,maj:+,premod:+,bank:~,!gender:!],(adj[maj:+]), (prep[lemma:di], det[art]), noun[prop:+,region:+,sport=~,org=~],
where (inst##1=1).			//it doesn't work because it is a mwe
9> noun[prop=+,inst=+,mwe=+,featprop:!] @= noun[!inst:+!,maj:+,premod:+,bank:~,people:~, newspaper:~,!gender:!,geo:~,featartefact:~,featgeo=~],(adj[maj:+]), (prep[lemma:di], det*[art]), noun[prop:+,city:+,sport=~,org=~].


//Il ministero del Welfare 
//9> noun[prop=+,inst=+,mwe=+,sg=+,featprop:!] @= noun#1[premod, inst, sg,bank:~,featartefact:~,masc:+,!masc:!, maj:+], (adj[geo:+,poss:~, dem:~]), prep, det[def], noun[people:~, continent:~, country:~,maj:+], adj*,(conj[coord]), (prep), (noun[people:~, continent:~, country:~]), (adj).

// Il Ministero della Pubblica [D/d]istruzione (Min. and Pubbl.: only Cap lett)
//9> noun[prop=+,inst=+,mwe=+,sg=+,featprop:!] @= noun[premod, inst, sg, !masc:!, maj:+], (adj[geo:+]), prep, det[def], adj[poss:~, dem:~,maj:+], noun[people:~, continent:~, country:~].
9> noun[prop=+,inst=+,mwe=+,sg=+,featprop:!] @= noun[premod, people:~, title:~, prof:~, inst, sg, !masc:!], (adj[geo:+]), prep, det[def], adj[poss:~, dem:~,maj:+], noun#1[people:~, continent:~, country:~].

//Il ministero del Welfare (Cap lett)
9> noun[prop=+,inst=+,mwe=+,sg=+,featprop:!] @=  noun[premod,people:~, title:~, prof:~,  inst,!featorg:!, sg, !masc:!, maj:~], (adj[geo:+,poss:~, dem:~]), prep, det[def], noun[people:~, continent:~, country:~,maj:+], adj*. // (conj[coord]), (prep), (noun[people:~, continent:~, country:~]), (adj).

// Il ministero della Pubblica [D/d]istruzione (Min. and Pubbl.: only Cap lett)
9> noun[prop=+,inst=+,mwe=+,sg=+,featprop:!] @= | noun[premod, inst, sg, !masc:!, maj:~], (adj[geo:+]), prep, det[def] | adj[poss:~, dem:~,maj:+], noun[people:~, continent:~, country:~].


//La Repubblica italiana; la repubblica di Corea
9> noun[prop=+,inst=+,mwe=+,featprop:!] @= noun[!inst:+!,maj:+,premod:+,bank:~,!gender:!],(adj[maj:+]), prep[lemma:di], (det[art]), noun[prop:+,country:+,sport=~,org=~].
9> noun[prop=+,inst=+,mwe=+,featprop:!] @= noun[!inst:+!,maj:+,premod:+,bank:~,!gender:!],(adj[maj:+]), adj[demonym:+,country].


//Il Consiglio dei ministri/ 
9> noun[prop=+,inst=+,mwe=+,featprop:!] @= noun#1[!inst:+!,maj:+,premod:+,bank:~,!gender:!], (adj[demonym]), prep[lemma:di], (det[art]), noun[postmod,sport=~,org=~],
where(inst##1=1).

//Il Consiglio d'Europa
9> noun[prop=+,inst=+,mwe=+,featprop:!] @= noun#1[!inst:+!,maj:+,premod:+,bank:~,!gender:!], prep[lemma:di], (det[art]), noun[prop:+,featgeo,postmod,sport=~,org=~],
where(inst##1=1).



// Il Ministro della Pubblica [D/d]istruzione
9> noun[prop=+,inst=+,mwe=+,sg=+,featprop:!] @= | noun[premod, people, title:+,prof:+], (adj[geo:+]), prep, det[def] | adj[poss:~, dem:~,maj:+], noun#1[people:~, continent:~, country:~].

// Il Ministro per lo Sviluppo economico
9> noun[prop=+,inst=+,mwe=+,sg=+,featprop:!] @= | noun[premod, people, title:+,prof:+], (adj[geo:+]), prep, det[def] | noun#1[people:~, continent:~, country:~], adj[poss:~, dem:~,maj:+] .


//Banks.
10> noun[prop=+,org=+,mwe=+,featprop:!] @= noun[premod,maj:+,bank:+,!bank:!,!gender:!,featartefact:~,sg], (adj[geo:+,poss:~, dem:~]), (adj[geo:~,poss:~,indef:~,dem:~]), prep, (det[def]), noun[people:~,featgeo,maj:+,featgeo=~,featorg=~], (conj[coord]), (prep), (noun[people:~, continent, country,maj:+]), (adj).		//Banca d'Italia, Banco di Napoli
10> noun[prop=+,org=+,mwe=+,featprop:!] @= noun[premod,maj:+,!bank:!,!gender:!,sg], (adj[geo,poss:~,indef:~,dem:~]), prep, (det[def]), noun[people:~,prof:+,maj:+,country=~,featorg=~]. //


10> noun[prop=+,org=+,mwe=+,featprop:!] @= noun[premod,maj:+,!bank:!,!masc:!,sg], (adj[geo:+,poss:~, dem:~]), prep, (det[def]), noun[people:~, country:+,maj:+,country=~,featorg=~], (conj[coord]), (prep), (noun[people:~, continent, country,maj:+]), (adj).







// // // PEOPLE


//Giovanni Depau dice; Come dice Mario Murru
10> noun[prop=+,people=+,mwe=+,featprop:!] @= ?+[firstname,!gender:!,!number:!,featgeo=~,featorg=~,featartefact=~], ?[prop:+] | (verb[pastpart:+]), verb[dicendi:+] | .
10> noun[prop=+,people=+,mwe=+,featprop:!] @= ?+[firstname,!gender:!,!number:!,featgeo=~,featorg=~,featartefact=~], ?[prop:+] | (verb[form:favere]), verb[dicendi:+] | .
10> noun[prop=+,people=+,mwe=+,featprop:!] @= |verb[dicendi:+]| ?+[firstname,!gender:!,!number:!,featgeo=~,featorg=~,featartefact=~], ?[prop:+] .



//Giovanni Depau Serra/ Miguel Angel Perez (y) Cruz
10> noun[prop=+,people=+,mwe=+,featprop:!] @= ?+[firstname,!gender:!,!number:!,featgeo=~,featorg=~,featartefact=~], ?[maj:+,dig:~,featgeo=~,featorg=~], noun*[spa:+,lemma:y], ?[maj:+,dig:~,featgeo=~,featorg=~].



//Mario De Rosa; Mario de Rosa, Alain de Benoist; Aldo Di Napoli. 
10> noun[prop=+,people=+,mwe=+,maj=+,featprop:!] @= noun[firstname,!gender:!,!number:!,featgeo=~,featorg=~,featartefact=~], prep#1[lemma:di,maj:+], det*[art], ?#1[maj:+],
where (lastname##1=1).
10> noun[prop=+,people=+,mwe=+,featprop:!] @= noun[firstname,!gender:!,!number:!,featgeo=~,featorg=~,featartefact=~], prep[surface:de], ?[maj:+].


//Giovanni Brambilla; Anna Rossi. (very spread last names, already in FST lists)
10> noun[prop=+,people=+,mwe=+,featprop:!] @= ?+[firstname,!gender:!,featgeo=~,featorg=~,featartefact=~],noun+[lastname,featgeo=~,featorg=~,featartefact=~].


//Giovanni Depau; Anne Schiller |~num[dig,rom]|
10> noun[prop=+,people=+,mwe=+,featprop:!] @= ?+[firstname,!gender:!,featgeo=~],?#1[maj:+,dig:~,featgeo=~,featorg=~,title:~,prof:~,prep:~, verb=~] ,
where (lastname##1=1).
10> noun[prop=+,people=+,mwe=+,featprop:!] @= | det[art], ?*[poss:+], noun[prof:+,!gender:!] | noun[firstname,!gender:!,featgeo=~,featorg=~,featartefact=~],?[maj:+,dig:~,title:~,prof:~,prep:~].		//il nostro inviato marek Klondinsky
10> noun[prop=+,people=+,mwe=+,featprop:!] @= | det[art], ?*[poss:+], noun[prof:+,!gender:!] | noun+[maj:+,featgeo=~,featorg=~,featartefact=~],noun[maj:+,dig:~,title:~,prof:~,prep:~,featgeo=~,featorg=~,featartefact=~].		//il nostro inviato marek Klondinsky



// Il candidato/collega Franco Puddu
10> noun[prop=+,lastname=+,mwe=+,featprop:!] @= |noun[premod,people,prof,!featprop:!]| (?+[!firstname:!,!gender:!,!number:!,title:~,prof:~,featgeo=~,featorg=~,featartefact=~]), prep#1[lemma:di,maj:+], noun+#1[maj:+,featgeo=~,featorg=~,featartefact=~]  ,
where (lastname##1=1).



//il colonnello Murru
10> noun[prop=+,lastname=+,featprop:!,mwe=+] @= |noun[premod,people,prof,!featprop:!]| (?#1[prop:+, firstname:~,maj:+,title:~,prof:~,featgeo=~,featorg=~,featartefact=~]),?#2[prop:+, firstname:~,maj:+,title:~,prof:~,featgeo=~,featorg=~,featartefact=~],
where (multiname##1=1 & multiname##2=1).

10> noun[prop=+,lastname=+,featprop:!,mwe=+] = noun#1, noun#2,
where (multiname##1:1 & multiname##2:1).


//il dottor - il professor - l'onorevole Murru
10> noun[prop=+,lastname=+,featprop:!,mwe=+] @= |noun[premod,people,title,!featprop:!]| (noun#1+[prop:+, maj:+,firstname:~,title:~,prof:~,prep:~,featgeo=~,featorg=~,featartefact=~]), ?#1+[maj:+,firstname:~,title:~,prof:~,prep:~,featgeo=~,featorg=~,featartefact=~] ,
where (lastname##1=1).


//il dottor - il professor - l'onorevole De Murru
10> noun[prop=+,lastname=+,mwe=+,featprop:!] @= |noun[premod,people,title,!featprop:!]| prep#1[lemma:di,maj:+], ?#1+[maj:+,firstname:~,title:~,prof:~,featgeo=~,featorg=~,prep:~,featgeo=~,featorg=~,featartefact=~],
where (lastname##1=1).
10> noun[prop=+,lastname=+,mwe=+,featprop:!] @= |noun[premod,people,title,!featprop:!]| prep[surface:de], ?[maj:+,featgeo=~,featorg=~,featartefact=~].



//Il (attuale/vecchio/ex) sindaco (di Cagliari) (,/è) Pinuccio Puddu
10> noun[mwe=+,prop=+,people=+,featprop:!] @= |det, (adj)*, noun[prof:+,sg:+], (adj[nat:+];adj;noun[org:+]), (prep[lemma:di], det*[art], noun[maj,city];noun[maj,org]), (punct[comma];verb[form:fessere])| noun+[maj:+,firstname,featorg:~,featgeo:~], noun#1[firstname:~,maj:+,featgeo=~,featorg=~,featartefact=~],
where (lastname##1=1).

//Il (attuale/vecchio/ex) sindaco (di Cagliari) (,/è) Puddu
10> noun[mwe=+,prop=+,lastname=+,featprop:!] @= |det, (adj)*, noun[prof:+,sg:+], (adj[nat:+];adj;noun[org:+]), (prep[lemma:di], det*[art], noun[maj,city];noun[maj,org]), (punct[comma];verb[form:fessere])| noun#1+[firstname:~,maj:+,featgeo=~,featorg=~,featartefact=~],
where (lastname##1=1).



//Il (attuale/vecchio/ex) presidente della Commissione (,/è) Pinuccio Puddu
10> noun[mwe=+,prop=+,people=+,featprop:!] @= |det, (adj)*, noun[prof:+,sg:+], (adj[nat:+];adj;noun[org:+]), (prep[lemma:di], det*[art], noun[maj,inst]), (punct[comma];verb[form:fessere])| (noun[title]), noun+[maj:+,firstname,featorg:~,featgeo:~], (noun#1[firstname:~,maj:+]),noun#1[firstname:~,maj:+],
where (lastname##1=1).

//Il (attuale/vecchio/ex) presidente della Commissione (,/è) Pinuccio Puddu
10> noun[mwe=+,prop=+,lastname=+,featprop:!] @= |det, (adj)*, noun[prof:+,sg:+], (adj[nat:+];adj;noun[org:+]), (prep[lemma:di], det*[art], noun[maj,inst]), (punct[comma];verb[form:fessere]), (noun[title])| (noun#1[firstname:~,maj:+,featgeo=~,featorg=~,featartefact=~]),noun#1[firstname:~,maj:+,featgeo=~,featorg=~,featartefact=~],
where (lastname##1=1).




//Il Principe Alberto di Monaco; il Re Luigi XV.
11> noun[prop=+,people=+,mwe=+,featprop:!] @= |noun[title,featgeo=~]| noun+[prop:+,firstname:+,featgeo=~],(num[rom:+]),prep[lemma:di], noun[maj:+,featgeo,featorg=~,featgeo=~]. //Il Principe alberto di Monaco "featgeo=~" ang "featorg=~" in order to avoid the dependencies "LOC" and "ORG".
11> noun[prop=+,people=+,mwe=+,featprop:!] @= |noun[title,city=~,featorg=~]| noun+[prop:+,firstname:+],num[rom:+],prep[lemma:di],( noun[featgeo], conj[coord] ), noun[featgeo,featorg=~,featgeo=~]. // Il Re Luigi XV (di Catalogna e Aragona). "featgeo=~" ang "featorg=~" in order to avoid the dependencies "LOC" and "ORG".
11> noun[prop=+,people=+,mwe=+,featprop:!] @= |noun[title,city=~,firstname=~,featorg=~]| noun+[prop:+,firstname:+],num[rom:+].



//Papa Giovanni Paolo II; not "Papa Woitila" other rules match at its place.
10> noun[prop=+,people=+,mwe=+,featprop:!] @= noun[premod,title,featprop=~,maj:+],  noun+[prop:+,firstname:+], num[dig:+,rom:+].




11> noun[lastname=+] = (prep), noun,
where (lastname##0:1).






// // // LOC

//Abita in via machiavelli 
12> noun[prop=+,place=+,mwe=+,featprop:!] @= |verb[stay_place:+], prep | noun[geo:+,premod,!featprop:!,!gender:!,!number:!,featpeople=~,featorg=~], prep*, (det[art]), noun, (punct[comma:+]), num*.  //celle-ci est un peu risquée à cause du noun commun...je la laisse pour l'instant mais je ne suis pas très sûr;  // il faudra rajouter les traits noun[prop,maj], qui, pour l'instant, sont inutiles  
12> noun[prop=+,place=+,mwe=+,featprop:!] @= | prep | noun[geo,premod,!featprop:!,!gender:!,!number:!], noun[maj:+,prop:+], (punct*[comma], num). //il vaudrait mieux remplacer maj, prop avec people, celeb, etxci




12> noun[prop=+,mwe=+,featprop:!] @= | noun[geo:+,geo=~,premod,!featprop:!,!gender:!], prep*, (det[art])| noun#1[prop:+,maj:+,!featprop:!],		
where (place##1=1).								//"il monte (fiume, lago, etc.) (di) Putzucugurra"; then :"(il) Putzucugurra". 

12> noun[prop=+,mwe=+,featprop:!] @= noun[geo:+,geo=~,premod,maj:+,!featprop:!,!gender:!], prep*, (det[art]), noun#1[maj:+],
where (place##1=1).								//"il Monte (Fiume, Lago, etc.) (di) Putzucugurra"; then :"(il) Putzucugurra". 




//l'Africa Occidentale
15> noun[prop=+,region=+,mwe=+,featprop:!] @= |(prep), det*[art]| noun[!continent:!,maj:+];noun[!country:!,maj:+];noun[region,maj:+], prep[lemma:di], (det[def]), ?[cardpoint].
15> noun[prop=+,region=+,mwe=+,featprop:!] @= |(prep), det*[art]| noun[continent];noun[country];noun[region], (?[cardpoint]), punct*[hyphen], adj[postmod:+,locadj].




//Alle porte di Roma/ Putzumannu; Dalle parti di ...
12> noun[prop=+,city=+,mwe=+,featprop:!] @= |prep, det[art,pl:+,fem:+], noun[lemma:parte], prep | noun#1[prop:+,maj:+,featgeo:~,featorg:~,featpeople:~],		
where (place##1=1).		

12> noun[prop=+,city=+,mwe=+,featprop:!] @= |prep, det[art,def:+,pl:+,fem:+], noun[lemma:porta], prep | noun#1[prop:+,maj:+,featgeo:~,featorg:~,featpeople:~],		
where (place##1=1).	









// // // ARTEFACTS

14> noun[prop=+,award=+,mwe=+,featprop:!] = ?+[src:guess, maj], noun[postmod:+,award:+,!featprop:!] |num*[year]|. 
14> noun[prop=+,award=+,mwe=+,featprop:!] = noun[premod:+,award:+,!featprop:!,maj], (adj[geo, demonym]), (prep), (det[art]), noun |num*[year]|.   //Premio internazionale della Moda


//il settimanale (economico / francese / economico francese "Bon [M/m]arché"); it doesn't work in sentences like La rivista di moda svedese XYZ
15> noun[prop=+,newspaper=+,featprop:!] @= |noun[printtitle:+, prop:~], (adj*), (?[quote])| ?[maj:+], ?*[last:+] |(?[quote]) |.

15> noun[prop=+,newspaper=+,mwe=+,featprop:!] @= noun#1[printtitle:+,maj:+,!gender:!], prep[lemma:di], (det[art]), noun[featgeo,featgeo=~,featdate=~,featpeople=~],
where (newspaper##1=1).

15> noun[prop=+,newspaper=+,mwe=+,featprop:!] @= noun#1[printtitle:+,maj:+,!gender:!], prep[lemma:di], (det[art]), noun[period,featgeo=~,featdate=~,featpeople=~],
where (newspaper##1=1).

//Il Trattato di Roma del 1945
15> noun[prop=+,agreement=+,mwe=+,featprop:!] @= noun#1[(premod,agreement:+,maj:+,!gender:!], prep[lemma:di], (det[art]), noun[city,featgeo=~,featdate=~,featpeople=~] ,
where (agreement##1=1).


// La Direttiva di Ginevra ; la D. Seveso II
15> noun[prop=+,law=+,mwe=+,featprop:!] @= noun#1[premod:+,law:+,maj:+,!gender:!], prep[lemma:di], (det[art]), noun[city,featgeo=~,featdate=~,featpeople=~] ,	 //La Direttiva dell'Aja.
where (law##1=1).
	
15> noun[prop=+,law=+,mwe=+,featprop:!] @= noun#1[premod:+,law:+,maj:+,!gender:!,featartefact,!featartefact:!], (adj[locadj]), (det[art]), noun*[city,featgeo=~,featdate=~,featpeople=~], (num[dig:+]) , //La direttiva Seveso (II)
where (law##1=1).






// // // DATES

////Dates: first approach...

18> noun[date=+,featprop:!] @= | noun[city:+, featgeo,featorg=~,featpeople=~], (punct[comma]) | (noun[day:+]), num[day:+], noun[month:+], (num[year:+]) .	//Roma, giovedi 13 marzo 2008
18> noun[date=+,featprop:!] @= | verb[lemma:nascere], det[art,def,surface:il] | num[day:+], noun[month:+], (num[year:+]) . //Vanessa è nata il 7 novembre 1989
18> noun[date=+,featprop:!] @= | (verb[lemma:nascere]), prep, det[art,def,sg] | num[year:+] . 		//Maria è nata nel 1998
18> noun[date=+,featprop:!] @= noun[month:+], num[year:+] . 	// il gennaio 2008
18> noun[date=+,featprop:!] @= | det[art,def,sg] | num[year:+] . 	// il 2008
18> noun[date=+,featprop:!] @= | det[sg:+,masc:+] | num[dig:+,rom:+], noun[period:+,lemma:secolo], (adv[date:+,postmod:+]) .		//dal XV secolo a.C. [a.C.=> ambiguous]
18> noun[date=+,featprop:!] @= | det[sg:+,masc:+] | num[dig:+,ord:+], noun[period:+,lemma:secolo], (adv[date:+,postmod:+]) .		//Sono nato nel XX secolo,nel 20° secolo
18> noun[date=+,featprop:!] @= | det[sg:+,masc:+] | num[dig:+,card:+], noun[period:+,lemma:secolo], (adv[date:+,postmod:+]) .		//Sono nato nel XX secolo,nel 20° secolo
18> noun[date=+,featprop:!] @= | det[sg:+,masc:+] | num[dig:+,year:+], adv[date:+,postmod:+] .				//nel 500 a.C. [a.C.=> ambiguous]
18> noun[date=+,featprop:!] @= | det[sg:+,masc:+] | num[dig:+,day:+], noun[time:+,period:+,month:+], num*[year:+].		//il 24 ottobre
18> noun[date=+,featprop:!] @= | noun*[city:+], punct*[comma] | (noun[day:+]), num[day:+], noun[month:+], (num[year:+]) .	//sabato 27 settembre 2008
18> noun[date=+,featprop:!] @= | det[pl:+] | noun[surface:anni], num[dig:+,year:+] .				//gli anni 70.
18> noun[date=+,featprop:!] @= | det[pl:+] | noun[period], num[card:+] .					//negli anni Settanta	
