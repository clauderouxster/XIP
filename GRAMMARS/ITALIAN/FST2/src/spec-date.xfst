# =====================================================================
# TYPE:		(x)fst script
# CONTENT:	definition of digital standard date expressions
#		(cf. http://www.w3.org/QA/Tips/iso-date)
# CHARENCODING:	Unicode (UTF-8)
# LANGUAGE:	(ANY)
#
# AUTHOR:	Anne Schiller
# CREATED:	19-Oct-2005
# =====================================================================
# USAGE:	fst -utf8 -l [ThisFile]
# =====================================================================

# time zones from http://www.timeanddate.com/library/abbreviations/timezones/

define TIMEZONE [
  {ACDT}	# Australian Central Daylight Time	Australia	UTC + 10:30 hours
| {ACST}	# Australian Central Standard Time	Australia	UTC + 9:30 hours
| {ADT}		# Atlantic Daylight Time		North America	UTC - 3 hours
| {AEDT}	# Australian Eastern Daylight Time	Australia	UTC + 11 hours
| {AEST}	# Australian Eastern Standard Time	Australia	UTC + 10 hours
| {AKDT}	# Alaska Daylight Time			North America	UTC - 8 hours
| {AKST}	# Alaska Standard Time			North America	UTC - 9 hours
| {AST}		# Atlantic Standard Time		North America	UTC - 4 hours
| {AWST}	# Australian Western Standard Time	Australia	UTC + 8 hours
| {BST}		# British Summer Time			Europe		UTC + 1 hour
| {CDT}		# Central Daylight Time			North America	UTC - 5 hours
| {CEDT}	# Central European Daylight Time	Europe		UTC + 2 hours
| {CEST}	# Central European Summer Time		Europe		UTC + 2 hours
| {CET}		# Central European Time			Europe		UTC + 1 hour
| {CST}		# Central Summer(Daylight) Time		Australia	UTC + 10:30 hours
| {CST}		# Central Standard Time			Australia	UTC + 9:30 hours
| {CST}		# Central Standard Time			North America	UTC - 6 hours
| {CXT}		# Christmas Island Time			Australia	UTC + 7 hours
| {EDT}		# Eastern Daylight Time			North America	UTC - 4 hours
| {EEDT}	# Eastern European Daylight Time	Europe		UTC + 3 hours
| {EEST}	# Eastern European Summer Time		Europe		UTC + 3 hours
| {EET}		# Eastern European Time			Europe		UTC + 2 hours
| {EST}		# Eastern Summer(Daylight) Time		Australia	UTC + 11 hours
| {EST}		# Eastern Standard Time			Australia	UTC + 10 hours
| {EST}		# Eastern Standard Time			North America	UTC - 5 hours
| {GMT}		# Greenwich Mean Time			Europe		UTC
| {HAA}		# Heure Avancée de l'Atlantique		North America	UTC - 3 hours
| {HAC}		# Heure Avancée du Centre		North America	UTC - 5 hours
| {HADT}	# Hawaii-Aleutian Daylight Time		North America	UTC - 9 hours
| {HAE}		# Heure Avancée de l'Est		North America	UTC - 4 hours
| {HAP}		# Heure Avancée du Pacifique		North America	UTC - 7 hours
| {HAR}		# Heure Avancée des Rocheuses		North America	UTC - 6 hours
| {HAST}	# Hawaii-Aleutian Standard Time		North America	UTC - 10 hours
| {HAT}		# Heure Avancée de Terre-Neuve		North America	UTC - 2:30 hours
| {HAY}		# Heure Avancée du Yukon		North America	UTC - 8 hours
| {HNA}		# Heure Normale de l'Atlantique		North America	UTC - 4 hours
| {HNC}		# Heure Normale du Centre		North America	UTC - 6 hours
| {HNE}		# Heure Normale de l'Est		North America	UTC - 5 hours
| {HNP}		# Heure Normale du Pacifique		North America	UTC - 8 hours
| {HNR}		# Heure Normale des Rocheuses		North America	UTC - 7 hours
| {HNT}		# Heure Normale de Terre-Neuve		North America	UTC - 3:30 hours
| {HNY}		# Heure Normale du Yukon		North America	UTC - 9 hours
| {IST}		# Irish Summer Time			Europe		UTC + 1 hour
| {MDT}		# Mountain Daylight Time		North America	UTC - 6 hours
| {MESZ}	# Mitteleuropäische Sommerzeit		Europe		UTC + 2 hours
| {MEZ}		# Mitteleuropäische Zeit		Europe		UTC + 1 hour
| {MST}		# Mountain Standard Time		North America	UTC - 7 hours
| {NDT}		# Newfoundland Daylight Time		North America	UTC - 2:30 hours
| {NFT}		# Norfolk (Island) Time			Australia	UTC + 11:30 hours
| {NST}		# Newfoundland Standard Time		North America	UTC - 3:30 hours
| {PDT}		# Pacific Daylight Time			North America	UTC - 7 hours
| {PST}		# Pacific Standard Time			North America	UTC - 8 hours
| {UTC}		# Coordinated Universal Time		Europe		UTC
| {WEDT}	# Western European Daylight Time	Europe		UTC + 1 hour
| {WEST}	# Western European Summer Time		Europe		UTC + 1 hour
| {WET}		# Western European Time			Europe		UTC
| {WST}		# Western Standard Time			Australia	UTC + 8 hours
];

define TIMEZONEMIL [
  {A}		# Alpha Time Zone	Military	UTC + 1 hour
| {B}		# Bravo Time Zone	Military	UTC + 2 hours
| {C}		# Charlie Time Zone	Military	UTC + 3 hours
| {D}		# Delta Time Zone	Military	UTC + 4 hours
| {E}		# Echo Time Zone	Military	UTC + 5 hours
| {F}		# Foxtrot Time Zone	Military	UTC + 6 hours
| {G}		# Golf Time Zone	Military	UTC + 7 hours
| {H}		# Hotel Time Zone	Military	UTC + 8 hours
| {I}		# India Time Zone	Military	UTC + 9 hours
| {K}		# Kilo Time Zone	Military	UTC + 10 hours
| {L}		# Lima Time Zone	Military	UTC + 11 hours
| {M}		# Mike Time Zone	Military	UTC + 12 hours
| {N}		# November Time Zone	Military	UTC - 1 hour
| {O}		# Oscar Time Zone	Military	UTC - 2 hours
| {P}		# Papa Time Zone	Military	UTC - 3 hours
| {Q}		# Quebec Time Zone	Military	UTC - 4 hours
| {R}		# Romeo Time Zone	Military	UTC - 5 hours
| {S}		# Sierra Time Zone	Military	UTC - 6 hours
| {T}		# Tango Time Zone	Military	UTC - 7 hours
| {U}		# Uniform Time Zone	Military	UTC - 8 hours
| {V}		# Victor Time Zone	Military	UTC - 9 hours
| {W}		# Whiskey Time Zone	Military	UTC - 10 hours
| {X}		# X-ray Time Zone	Military	UTC - 11 hours
| {Y}		# Yankee Time Zone	Military	UTC - 12 hours
| {Z}		# Zulu Time Zone	Military	UTC
];

define ZERO	"\u0030" ;
define DIG1	[1|2|3|4|5|6|7|8|9];
define DIGIT	ZERO | DIG1 ;
define HYPHEN	"\u002D" ;
define COLON	"\u003A" ;
define SLASH	"\u002F" ;
define PERIOD	"\u002E" ;
define SPACE	"\u0020" ;
define NBSP	"\u00A0" ;  # NO-BREAK SPACE

define WS	SPACE | NBSP ;

define OPTZERO	(ZERO) ;

define DAY1	[ OPTZERO DIG1 | [1|2] DIGIT | 3 [ZERO|1] ] ;
define DAY	[ ZERO DIG1 | [1|2] DIGIT | 3 [ZERO|1] ] ;

define MONTH1	[ OPTZERO DIG1 | 1 [ZERO|1|2] ] ;
define MONTH	[ ZERO DIG1 | 1 [ZERO|1|2] ] ;

define ROMAN	[ I (I (I)) | I V | V (I (I (I))) | I X | X (I (I)) ];

define YEAR1	[ ((DIGIT) DIGIT) DIGIT DIGIT ] ;
define YEAR	[ DIGIT DIGIT DIGIT DIGIT ] ;

define HOUR12	[ OPTZERO DIGIT | 1 [ZERO|1|2] ] ;		# 1 - 12
define HOUR24	[ OPTZERO | 1 ] DIGIT | 2 [ZERO|1|2|3] ;	# 1 - 23

define MINUTE	[ ZERO | 1 | 2 | 3 | 4 | 5 ] DIGIT ;
define SECOND	[ ZERO | 1 | 2 | 3 | 4 | 5 ] DIGIT ;

# ISO standard format (YYYY-MM-DD)
define ISODATE	YEAR HYPHEN MONTH HYPHEN DAY ;
define ISOTIME	[ HOUR24 COLON MINUTE (COLON SECOND) 
		| {24} COLON {00} (COLON SECOND) ]
		( (WS) TIMEZONE) ;


# European Format D/M/YY
define EUDATE	DAY1 SLASH MONTH1 SLASH YEAR1 ;

# US Format M/D/YY
define USDATE	MONTH1 SLASH DAY1 SLASH YEAR1 ;
define TM	[ [a|p] (PERIOD) m (PERIOD) ] | [ [A|P] (PERIOD) M (PERIOD) ] ;

define USTIME	[ HOUR12 COLON  MINUTE (COLON SECOND) 
		| HOUR12 PERIOD MINUTE (PERIOD SECOND) ] ( (WS) TM ) ( (WS) TIMEZONE) ;


# hour without separators
# define DIGTIME	[ HOUR24 MINUTE | {2400} ] ( (WS) TIMEZONE) ;

define DIGTIME24	[ HOUR24 MINUTE | {2400} ] (WS) TIMEZONE ;
define DIGTIME12	[ HOUR12 MINUTE ] (WS) TM ( (WS) TIMEZONE ) ;


# Japanese format YY/M/D
define JPDATE	YEAR1 SLASH MONTH1 SLASH DAY1 ;

# German Format DD.MM.YY
define GERDATE	DAY1 PERIOD MONTH1 PERIOD YEAR1 ;

# Months with roman numbers
define ROMDATE	DAY1 WS ROMAN WS YEAR1 
		| DAY1 HYPHEN ROMAN HYPHEN YEAR1 ;

# define tags ...
read regex [
	ISODATE		%+Dig:0 %+Date:0 %+YMD:0
|	EUDATE		%+Dig:0 %+Date:0 %+DMY:0
|	USDATE		%+Dig:0 %+Date:0 %+MDY:0
|	JPDATE		%+Dig:0 %+Date:0 %+YMD:0

|	GERDATE		%+Dig:0 %+Date:0 %+DMY:0
|	ROMDATE		%+Dig:0 %+Date:0 %+DMY:0

|	ISOTIME 	%+Dig:0 %+Time:0
|	USTIME 		%+Dig:0 %+Time:0 
|	DIGTIME24	%+Dig:0 %+Time:0 
|	DIGTIME12	%+Dig:0 %+Time:0 

# partial date expressions
|	DAY1		%+Dig:0 %+Day:0
|	MONTH1		%+Dig:0 %+Month:0
|	DIGIT DIGIT	%+Dig:0 %+ShortYear:0
|	(DIGIT) DIGIT^3	%+Dig:0 %+Year:0
|	%' DIGIT (DIGIT (DIGIT))	%+Dig:0 %+Year:0
];


