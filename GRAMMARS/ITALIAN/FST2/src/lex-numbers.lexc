!# ============================================================================
!# TYPE:	lexc
!# ENCODING:	UTF-8 [€àéù]
!# CONTENT:     written numbers
!# 
!# LANGUAGE:    ITALIAN
!#
!# CREATED:	14-May-2008
!#
!# ============================================================================

Multichar_Symbols

+Number
+Card +Ord

+Fem +Masc
+Sg +Pl

+Apoc +Elided

! internal symbols
^] 	   ! lemma boundary

! ================================================================================

LEXICON ROOT
  mld           Number;        ! krb
  zero		Number;		! Cardinal number 0
  milione	Number;		! Cardinal number 1.000.000
  miliardo	Number;		! Cardinal number 1.000.000.000
  bilione	Number;		! Cardinal number 1.000.000.000 according to the
				! contemporary Italian, French and Usa usage;
				! cardinal number 1.000.000.000.000 according to ancient
				! Italian and contemporary German and English usage.

		Num1to1999;	! Cardinal numbers from 1 to 999999


		ONumbers;	! Ordinal numbers from 0 to 1000

  millesim	ONumber;	! Ordinal number 1.000
  milionesim	ONumber;	! Ordinal number 1.000.000
  miliardesim	ONumber;	! Ordinal number 1.000.000.000
  bilionesim	ONumber;	! Ordinal number 1.000.000.000 according to the 
				! contemporary Italian, French and Usa usage;
				! ordinal number 1.000.000.000.000 according to ancient
				! Italian and contemporary German and English usage.


LEXICON Number
  +Number+Card:0	# ;


LEXICON NumberM
		Number;
  mila		Num1to999eps;	! NOTE: a rule kills the recursive possibilities.
  

LEXICON Num1to9		! 1..9 and 1000..9999
  uno		NumberM;	! NOTE: this is to generate ventunomila... ecc. ma
				! le forme unomila... sono eliminate da una regola

  uno+Number+Card+Apoc:un	#;
  due				NumberM;
  tre				NumberM;
  quattro			NumberM;
  quattro+Number+Card		ELID;
  cinque			NumberM;
  cinque+Number+Card		ELID;
  sei				NumberM;
  sette				NumberM;
  sette+Number+Card		ELID;
  otto				NumberM;
  otto+Number+Card		ELID;
  nove				NumberM;
  nove+Number+Card		ELID;


LEXICON Num1to9eps
		Num1to9;
		NumberM;


LEXICON Num1to99	! adds 10..99 and 10000..99999 to Num1to9
		Num1to9;
  dieci		NumberM;
  undici	NumberM;
  dodici	NumberM;
  tredici	NumberM;
  quattordici	NumberM;
  quindici	NumberM;
  sedici	NumberM;
  diciassette	NumberM;
  diciotto	NumberM;
  diciannove	NumberM;
  venti		Num1to9eps;
  trenta	Num1to9eps;
  quaranta	Num1to9eps;
  cinquanta	Num1to9eps;
  sessanta	Num1to9eps;
  settanta	Num1to9eps;
  ottanta	Num1to9eps;
  novanta	Num1to9eps;

  diciassette+Number+Card	ELID;
  diciotto+Number+Card		ELID;
  diciannove+Number+Card	ELID;
  venti+Number+Card		ELID;
  trenta+Number+Card		ELID;
  quaranta+Number+Card		ELID;
  cinquanta+Number+Card		ELID;
  sessanta+Number+Card		ELID;
  settanta+Number+Card		ELID;
  ottanta+Number+Card		ELID;
  novanta+Number+Card		ELID;

LEXICON Num1to99eps
		Num1to99;
		NumberM;


LEXICON Num1to999	! adds 100.999 and 100000..999999 to Num1to99
		Num1to99;
  cento		Num1to99eps;
  duecento	Num1to99eps;
  trecento	Num1to99eps;
  quattrocento	Num1to99eps;
  cinquecento	Num1to99eps;
  seicento	Num1to99eps;
  settecento	Num1to99eps;
  ottocento	Num1to99eps;
  novecento	Num1to99eps;

  cento+Number+Card		ELID;
  duecento+Number+Card		ELID;
  trecento+Number+Card		ELID;
  quattrocento+Number+Card	ELID;
  cinquecento+Number+Card	ELID;
  seicento+Number+Card		ELID;
  settecento+Number+Card	ELID;
  ottocento+Number+Card		ELID;
  novecento+Number+Card		ELID;

  
LEXICON Num1to999eps
		Num1to999;
		Number;


LEXICON Num1to1999
		Num1to999;
  mille		Num1to999eps;	! right form for numbers from 1000 to 1999


LEXICON ONumber
  o^]+Number+Ord		MFSGPL;


LEXICON ONumbers
  zeresim		ONumber;
  prim			ONumber;
  second		ONumber;
  terz			ONumber;
  quart			ONumber;
  quint			ONumber;
  sest			ONumber;
  settim		ONumber;
  ottav			ONumber;
  non			ONumber;
  decimoprim		ONumber;
  decimosecond		ONumber;
  decimoterz		ONumber;
  terzodecim		ONumber;
  decimoquart		ONumber;
  quartodecim		ONumber;
  decimoquint		ONumber;
  quintodecim		ONumber;
  decimosest		ONumber;
  sestodecim		ONumber;
  decimosettim		ONumber;
  decimottav		ONumber;
  decimonon		ONumber;

			ONum10to99;
			ONum100to999;


LEXICON ONum10to99
  decim			ONumber;
  undicesim		ONumber;
  dodicesim		ONumber;
  tredicesim		ONumber;
  quattordicesim	ONumber;
  quindicesim		ONumber;
  sedicesim		ONumber;
  diciassettesim	ONumber;
  diciottesim		ONumber;
  diciannovesim		ONumber;

  venti			ONumDec;
  trenta		ONumDec;
  quaranta		ONumDec;
  cinquanta		ONumDec;
  sessanta		ONumDec;
  settanta		ONumDec;
  ottanta		ONumDec;
  novanta		ONumDec;


LEXICON ONum100to999
  cent			ONumCent;
  duecent		ONumCent;
  trecent		ONumCent;
  quattrocent		ONumCent;
  cinquecent		ONumCent;
  seicent		ONumCent;
  settecent		ONumCent;
  ottocent		ONumCent;
  novecent		ONumCent;


LEXICON ONumDec
  esim			ONumber;
			ONum1to9;

LEXICON ONum1to9
  unesim		ONumber;
  duesim		ONumber;
  treesim		ONumber;
  quattresim		ONumber;
  cinquesim		ONumber;
  seiesim		ONumber;
  settesim		ONumber;
  ottesim		ONumber;
  novesim		ONumber;

LEXICON ONum1to9bis
			ONum1to9;
  ottav			ONumber;  ! centottavo is another possibility for centottesimo
  non			ONumber;  ! centonono is another possibility for centonovesimo

LEXICON ONumCent
  esim			ONumber;
  o			ONum1to9bis;
  o			ONum10to99;



!# -------------------------------------------------------
!# Continuation class for (optional) elision of final vowel
!# e.g.  di --> di or d'

LEXICON ELID
+Elided	#;


!# general continuation classes
LEXICON	MSG	+Masc+Sg	#;
LEXICON	FSG	+Fem+Sg		#;
LEXICON	MPL	+Masc+Pl	#;
LEXICON	FPL	+Fem+Pl		#;

LEXICON MSGPL	MSG; MPL;
LEXICON FSGPL	FSG; FPL;
LEXICON MFSGPL	MSGPL; FSGPL;

