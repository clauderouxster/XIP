!# ============================================================================
!# TYPE:	lexc
!# ENCODING:	UTF-8
!# CONTENT:     function words (pronouns, prepositiions, conjunctions, ...)
!# 
!# LANGUAGE:    ITALIAN
!#
!# AUTHOR:      A. Schiller, G. Depau
!#
!# CREATED:	14-May-2008
!#
!# ============================================================================

Multichar_Symbols
+Adj		! adjective
+Adv 		! adverb
+Art 		! article
+Conj 		! conjunction
+Det		! determiner
+PreDet		! predeteminer
+Prep		! preposition
+Pron		! pronoun
+Postmod
+Premod

+Quant +QuantPron	! quantifier	

+Coord		! coordinating (conjunction)
+Subord  	! subordinating (conjunction)
+Inf		! infinitival (conjunction)

+AConj		! Adverb that may occur as conjunctions

+Def +Indef +Dem 
+Rel +Interrog +Ne
+Pers +Poss +Refl
+Ton +Aton +Preclit
+Date

+Sg +Pl	+SP		! number
+Masc +Fem +MF		! gender
+1P +2P +3P		! person
+Nom +Acc +Dat		! case


+Elided +Apoc +Euph

! special symbols
^=		! token boundary in contracted forms
^]		! lemma boundary (for regular inflection)

!# ========================================================================
LEXICON ROOT
!# ---------------------------------------------------------------
!# Determiners, Articles
!# ---------------------------------------------------------------
lo+Art+Def+Masc+Pl:gli		ELID;
lo+Art+Def+Fem+Sg:la		ELID;
lo+Art+Def+Masc+Sg:lo		ELID;
lo+Art+Def+Fem+Pl:le		#;

il+Art+Def+Masc+Pl:i		#;
il+Art+Def+Masc+Sg:il		#;

uno+Art+Indef+Fem+Sg:una		ELID;
uno+Art+Indef+Masc+Sg:uno		#;
uno+Art+Indef+Masc+Sg+Apoc:un		#;

codesto+Det+Dem+Masc+Sg:codesto		ELID;	!?
codesto+Det+Dem+Fem+Sg:codesta		ELID;	!?
codesto+Det+Dem+Masc+Pl:codesti		#;
codesto+Det+Dem+Fem+Pl:codeste		#;

quello+Det+Dem+Masc+Sg:quello		ELID;
quello+Det+Dem+Masc+Sg+Apoc:quel		#;
quello+Det+Dem+Fem+Sg:quella		ELID;
quello+Det+Dem+Fem+Pl:quelle		#;
quello+Det+Dem+Masc+Pl:quegli		ELID;	
quello+Det+Dem+Masc+Pl+Apoc:quei		#;

questo+Det+Dem+Masc+Sg:questo		ELID;
questo+Det+Dem+Fem+Sg:questa		ELID;
questo+Det+Dem+Fem+Pl:queste		#;
questo+Det+Dem+Masc+Pl:questi		#;

tale+Det+Dem+MF+Sg:tale			#;
tale+Det+Dem+MF+Sg+Apoc:tal			#;
tale+Det+Dem+MF+Pl:tali			#;



tutto+PreDet+Fem+Pl:tutte		ELID;
tutto+PreDet+Masc+Pl:tutti		ELID;
tutto+PreDet+Fem+Sg:tutta		ELID;
tutto+PreDet+Masc+Sg:tutto		ELID;

entrambi+PreDet+Fem+Pl:entrambe		ELID;
entrambi+PreDet+Masc+Pl:entrambi	ELID;

ambedue+PreDet+MF+Pl			#;



!# ---------------------------------------------------------------
!# Conjunctions
!# ---------------------------------------------------------------


allora+Adv+AConj		#;
altrimenti+Adv+AConj		#;
anche+Conj+Coord		ELID;	!also Adv, but in a litteral register
come+Conj+Subord		ELID;	!BOTH Conj and Adv 
dove+Conj+Subord		ELID;	!BOTH Conj and Adv 
piuttosto% che+Conj+Subord	ELID;
pure+Conj+Subord		ELID;	
quando+Conj+Subord		#;	!BOTH Conj and Adv 
quasi+Adv+AConj			#;	!with subjunctive

!qualsiasi+Conj			#;	!? only Quant I'd say ADJ (indef:chiamami a qualsiasi ora, rel: una persona qualsiasi)...
!qualunque+Conj			#;	!? only QuantI'd say ADJ (indef:chiamami a qualunque  ora, rel: una persona qualunque)...

a% condizione% che+Conj+Subord	ELID;	!with subjunctive
a% condizione% di+Conj+Inf	#;
a% meno% che+Conj+Subord	ELID;	!with subjunctive
a% patto% che+Conj+Subord	ELID;	!with subjunctive
acciocché+Conj+Subord		#;
ad% esempio+Adv+AConj		#;	!also conjunction
p.es.+Adv+AConj	 		#;
p.0es.+Adv:p.% es.+AConj	#;
ad% es.+Adv+AConj    		#;


affinché+Conj+Subord		#;
al% fine% di+Conj+Inf		#;	!? loc.cong. con valore finale, allo scopo di, per: fare domanda al f. di ottenere un rimborso
allorché+Conj+Subord		ELID;
allorquando+Conj+Subord		#;	!? conj. introduce proposizioni subordinate temporali: quando, allorché
altrimenti% che+Conj+Subord	ELID;
ammesso% che+Conj+Subord	ELID;	!? loc.cong. ipotizzato che: a. che arrivi entro cinque minuti, non riusciremo comunque a fare in tempo
anche% se+Conj+Subord		ELID;
ancoraché+Conj+Subord		#;
ancorché+Conj+Subord		#;
ancorquando+Conj+Subord		#;
anzi+Conj+Coord			#;
anziché+Conj+Subord		#;
benché+Conj+Subord		#;
bensì+Conj+Subord		#;
che+Conj+Subord			ELID;	
ché+Conj+Subord			#;	!? cong. BU perché (introduce proposizioni interrogative, causali o finali)
ciononostante+Conj+Subord	#;
cioè+Conj+Coord			#;
comunque+Conj+Subord		#;
cosicché+Conj+Subord		#;
così% che+Conj+Subord		ELID;
dacché+Conj+Subord		#;	!? conj
dal% momento% che+Conj+Subord	ELID;
dato% che+Conj+Subord		#;
data% che+Conj+Subord		#;
dati% che+Conj+Subord		#;
date% che+Conj+Subord		#;
dunque+Conj+Coord		#;
e+Conj+Coord			#;
e/o+Conj+Coord			#;
e+Conj+Coord+Euph:ed		#;
ebbene+Conj+Coord		#;
eccetto% che+Conj+Subord	#;
eccettoché+Conj+Subord		#;
eppure+Conj+Coord		#;
finché+Conj+Subord		#;
fintanto% che+Conj+Subord	ELID;
fintantoché+Conj+Subord		#;
fuorché+Conj+Subord		ELID;
giacché+Conj+Subord		ELID;
in% modo% che+Conj+Subord	ELID;
in% modo% da+Conj+Inf		#;	!? Subord? prep?
in% quanto+Conj+Subord		#;
infatti+Conj+Coord		#;
intanto% che+Conj+Subord	ELID;
invece% di+Conj+Inf		#;
invero+Conj+Coord		#;
laddove+Conj+Subord		#;
ma+Conj+Coord			#;
mentre+Conj+Subord		#;
nonché+Conj+Coord		#;
nondimeno+Conj+Coord		#;
nonostante+Conj+Subord		#;	!? PREP avversat, malgrado: n. il brutto tempo ci siamo divertiti; CONJ concess, sebbene, benché: n. sia faticoso, il lavoro è bello
né+Conj+Coord			#;
o+Conj+Coord			#;
o+Conj+Coord+Euph:od		#;
ogniqualvolta+Conj+Subord	#;
oppure+Conj+Coord		#;
orbene+Conj+Subord		#;
ossia+Conj+Coord		#;
ovvero+Conj+Coord		#;
ovverossia+Conj+Subord		#;
per% quanto+Conj+Subord		#;
perché+Conj+Subord		#;
perciò+Conj+Coord		#;
pertanto+Conj+Coord		#;
però+Conj+Coord			#;
piuttosto% che+Conj+Subord	ELID;
poiché+Conj+Subord		#;
prima% che+Conj+Subord		ELID;
pur% di+Conj+Inf		#;
purché+Conj+Subord		#;
qualora+Conj+Subord		#;
qualvolta+Conj+Subord		#;
quand'anche+Conj+Subord		#;
quandanche+Conj+Subord		#;
quanto+Conj+Subord		#;
quantunque+Conj+Subord		#;
quindi+Conj+Coord		#;
salvo% che+Conj+Subord		#;
se+Conj+Subord			ELID;	
sebbene+Conj+Subord		#;
secondo% che+Conj+Subord	ELID;
semmai+Conj+Subord		#;
sempre% che+Conj+Subord		ELID;
sempreché+Conj+Subord		ELID;
sennonché+Conj+Subord		ELID;
senonché+Conj+Subord		ELID;
senza% che+Conj+Subord		ELID;
seppure+Conj+Subord		#;
seppure+Conj+Subord+Apoc:seppur	#;
sia+Conj+Coord			#;	!? in relazione con un altro sia e con le congiuzioni che, o(tanto...quanto, così...come: ci sono s. mele s. arance
sicché+Conj+Subord		#;
siccome+Conj+Subord		#;
solo% che+Conj+Subord		ELID;
soltanto+Conj+Subord		#;
talché+Conj+Subord		#;
tranne% che+Conj+Subord		ELID;
tuttavia+Conj+Coord		#;
visto% che+Conj+Subord		ELID;
vuoi+Conj+Subord		#;


!# ---------------------------------------------------------------
!# Prepositions
!# ---------------------------------------------------------------
PREP-AL;
PREP-DAL;
PREP-DEL;

a% cagione		WITH-DEL;
a% causa		WITH-DEL;
a% favore		WITH-DEL;
a% meno			WITH-DEL;
a% proposito		WITH-DEL;
a% scapito		WITH-DEL;
accanto			WITH-AL;
al% di% fuori		WITH-DALDEL;
al% di% là		WITH-DALDEL;
al% di% qua		WITH-DALDEL;
al% di% sopra		WITH-DEL;
al% di% sotto		WITH-DEL;
all'infuori		WITH-DI;
assieme			WITH-AL;
attorno			WITH-AL;
attorno+Prep		#;
attraverso		WITH-DI;
attraverso+Prep		#;
avanti			WITH-AL;
avanti+Prep		#;
con+Prep		#;
con+Prep^=:co		ARTICLE;	! collo, colla, colli, colle, col, coi, ?ne'
contro			WITH-AL;
contro+Prep		#;
da+Prep			#;
dato+Prep		#;		!? CONJ (data la situazione, bisognerà aspettare)
davanti			WITH-AL;
davanti+Prep		#;
dentro			WITH-AL;
dentro+Prep		#;
di% fronte		WITH-AL;
dietro			WITH-AL;
dietro+Prep		#;
dinanzi			WITH-AL;
dinanzi+Prep		#;
discosto		WITH-DAL;
dopo			WITH-DI;
dopo+Prep		#;
durante+Prep		#;
eccetto+Prep		#;
entro+Prep		#;
extra+Prep		#;
fin+Prep		#;
fino			WITH-AL;
fino+Prep		#;
fra+Prep		#;
fuori			WITH-DI;
fuori+Prep		#;
fuori+Prep+Apoc:fuor	#;
grazie			WITH-AL;
in% luogo		WITH-DEL;
in% proporzione		WITH-AL;
in% rapporto		WITH-AL;
in% relazione		WITH-AL;
in+Prep			#;
in+Prep^=:ne		ARTICLE;	! nello, nella, nelli, nelle, nel, nei, ?ne'
incontro+Prep		#;
innanzi			WITH-AL;
innanzi+Prep		#;
intorno			WITH-AL;
intorno+Prep		#;
invece			WITH-DEL;
lontano 		WITH-DAL;
lungi 			WITH-DAL;
lungo+Prep		#;
malgrado+Prep		#;
mediante+Prep		#;
nonostante+Prep		#;
oltre+Prep		#;
per% interesse		WITH-DEL;
per% mezzo		WITH-DEL;
per+Prep		#;
per+Prep^=il+Art+Def+Masc+Pl:pei	#;	! per+i	  = pei
per+Prep^=il+Art+Def+Masc+Sg:pel	#;	! per+l	  = pel
per+Prep^=lo+Art+Def+Masc+Pl:gli	#;	! per+gli = pegli
presso			WITH-DI;
presso+Prep		#;
prima			WITH-DEL;
pro+Prep		#;
quanto	        	WITH-AL;
rasente+Prep		#;
riguardo		WITH-DI;	!? was: prePrep
rispetto		WITH-DI;	!? was: prePrep
rispetto		WITH-AL;
salvo+Prep		#;
secondo+Prep		#;
senza+Prep		ELID;		! senza, senz'
sin+Prep		#;
sino+Prep		#;
sopra			WITH-DI;
sopra+Prep		#;
sotto			WITH-DI;
sotto+Prep		ELID;		! sotto, sott'
stante+Prep		#;
su			WITH-DI;
su+Prep			#;
su+Prep^=:su		ARTICLE;	! sullo, sulla, sulli, sulle, sul, sui, ?su' 
tolto+Prep		#;
tra+Prep		#;
tramite+Prep		#;
tranne+Prep		#;
tratto+Prep		#;
verso+Prep		#;
via+Prep		#;
vicino			WITH-DI;	!? was: prePrep
vicino+Prep		#;

!# ------------------------------------------------------------------------
!# PERSONAL PRONOUNS (Note: added ferature +Pers)

io+Pron+Pers+Nom+1P+MF+Sg:io			#;
io+Pron+Pers+Acc+Aton+1P+MF+Sg:mi		ELID;
io+Pron+Pers+Acc+Aton+1P+MF+Sg+Preclit:me	#;	!? added
io+Pron+Pers+Acc+Ton+1P+MF+Sg:me		#;
io+Pron+Pers+Dat+Aton+1P+MF+Sg:mi		ELID;

tu+Pron+Pers+Nom+2P+MF+Sg:tu			#;
tu+Pron+Pers+Acc+Aton+2P+MF+Sg:ti		ELID;
tu+Pron+Pers+Acc+Aton+2P+MF+Sg+Preclit:te	#;	!? added
tu+Pron+Pers+Acc+Ton+2P+MF+Sg:te		#;
tu+Pron+Pers+Dat+Aton+2P+MF+Sg:ti		ELID;

lui+Pron+Pers+Nom+3P+Masc+Sg:lui		#;
lui+Pron+Pers+Nom+3P+Fem+Sg:lei			#;
lui+Pron+Pers+Acc+Aton+3P+Masc+Sg:lo		ELID;
lui+Pron+Pers+Acc+Aton+3P+Fem+Sg:la		ELID;
lui+Pron+Pers+Acc+Ton+3P+Fem+Sg:lei		#;
lui+Pron+Pers+Acc+Ton+3P+Masc+Sg:lui		#;
lui+Pron+Pers+Dat+Aton+3P+Fem+Sg:le		#;	!? ELID?
lui+Pron+Pers+Dat+Aton+3P+Masc+Sg:gli		#;	!? ELID?

lui+Pron+Pers+Dat+Aton+3P+MF+Sg^=:glie		GLI-PRON;	! glielo, etc.

noi+Pron+Pers+Nom+1P+MF+Pl:noi			#;
noi+Pron+Pers+Acc+Aton+1P+MF+Pl:ci		ELID;
noi+Pron+Pers+Acc+Aton+1P+MF+Pl+Preclit:ce	#;	!? added
noi+Pron+Pers+Acc+Ton+1P+MF+Pl:noi		#;
noi+Pron+Pers+Dat+Aton+1P+MF+Pl:ci		ELID;

noialtri+Pron+Pers+Nom+1P+Fem+Pl:noialtre		#;
noialtri+Pron+Pers+Nom+1P+Masc+Pl:noialtri		#;

voi+Pron+Pers+Nom+2P+MF+Pl:voi			#;
voi+Pron+Pers+Acc+Aton+2P+MF+Pl:vi		ELID;
voi+Pron+Pers+Acc+Aton+2P+MF+Pl+Preclit:ve	#;	!? added
voi+Pron+Pers+Acc+Ton+2P+MF+Pl:voi		#;
voi+Pron+Pers+Dat+Aton+2P+MF+Pl:vi		ELID;

voialtri+Pron+Pers+Nom+2P+Fem+Pl:voialtre		#;
voialtri+Pron+Pers+Nom+2P+Masc+Pl:voialtri		#;

loro+Pron+Pers+Nom+3P+MF+Pl:loro		#;
loro+Pron+Pers+Acc+Aton+3P+Fem+Pl:le		#;
loro+Pron+Pers+Acc+Aton+3P+Masc+Pl:li		#;
loro+Pron+Pers+Acc+Ton+3P+MF+Pl:loro		#;

sé+Pron+Refl+Ton+3P+MF+SP:sé			#;
si+Pron+Refl+Aton+3P+MF+SP:si			ELID;
si+Pron+Refl+Aton+3P+MF+SP+Preclit:se		#;	!? added

!# ------------------------------------------------------------------------
!# POSSESSIVE PRONOUNS

mio+Pron+Poss+Fem+Pl:mie			#;
mio+Pron+Poss+Fem+Sg:mia			#;
mio+Pron+Poss+Masc+Pl:miei			#;
mio+Pron+Poss+Masc+Sg:mio			#;

tuo+Pron+Poss+Fem+Pl:tue			#;
tuo+Pron+Poss+Fem+Sg:tua			#;
tuo+Pron+Poss+Masc+Pl:tuoi			#;
tuo+Pron+Poss+Masc+Sg:tuo			#;

suo+Pron+Poss+Fem+Pl:sue			#;
suo+Pron+Poss+Fem+Sg:sua			#;
suo+Pron+Poss+Masc+Pl:suoi			#;
suo+Pron+Poss+Masc+Sg:suo			#;

nostro+Pron+Poss+Fem+Pl:nostre			#;
nostro+Pron+Poss+Fem+Sg:nostra			#;
nostro+Pron+Poss+Masc+Pl:nostri			#;
nostro+Pron+Poss+Masc+Sg:nostro			#;

vostro+Pron+Poss+Fem+Pl:vostre			#;
vostro+Pron+Poss+Fem+Sg:vostra			#;
vostro+Pron+Poss+Masc+Pl:vostri			#;
vostro+Pron+Poss+Masc+Sg:vostro			#;

loro+Pron+Poss+MF+Pl:loro			#;
loro+Pron+Poss+MF+Sg:loro			#;

proprio+Pron+Poss+Fem+Pl:proprie		#;	!?
proprio+Pron+Poss+Fem+Sg:propria		#;	!?
proprio+Pron+Poss+Masc+Pl:propri		#;	!?
proprio+Pron+Poss+Masc+Sg:proprio		#;	!?

mio+Adj+Poss+Fem+Pl:mie				#;
mio+Adj+Poss+Fem+Sg:mia				#;
mio+Adj+Poss+Masc+Pl:miei			#;
mio+Adj+Poss+Masc+Sg:mio			#;

tuo+Adj+Poss+Fem+Pl:tue				#;
tuo+Adj+Poss+Fem+Sg:tua				#;
tuo+Adj+Poss+Masc+Pl:tuoi			#;
tuo+Adj+Poss+Masc+Sg:tuo			#;

suo+Adj+Poss+Fem+Pl:sue				#;
suo+Adj+Poss+Fem+Sg:sua				#;
suo+Adj+Poss+Masc+Pl:suoi			#;
suo+Adj+Poss+Masc+Sg:suo			#;

nostro+Adj+Poss+Fem+Pl:nostre			#;
nostro+Adj+Poss+Fem+Sg:nostra			#;
nostro+Adj+Poss+Masc+Pl:nostri			#;
nostro+Adj+Poss+Masc+Sg:nostro			#;

vostro+Adj+Poss+Fem+Pl:vostre			#;
vostro+Adj+Poss+Fem+Sg:vostra			#;
vostro+Adj+Poss+Masc+Pl:vostri			#;
vostro+Adj+Poss+Masc+Sg:vostro			#;

loro+Adj+Poss+MF+Pl:loro			#;
loro+Adj+Poss+MF+Sg:loro			#;

altrui+Adj+Poss+MF+Pl:altrui			#;
altrui+Adj+Poss+MF+Sg:altrui			#;

proprio+Adj+Poss+Fem+Pl:proprie			#;
proprio+Adj+Poss+Fem+Sg:propria			#;
proprio+Adj+Poss+Masc+Pl:propri			#;
proprio+Adj+Poss+Masc+Sg:proprio		#;

!# ------------------------------------------------------------------------
!# DEMONSTRATIVE PRONOUNS AND DETERMINERS

ciò+Pron+Dem+Masc+Sg:ciò			#;	!?
ci+Pron+Dem+Masc+Sg:ci				#;	!? ELID? (ELID: Popular register)
lo+Pron+Dem+Masc+Sg:lo				#;	!?
vi+Pron+Dem+Masc+Sg:vi				#;	!? ELID? (ELID: formal register)

!idem+Pron+Dem+MF+SP:idem			#;	!? rather Adv; IN Technical contexts (bibliographies) it is rather Pron+Dem+MF+SP:idem
!tanto+Pron+Dem+Masc+Sg				ELID;	!? rather QuantPron? (It may be pron.dimostr. as well: ciò, tutto questo: t. ti serva di risposta, per ora t. ti basti

colui+Pron+Dem+Fem+Sg:colei			#;
colui+Pron+Dem+Masc+Sg:colui			#;
colui+Pron+Dem+MF+Pl:coloro			#;

costui+Pron+Dem+Fem+Sg:costei			#;
costui+Pron+Dem+Masc+Sg:costui			#;
costui+Pron+Dem+MF+Pl:costoro			#;

codesto^]+Pron+Dem				MFSGPL;

cotesto^]+Pron+Dem				MFSGPL;
medesimo^]+Pron+Dem				MFSGPL;
quegli+Pron+Dem+Masc+Sg				#;
quello^]+Pron+Dem				MFSGPL;
questi+Pron+Dem+Masc+Sg				#;
questo^]+Pron+Dem				MFSGPL;
stesso^]+Pron+Dem				MFSGPL;

tale+Pron+Dem+MF+Pl:tali			#;
tale+Pron+Dem+MF+Sg:tale			#;


medesimo+Det+Dem+Masc+Sg:medesimo		#;
medesimo+Det+Dem+Fem+Sg:medesima		#;
medesimo+Det+Dem+Fem+Pl:medesime		#;
medesimo+Det+Dem+Masc+Pl:medesimi		#;

stesso+Det+Dem+Masc+Sg:stesso			#;
stesso+Det+Dem+Fem+Sg:stessa			#;
stesso+Det+Dem+Fem+Pl:stesse			#;
stesso+Det+Dem+Masc+Pl:stessi			#;

!#  ------------------------------------------------------------------------
!# RELATIVE PRONOUNS

che+Pron+Rel+MF+SP:che				ELID;
chi+Pron+Rel+MF+Sg:chi				#;
cui+Pron+Rel+MF+SP:cui				#;
quale+Pron+Rel+MF+Pl:quali			#;
quale+Pron+Rel+MF+Sg:quale			#;
quanto+Pron+Rel+Fem+Pl:quante			#;
quanto+Pron+Rel+Fem+Sg:quanta			#;
quanto+Pron+Rel+Masc+Pl:quanti			#;
quanto+Pron+Rel+Masc+Sg:quanto			#;

quale+Det+Rel+MF+Sg:quale			#;
quale+Det+Rel+MF+Pl:quali			#;

!#  ------------------------------------------------------------------------
!# INDEFINITE PRONOUNS

alcunché+Pron+Indef+MF+Sg:alcunché		#;
altri+Pron+Indef+Masc+Sg:altri			#;
altro+Pron+Indef+Fem+Pl:altre			#;
altro+Pron+Indef+Fem+Sg:altra			#;
altro+Pron+Indef+Masc+Pl:altri			#;
altro+Pron+Indef+Masc+Sg:altro			#;
altrui+Pron+Indef+MF+Sg:altrui			#;
ambedue+Pron+Indef+MF+Pl:ambedue		#;
ambo+Pron+Indef+MF+Pl:ambo			#;
checché+Pron+Indef+Masc+Sg:checché		#;
checchessia+Pron+Indef+Masc+Sg:checchessia	#;
che+Pron+Indef+MF+Sg:che			#;
chicchessia+Pron+Indef+MF+Sg:chicchessia	#;
entrambi+Pron+Indef+Fem+Pl:entrambe		#;
entrambi+Pron+Indef+Masc+Pl:entrambi		#;
niente+Pron+Indef+MF+Sg:niente			#;
nulla+Pron+Indef+MF+Sg:nulla			#;
ognuno+Pron+Indef+Fem+Sg:ognuna			#;
ognuno+Pron+Indef+Masc+Sg+Apoc:ognun		#;
ognuno+Pron+Indef+Masc+Sg:ognuno		#;
qualcheduno+Pron+Indef+Fem+Sg:qualcheduna	ELID;
qualcheduno+Pron+Indef+Masc+Sg+Apoc:qualchedun	#;
qualcheduno+Pron+Indef+Masc+Sg:qualcheduno	#;
qualche% cosa+Pron+Indef+Masc+Sg:qualche% cosa	#;
qualcosa+Pron+Indef+Masc+Sg:qualcosa		ELID;
qualcuno+Pron+Indef+Fem+Sg:qualcuna		ELID;
qualcuno+Pron+Indef+Masc+Sg+Apoc:qualcun	#;
qualcuno+Pron+Indef+Masc+Sg:qualcuno		#;
talaltro+Pron+Indef+Fem+Pl:talaltre		#;
talaltro+Pron+Indef+Fem+Sg:talaltra		#;
talaltro+Pron+Indef+Masc+Pl:talaltri		#;
talaltro+Pron+Indef+Masc+Sg:talaltro		#;
tutto+Pron+Indef+Fem+Pl:tutte			#;
tutto+Pron+Indef+Fem+Sg:tutta			#;
tutto+Pron+Indef+Masc+Pl:tutti			#;
tutto+Pron+Indef+Masc+Sg:tutto			#;
uno+Pron+Indef+Fem+Pl:une			#;
uno+Pron+Indef+Fem+Sg:una			#;
uno+Pron+Indef+Masc+Pl:uni			#;
uno+Pron+Indef+Masc+Sg:uno			#;

!# ------------------------------------------------------------------------
!# INTERROGATIVE PRONOUNS
che+Pron+Interrog+MF+SP:che			#;
chi+Pron+Interrog+MF+Sg:chi			#;
chiunque+Pron+Indef+MF+Sg:chiunque		#;
cosa+Pron+Interrog+MF+Sg:cosa			ELID;
quale+Pron+Interrog+MF+Pl:quali			#;
quale+Pron+Interrog+MF+Sg:quale			#;
quale+Pron+Interrog+MF+SP+Apoc:qual		#;
quanto+Pron+Interrog+Fem+Pl:quante		#;
quanto+Pron+Interrog+Fem+Sg:quanta		#;
quanto+Pron+Interrog+Masc+Pl:quanti		#;
quanto+Pron+Interrog+Masc+Sg:quanto		#;

quale+Det+Interrog+MF+Sg:quale			#;
quale+Det+Interrog+MF+Pl:quali			#;


!# ------------------------------------------------------------------------
!# TO CLASSIFY 

egli+Pron+Pers+Nom+3P+Fem+Sg:ella		#;
egli+Pron+Pers+Nom+3P+Masc+Sg:egli		#;
essi+Pron+Pers+Acc+Ton+3P+Fem+Pl:esse		#;
essi+Pron+Pers+Acc+Ton+3P+Masc+Pl:essi		#;
essi+Pron+Pers+Nom+3P+Fem+Pl:esse		#;
essi+Pron+Pers+Nom+3P+Masc+Pl:essi		#;
esso+Pron+Pers+Acc+Ton+3P+Fem+Sg:essa		#;
esso+Pron+Pers+Acc+Ton+3P+Masc+Sg:esso		#;
esso+Pron+Pers+Nom+3P+Fem+Sg:essa		#;
esso+Pron+Pers+Nom+3P+Masc+Sg:esso		#;

ne+Pron+Ne+MF+SP:ne				ELID;



!# ------------------------------------------------------------------------
!# QUANTIFIERS

!!!! CHECK: change into +Det +Quant ?? 
qualunque+Quant+MF+Sg				#;
qualsivoglia+Quant+MF+Sg			#;
qualsisia+Quant+MF+Sg				#;
qualsiasi+Quant+MF+Sg				#;
qualche+Quant+MF+Sg				#;
ogni+Quant+MF+Sg				#;

più+Quant+MF+SP					#;	! più gente	!AS@26/08/2008
meno+Quant+MF+SP				#;	! meno tempo 	!AS@26/08/2008


!!!! CHECK: change into +Pron +Quant ???
alcuno+QuantPron+Fem+Pl:alcune			#;
alcuno+QuantPron+Fem+Sg:alcuna			#;
alcuno+QuantPron+Masc+Pl:alcuni			#;
alcuno+QuantPron+Masc+Sg:alcuno			#;
alcuno+QuantPron+Masc+Sg+Apoc:alcun		#;

alquanto+QuantPron+Fem+Pl:alquante		#;
alquanto+QuantPron+Fem+Sg:alquanta		#;
alquanto+QuantPron+Masc+Pl:alquanti		#;
alquanto+QuantPron+Masc+Sg:alquanto		#;

altrettanto+QuantPron+Fem+Pl:altrettante	#;
altrettanto+QuantPron+Fem+Sg:altrettanta	#;
altrettanto+QuantPron+Masc+Pl:altrettanti	#;
altrettanto+QuantPron+Masc+Sg:altrettanto	#;

certo+QuantPron+Fem+Pl:certe			#;
certo+QuantPron+Masc+Pl:certi			#;

certuno+QuantPron+Fem+Pl:certune		#;
certuno+QuantPron+Fem+Sg:certuna		#;
certuno+QuantPron+Masc+Pl:certuni		#;
certuno+QuantPron+Masc+Sg+Apoc:certun		#;
certuno+QuantPron+Masc+Sg:certuno		#;

ciascheduno+QuantPron+Fem+Sg:ciascheduna	#;
ciascheduno+QuantPron+Masc+Sg+Apoc:ciaschedun	#;
ciascheduno+QuantPron+Masc+Sg:ciascheduno	#;

ciascuno+QuantPron+Fem+Sg:ciascuna		#;
ciascuno+QuantPron+Masc+Sg+Apoc:ciascun		#;
ciascuno+QuantPron+Masc+Sg:ciascuno		#;

moltissimo+QuantPron+Fem+Pl:moltissime		#;
moltissimo+QuantPron+Fem+Sg:moltissima		#;
moltissimo+QuantPron+Masc+Pl:moltissimi		#;
moltissimo+QuantPron+Masc+Sg:moltissimo		#;

molto+QuantPron+Fem+Pl:molte			#;
molto+QuantPron+Fem+Sg:molta			#;
molto+QuantPron+Masc+Pl:molti			#;
molto+QuantPron+Masc+Sg:molto			#;

nessuno+QuantPron+Fem+Sg:nessuna		#;
nessuno+QuantPron+Masc+Sg+Apoc:nessun		#;
nessuno+QuantPron+Masc+Sg:nessuno		#;

niuno+QuantPron+Fem+Sg:niuna			#;
niuno+QuantPron+Masc+Sg+Apoc:niun		#;
niuno+QuantPron+Masc+Sg:niuno			#;

parecchio+QuantPron+Fem+Pl:parecchie		#;
parecchio+QuantPron+Fem+Sg:parecchia		#;
parecchio+QuantPron+Masc+Pl:parecchi		#;
parecchio+QuantPron+Masc+Sg:parecchio		#;

pochissimo+QuantPron+Fem+Pl:pochissime		#;
pochissimo+QuantPron+Fem+Sg:pochissima		#;
pochissimo+QuantPron+Masc+Pl:pochissimi		#;
pochissimo+QuantPron+Masc+Sg:pochissimo		#;

poco+QuantPron+Fem+Pl:poche			#;
poco+QuantPron+Fem+Sg:poca			#;
poco+QuantPron+Masc+Pl:pochi			#;
poco+QuantPron+Masc+Sg+Apoc:po'			#;
poco+QuantPron+Masc+Sg:poco			#;

quanto+QuantPron+Fem+Pl:quante			ELID;
quanto+QuantPron+Fem+Sg:quanta			ELID;
quanto+QuantPron+Masc+Pl:quanti			ELID;
quanto+QuantPron+Masc+Sg:quanto			ELID;

tale+QuantPron+MF+Pl:tali			#;
tale+QuantPron+MF+Sg:tale			#;

taluno+QuantPron+Fem+Pl:talune			#;
taluno+QuantPron+Fem+Sg:taluna			#;
taluno+QuantPron+Masc+Pl:taluni			#;
taluno+QuantPron+Masc+Sg+Apoc:talun		#;
taluno+QuantPron+Masc+Sg:taluno			#;

tantissimo+QuantPron+Fem+Pl:tantissime		#;
tantissimo+QuantPron+Fem+Sg:tantissima		#;
tantissimo+QuantPron+Masc+Pl:tantissimi		#;
tantissimo+QuantPron+Masc+Sg:tantissimo		#;

tanto+QuantPron+Fem+Pl:tante			#;
tanto+QuantPron+Fem+Sg:tanta			#;
tanto+QuantPron+Masc+Pl:tanti			#;
tanto+QuantPron+Masc+Sg:tanto			#;
	
troppo+QuantPron+Fem+Pl:troppe			#;
troppo+QuantPron+Fem+Sg:troppa			#;
troppo+QuantPron+Masc+Pl:troppi			#;
troppo+QuantPron+Masc+Sg:troppo			#;

tutto+QuantPron+Fem+Pl:tutte			#;
tutto+QuantPron+Fem+Sg:tutta			#;
tutto+QuantPron+Masc+Pl:tutti			#;
tutto+QuantPron+Masc+Sg:tutto			#;

veruno+QuantPron+Fem+Pl:verune			#;
veruno+QuantPron+Fem+Sg:veruna			#;
veruno+QuantPron+Masc+Pl:veruni			#;
veruno+QuantPron+Masc+Sg+Apoc:verun		#;
veruno+QuantPron+Masc+Sg:veruno			#;

!# Adverbs

tutto+Adv					ELID;	!? tutt'altro YES
ci+Adv						ELID;
vi+Adv						ELID;



!# ========================================================================
LEXICON PREP-A		
a+Prep			#;
a+Prep+Euph:ad		#;

LEXICON PREP-AL
PREP-A;
a+Prep^=:a		ARTICLE;

LEXICON PREP-DA
da+Prep			#;

LEXICON PREP-DAL
PREP-DA;
da+Prep^=:da		ARTICLE ;

LEXICON PREP-DI
di+Prep			ELID;
di+Prep+Apoc:de		#;


LEXICON PREP-DEL
PREP-DI;
di+Prep^=:de		ARTICLE ;

LEXICON WITH-DI		< %  >	PREP-DI;

LEXICON WITH-AL		< %  >	PREP-AL;
LEXICON WITH-DAL	< %  >	PREP-DAL;
LEXICON WITH-DEL	< %  >	PREP-DEL;
LEXICON WITH-DALDEL	WITH-DAL; WITH-DEL;

LEXICON ARTICLE
lo+Art+Def+Masc+Sg:llo		ELID;
lo+Art+Def+Fem+Sg:lla		ELID;
lo+Art+Def+Masc+Pl:gli		ELID;
lo+Art+Def+Fem+Pl:lle		#;		! ELID

il+Art+Def+Masc+Sg:l		#;	! al, dal, del
il+Art+Def+Masc+Pl:i		#;	! ai, dai, dei
il+Art+Def+Masc+Pl+Apoc:%'	#;	! a', da', de'

!# ------------------------------------------------------------------------

LEXICON GLI-PRON	! preceeded by "glie-"
ne+Pron+Ne+MF+SP:ne				#;
lui+Pron+Pers+Acc+Aton+3P+Masc+Sg:lo		ELID;
lui+Pron+Pers+Acc+Aton+3P+Fem+Sg:la		ELID;
loro+Pron+Pers+Acc+Aton+3P+Masc+Pl:li		#;
loro+Pron+Pers+Acc+Aton+3P+Fem+Pl:le		#;

!# -------------------------------------------------------
!# Continuation class for (optional) elision of final vowel
!# e.g.  di --> di or d'

LEXICON ELID
  	#;  
+Elided	#;


!# general continuation classes
LEXICON	MSG	+Masc+Sg	#;
LEXICON	FSG	+Fem+Sg		#;
LEXICON	MPL	+Masc+Pl	#;
LEXICON	FPL	+Fem+Pl		#;

LEXICON MSGPL	MSG; MPL;
LEXICON FSGPL	FSG; FPL;
LEXICON MFSGPL	MSGPL; FSGPL;

