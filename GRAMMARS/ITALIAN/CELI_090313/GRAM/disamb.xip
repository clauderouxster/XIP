//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Copyright (c) 2006 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//! File:	disamb.xip
//! Content:	rules for POS disambiguation
//!
//! Language:	ITALIAN
//! ENCODING:	UTF-8	[€àâáäã]
//!
//! Author:	Anne Schiller
//!
//! Date: 	17-Jan-2006	:: initial version
//! Update:	08-Apr-2008	:: changed to UTF-8
//!
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Tagging:

// ------------------------------------------------------------------------------------------------------

// 22.8.2008 (SM):
// tutta la tua grande forza 
//1> noun<sg>,noun<pl> = |(det<sg>), (adj<sg>), (adj<sg>)| noun<sg> .
// 27.8.2008 (SM):
// ha impiegato 12h 25'
1> noun<sg:+>, noun<pl:+> = | (det<sg:+>), (adj<sg:+>), adj<sg:+>, ~noun | noun<sg:+> ~| (adj)*, noun |.

1> noun,verb,adj = noun[prop] |noun[postmod:+,featprop]|.

1> noun, symbol = |num[dig:+]| symbol | num[dig:+]|.

1> verb<form:favere>, symbol<lemma:ha> = | noun | verb<form:favere> .
1> verb<form:favere>, symbol<lemma:ha> = | adv | verb<form:favere> .
1> verb<form:favere>, symbol<lemma:ha> = | num+ | symbol<lemma:ha> .

1> ?<surface:deriva> = | noun | verb .
1> ?<surface:deriva> = verb | ~verb[fin] | .
1> ?<surface:deriva> = | det | noun .
1> ?<surface:deriva> = | det | noun .

1> noun,adj = | noun[title,people] | noun[maj] .

// Prep ~ Adj
1> prep;adj = |verb[form:fessere]| adj.



1> adj<lemma:pronto>;interj<lemma:pronto> = |punct[quote]| interj |punct[quote] |.
1> adj<lemma:pronto>;interj<lemma:pronto> = |verb[form:fessere]| adj .




//Disambiguation of "gli" pron / det[art]. "Gli stessi giudici"; "Gli stessi dicendo".
1> det[art],pron = det[art] | (adj), (adv), (adj), noun, prep~| .
1> det[art],pron = det[art] | pron[dem:+,poss:+] |.

// 6.11.2008 (SM):
// Le ' vacanze ' annunciate
//1> det[art],pron = det[art] | noun| .		// ex: " Le case"
1> det[art:+], pron = det[art:+] | (punct[quote:+]), noun |.

1> det[art],pron = det[art] | adj | .		// ex: "La bella statuina."; ex: "Gli stessi giudici"---Ca marche !!!! 

// 9.10.2008 (SM):
// Lo fa Dashim Shehi, presidente della commissione Industria.
//1> det[art],pron = det[art] | adv | .		// ex: "la sempre maggiore diffidenza."
1> det[art],pron = det[art] | adv[verb:~] | .

1> det[art],pron = det[art] | pron[dat:~,acc:~]|. 	// "Le stesse"
1> det[art],pron = det[art] | num | .		// "Verso le 10.15"
1> det[poss],pron = det[art] | num | .		// "Verso le 10.15"

1> det[art],pron<acc> = |conjche| pron[acc:+,aton:+] | verb[ind:+]|.
1> det[art],pron<acc> = |pron, pron<dat,preclit>| pron<acc> .
1> det[art],pron<acc> = |pron<dat,preclit>| pron<acc> | verb<ind>|.
1> det[art],pron<acc> = |pron<dat,preclit>| pron<acc> | verb<subj>|.
1> det[art],pron<acc> = |pron<dat,preclit>| pron<acc> | verb<impv>|.
1> det[art],pron = pron | verb<prespart:~;pastpart:~;inf:~,gerund:~,impv:~> | .  / ex: "Gli stessi parlando.  nfv~;gv~\


// 22.8.2008 (SM):
// questa tua vecchia macchina
// 25.8.2008 (SM):
// questi due tuoi vecchi amici
// 4.9.2008 (SM):
// Anche questa volta Valona si é guadagnata la prima pagina della breve storia dell'Albania .
1> adj<det:+>, det<dem:+>, pron = ~| det | adj | (adj), (num), adj*, noun[adj:~] | .


// Prep ~ Verb (namely, "Dai, Dallo, Dalla, Dagli, Dalle"):

1> prep;verb<impv> = verb[impv:+]   | pron*[nom:+,dem:+], det[art:+], ~noun |.
1> prep;verb<impv> = verb[impv:+]   | prep |.
1> prep;verb<impv> = | verb[pastpart:+] |   prep   | adj[poss:+]|.
1> prep;verb<impv> = | adj |  prep  | adj[poss:+]|.
1> prep;verb<impv> = prep   | adj[poss:+]|.
1> prep;verb<impv> = prep   | det, noun |.
1> prep;verb<impv> = prep   | (adv), (adj), noun|.



1> adj,pron,verb<form:fstare> = |pron| verb<form:fstare> |verb[gerund:+,prespart:~,pastpart:~,fin:~,inf:~], punct~, conj~|.
1> adj,pron,verb<form:fstare> = |det[art:+]| adj | (adj), (det[poss:+]), (num), noun|. 
1> adj,pron,verb<form:fstare> = |det[art:+]| pron |verb[pastpart:+,fin:+,inf:+], punct, conj|.




//"Un altro" un=det[art].
1> det[art,indef],num = det[art,indef] |pron|.
1> det[art,indef],num = det[art,indef] |adj*, noun|.

//"Uno dei miei cani; uno dei migliori; una delle componenti fondamentali.
1> det[art,indef];pron<indef> = pron<indef> | prep|.

1> ?[form:fora] = | det[art], num*| noun |prep*|.
1> ?[form:fora] = | num, ~punct| noun |prep*|.
1> ?[form:fora] = |prep, adj | noun.
1> ?[form:fora] = adv | det[art]|.
1> ?[form:fora] = |prep | adv.
1> ?[form:fora] = |pron, (punct[comma:+]) | adv.
1> ?[form:fora] = | punct[comma:+] | adv.



// upper case words (other than at the beginning of a sentence) must be a  proper name
1> ?[maj:+,first:~] = noun[prop:+].

// a guessed noun with a capital is a proper noun "Giovanni Depau"
1> noun[maj:+] = noun[prop=+,masc=+,fem=+,sg=+,pl=+,maj=+, src:guess].
//1> ?<maj:+,start:~> = ?<prop:+> .

//Addio : interj ~ noun
1> noun<lemma:addio>,interj<lemma:addio> = |det[art] | noun<lemma:addio>.
1> noun<lemma:addio>,interj<lemma:addio> = |?[poss:+] | noun<lemma:addio>.
1> noun<lemma:addio>,interj<lemma:addio> = | ?[dem:+] | noun<lemma:addio>.



//Punta :{noun:already done}; FV [presind/ impv Puntare] ~ NFV[pastpartPungere]

1> verb<ind>,verb<pastpart> = | verb[form:fessere,ind:+], adv*, verb[form:fessere,pastpart:+], (adv) | verb<pastpart>. //"Era (già) stata punta" ; Era stata già punta.
1> verb<ind>,verb<pastpart> = | prep, noun, adj* | verb<pastpart> |prep, noun|.
1> verb<ind>,verb<pastpart> = | prep, noun[pl:+], adj* | verb<pastpart,pl> |prep, noun|.
1> verb<ind>,verb<pastpart> = |pron[refl:+], verb[form:fessere,ind:+], adv*| verb<pastpart>. //"Si è (Era) (già) punta" .
1> verb<ind>,verb<pastpart> = |pron[dat:+,acc:+], verb[form:favere,ind:+], adv*| verb<pastpart>. //"L'aveva (già) punta".

// 25.9.2008 (SM):
// ha rivolto un appello finale
//1> verb<ind>,verb<pastpart> = |noun*, (pron[nom:+]), (pron[dat:+,acc:+]) | verb<ind> | adv*, det[art] | . //Lui gli puntò la pistola sul petto.
// 29.8.2008 (SM):
// 9.3.2009 (SM):
// nel 1912 proclamò per la prima volta l'indipendenza nazionale
1> verb<ind:+>, verb<pastpart:+> = | noun*, (pron[nom:+]), (pron[dat:+, acc:+]), ?[verb:~, adj:~] | verb<ind>
 | adv*, det[art:+] |.

1> verb<ind>,verb<pastpart> = |det[art:+,tokenend:~], noun, (pron[nom:+]), (pron[dat:+,acc:+]) | verb<ind> | adv*, prep | . //La Juve punta allo scudetto.
1> verb<ind>,verb<pastpart> = | noun[prop:+], (pron[nom:+]), (pron[dat:+,acc:+]) | verb<ind> | adv*, prep | . //La Juve punta allo scuadjto.



// I legami commerciali diretti[noun:~]. La maggiore[adj] competitivitï¿½[noun] dei mercati. Il Dip. di Stato consiglia di seguire le notizie dei media[noun].
//Warning: I consigli commerciali diretti[pastpart] ai consumatori...;

1> noun;adj = | prep, det[art], punct*[quote] | adj | punct*[quote], noun |.
1> noun;adj = | adv[form:fpiù]| adj .

// 7.10.2008 (SM):
// Per Azem Hajdari molti politici sono coinvolti nel crack delle finanziarie.
//1> noun;adj = | noun, adj | adj .
1> noun; adj = | noun, adj[quant:~] | adj .

// 28.8.2008 (SM):
// e forse anche di quella italiana.
//1> noun;adj = | noun | adj .
1> noun; adj = | noun[adj:~]; pron[dem:+] | adj .

1> noun;adj = | noun, adj, conj<coord> | adj .
1> noun;adj = | (verb[pastpart:+]), prep | adj | noun |.
1> noun;adj = | prep[lemma:per] | adj .

// 24.11.2008 (SM):
// il generale Ferrero promise l' indipendenza
//1> noun;adj = | (prep), det[art], (adv) | adj | noun |.
1> noun; adj = | (prep), det[art:+], (adv) | adj | noun[prop:~] |.

// 13.11.2008 (SM):
// Ho chiamato l'ambulanza e siamo corsi verso l'ospedale.
//1> noun;adj = | ~adv[lemma:ci], verb[form:fessere] | adj .
1> noun[pastpart:~]; adj = | ~adv[lemma:ci], verb[form:fessere] | adj .

1> noun;adj = | noun, adj, conj<coord> | adj | ~adj|.
1> noun;adj = | (prep), det[art], (adv) | adj | num |.

// 9.3.2009 (SM):
// Noi non vogliamo uccidere civili - conclude Mehemet - .
1> noun; adj = | verb | noun.

// 9.3.2009 (SM):
// per la prima grande manifestazione
//1> noun;adj = | (prep), det[art] | noun | ~noun|.
1> noun;adj = | (prep), det[art:+] | noun ~| (adj), noun |.

1> noun;adj = | (prep), quant | noun | ~noun|.

// 12.11.2008 (SM):
// con 309,4 miliardi di lire di export
1> noun; adj = | prep[lemma:di] | noun.

// 13.11.2008 (SM):
// Oltre 100 mila albanesi, tra cui anche avvocati, medici e giornalisti, le hanno creduto.
1> noun; adj = | noun, punct[comma:+]; conj[coord:+] | noun.

// 22.8.2008 (SM):
// questa tua vecchia macchina
// 25.8.2008 (SM):
// ci è molto utile
// 26.8.2008 (SM):
// i nostri cari politici
//1> noun;adj = | adj*| noun | ?*[det:~, noun:~] |.
1> noun; adj = | (det), adj+, (~adv) | noun ~| noun; pron |.
// 26.8.2008 (SM):
// i nostri cari politici
1> noun; adj = | (det), adj+, (~adv) | noun[end:+].
// 26.8.2008 (SM):
// i nostri cari politici.
1> noun; adj = | (det), (adj), (~adv) | noun | punct[end:+] |.

1> noun;adj = | num | noun | ~noun |.
1> noun;adj = | ~noun, adj, ~conj[coord]| noun | ~noun |.
1> noun[maj:+,prop:+,firstname:+],verb<pastpart>,adj = | noun[lemma:papa] | noun[maj,prop,firstname] | num[dig:+,rom:+]|.

// 28.8.2008 (SM):
// con mezzi alternativi
// 9.3.2008 (SM):
// per la prima grande manifestazione
//1> noun; adj = | prep, (det) | noun | adj |.
1> noun; adj = | prep, (det) | noun | adj, ~?[noun:+] |.

// 29.8.2008 (SM):
// anche in Campania c'erano finanziarie che offrivano
//1> noun[prep:~]; adj = | verb[adv:~] | noun.
// 17.10.2008 (SM):
// E' vero che ...
1> noun[prep:~]; adj = | pron[loc:+], verb | noun.
1> noun[prep:~, pastpart:~]; adj = | verb[form:fessere] | adj.

// 9.9.2008 (SM):
// Chi sono i politici corrotti ?
// 9.3.2009 (SM):
//1> noun; adj = | det | noun.

1> noun<lemma:cosa>,pron<lemma:cosa> = | ?[dem:+] | noun.
1> noun<lemma:cosa>,pron<lemma:cosa> = | ?[det:+] | noun.
1> noun<lemma:cosa>,pron<lemma:cosa> = | ?[conjche:+] | pron.
1> noun<lemma:cosa>,pron<lemma:cosa> = pron | verb | .



// 26.8.2008 (SM):
// anche se interni e portiere sono identiche
//1> verb<ind>;adj = |pron*<nom,dat,acc,refl> | verb | ~noun|. // questa me la paghi! Questa politica SI INTEGRA in un'ideologia generale.
// 10.9.2008 (SM):
// annoverando tra le sue proprietà una catena di 20 supermercati, centri turistici e una miniera.
// 29.9.2008 (SM):
//1> verb<ind>; adj = | pron*<nom, dat, acc, refl> | verb ~| (adj), noun; conj[coord:+] |.

1> verb<ind>;adj = |pron<refl> | verb. 

1> verb<pastpart>;adj = |verb[form:favere], (adv)|  verb.   		//(mi) ha seguito)
1> verb<pastpart>;adj = |verb+<form:fessere>, (adv)|  verb<pastpart>.	// sono (stato)  (spesso) seguito
1> verb<pastpart>;adj = |adj | adj | noun|.			
1> verb<pastpart>;adj = |verb, det[art:+,def:+] | adj | noun|.
1> verb<pastpart>;adj = |det[art], noun| adj.
1> verb<pastpart>;adj = |det[art], noun| adj | ~prep<lemma:da>|.

// 17.10.2008 (SM):
// ... dove le assise del partito del cancelliere Vranitzsky sono in corso da giovedì . 
//1> verb<pastpart>;adj = verb<pastpart> | prep<lemma:da> |.
1> verb<pastpart:+>; adj = | ~prep | verb<pastpart:+> | prep<lemma:da> |.

// 10.11.2008 (SM):
// L' esplosione di rabbia é genuina ma viene anche ispirata e organizzata da malavitosi
1> verb<pastpart:+>; adj = verb<pastpart:+> | conj[coord:+], verb<pastpart:+> |.

1> verb<pastpart>;adj = |verb[form:favere], verb[pastpart:+], conj[coord]|  verb[pastpart].   		//ha organizzato e eseguito la rapina

1> verb<subj>;adj = |pron[nom:+,dat:+,acc:+,refl:+] | verb. //Voglio che tu paghi il giusto prezzo.

// 9.10.2008 (SM):
// Il ministro vive in un modesto appartamento.
//1> verb;adj = |noun[prop:+]| verb.  //WARNING: Ho visto le piante di Maria VIVE (ADJ)  e rigogliose.
1> verb; adj = | noun[prop:+]; noun[people:+] | verb.

// 13.11.2008 (SM):
// Il giovane continua a proporre servizi
1> verb; adj = verb | prep, verb[inf:+] |.

1> verb;adj = |noun[masc:+,pl:+]| adj[masc:+,pl:+].
1> verb;adj = |noun[masc:+,pl:+]| adj[masc:+,pl:+] |(conj), prep, (det[art])|.
1> verb;adj = |noun[masc:+,sg:+]| adj[masc:+,sg:+] |(conj), prep, (det[art])|.
1> verb;adj = |noun[masc:+,sg:+]| adj[masc:+,sg:+] .
1> verb<inf>;adj = |adj| verb<inf> .
1> verb<inf>;adj = |adv| verb<inf> .

// 10.3.2009 (SM):
// ... che dura ...
1> verb; adj = | conjche | verb.

// 28.8.2008 (SM):
// l'attività regolare degli impianti
//1> verb<inf>;adj = |noun| adj |adj|.
1> verb<inf>; adj = | (det), noun | adj | adj; prep |.

1> verb<impv>;adj = |prep, (det[art]), noun| adj .  // La lingua UFFICIALE della Repubblica è l'italiano.
1> verb;adj = |noun[prop:+,first:+]| verb .
1> verb<prespart>;adj = |det*[art], noun| adj |?*[noun:~]| .

// 4.11.2008 (SM):
// vogliamo tenere
//1> verb;adj = |verb, (pron[indef:+]) | adj . 
1> verb; adj = | verb[modal:~], (pron[indef:+]) | adj. 
1> verb[inf:+]; adj = | verb[modal:+], (pron) | verb. 


//fra prep~noun(frate)
1> noun;prep = | prep | noun |noun[prop:+]|.	// fra Martino. ATTENTION :Anna si trova fra Lucia e Agnese
1> noun;prep = | verb[stay_place:+],prep | noun | prep*, (det[art]), noun[prop:+, firstname:+]| .
1> noun;prep = | prep | noun | noun[prop:+], prep, noun[prop:+]|.		// fra Nicola da Laconi.
1> noun;prep = prep |noun[prop:+], (punct[comma:+]), (conj), noun[prop:+]|.	//Anna si trova fra Lucia e Agnese
1> noun;prep = prep |det[art]|.		// fra i fiancheggiatori.
1> noun;prep = prep |quant|.		// arriverà fra qualche giorno.
1> noun;prep = prep |det |.		//sono rinchiuso fra queste quattro mura domestiche.

// 9.10.2008 (SM):
// se è vero quanto lei afferma
//1> noun;prep = prep |pron|.		//fra questo e quello.
// 16.10.2008 (SM):
// dalla lettura di quanto si è visto in Italia
//1> noun; prep = prep | pron[nom:~] |.
1> noun; prep = | ~prep | prep | pron[nom:~] |.

1> noun;prep = prep | num |.		//fra due anni. Fra 2 anni.

// 28.8.2008 (SM):
// per via ferroviaria
1> noun; prep = | prep, (det) | noun | adj; noun |.

// 25.8.2008 (SM):
// nei prossimi anni
//1> noun;prep = prep | noun |.		//fra moglie e marito non mettere il dito.
1> noun; prep = prep | (adj), noun[pron:~] |.

// 6.10.2008 (SM):
// Nel 1924 il giovane Ahmed Zogu assunse il potere e quattro anni dopo fu proclamato re.
1> noun; verb<inf:+> = | det, (adj), (num) | noun.

//"La porta aperta" : det[art] noun adj ~ pron[dat] fv[portare presind] adj ~  det[art] nfv[porgere Pastpart] adj.
//"In questo posto": posto=noun ~posto nfv[porre Pastpart]
1> noun;verb<pastpart> = | det[art], (adj), (num) | noun.
1> noun;verb<pastpart> = | (det[art]), adj, (num) | noun.
1> noun;verb<pastpart> = | (det[art]), ~noun, adj | noun.

// 4.11.2008 (SM):
// strange rule!!
//1> noun;verb<pastpart> = | noun, (adj), verb[ind:+,subj:+,pastpart:~], adv*, (verb<pastpart>), (adj;adj;quant) | noun .

1> noun;verb<pastpart> = | noun, (adj), verb[ind:+,subj:+,pastpart:~], adv*, (verb<pastpart>), adv[form:fpiù]  | noun[sg:~].	//il Governo fascista è già intervenuto più volte
1> noun;verb<pastpart> = | quant | noun.

// 28.10.2008 (SM):
// Ciò nondimeno, come visto sopra, la disambiguazione basata sul contesto locale non é certa:
//1> noun;verb<pastpart> = | prep | noun.
1> noun[adj:~]; verb<pastpart:+> = | prep[conj:~] | noun.

1> noun;verb<pastpart> = | noun, (neg), verb[form:fessere], adv* | verb<pastpart> | prep*|.  //la mela è mangiata da Piero
1> noun;verb<pastpart> = | noun, (neg), verb<form:fessere>, adv*, verb<form:fessere,pastpart>, adv* | verb<pastpart> | prep*|.  //Michele è stato ACCUSATO 
1> noun;verb<pastpart> = | (neg), verb[form:fessere,aux:+], adv* | verb[pastpart:+,lemma:fare]. //E' gia stato fatto; non è mai più stato fatto.
1> noun;verb<pastpart> = | verb[pastpart:+], adv*[form:fpiù~] | verb[pastpart:+]. //E' stato gia fatto; non è stato mai più fatto.
1> noun;verb<pastpart> = | (neg), verb[form:fessere,gerund:+], adv* | verb[pastpart:+].  // essendo arrivato..., avendo già fatto..., non avendo mai (più) fatto...
1> noun;verb<pastpart> = | (neg), verb[form:favere,gerund:+], adv* | verb[pastpart:+].  // essendo arrivato..., avendo già fatto..., non avendo mai (più) fatto...
1> noun;verb<pastpart> = | verb[form:favere] | verb[pastpart:+].
1> noun;verb<pastpart> = |  noun | verb[pastpart:+].
1> noun;verb<pastpart> = |  punct[comma:+] | verb[pastpart:+] | det, adj*, noun | .

// 28.8.2008
// si è vista costretta a procedere
1> noun; verb<pastpart:+> = | pron, (neg), verb[form:fessere], adv* | verb<pastpart:+>.

// 12.11.2008 (SM):
// sono stato catturato e quindi condannato a sei mesi
1> noun; verb<pastpart:+> = | verb[pastpart:+], (conj[coord:+]), (?[adv:+]) | verb<pastpart:+>.

//PP{per NP{ragioni}} SC{BG{che} FV{abbiamo}} ADVP{già} NFV{espresso} ~NFV{ più volte}  NP { più volte}
1> noun[pl:+];verb<pastpart> = | (verb<pastpart>), adv| noun[pl:+].


1> noun;verb<prespart> = | det[art], (adj), (num) | noun.
1> noun;verb<prespart> = | det*[art], adj, (num) | noun.
1> noun;verb<prespart> = | (det[art]), ~noun, adj | noun.
1> noun;verb<prespart> = | quant | noun.
1> noun;verb<prespart> = | prep | noun.
1> noun;verb<prespart> = noun | prep<lemma:di> | .


// 21.11.2008 (SM)
// non soltanto finanziava il Partito democratico ma era diventata uno dei centri
//1> noun;verb<ind> = | noun, adj*,verb*<prespart>, conj[coord:+]| noun |verb<pastpart> |.	// Rumore stridente o sibilo emesso dopo lo spegnimento
1> noun; verb<ind:+, form:~fessere> = | noun, adj*, verb*<prespart:+>, conj[coord:+] | noun | verb<pastpart:+> |.

1> noun;verb<ind> = | noun, nfv*[prespart:+], conj[coord:+]| noun |verb[pastpart:+] |.	// Rumore stridente o sibilo emesso dopo lo spegnimento

1> noun;verb<ind> = |(noun,pron,adj), verb[form:favere,prespart:+] | noun .	// "Le persone / coloro aventi titolo"
1> noun;verb<ind> = noun |verb[prespart:+] |.	// con leggi e decreti miranti a preservarne la purezza.

// 26.9.2008 (SM):
// Valona é in mano alla folla, fuori da ogni controllo, la polizia é scomparsa, rintanata nelle caserme.
//1> noun;verb<ind> = |verb[form:favere], quant | noun .
1> noun; verb<ind> = | (verb[form:favere]), quant | noun .

1> noun;verb<ind> = |verb[form:favere,gerund:+] | noun .

// 9.3.2009 (SM):
// Dayton non ci ha reso giustizia .
//1> noun;verb<ind> = | verb[form:favere,ind:+], adv*, verb[form:favere,pastpart:+] | noun . //Da quando questo ha avuto inizio
1> noun; verb<ind:+> = | verb[form:favere, ind:+], adv*, verb[pastpart:+] | noun.

// 6.10.2008 (SM):
// queste condizioni non sono ancora presenti
//1> noun;verb<ind> = | det[art] |  noun  | (neg), verb | .
1> noun; verb<ind:+> = | det[art:+]; adj[det:+] | noun | (neg), verb |.

// 29.8.2008 (SM):
// La gente non vuole più questo sistema.
1> noun; verb<ind> = | det[art] | noun.
1> noun; verb<ind> = | det[art]; det[dem] | noun.

1> noun;verb<ind> = |prep| noun.
1> noun;verb<ind> = | ~noun, adj | noun.
1> noun;verb<ind> = |num| noun.
1> noun;verb<ind> = |prep| noun.

// 29.8.2008 (SM):
// La mafia italiana opera in Albania.
//1> noun;verb<ind> = |(prep), adj| noun.
1> noun; verb<ind> = | (prep), adj | noun | ~prep |.
1> noun; verb<ind> = | (prep), (adj), noun[prep:~], (adj) | verb.

1> noun;verb<ind> = |prep, (adj,det[art]), ~noun, adj | noun.

// 9.3.2009 (SM):
// e ora aspetto che
//1> noun;verb<ind> = noun | adj| .
1> noun; verb<ind:+> = noun | adj[conj:~] | .
1> noun; verb<ind:+> = verb | conjche | .

1> noun;verb<ind> = |adv| noun | (adv), adj*, prep| .  // Promuoviamo l'uso della lingua come strumento di insegnamento...-(privilegiato) di comunicazione-.
1> noun;verb<ind> = |prep, verb[inf:+]| noun | (adv), adj*, prep| .

// 1.9.2008 (SM):
// una ventina sono rimasti feriti per colpi d'arma da fuoco nei violenti scontri con la polizia.
//1> noun;verb<ind> = |(det[art];adj), noun, adj* | verb<ind> | ?*, verb<pastpart>| .
1> noun; verb<ind> = | (det[art]; adj), noun[prep:~], adj* | verb<ind> | ?*, verb<pastpart>| .

1> noun;verb<ind> = |pron[nom:+]| verb<ind> .

// 24.10.2008 (SM):
// E lo studio che cita come ...
//1> noun;verb<ind> = verb<ind> |(det), noun| .
1> noun[coord:~]; verb<ind:+> = verb<ind:+> | (det), noun |.

1> noun;verb<ind> = | verb[inf], (det)| noun .

// 9.3.2009 (SM):
// esibendo ai controlli documenti che apparivano regolari .
1> noun; verb<impv:+> = noun | (conjche), (neg), verb |.


//1> noun;verb<impv> = verb |(det), noun| .
//1> noun;verb<ind> = | verb[form:favere,ind:+], adv*, verb[form:favere,pastpart:+] | noun . //Da quando questo ha preso forma
//1> noun;verb<ind> = verb<inf> |(det), noun| . 

1> noun;verb<subj> = noun | adj|.
1> verb<subj>;noun = | verb<aux>, ~punct | noun | ~adj, ~pron, ~det[art] |. //Non ci saranno tagli
1> verb<subj>;noun = | verb<modal>, ~punct | noun | ~adj, ~pron, ~det[art]| . //Non ci saranno tagli

//dalle stelle alle stalle DALLE: preart ~ impv[Dare]; STALLE: noun ~ impv[Stare]; i LEGAMI commerciali;
1> verb<impv>;noun = | det[art] | noun.
1> verb<impv>;noun = | pp | noun.
1> verb<impv>;noun = | det*[art], num| noun.

// 29.8.2008 (SM):
// Piovono pietre e insulti, anche contro gli stranieri e gli italiani . 
1> noun; verb = | verb, noun, (adj), conj[coord:+] | noun .

1> verb<perf>,verb<pastpart> = | verb[form:fessere,pastpart:+] | verb<pastpart>.
1> verb<perf>,verb<pastpart> = |noun | verb<perf> .
1> verb<perf>,verb<pastpart> = | ~noun | verb<pastpart>.


//Siamo impegnati da SEI mesi.
1> num<lemma:sei>;verb<form:fessere> = |prep| num<lemma:sei> |noun|.
1> num<lemma:sei>;verb<form:fessere> = |pron| verb<form:fessere> | verb<pastpart> |.
1> num<lemma:sei>;verb<form:fessere> = |pron| verb<form:fessere> | adj |.



//{Fin da} SUBITO
1> ?[form:fsubito] = |prep | adv.

// 25.8.2008 (SM):
// avrebbe dovuto essere subito licenziato
// 3.9.2008 (SM):
// E improvvisamente quell'irruzione, subito definita un esodo biblico
1> ?[form:fsubito] = | verb; punct | adv | verb[pastpart:+] |.


//"Fa" verb (Fare)~ adv.
//1> adv;verb<ind> = |(prep), num, noun[period:+] | adv[lemma:fa] |~adj, noun<prop>, verb[inf:~]|.	//Tre mesi fa.
//1> adv;verb<ind> = |(prep), quant, noun[period:+] | adv[lemma:fa]|~adj, noun<prop>, verb[inf:~]|.	//Alcuni mesi fa.
//1> adv;verb<ind> = |noun |verb |(det[art]), (quant), (adj), noun[prop:~], verb[gerund:~,prespart:~,pastpart:~,ind:~,impv:~]|.
//1> adv;verb<ind,impv,modal> = |~prep, quant, noun[period:+] | adv[lemma:fa].
//1> adv[lemma:fa];verb<ind> = |~prep, num, ~det[art], noun[period:+,prop:~] | adv[lemma:fa].

// 29.8.2008 (SM):
// Già un paio di mesi fa, quando non si parlava ancora di fallimenti
1> adv<lemma:fa>; verb = | (num; prep), noun[period:+] | adv.
1> adv<lemma:fa>; verb = | noun[period:+, start:+] | adv.

//SOLO
1> ?[form:fsolo] = |?*| conj |?*[fv:~]|.

// 8.12.2008 (SM):
// quasi tutti gli ufficiali
//1> det,pron =  pron | prep|.
1> det, pron =  pron | prep[det:~] |.

1> det,pron = pron | conjche |.

// 6.10.2008 (SM):
// Per questo la polizia si era ritirata...
//1> det,pron = det | det[art], noun |.
1> det<dem:~>, pron = det | det[art:+], noun |.

// 25.8.2008 (SM):
// non proprio molto bene
//1> adj,pron = adj | (adv), (det), noun |.
1> adj[poss:~, dem:~], pron = adj | (adv), (det), noun[pron:~] |.

// 29.8.2008 (SM):
// a chi affidava loro i propri risparmi.
//1> adj,pron = adj | det[art,indef:~] |. 
1> adj, pron = | ?[verb:~, prep:~] | adj | det[art:+, indef:~] |. 
1> adj, pron = | verb | pron | det[art:+, indef:~] |. 

// 28.11.2008 (SM):
// dicono a Tirana quelli che lo conoscono bene .
1> adj, pron = | verb, (prep, noun) | pron | conjche |. 

1> adj,pron = adj | det[dem:+],adj[poss:+]| .

// 6.10.2008 (SM):
// Per questo fare giustizia è difficile
//1> adj,pron = adj | verb<inf>| .
1> adj, pron = | ~prep | adj | verb<inf:+> | .

// 16.10.2008 (SM):
// dalla lettura di quanto si è visto in Italia
//1> adj,pron = adj | (det[art]), (det[dem:+]), (adj[poss:+]), pron[nom:~] |. //tutto questo.
// 10.3.2009 (SM):
// Erano loro quelli che in città guadagnavano di più .
//1> adj, pron = | ~prep | adj | (det[art:+]), (det[dem:+]), (adj[poss:+]), pron[nom:~] |.
1> adj, pron = ~| prep; verb | adj | (det[art:+]), (det[dem:+]), (adj[poss:+]), pron[nom:~] |.

// 10.3.2009 (SM):
// Erano loro quelli che in città guadagnavano di più .
1> adj, pron<nom:+> = | verb[inf:~] | pron.


1> adj,pron = adj | num|.
1> adj,pron = |det[art]| adj | adj|.
1> adj,pron = |(det[art];det[dem:+])| adj |(num), verb[prespart:+]|.  //Gli stessi (due) duellanti, questi (stessi) duellanti.
//1> adj,pron = | noun, verb[form:fessere] |  adj . 	//"questo telefono è mio".

// 25.8.2008 (SM):
// qualunque tuo buon amico
// 28.8.2008 (SM):
// nel sangue dei nostri morti
1> adj<poss:+>, pron = adj | (adj[adv:~]), noun |.

// 28.11.2008 (SM):
// E non poco.
1> adj; quant = | conj[coord:+], (neg) | quant.


1> det<dem>,pron<dem> =  pron | punct[comma]|. //dopo la figuraccia di ieri e QUELLA, mai dimenticata di martedi'...
// "in questo posto" ~ "voglio questo", "(Tutti) I nostri piÃ¹ bei momenti."

// 29.8.2008 (SM):
// Questa é una storia comune che può raccontare
//1> det<dem>,pron<dem> =  det | ~prep|. //L'atmosfera è quella(PRON) di due anni fa.
1> det<dem:+>, pron<dem:+> =  | ?[prep:~] | det | ?[noun:+, prep:~, verb:~] |.

//1> det,pron =  pron | prep|.
1> det,pron = |verb| pron<dem> | ?*[noun:~,det:~]|.
1> det,pron = |prep| pron<dem> | ?*[noun:~,det:~]|.
1> det<dem>,pron<dem> = |verb[form:fessere] | pron<dem> | det[art]|. //E' questo il posto di cui ti parlavo.
1> det<dem>,pron<dem> = pron<dem> | prep|. //L'atmosfera è quella di due anni fa.
1> det,pron = pron | (neg), pron[dat:+,acc:+,refl:+],pron*[acc:+,refl:+]|. //Questo l'ho fatto io!; Questo mi piace; questa me la paghi!, tutto si aggiusterà, Questa non è una difesa di C.B.
1> det,pron = pron | (neg), pron*, verb|. //Questo l'ho fatto io!; Questo mi piace; questa me la paghi!, tutto si aggiusterà, Questa non è una difesa di C.B.
1> adj,pron = |verb| pron | prep | . //Vanno tutti a casa.
1> adj,pron = pron | verb[inf:~] |  . // tutti andarono a casa. Voglio che tutti vadano  a casa.

// 10.11.2008 (SM):
// E improvvisamente quell' irruzione
1> det, adj<det:+> = | ~det | adj | noun |.

// 28.10.2008 (SM):
// in un momento , come gli altri contadini che lavoravano nel maggese ...
//1> det,pron = pron | adv*[end:+]|.	//Questo (poi) mai!
1> det, pron = pron | adv+[end:+] |.

1> det,pron = | verb| pron | det[art,indef:+]| .
1> det[rel:+],pron = |prep| pron | ~det[art], pron[dem:~] |. //La fine di tutto si avvicina.
1> det[rel:+],pron = |prep| pron | ~det[art], (pron[dem:~]), verb |. //La fine di tutto è vicina. 
1> det,pron = |prep| pron | punct |. //Questa è la scuola di TUTTI. 
1> det,pron = |det[art]| pron | punct |. //Ognuno rispetti l'ALTRO.
1> det,pron = |prep| pron | conj |. //Questa è la scuola di TUTTI ma...
1> det,pron = |det[art]| pron | conj |. //Ognuno rispetti l'ALTRO e ...

// 25.8.2008 (SM):
// tutti i tuoi vecchi amici
// quale buon amico
1> det, pron = det | (det), (adj[poss:+]), (adj)*, noun |.


//Disambiguation of "stessi" adj / verb [subj]. "Prendo le stesse che hai preso tu" I comuni stessi.
1> pron,verb<subj> = verb | ~noun, (adv), verb<gerund> | .
1> adj,verb<subj> = verb | ~noun, (adv), verb<gerund> | .
1> pron,verb = | det[art], (noun[pl:+]) |	pron[pl:+] 	| (adv), ~verb | .
1> adj,verb =  | det[art] |			adj[pl:+] 	| ~verb, noun[pl:+]| .


// 24.11.2008 (SM):
// quelli che lavorano nei servizi civili con i serbi ma anche coloro che intrattengono
1> pron; verb = pron | conjche, verb |.

// 26.8.2008 (SM):
// i nostri cari politici
// 9.9.2008 (SM):
// Il suo obiettivo strategico é quello di tenere insieme tutte le forze democratiche e anticomuniste.
1> adj, verb = | (det), adj*, ~prep | adj | (num), noun |.


// gli stessi giudici
//1> ?[form:fstesso] = adj | noun | .



//QUANTO Conj ~ Pron ~ Quant
1> ?<lemma:quanto> = |prep, (det[art,tokenend:+])| pron |pron*[acc:+,dat:+], verb |. // Concordo con QUANTO adjto. ------------------------------------------------------------------------------------------------------------------------

// 9.10.2008 (SM):
// mi domando quanti sono venuti
1> ?<lemma:quanto> = | verb | pron<interrog:+> | verb |.
// mi domando quante persone sono venute
1> ?<lemma:quanto> = | verb | adj<interrog:+> | ~det, noun |.
// quanto i paesi
1> ?<lemma:quanto> = prep | det, noun |.
// se è vero quanto lei afferma
1> ?<lemma:quanto> = pron<rel:+> | (pron), verb |.


//1> ?[form:fc] = adv | ?[form:fessere] | .

1> verb;prep = prep | ?[lemma:lui], noun |.

1> verb;prep = prep | ?[lemma:loro], noun |.

1> verb;prep = prep | ?[lemma:lo], noun |.

1> verb<lemma:partire>;noun<lemma:parte> = | prep, det[art], ?+<lemma:altro> | noun .
1> verb<lemma:partire>;noun<lemma:parte> = | verb[form:favere], verb<lemma:fare>| noun .
1> verb<lemma:partire>;noun<lemma:parte> = | verb<lemma:fare>| noun | prep<lemma:di>| .


1> verb<lemma:leggere>,noun<lemma:legge> = | pron[refl:+] | verb<lemma:leggere> . 


// dei + noun[pl] -> prep
1> ?[prepnoun] = |num, ~det[art] | prep .
1> ?[prepnoun] = |noun, ~det[art] | prep | det*[art], ~neg, adj[poss:+,pl:+], adj*, noun[pl:+] | .
1> ?[prepnoun] = |num, ~det[art] | prep | det*[art], ~neg, adj[poss:+,pl:+], adj*, noun[pl:+] | .
1> ?[prepnoun] = |adj, ~det[art] | prep | det*[art], pron[poss:+,pl:+] | .
1> ?[prepnoun] = |pron| prep | det*[art], pron[poss:+,pl:+] | .
1> ?[prepnoun] = |quant| prep | det*[art], pron[poss:+,pl:+] | .

1> ?[prepnoun] = |det[art,pl:+]| noun .
1> ?[prepnoun] = |quant[pl:+]| noun | adj*, (neg), verb[inf:~,pastpart:~]| .
1> ?[prepnoun] = |pron[dem:+,poss:+,pl:+]| noun | adj*, (neg), verb[inf:~,pastpart:~]| .
1> ?[prepnoun] = |det[pl:+]| noun | adj*, (neg), verb[inf:~,pastpart:~]| .


1> ?*[num:+] = num.

1> ?<lemma:convincere> = | verb<form:fessere> | adj.



//Faccia Verb~ Noun
1> verb<lemma:fare,subj>;noun<lemma:faccia> = | det[art]| noun.
1> verb<lemma:fare,subj>;noun<lemma:faccia> = | prep| noun.


//TENERE : adj ~ verb
1> adj<lemma:tenero>;verb<inf,lemma:tenere> = verb<inf,lemma:tenere> | det*, noun | .
1> adj<lemma:tenero>;verb<inf,lemma:tenere> = verb<inf,lemma:tenere> | pron | .

// ERA Noun~Verb     Nell'era glaciale ~ Nella mela c'era un verme 
1> verb<form:fessere>;noun = | adv<lemma:ci>| verb.
1> verb<form:fessere>;noun = | pron[dat:+,acc:+] | verb.
1> verb<form:fessere>;noun = verb | verb<pastpart>|.
1> verb<form:fessere>;noun = |noun[prop]| verb .

//STATO Stare ~ Essere
1> verb<form:fessere,pastpart>;verb<form:fstare,pastpart> = verb<form:fessere,pastpart> | adv*,verb<pastpart>|.

1> det,pron =  pron | prep|.

//CENTRO adj ~noun 
1> noun<lemma:centro>;adj<lemma:centro> = adj[locadj =+] | punct*[hyphen:+], adj[locadj:+] | .

1> noun;verb<pastpart> = | noun, (adj), verb[ind:+,subj:+,pastpart:~], adv*, (verb<pastpart>), (adj;adj;quant,adv[form:fpiù])  | noun.
// spirale[adj=~] salari-prezzi
1> adj[postnom] = | ?[noun:~] | noun .



//SIA conj ~ verb[form:fessere] Voglio sia questo sia quello; voglio sia questo che quello; voglio bene sia a Mimma sia (che) a Bebba.
//Voglio che sia 		...REGLE TRES TRES TRES LIMITEE POUR L'INSTANT...
1> conj<lemma:sia>;verb<form:fessere,subj> = | verb*, ?*[conjche:~]| conj<lemma:sia> | pron, ?*, conjche, pron|.
1> conj<lemma:sia>;verb<form:fessere,subj> = | verb*, ?*[conjche:~]| conj<lemma:sia> | det*, ?*, noun, conjche, ?*[verb:~]|.
1> conj<lemma:sia>;verb<form:fessere,subj> = | verb*, ?*[conjche:~]| conj<lemma:sia> | det*, ?*, noun, conjche|.

1> conj<lemma:sia>;verb<form:fessere,subj> = | verb, ?*, conjche| verb<form:fessere,subj> | adj|.
1> conj<lemma:sia>;verb<form:fessere,subj> = | verb, ?*, conjche,?*[art:+,pron:+,noun:+]| verb<form:fessere,subj> | det*[art], noun|.
1> conj<lemma:sia>;verb<form:fessere,subj> = | verb, ?*, conjche,?*[art:+,pron:+,noun:+]| verb<form:fessere,subj> | verb<pastpart>|.
1> conj<lemma:sia>;verb<form:fessere,subj> = | noun| verb<form:fessere,subj> | prep, ?*[conjche:~]|.
1> conj<lemma:sia>;verb<form:fessere,subj> = | conjche, det*,noun| verb<form:fessere,subj,impv> | prep, ?*[conjche:~]|.


1>noun<lemma:accetta>;verb<lemma:accettare> = | ?*[art:~;det:~;prep:~;adv:+,neg:+] | verb<lemma:accettare>.


// disambiguation of "se": if it's followed by another pronoun, except "ne", it's a conjunction
1> ?[form:fse] = conj | pron[ne:~] | .

// 26.8.2008 (SM):
// Sicuramente se investo su Enel mi sento più sereno.
1> ?[form:fse] = conj | verb | .


//Anche
// 5.9.2008 (SM):
// Nonostante l' assenza di episodi gravi, anche ieri l'atmosfera era carica di tensione.
//1> ?[form:fanche] = |?*[prep:~;art:~]| conj.
1> ?[form:fanche] = | ?[conj:+] | adv.
1> ?[form:fanche] = | ?*[prep:~; art:~] | adv | adv; prep |.
1> ?[form:fanche] = | ?*[prep:~; art:~] | conj ~| adv |.


//Sfuggita noun ~ nfv
1> noun[lemma:sfuggita];verb<pastpart,lemma:sfuggire> = | prep[lemma:di] | noun.

// secondo
1> ?[form:fsecondo] = prep | pron | .
1> ?[form:fsecondo] = prep | det[art] | .
1> ?[form:fsecondo] = prep | noun[pl] | .
1> ?[form:fsecondo] = | prep | num .
1> ?[form:fsecondo] = | det[art] | num .
1> ?[form:fsecondo] = prep | ?[prop] | .

// Ancora adv ~noun(àncora) ~verb(egli àncora) :"à" is often written without accent, this creates ambiguity.
1> ?[form:fancora] = |?*[art:~, adj:~, noun:~, verb:+] | adv | (det[art]), (adj), noun|.
1> ?[form:fancora] = | ?*[art:~, noun:~, verb:+]| adv | (det[art]), adj| .
1> ?[form:fancora] = |?*[art:~, noun:~, verb:+] | adv | (det[art]), adv| .
1> ?[form:fancora] = | ?*[noun:~, verb:~] | noun.

// 7.10.2008 (SM):
// nella quale è impegnato ancora il 50% della popolazione attiva
1> ?[form:fancora] = | ?*[art:~, noun:~, verb:+]| adv | (det[art]), num | .

// 10.10.2008 (SM):
// Questa é ancora ' terra di nessuno ' ,
1> ?[form:fancora] = | ?*[art:~, noun:~, verb:+]| adv | punct[quote:+] | .

// 29.8.2008 (SM):
// quando non si parlava ancora di fallimenti
// 1> ?[form:fancora] = | ?*[art:~,  verb:~, adj:~] | verb.
1> ?[form:fancora] = | verb; noun | adv | prep |.
// 9.3.2009 (SM):
// tutta una società ancora aggrappata ai clan sopravvissuti al crollo dello stalinismo .
1> ?[form:fancora] = | verb; noun | adv | verb |.
1> ?[form:fancora] = | ?+[art:~,  verb:~, adj:~] | verb.


//Noun ~ conj (namely, "E", which is considered "noun(EST,cardinal point)" in the beginning of the sentence).
// 24.10.2008 (SM):
// E lo studio che cita come ...
// E come ci considerano ...
//1> noun[lemma:est,maj:+,first],conj<coord> = |?*[prep:~,art:~]| conj<coord>.
1> noun<maj:+, start:+>, conj<coord:+> = conj<coord:+> | (adv), (pron), verb |.
// 10.11.2008 (SM):
// E improvvisamente quell'irruzione, subito definita un esodo biblico, ci rivelò che
1> noun<maj:+, start:+>, conj<coord:+> = conj<coord:+> | (adv), (det; adj[det:+]), noun |.
// 28.11.2008 (SM):
// E non poco.
1> noun<maj:+, start:+>, conj<coord:+> = conj<coord:+> | (neg), quant |.


//Lungo Adj(il fiume più LUNGO del mondo il Flumendosa...) ~ Prep(quanta occupazione sarà persa LUNGO la strada della ripresa?
1> ?<lemma:lungo> = |verb | prep | det[art]|.
1> ?<lemma:lungo> = |verb | prep | adj|.
1> ?<lemma:lungo> = |noun*,adv* | adj | ~det[art] |.


// 26.8.2008 (SM):
// più bella, molto più bello, non molto più bello
// più affidabilità, e meno soldi
// 29.8.2008 (SM):
// La gente non vuole più questo sistema . 
1> ?<lemma:più> =  adv | (adv), adj |.
1> ?<lemma:più> =  quant | noun |.
1> ?<lemma:più> =  | verb | adv.
1> ?<lemma:meno> =  adv | (adv), adj |.
1> ?<lemma:meno> =  quant | noun |.
1> ?<lemma:meno> =  | verb | adv.

//Molto ADV~QUANT
1> ?<lemma:molto> = |verb | adv | det[art]|.
1> ?<lemma:molto> = |verb | adv | pron|.
1> ?<lemma:molto> = |verb | quant | ~pron|.
1> ?<lemma:molto> = |verb | quant | ~det[art]|.
1> ?<lemma:molto> = |verb | quant | (adj), noun|.
1> ?<lemma:molto> = |verb | quant | noun|.

// 25.8.2008 (SM):
// un attrezzo molto utile
// più bella, molto più bello, non molto più bello
1> ?<lemma:molto> =  adv | (adv), adj |.
// too specific rule !!
// non proprio molto bene
1> ?<lemma:molto> =  adv | (adv), adv |.

// 28.8.2008 (SM):
// non molti sono andati via.
1> ?<lemma:molto> = | (neg) | quant ~| noun |.

// 7.10.2008 (SM):
// Per Azem Hajdari molti politici sono coinvolti nel crack delle finanziarie.
1> ?<lemma:molto> = adj | noun, verb |.

// 25.8.2008 (SM):
// too specific rule !!
// non proprio molto bene
// 26.8.2008 (SM):
// non proprio al 100%
1> ?<lemma:proprio> =  adv | adv; prep |.

// 25.8.2008 (SM):
// too specific rule !!
// non proprio molto bene, funziona veramente bene
1> ?<lemma:bene> = | adv | adv .



//DOPO Adv ~ Conj ~ Prep
1> ?*<lemma:dopo> = conj | verb<inf> |.
1> ?*<lemma:dopo> = conj | verb<pastpart> |.
1> ?*<lemma:dopo> = prep | (det[art]), noun |.
1> ?*<lemma:dopo> = adv | ?[art:~, noun:~]|.


//Punto FV ~ Noun
1> noun<lemma:punto>;verb<lemma:puntare,ind:+> = | det[art] | noun.
1> noun<lemma:punto>;verb<lemma:puntare,ind:+> = | num | noun.
1> noun<lemma:punto>;verb<lemma:puntare,ind:+> = | prep | noun.
1> noun<lemma:punto>;verb<lemma:puntare,ind:+> = | adj | noun.


//Sinistra Adj ~Noun
1> ?<lemma:sinistro> = | prep | noun.


//Essere: potrebbe essere Rijkaard ~ l'essere umano/gli esseri umani.
1> noun;verb<form:fessere> = |det[art]| noun | adj<lemma:umano>|.
1> noun<form:fessere>;verb<form:fessere> = | ?*[prep:+,det:~,adj:~,num:~]| verb[form:fessere,pl:~] | ?*<lemma:umano~> |.


//Ispirato Pastpart ~ adj
1> adj<lemma:ispirato>;verb<lemma:ispirare,pastpart:+> = verb | prep, det*[art], noun |.


//"Attraverso" verb~prep; verso "verb,prep"
1> verb;prep = |verb | prep.
//1> verb<lemma:versare>;prep<lemma:verso> = |verb| prep | det, num[dig] | .
//1> verb<lemma:versare> = ?[lemma:~versare]| det[art:+,def:+,pl:+,fem:+], num[dig] | .


//Via = adv
1> ?*<lemma:via> = | verb | adv.

//Bene: adv ~noun
1> ?*<lemma:bene> = | verb | adv.


//La Confederazione Nazionale dei Camionisti ha fatto sciopero
1> noun<lemma:sciopero>,verb<surface:sciopero> = | verb<lemma:fare> | noun.

1> ?[maj:+, first:~] = noun[prop:+] .


// 28.10.2008 (SM):
// should only be pron if followed by a verb!
1> ?<pron:+, subord:+> = ?[pron:~] ~| verb |.

// 3.11.2008 (SM):
// should only be adj if followed by a noun!
1> ?<adj:+, interrog:+> = ?[adj:~] ~| noun |.

// 24.10.2008 (SM):
// mangia come l'uomo vicino.
1> prep; conj = prep | (prep), (det), (adj; num)*, (punct[quote:+]), noun[verb:~]; pron[aton:~], ~verb[subj:+] |.

// 28.10.2008 (SM):
// ha ricordato come l'Osce abbia un ruolo ...
1> prep; conj = | punct[comma:+]; verb[subfin:+] | conj.

// 24.10.2008 (SM):
// ricordo quando parla
// non so quando parla
1> conj; pron<subord:+> = | verb[subfin:+] | pron ~| (det), (adj; num)*, noun; pron[aton:~] |.

// 24.11.2008 (SM):
// non aspettare i processi , se mai ci saranno , 
1> conj<form:fse>; pron = | punct[comma:+] | conj.

// 29.8.2008 (SM):
// Già un paio di mesi fa, quando non si parlava ancora di fallimenti
// 24.10.2008 (SM):
// quando il nuovo primo segretario, come ora si chiamerà
1> adv; conj = conj | (det), (adj; num)*, noun[pastpart:~]; pron[aton:~] |.
1> adv; conj = ~| conj | conj | (neg), (adv), (pron[nom:~]), verb, ?*, punct[lemma:~?] |.
// 28.11.2008 (SM):
1> adv; conj = adv | (neg), (adv), (pron[nom:~]), verb, ?*, punct[lemma:?] |.

// 28.10.2008 (SM):
// E come ci considerano dal punto di vista delle loro latitudini i principi di Oslo :
1> adv; conj = | conj | adv | (neg), (adv), (pron[nom:~]), verb |.

// 13.11.2008 (SM):
// in cui poi non successe granché , certo niente a che vedere
1> adv; adj = adv | pron |.

// 28.11.2008 (SM):
// è solo una copertura per riciclare denaro e distribuire tangenti .
1> adv; adj = | verb[form:fessere] | adv | (det), noun |.


// 5.9.2008 (SM):
// Noi moriremo tutti insieme e non permetteremo a nessuno di imbrogliarci.
1> ?[lemma:insieme] = | pron[lemma:tutto] | adv.
// 9.9.2008 (SM):
// Il suo obiettivo strategico é quello di tenere insieme tutte le forze democratiche e anticomuniste.
1> ?[lemma:insieme] = | verb | adv.

// 25.9.2008 (SM):
// Berisha aveva fatto fuori il capo
1> ?[lemma:fuori] = adv | det |.
1> ?[lemma:dentro] = adv | det |.

// 26.9.2008 (SM):
// Valona é in mano alla folla, fuori da ogni controllo, la polizia é scomparsa, rintanata nelle caserme.
1> ?[lemma:fuori] = prep | prep[lemma:di]; prep[lemma:da] |.

// 1.10.2008 (SM):
// c'è una persona competente, ci sono delle persone
1> pron<loc:+>; adv = pron | (pron[ne:+]), verb[form:fessere], (prep; adv)*, (det), adj*, noun |.

// 24.10.2008 (SM):
// non so che persone siano venute
1> adj<interrog:+>; conjche = adj | noun |.
1> adj<interrog:+>; conjche = conjche | ~noun |.



//------------------------------------------------------------------------------------------------------

// modified 22.8.2008 (Sigrid Maurel, CELI France):
// the article "la" is often tagged as a noun if followed by an adj (ca. 30)
// disambiguation pb (ca. 170), changed "| ?*[det:~, noun:~] |" to "| ~noun |"
// added a rule to disambiguate between a det[dem] and a pron (ca. 80)

// modified 25.8.2008 (SM):
// "tutto" is not a pronoun if it is followed by an determiner an a possessive (ca. 375)
// added "(num)" (ca. 80)
// added rule for disamb between an adj[poss] and a pron (ca. 360)
// added "(adj)" in the rule for prep and noun (ca. 440)
// added rules for "molto" as adv before an adj (ca. 570)
// added "adj[poss:~]" (ca. 335)
// added rule for "proprio" as adv before another adv (ca. 590)
// added rule for "bene" as adv after another adv (ca. 595)
// these last 2 rules are much too specific!!!
// added rule for "subito" (ca. 355)

// modified 26.8.2008 (SM):
// added rules for "più" and "meno" (ca. 600-605) after putting them also as "quant" in the lexicon
// added another rules for disambiguation pb (ca. 165-175)
// added rule for disambiguation of adj and verb (ca. 455)
// modified rule for "proprio", add of prep in context (ca. 590)
// added rule for conj "se" (ca. 505)
// modified rule for verb/adj (ca. 215), add of coord in context

// modified 27.8.2008 (SM):
// modified rule for nouns, at least one adj should be obligatory (ca. 30)

// modified 28.8.2008 (SM):
// added rule for noun/adj (ca. 180)
// added rule for noun/prep (ca. 435)
// modified rule for verb<inf>/adj (ca. 220)
// modified rule for disamb between an adj[poss] and a pron (ca. 360)
// modified ruel for noun/adj, add of "pron[dem:+]" in context (ca. 155)
// added rule for "molto" as quant when not followed by noun (ca. 580)

// modified 29.8.2008 (SM):
// added rule for noun/verb (ca. 330)
// added rule for verb/adv "fa" (ca. 360)
// added rule for adv/conj (ca. 795)
// modified and added rule for "ancora" (ca. 580)
// modified rule for verb, add of "~adj" (ca. 145)
// modified rule for noun/verb, add of "det[dem:+]" (ca. 290)
// added rules for "più" and "meno" (ca. 600-605)
// modified and added rule for noun/verb (ca. 300)
// added rule for noun/adj (ca. 195)
// modified and added rules for adj/pron (ca. 380)
// modified rule for det/pron (ca. 405)

// modified 1.9.2008 (SM):
// modified rule for noun/verb, add of "[prep:~]" (ca. 350)

// modified 3.9.2008 (SM):
// modified rule for "subito" (ca. 355)

// modified 4.9.2008 (SM):
// modified rule for det/pron, add of "adj<det:+>" (ca. 80)

// modified 5.9.2008 (SM):
// modified rules for "anche" (ca. 575)
// added rule for "insieme" (ca. 715)

// modified 9.9.2008 (SM):
// added rule for noun/adj (ca. 200)
// modified rule for adj/verb, add of "~prep" (ca. 455)
// added rule for "insieme" (ca. 720)

// modified 10.9.2008 (SM):
// modified rule for verb/adj, add of "(adj)" in context (ca. 220)

// modified 25.9.2008 (SM):
// added rules for "fuori" and "dentro" (ca. 730)
// modified rule for verb, add of "~verb" (ca. 145)

// modified 26.9.2008 (SM):
// modified rule for quant (ca. 295)
// added rule for "fuori" (ca. 735)
// changed place of "noun;prep" rules (now ca. 250-270)

// modified 29.9.2008 (SM):
// commented rule (ca. 220), seems not necessary and unwell formed

// modified 1.10.2008 (SM):
// added rule for "ci" (ca. 740)

// modified 6.10.2008 (SM):
// modified rule for det/pron (ca. 420)
// modified rule for adj/pron (ca. 435)
// modified rule for noun/verb (ca. 325)
// added rule for noun/verb<inf> (ca. 275)

// modified 7.10.2008 (SM):
// added rule for "ancora" (ca. 630)

// modified 9.10.2008 (SM):
// modified adv rule (ca. 65)
// modified verb/adj rule (ca. 245)
// added rules for "quanto" (ca. 530-540)
// modified rule for noun/prep (ca. 280)

// modified 10.10.2008 (SM):
// added rule for "ancora" (ca. 665)

// modified 16.10.2008 (SM):
// modified rule for noun/prep (ca. 285)
// modified rule for adj/pron (ca. 470)

// modified 17.10.2008 (SM):
// modified rule for adv/conj (ca. 795)
// modified rule for noun/adj (ca. 215)

// modified 24.10.2008 (SM):
// modified rule for noun/conj at beginning of sentence (ca. 695)
// modified rule for noun/verb (ca. 395)
// added rules for pron<subord:+> (ca. 800-820)
// added rules for adj/conjche (ca. 840)

// modified 27.10.2008 (SM):
// added rule for prep/conj (ca. 800)

// modified 28.10.2008 (SM):
// modified and rearranged rules for conj/adv/prep/pron (ca. 815-840)
// modified rule for det/pron (ca. 520)
// modified rule for verb/noun (ca. 320)

// modified 3.11.2008 (SM):
// added rule for adj[interrog] (ca. 820)

// modified 3.11.2008 (SM):
// modified rule for verb/adj (ca. 275)

// modified 6.11.2008 (SM):
// modified rule for det/pron (ca. 60)

// modified 10.11.2008 (SM):
// added rule for det/adj (ca. 535)
// modified rule for noun/conj (ca. 725)
// added rule for adj/verb (ca. 255)

// modified 12.11.2008 (SM):
// added rule for noun/verb (ca. 360)
// added rule for noun/adj (ca. 190)

// modified 13.11.2008 (SM):
// added rule for noun/adj (ca. 200)
// modified rule for noun/adj (ca. 185, 230)
// added rule for verb/adj (ca. 280)
// added rule for adv/adj (ca. 890)

// modified 21.11.2008 (SM):
// modified rule for noun/verb (ca. 390)

// modified 24.11.2008 (SM):
// added rule for verb/pron (ca. 590)
// modified rule for noun/adj (ca. 185)
// added rule for pron/conj (ca. 890)

// modified 28.11.2008 (SM):
// added rule for adj/quant (ca. 550)
// added rule for conj/noun (ca. 770)
// added rule for adv/conj (ca. 910)
// added rule for adv/adj (ca. 920)
// added rule for adj/pron (ca. 525)

// modified 8.12.2008 (SM):
// modified rule for det/pron (ca. 505)

// modified 9.3.2009 (SM):
// modified rule for verb<ind>/verb<pastpart> (ca. 155)
// added rule for noun/verb (ca. 465)
// modified/commented rules for noun/adj (ca. 205, 240, 255)
// modified and added rule for noun/verb (ca. 445)
// modified rule for noun/verb (ca. 420)
// added rule for noun/adj (ca. 200)
// added rule for "ancora" (ca. 790)

// modified 10.3.2009 (SM):
// added rule for adj/verb (ca. 310)
// modified/added rules for adj/pron (ca. 575, 580)
