// dep-coord.xip
// dependencies for coordiantion
// created: 10.11.2008 (Sigrid Maurel, CELI France)
// modified: 28.11.2008 (Sigrid Maurel, CELI France)


DependencyRules:

//------------------------------------------------------------------------------------------------------
// coordination first item
// 10.11.2008 (SM):
// Piovono pietre e insulti , anche contro gli stranieri e gli italiani . 
| NP{?*, #1[last:+]}, (punct), conj#2[coordi:+]; punct#2[comma:+] |
	COORD_BASE(#1, #2).

// 12.11.2008 (SM):
// Valona , Saranda e il Sud sono fuori controllo
| NP{?*, #1[last:+]}, SC{?*, conj#2[coordi:+]; punct#2[comma:+]} |
	COORD_BASE(#1, #2).

// 13.11.2008 (SM):
// i centralini dei Vigili del Fuoco , di Carabinieri e Polizia sono stati sommersi
| PP{?*, NP{?*, #1[last:+]}}, SC{?*, conj#2[coordi:+]; punct#2[comma:+]} |
	COORD_BASE(#1, #2).

// 10.11.2008 (SM):
// l'ho visto a casa, all'ufficio e nella città
| PP{?*, NP{?*, #1[last:+]}}, (punct), conj#2[coordi:+]; punct#2[comma:+] |
	COORD_BASE(#1, #2).

// 10.11.2008 (SM):
// del potere centrale e periferico
| AP{?*, #1[last:+]}, (punct), conj#2[coordi:+]; punct#2[comma:+] |
	COORD_BASE(#1, #2).

// 20.11.2008 (SM):
// A lei rincresceva di vederlo così col viso lungo, però non aveva cuore di lusingarlo con belle parole . 
| AP{?*, #1[last:+]}, SC{?*, conj#2[coordi:+]; punct#2[comma:+]} |
	COORD_BASE(#1, #2).

// 10.11.2008 (SM):
// Giovanni mangia, beve e ride
| SC{?*, FV{?*, #1[last:+]}}, (ADVP), (punct), conj#2[coordi:+]; punct#2[comma:+] |
	COORD_BASE(#1, #2).

// 24.11.2008 (SM):
// La storia ci univa , ma anche ci divideva . 
| SC{?*, FV{?*, #1[last:+]}}, (ADVP), SC{?*, conj#2[coordi:+]; punct#2[comma:+]} |
	COORD_BASE(#1, #2).

// 10.11.2008 (SM):
// Giovanni si lava, beve e ride
| SC{?*, RFV{?*, #1[last:+]}}, (ADVP), (punct), conj#2[coordi:+]; punct#2[comma:+] |
	COORD_BASE(#1, #2).

// 10.11.2008 (SM):
// Giovanni ha mangiato, bevuto e riso
| NFV{?*, #1[last:+]}, (ADVP), (punct), conj#2[coordi:+]; punct#2[comma:+] |
	COORD_BASE(#1, #2).

// 20.11.2008 (SM):
// Nemmeno lo 007 dell' Ufficio indagini l' ha visto , però sono stato sospeso per trenta giorni.
| NFV{?*, #1[last:+]}, (ADVP), SC{?*, conj#2[coordi:+]; punct#2[comma:+]} |
	COORD_BASE(#1, #2).

// 10.11.2008 (SM):
// Giovanni ama mangiare e bere
| IV{?*, #1[inf:+]}, (ADVP), (punct), conj#2[coordi:+]; punct#2[comma:+] |
	COORD_BASE(#1, #2).

// 10.11.2008 (SM):
// Giovanni sta mangiando e bevando
| GV{?*, #1[last:+]}, (ADVP), (punct), conj#2[coordi:+]; punct#2[comma:+] |
	COORD_BASE(#1, #2).


//------------------------------------------------------------------------------------------------------
// coordination second item
// 10.11.2008 (SM):
// Piovono pietre e insulti , anche contro gli stranieri e gli italiani . 
| punct#1[comma:+], NP{?*, #2[last:+]} |
	if ( COORD_BASE(#10[noun:+], #1) | COORD_BASE(#10[pron:+], #1) | COORD_BASE(#10[adj:+], #1) )
	COORD2ND_BASE(#1, #2).
| conj#1[coordi:+], (ADVP; punct[quote:+]; neg), NP{?*, #2[last:+]} |
	if ( COORD_BASE(#10[noun:+], #1) | COORD_BASE(#10[pron:+], #1) | COORD_BASE(#10[adj:+], #1) )
	COORD2ND_BASE[finalcoord=+](#1, #2).

// 10.11.2008 (SM):
// l'ho visto a casa, all'ufficio e nella città
| punct#1[comma:+], PP{?*, NP{?*, #2[last:+]}} |
	if ( COORD_BASE(#10[noun:+], #1) | COORD_BASE(#10[pron:+], #1) | COORD_BASE(#10[adj:+], #1) )
	COORD2ND_BASE(#1, #2).
| conj#1[coordi:+], (ADVP; punct[quote:+]; neg), PP{?*, NP{?*, #2[last:+]}} |
	if ( COORD_BASE(#10[noun:+], #1) | COORD_BASE(#10[pron:+], #1) | COORD_BASE(#10[adj:+], #1) )
	COORD2ND_BASE[finalcoord=+](#1, #2).

// 10.11.2008 (SM):
// Tirana e il porto sull'Adriatico si toccano
| punct#1[comma:+], SC{?*, NP{?*, #2[last:+, pron:~]}} |
	if ( COORD_BASE(#10[noun:+], #1) | COORD_BASE(#10[pron:+], #1) | COORD_BASE(#10[adj:+], #1) )
	COORD2ND_BASE(#1, #2).
| conj#1[coordi:+], (ADVP; punct[quote:+]; neg), SC{?*, NP{?*, #2[last:+, pron:~]}} |
	if ( COORD_BASE(#10[noun:+], #1) | COORD_BASE(#10[pron:+], #1) | COORD_BASE(#10[adj:+], #1) 
		& ~COORD2ND_BASE(#1, #20) )
	COORD2ND_BASE[finalcoord=+](#1, #2).

// 10.11.2008 (SM):
// del potere centrale e periferico
| punct#1[comma:+], AP{?*, #2[last:+]} |
	if ( COORD_BASE(#10[adj:+], #1) )
	COORD2ND_BASE(#1, #2).
| conj#1[coordi:+], (ADVP; punct[quote:+]; neg), AP{?*, #2[last:+]} |
	if ( COORD_BASE(#10[adj:+], #1) )
	COORD2ND_BASE[finalcoord=+](#1, #2).

// 10.11.2008 (SM):
// Giovanni mangia, beve e ride
| punct#1[comma:+], SC{?*, FV{?*, #2[last:+]}} |
	if ( COORD_BASE(#10[verb:+], #1) & ~COORD2ND_BASE(#1, #20) )
	COORD2ND_BASE(#1, #2).
| conj#1[coordi:+], (ADVP), SC{?*, FV{?*, #2[last:+]}} |
	if ( COORD_BASE(#10[verb:+], #1) & ~COORD2ND_BASE(#1, #20) )
	COORD2ND_BASE[finalcoord=+](#1, #2).

// 13.11.2008 (SM):
// in questo caso Rivoli , e sapevo che lui aveva
| SC{?*, conj#1[coordi:+], (ADVP), FV{?*, #2[last:+]}} |
	if ( COORD_BASE(#10[verb:+], #1) & ~COORD2ND_BASE(#1, #20) )
	COORD2ND_BASE[finalcoord=+](#1, #2).

// 10.11.2008 (SM):
// Giovanni si lava , beve e ride
| punct#1[comma:+], SC{?*, RFV{?*, #2[last:+]}} |
	if ( COORD_BASE(#10[verb:+], #1) & ~COORD2ND_BASE(#1, #20) )
	COORD2ND_BASE(#1, #2).
| conj#1[coordi:+], (ADVP), SC{?*, RFV{?*, #2[last:+]}} |
	if ( COORD_BASE(#10[verb:+], #1) & ~COORD2ND_BASE(#1, #20) )
//	if ( ~COORD2ND_BASE(#1, #20) )
	COORD2ND_BASE[finalcoord=+](#1, #2).

// 10.11.2008 (SM):
// Giovanni ha mangiato, bevuto e riso
| punct#1[comma:+], NFV{?*, #2[last:+]} |
	if ( COORD_BASE(#10[pastpart:+], #1) )
	COORD2ND_BASE(#1, #2).
| conj#1[coordi:+], (ADVP), NFV{?*, #2[last:+]} |
	if ( COORD_BASE(#10[pastpart:+], #1) )
	COORD2ND_BASE[finalcoord=+](#1, #2).

// 10.11.2008 (SM):
// Giovanni ama mangiare e bere
| punct#1[comma:+], IV{?*, #2[inf:+]} |
	if ( COORD_BASE(#10[inf:+], #1) )
	COORD2ND_BASE(#1, #2).
| conj#1[coordi:+], (ADVP), IV{?*, #2[inf:+]} |
	if ( COORD_BASE(#10[inf:+], #1) )
	COORD2ND_BASE[finalcoord=+](#1, #2).

// 10.11.2008 (SM):
// Giovanni sta mangiando e bevando
| punct#1[comma:+], GV{?*, #2[last:+]} |
	if ( COORD_BASE(#10[gerund:+], #1) )
	COORD2ND_BASE(#1, #2).
| conj#1[coordi:+], (ADVP), GV{?*, #2[last:+]} |
	if ( COORD_BASE(#10[gerund:+], #1) )
	COORD2ND_BASE[finalcoord=+](#1, #2).

// 28.11.2008 (SM):
// E non poco .
| conj#1[coordi:+, start:+], (ADVP), NP{?*, #2[last:+]} |
	COORD2ND_BASE(#1, #2).

// 28.11.2008 (SM):
// E finalemente vengano .
| conj#1[coordi:+, start:+], (ADVP), SC{?*, FV{?*, #2[last:+]}} |
	COORD2ND_BASE(#1, #2).


//------------------------------------------------------------------------------------------------------
// reattachment if auxiliar
// 12.11.2008 (SM):
// Poteva finire così, ma Kola non si é accontentato e ha rivolto un appello finale.
if ( ^COORD2ND_BASE(#1, #2) & AUX_TENSE(#3, #2) )
	COORD2ND_BASE(#1, #3).

// deletion of first item if no second item (with "e" or "o") follows
// 10.11.2008 (SM):
// Ci sono barricate, bagliori e gente inferocita .
// Piovono pietre e insulti, anche contro gli stranieri.
//if ( ^COORD_BASE(#1, #2[coordi:~]) & ~( COORD2ND_BASE(#2, #3) & COORD_BASE(#3, #4[coordi:+]) & #2<#4 ) )
if ( ^COORD_BASE(#1, #2[coordi:~]) & COORD2ND_BASE[finalcoord:+](#10, #1) )
	~.

// 12.11.2008 (SM):
// Poteva finire così, con queste parole sconcertanti, ma Kola non si é accontentato
if ( ^COORD_BASE(#1, #2[coordi:~]) & ~COORD2ND_BASE(#2, #10) )
	~.

// deletion of second item if no first item with same separator preceeds
// 10.11.2008 (SM):
// Valona é in mano alla folla , fuori da ogni controllo , la polizia é scomparsa , rintanata nelle caserme . 
if ( ^COORD2ND_BASE(#2[coordi:~], #3) & ~COORD_BASE(#1, #2) )
	~.


//------------------------------------------------------------------------------------------------------
// COORD_ADVERS, coordination with "ma"
// 18.11.2008 (SM):
// 20.11.2008 (SM):
// L'esplosione di rabbia é genuina ma viene anche ispirata e organizzata da malavitosi
// A lei rincresceva di vederlo così col viso lungo però non aveva cuore di lusingarlo con belle parole . 
if ( ^COORD_BASE(#1, #2[advers:+]) )
	COORD_ADVERS(#1, #2).

// 18.11.2008 (SM):
// 20.11.2008 (SM):
if ( ^COORD2ND_BASE(#1[advers:+], #2) )
	COORD2ND_ADVERS(#1, #2).


//------------------------------------------------------------------------------------------------------
// COORD_RANGE, coordination preceeded by "fra" and "tra" (or bettrer COORD_SYMMETRIC?)
// 27.11.2008 (SM):
// In quell' ora fra vespero e nona 
if ( ^COORD_BASE(#1, #2) & (PREPOBJ(#1, #3[lemma:fra]) | PREPOBJ(#1, #3[lemma:tra])) )
	COORD_RANGE(#1, #2).

// 27.11.2008 (SM):
if ( ^COORD2ND_BASE(#2, #4) & COORD_RANGE(#1, #2) )
	COORD2ND_RANGE(#2, #4).




//------------------------------------------------------------------------------------------------------
// modified 10.11.2008 (Sigrid Maurel, CELI France):
// add of rules for coordination

// modified 12.11.2008 (SM):
// commented restriction on all COORD2ND_BASE, gives better results when evaluated with TUT
// add of reattachment rule if auxiliar (ca. 150)
// add of deletion rule (ca. 165)
// added restriction for SC rule (ca. 95-115)
// maybe it would be interesting to put the COORD2ND rules before the COORD rules,
//	and then select only the COORD rules that are the same category than the corresponding
//	COORD2ND item

// modified 13.11.2008 (SM):
// added rule for COORD_BASE (ca. 20)
// added rule for COORD2ND_BASE (ca. 120)
// uncommented restriction on all COORD2ND_BASE, gives worse results when evaluated with TUT
//	but produces less incorrect output, we only accept coordinations between same categories

// modified 18.11.2008 (SM):
// added rules for COORD_ADVERS for "ma" (ca. 185-195)

// modified 20.11.2008 (SM):
// modified rules for COORD_ADVERS (ca. 185-200)
// added optional punctuation in COORD_BASE rules before the coordination
// added rule for COORD_BASE with AP (ca. 35)

// modified 24.11.2008 (SM):
// added rule for COORD_BASE (ca. 45)

// modified 27.11.2008 (SM):
// added rules for COORD_RANGE for "fra" and "tra" (ca. 210-220)
// 	but difference in TUT between COORD_RANGE and COORD_SYMMETRIC is not clear!

// modified 28.11.2008 (SM):
// added rules for COORD2ND_BASE with conjunction at beginning of sentence (ca. 170, 175)
