//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Copyright (c) 2006 - 2007 Xerox Corporation.  All Rights Reserved.
//!
//! File:	terms.xip
//! Content:	NEs
//!
//! Language:	ITALIAN
//! ENCODING:	UTF-8	[€àâáäã]
//!
//! Author:	Anne Schiller
//!
//! Date: 	17-Jan-2006	:: initial version
//! Update:	08-Apr-2008	:: changed to UTF-8
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Sequence:

// 9.10.2008 (SM):
// dice il direttore Lesi, mentre si aggira tristemente tra scrivanie carbonizzate.
1> noun[prop=+, verb=~] = | noun[people:+] | ?<maj:+>.

// 24.11.2008 (SM):
// il generale Ferrero promise l' indipendenza
1> noun[prop=+, verb=~] = | noun[people:+, tokenstart:+], ?[tokenend:+] | ?<maj:+>.

//10> noun[prop=+] = noun;?[src:guess], noun[postmod,featprop,!featprop:!].



// // // ORGANISATIONS


//La Confederazione[premod, org] Nazionale[adj, place] dei Camionisti[noun, prof, maj] ha fatto sciopero.
8> noun[prop=+,org=+,mwe=+,featprop:!] = noun[premod,featprop,!gender:!,!number:!], (adj[place:+])+, prep, (det[art]), noun[!prof:+!,!featprop:!].



//La Confederazione[premod org] Generale[adj] Italiana dei Lavoratori[noun, prof, maj] ha fatto sciopero.
8> noun[prop=+,org=+,mwe=+,featprop:!] = noun[premod,featprop,!gender:!,!number:!], (adj)+, adj[demonym], prep, (det[art]), noun[!prof:+!,!featprop:!].



//L'Associazione[premod org] (Nazionale) Italiana [adj,demonym:+] Biblioteche [noun,  maj] ha fatto sciopero.
8> noun[prop=+,org=+,mwe=+,featprop:!] = |det[art,def]| noun[maj:+,premod,featprop,org,!featprop:!], (adj[place:+])*, adj[demonym:+], noun[maj:+,prof:+].	//L'Associazione Italiana Editori è nata nel 1989; L'Associazione Nazionale Italiana Editori è nata nel 1989;
8> noun[prop=+,org=+,mwe=+,featprop:!] = |det[art,def]| noun[maj:+,premod,featprop,org,!featprop:!], (adj[place:+])*, adj[demonym:+], noun[maj:+].	//L'Associazione Italiana Biblioteche è nata nel 1989; L'Associazione Nazionale Italiana Biblioteche è nata nel 1989;
8> noun[prop=+,org=+,mwe=+,featprop:!] = |det[art,def]| noun[maj:+,premod,featprop,org,!featprop:!], adj[place:+], noun[maj:+,prof:+].	//L'Associazione Nazionale Editori è nata nel 1989;
8> noun[prop=+,org=+,mwe=+,featprop:!] = |det[art,def]| noun[maj:+,premod,featprop,org,!featprop:!], adj[place:+], noun[maj:+].	//L'Associazione Nazionale Biblioteche è nata nel 1989.




//L'As Roma SpA (société de football - ici en tant que sociétà per azioni) 
8> noun[prop=+,org=+,mwe=+,featprop:!] = (noun[sport,premod,featprop,!featprop:!, maj]), (det[art]), ?[prop:+,maj,!gender:!,!number:!], noun[postmod,company,featprop,!featprop:!].
8> noun[prop=+,org=+,mwe=+,featprop:!] = | noun[premod,org:+,featprop,!featprop:!, maj:~]| (det[art]), ?[prop:+,maj,!gender:!,!number:!,sport], noun[postmod,featprop,!featprop:!].	//la società Juventus Football Club


//il Milan Ac
8> noun[prop=+,org=+,mwe=+,featprop:!] = ?[prop:+,maj,!gender:!,!number:!, featgeo=~], noun[postmod,sport:+,!sport:!,company:~].



//FC Internazionale
8> noun[prop=+,org=+,mwe=+,featprop:!] = noun[premod,sport,!sport:!,company:~], ?[maj,!gender:!,!number:!,firstname=~,featgeo=~].





//La società Francesco (Aldo) Flumendosa (Murru) Srl è fallita
8> noun[prop=+,soc=+,mwe=+,featprop:!] @=| (noun[premod:+,org:+]) | noun+[firstname,featpeople=~], noun+[maj:+,dig:~,featgeo=~,featorg=~,company:~], (conj[coord]),(noun[lemma:figlio]),noun[postmod:+,company:+].
//where (soc##1=1).

//La società Bellighé Srl è fallita
8> noun[prop=+,soc=+,mwe=+,featprop:!] @=| (noun[premod:+,org:+]) | noun+#1[maj:+,dig:~,featgeo=~,featorg=~,company:~], (conj[coord]),(noun[lemma:figlio]),noun[postmod:+,company:+],
where (soc##1=1).


//La società/ la ditta Puddu
8> noun[prop=+,mwe=+,soc=+] @=| det*[art], noun[premod:+,org:+,fem:+,title:~,prof:~] | noun+[maj:+,dig:~,featgeo=~,featorg=~,company:~], (adj[place:~,dem:~,poss:~]).


//La Regione (Autonoma) (della) Sardegna, il Comune di Cagliari.
9> noun[prop=+,inst=+,mwe=+,featprop:!] @= noun[!inst:+!,maj:+,premod:+,bank:~,!gender:!],(adj[maj:+]), (prep[lemma:di], det[art]), noun[prop:+,region:+,sport=~,org=~].

// 3.10.2008 (SM):
//9> noun[prop=+,inst=+,mwe=+,featprop:!] @= noun[!inst:+!,maj:+,premod:+,bank:~,people:~, newspaper:~,!gender:!,place:~,featartefact:~,featgeo=~],(adj[maj:+]), (prep[lemma:di], det*[art]), noun[prop:+,city:+,sport=~,org=~].
9> noun[prop=+, inst=+, mwe=+, featprop:!] @= noun[!number:!, !inst:+!, maj:+, premod:+, bank:~, people:~,
 newspaper:~, !gender:!, place:~, featartefact:~, featgeo=~], (adj[maj:+]), (prep[lemma:di], det*[art]),
 noun[prop:+, city:+, sport=~, org=~].


//Il Ministero del Welfare (only Cap lett)
9> noun[prop=+,inst=+,mwe=+,sg=+,featprop:!] @= noun#1[premod, inst, sg,bank:~,newspaper:~,masc:+,!masc:!, maj:+], (adj[place:+,poss:~, dem:~]), prep, det[def], noun[people:~, continent:~, country:~,maj:+], (conj[coord]), (prep), (noun[people:~, continent:~, country:~]), (adj).

// Il Ministero della Pubblica [D/d]istruzione (Min. and Pubbl.: only Cap lett)
//9> noun[prop=+,inst=+,mwe=+,sg=+,featprop:!] @= noun[premod, inst, sg, !masc:!, maj:+], (adj[place:+]), prep, det[def], adj[poss:~, dem:~,maj:+], noun[people:~, continent:~, country:~].

// 6.10.2008 (SM):
// il ministero degli Interni mostra i muscoli
//9> noun[prop=+,inst=+,mwe=+,sg=+,featprop:!] @= noun[premod, inst, sg, !masc:!], (adj[place:+]), prep, det[def], adj[poss:~, dem:~,maj:+], noun#1[people:~, continent:~, country:~].
9> noun[prop=+, inst=+, mwe=+, sg=+, featprop:!] @= noun[premod:+, inst:+, sg:+, !masc:!], (adj[place:+]),
 prep, det[def:+], adj[poss:~, dem:~, maj:+], noun[people:~, continent:~, country:~, verb:~].

//Il ministero del Welfare (Cap lett)
//9> noun[prop=+,inst=+,mwe=+,sg=+,featprop:!] @= | noun[premod, inst,!featorg:!, sg, !masc:!, maj:~], (adj[place:+,poss:~, dem:~]), prep, det[def] | noun[people:~, continent:~, country:~,maj:+]. // (conj[coord]), (prep), (noun[people:~, continent:~, country:~]), (adj).

// Il ministero della Pubblica [D/d]istruzione (Min. and Pubbl.: only Cap lett)
// 6.10.2008 (SM):
// il ministero degli Interni mostra i muscoli
// 9> noun[prop=+,inst=+,mwe=+,sg=+,featprop:!] @= | noun[premod, inst, sg, !masc:!, maj:~], (adj[place:+]), prep, det[def] | adj[poss:~, dem:~,maj:+], noun[people:~, continent:~, country:~].
9> noun[prop=+, inst=+, mwe=+, sg=+, featprop:!] @= | noun[premod:+, inst:+, sg:+, !masc:!, maj:~],
 (adj[place:+]), prep, det[def:+] | adj[poss:~, dem:~, maj:+], noun[people:~, continent:~, country:~, verb:~].


//La Repubblica italiana; la repubblica di Corea
9> noun[prop=+,inst=+,mwe=+,featprop:!] @= noun[!inst:+!,maj:+,premod:+,bank:~,!gender:!],(adj[maj:+]), prep[lemma:di], (det[art]), noun[prop:+,country:+,sport=~,org=~].

// 6.10.2008 (SM):
//9> noun[prop=+,inst=+,mwe=+,featprop:!] @= noun[!inst:+!,maj:+,premod:+,bank:~,!gender:!],(adj[maj:+]), adj[demonym:+,country].
9> noun[prop=+, inst=+, mwe=+, featprop:!] @= noun[!number:!, !inst:+!, maj:+, premod:+, bank:~, !gender:!],
 (adj[maj:+]), adj[demonym:+, country:+].


//Il Consiglio dei ministri/ 
9> noun[prop=+,inst=+,mwe=+,featprop:!] @= noun#1[!inst:+!,maj:+,premod:+,bank:~,!gender:!], (adj[demonym]), prep[lemma:di], (det[art]), noun[postmod,sport=~,org=~],
where(inst##1=1).

//Il Consiglio d'Europa
9> noun[prop=+,inst=+,mwe=+,featprop:!] @= noun#1[!inst:+!,maj:+,premod:+,bank:~,!gender:!], prep[lemma:di], (det[art]), noun[prop:+,featgeo,postmod,sport=~,org=~],
where(inst##1=1).


//Banks.
// 7.11.2008 (SM):
//10> noun[prop=+,org=+,mwe=+,featprop:!] @= noun[premod,maj:+,bank:+,!bank:!,!gender:!,featartefact:~,sg], (adj[place:+,poss:~, dem:~]), (adj[place:~,poss:~,indef:~,dem:~]), prep, (det[def]), noun[people:~,featgeo,maj:+,featgeo=~,featorg=~], (conj[coord]), (prep), (noun[people:~, continent, country,maj:+]), (adj).		//Banca d'Italia, Banco di Napoli
10> noun[prop=+, org=+, mwe=+, featprop:!] @= noun[premod:+, maj:+, bank:+, !bank:!, !gender:!, featartefact:~,
 sg:+], (adj[place:+, poss:~, dem:~]), (adj[place:~, poss:~, indef:~, dem:~]), prep, (det[def:+]),
 noun[people:~, featgeo, maj:+, featgeo=~, featorg=~], (conj[coord:+]), (prep), (noun[people:~, continent:+,
 country:+, maj:+]), (adj[interrog:~]).

10> noun[prop=+,org=+,mwe=+,featprop:!] @= noun[premod,maj:+,!bank:!,!gender:!,sg], (adj[place,poss:~,indef:~,dem:~]), prep, (det[def]), noun[people:~,prof:+,maj:+,country=~,featorg=~]. //


10> noun[prop=+,org=+,mwe=+,featprop:!] @= noun[premod,maj:+,!bank:!,!masc:!,sg], (adj[place:+,poss:~, dem:~]), prep, (det[def]), noun[people:~, country:+,maj:+,country=~,featorg=~], (conj[coord]), (prep), (noun[people:~, continent, country,maj:+]), (adj).







// // // PEOPLE

// 27.8.2008 (SM):
// Patrick O'Reilly
9> noun[lastname=+] = ?[surface:O], symbol[lemma:"'"], noun[prop:+, maj:+].

//Giovanni Depau dice; Come dice Mario Murru
10> noun[prop=+,people=+,mwe=+,featprop:!] @= ?+[firstname,!gender:!,!number:!], ?[prop:+] | (verb[pastpart:+]), verb[dicendi:+] | .
10> noun[prop=+,people=+,mwe=+,featprop:!] @= ?+[firstname,!gender:!,!number:!], ?[prop:+] | (verb[form:favere]), verb[dicendi:+] | .
10> noun[prop=+,people=+,mwe=+,featprop:!] @= |verb[dicendi:+]| ?+[firstname,!gender:!,!number:!], ?[prop:+] .

// 9.10.2008 (SM):
// Dashim Shei
10> noun[prop=+, people=+, mwe=+, featprop:!] @= | verb[dicendi:+] | ?+[src:guess, prop:+,!gender:!,!number:!], ?[prop:+] .


//Giovanni Depau Serra/ Miguel Angel Perez (y) Cruz
10> noun[prop=+,people=+,mwe=+,featprop:!] @= ?+[firstname,!gender:!,!number:!], ?[maj:+,dig:~,featgeo=~,featorg=~], noun*[spa:+,lemma:y], ?[maj:+,dig:~,featgeo=~,featorg=~].



//Mario De Rosa; Mario de Rosa, Alain de Benoist; Aldo Di Napoli. 
10> noun[prop=+,people=+,mwe=+,maj=+,featprop:!] @= noun[firstname,!gender:!,!number:!], prep#1[lemma:di,maj:+], ?#1[maj:+],
where (lastname##1=1).
10> noun[prop=+,people=+,mwe=+,featprop:!] @= noun[firstname,!gender:!,!number:!], prep[surface:de], ?[maj:+].


//Giovanni Brambilla; Anna Rossi. (very spread last names, already in FST lists)
// 19.8.2008 (SM):
//10> noun[prop=+,people=+,mwe=+,featprop:!] @= ?+[firstname,!gender:!],noun+[lastname,featgeo=~,featorg=~].
10> noun[prop=+, people=+, mwe=+, featprop:!] @= ?+[firstname, !gender:!], noun+[!number:!, lastname, featgeo=~, featorg=~].


//Giovanni Depau; Anne Schiller |~num[dig,rom]|

// 19.8.2008 (SM):
// Sigrid Maurel, not in FST
//10> noun[prop=+,people=+,mwe=+,featprop:!] @= ?+[firstname,!gender:!],?#1[noun=+,maj:+,dig:~,featgeo=~,featorg=~,title:~,prof:~] ,
//where (lastname##1=1).
10> noun[prop=+, people=+, mwe=+, featprop:!] @= ?+[firstname, !gender:!], ?#1[!number:!, noun=+, maj:+, dig:~, featgeo=~,
 featorg=~, title:~, prof:~, coord:~, !src:!], where (lastname##1=1).

10> noun[prop=+,people=+,mwe=+,featprop:!] @= | det[art], ?*[poss:+], noun[prof:+,!gender:!] | noun[firstname,!gender:!],?[maj:+,dig:~,title:~,prof:~].		//il nostro inviato marek Klondinsky
10> noun[prop=+,people=+,mwe=+,featprop:!] @= | det[art], ?*[poss:+], noun[prof:+,!gender:!] | noun+[maj:+],noun[maj:+,dig:~,title:~,prof:~].		//il nostro inviato marek Klondinsky



// Il candidato/collega Franco Puddu
10> noun[prop=+,lastname=+,mwe=+,featprop:!] @= |noun[premod,people,prof,!featprop:!]| (?+[!firstname:!,!gender:!,!number:!,title:~,prof:~]), prep#1[lemma:di,maj:+], noun+#1[maj:+,featgeo=~,featorg=~]  ,
where (lastname##1=1).



//il colonnello Murru
10> noun[prop=+,lastname=+,featprop:!,mwe=+] @= |noun[premod,people,prof,!featprop:!]| (?#1[prop:+, firstname:~,maj:+,title:~,prof:~]),?#2[prop:+, firstname:~,maj:+,title:~,prof:~],
where (multiname##1=1 & multiname##2=1).

10> noun[prop=+,lastname=+,featprop:!,mwe=+] = noun#1, noun#2,
where (multiname##1:1 & multiname##2:1).


//il dottor - il professor - l'onorevole Murru
10> noun[prop=+,lastname=+,featprop:!,mwe=+] @= |noun[premod,people,title,!featprop:!]| (noun#1+[prop:+, maj:+,firstname:~,title:~,prof:~,prep:~]), ?#1+[maj:+,firstname:~,title:~,prof:~,prep:~] ,
where (lastname##1=1).


//il dottor - il professor - l'onorevole De Murru
10> noun[prop=+,lastname=+,mwe=+,featprop:!] @= |noun[premod,people,title,!featprop:!]| prep#1[lemma:di,maj:+], ?#1+[maj:+,firstname:~,title:~,prof:~,featgeo=~,featorg=~,prep:~],
where (lastname##1=1).
10> noun[prop=+,lastname=+,mwe=+,featprop:!] @= |noun[premod,people,title,!featprop:!]| prep[surface:de], ?[maj:+].



//Il (attuale/vecchio/ex) sindaco (di Cagliari) (,/è) Pinuccio Puddu
10> noun[mwe=+,prop=+,people=+,featprop:!] @= |det, (adj)*, noun[prof:+,sg:+], (adj[nat:+];adj;noun[org:+]), (prep[lemma:di], det*[art], noun[maj,city];noun[maj,org]), (punct[comma];verb[form:fessere])| noun+[maj:+,firstname,featorg:~,featgeo:~], noun#1[firstname:~,maj:+],
where (lastname##1=1).

//Il (attuale/vecchio/ex) sindaco (di Cagliari) (,/è) Puddu
10> noun[mwe=+,prop=+,lastname=+,featprop:!] @= |det, (adj)*, noun[prof:+,sg:+], (adj[nat:+];adj;noun[org:+]), (prep[lemma:di], det*[art], noun[maj,city];noun[maj,org]), (punct[comma];verb[form:fessere])| noun#1+[firstname:~,maj:+],
where (lastname##1=1).



//Il (attuale/vecchio/ex) presidente della Commissione (,/è) Pinuccio Puddu
10> noun[mwe=+,prop=+,people=+,featprop:!] @= |det, (adj)*, noun[prof:+,sg:+], (adj[nat:+];adj;noun[org:+]), (prep[lemma:di], det*[art], noun[maj,inst]), (punct[comma];verb[form:fessere])| (noun[title]), noun+[maj:+,firstname,featorg:~,featgeo:~], (noun#1[firstname:~,maj:+]),noun#1[firstname:~,maj:+],
where (lastname##1=1).

//Il (attuale/vecchio/ex) presidente della Commissione (,/è) Pinuccio Puddu
10> noun[mwe=+,prop=+,lastname=+,featprop:!] @= |det, (adj)*, noun[prof:+,sg:+], (adj[nat:+];adj;noun[org:+]), (prep[lemma:di], det*[art], noun[maj,inst]), (punct[comma];verb[form:fessere])| (noun[title]), (noun#1[firstname:~,maj:+]),noun#1[firstname:~,maj:+],
where (lastname##1=1).



//Il Principe Alberto di Monaco; il Re Luigi XV.
11> noun[prop=+,people=+,mwe=+,featprop:!] @= |noun[title,featgeo=~]| noun+[prop:+,firstname:+,featgeo=~],(num[rom:+]),prep[lemma:di], noun[maj:+,featgeo,featorg=~,featgeo=~]. //Il Principe alberto di Monaco "featgeo=~" ang "featorg=~" in order to avoid the dependencies "LOC" and "ORG".
11> noun[prop=+,people=+,mwe=+,featprop:!] @= |noun[title,city=~,featorg=~]| noun+[prop:+,firstname:+],num[rom:+],prep[lemma:di],( noun[featgeo], conj[coord] ), noun[featgeo,featorg=~,featgeo=~]. // Il Re Luigi XV (di Catalogna e Aragona). "featgeo=~" ang "featorg=~" in order to avoid the dependencies "LOC" and "ORG".
11> noun[prop=+,people=+,mwe=+,featprop:!] @= |noun[title,city=~,firstname=~,featorg=~]| noun+[prop:+,firstname:+],num[rom:+].



//Papa Giovanni Paolo II; not "Papa Woitila" other rules match at its place.
10> noun[prop=+,people=+,mwe=+,featprop:!] @= noun[premod,title,featprop=~,maj:+],  noun+[prop:+,firstname:+], num[dig:+,rom:+].




11> noun[lastname=+] = (prep), noun,
where (lastname##0:1).






// // // LOC

//Abita in via machiavelli 
12> noun[prop=+,mwe=+,featprop:!] @= |verb[stay_place:+], prep | noun[place:+,premod,!featprop:!,!gender:!,!number:!,featpeople=~,featorg=~], prep*, (det[art]), noun, (punct[comma:+]), num*.  //celle-ci est un peu risquée à cause du noun commun...je la laisse pour l'instant mais je ne suis pas très sûr;  // il faudra rajouter les traits noun[prop,maj], qui, pour l'instant, sont inutiles  
12> noun[prop=+,mwe=+,featprop:!] @= | prep | noun[place,premod,!featprop:!,!gender:!,!number:!], noun[maj:+,prop:+], (punct[comma:+]), num*. //il vaudrait mieux remplacer maj, prop avec people, celeb, etxci




12> noun[prop=+,mwe=+,featprop:!] @= |~verb, noun[prop:~,place:+,place=~,premod,!featprop:!,!gender:!], prep*, (det[art])| noun#1[prop:+,maj:+,!featprop:!],		
where (place##1=1).								//"il monte (fiume, lago, etc.) (di) Putzucugurra"; then :"(il) Putzucugurra". 

12> noun[prop=+,mwe=+,featprop:!] @= noun[prop:~,place:+,place=~,premod,maj:+,!featprop:!,!gender:!], prep*, (det[art]), noun#1[prop:+,maj:+],
where (place##1=1).								//"il Monte (Fiume, Lago, etc.) (di) Putzucugurra"; then :"(il) Putzucugurra". 




//l'Africa Occidentale
15> noun[prop=+,region=+,mwe=+,featprop:!] @= |(prep), det*[art]| noun[!continent:!,maj:+];noun[!country:!,maj:+];noun[region,maj:+], prep[lemma:di], (det[def]), ?[cardpoint].
15> noun[prop=+,region=+,mwe=+,featprop:!] @= |(prep), det*[art]| noun[continent];noun[country];noun[region], adj[postmod:+,locadj].




//Alle porte di Roma/ Putzumannu; Dalle parti di ...
12> noun[prop=+,city=+,mwe=+,featprop:!] @= |prep, det[art,pl:+,fem:+], noun[lemma:parte], prep | noun#1[prop:+,maj:+,featgeo:~,featorg:~,featpeople:~],		
where (place##1=1).		

12> noun[prop=+,city=+,mwe=+,featprop:!] @= |prep, det[art,def:+,pl:+,fem:+], noun[lemma:porta], prep | noun#1[prop:+,maj:+,featgeo:~,featorg:~,featpeople:~],		
where (place##1=1).	









// // // ARTEFACTS

14> noun[prop=+,award=+,mwe=+,featprop:!] = ?+[src:guess, maj], noun[postmod:+,award:+,!featprop:!] |num*[year]|. 
14> noun[prop=+,award=+,mwe=+,featprop:!] = noun[premod:+,award:+,!featprop:!,maj], (adj[place, demonym]), (prep), (det[art]), noun |num*[year]|.   //Premio internazionale della Moda


//il settimanale (economico / francese / economico francese "Bon [M/m]arché"); it doesn't work in sentences like La rivista di moda svedese XYZ
15> noun[prop=+,newspaper=+,featprop:!] @= |noun[printtitle:+, prop:~], (adj*), (?[quote])| ?[maj:+], ?*[last:+] |(?[quote]) |.

15> noun[prop=+,newspaper=+,mwe=+,featprop:!] @= noun#1[printtitle:+,maj:+,!gender:!], prep[lemma:di], (det[art]), noun[featgeo,featgeo=~,featdate=~,featpeople=~],
where (newspaper##1=1).

15> noun[prop=+,newspaper=+,mwe=+,featprop:!] @= noun#1[printtitle:+,maj:+,!gender:!], prep[lemma:di], (det[art]), noun[period,featgeo=~,featdate=~,featpeople=~],
where (newspaper##1=1).

//Il Trattato di Roma del 1945
15> noun[prop=+,agreement=+,mwe=+,featprop:!] @= noun#1[(premod,agreement:+,maj:+,!gender:!], prep[lemma:di], (det[art]), noun[city,featgeo=~,featdate=~,featpeople=~] ,
where (agreement##1=1).


// La Direttiva di Ginevra ; la D. Seveso II
15> noun[prop=+,law=+,mwe=+,featprop:!] @= noun#1[premod:+,law:+,maj:+,!gender:!], prep[lemma:di], (det[art]), noun[city,featgeo=~,featdate=~,featpeople=~] ,	 //La Direttiva dell'Aja.
where (law##1=1).
	
15> noun[prop=+,law=+,mwe=+,featprop:!] @= noun#1[premod:+,law:+,maj:+,!gender:!,featartefact,!featartefact:!], (adj[locadj]), (det[art]), noun*[city,featgeo=~,featdate=~,featpeople=~], (num[dig:+]) , //La direttiva Seveso (II)
where (law##1=1).






// // // DATES

////Dates: first approach...

18> noun[date=+,featprop:!] @= | noun[city:+, featgeo,featorg=~,featpeople=~], punct[comma] | (noun[day:+]), num[day:+], noun[month:+], (num[year:+]) .	//Roma, giovedi 13 marzo 2008
18> noun[date=+,featprop:!] @= | verb[lemma:nascere], det[art,def,surface:il] | num[day:+], noun[month:+], (num[year:+]) . //Vanessa è nata il 7 novembre 1989
18> noun[date=+,featprop:!] @= | (verb[lemma:nascere]), prep, det[art,def,sg] | num[year:+] . 		//Maria è nata nel 1998
18> noun[date=+,featprop:!] @= noun[month:+], num[year:+] . 	// il gennaio 2008
18> noun[date=+,featprop:!] @= | det[art,def,sg] | num[year:+] . 	// il 2008
18> noun[date=+,featprop:!] @= | det[sg:+,masc:+] | num[dig:+,rom:+], noun[period:+,lemma:secolo], (adv[date:+,postmod:+]) .		//dal XV secolo a.C. [a.C.=> ambiguous]
18> noun[date=+,featprop:!] @= | det[sg:+,masc:+] | num[dig:+,ord:+], noun[period:+,lemma:secolo], (adv[date:+,postmod:+]) .		//Sono nato nel XX secolo,nel 20° secolo
18> noun[date=+,featprop:!] @= | det[sg:+,masc:+] | num[dig:+,card:+], noun[period:+,lemma:secolo], (adv[date:+,postmod:+]) .		//Sono nato nel XX secolo,nel 20° secolo
18> noun[date=+,featprop:!] @= | det[sg:+,masc:+] | num[dig:+,year:+], adv[date:+,postmod:+] .				//nel 500 a.C. [a.C.=> ambiguous]
18> noun[date=+,featprop:!] @= | det[sg:+,masc:+] | num[dig:+,day:+], noun[time:+,period:+,month:+], num*[year:+].		//il 24 ottobre
18> noun[date=+,featprop:!] @= | noun*[city:+], punct*[comma] | (noun[day:+]), num[day:+], noun[month:+], (num[year:+]) .	//sabato 27 settembre 2008
18> noun[date=+,featprop:!] @= | det[pl:+] | noun[surface:anni], num[dig:+,year:+] .				//gli anni 70.
18> noun[date=+,featprop:!] @= | det[pl:+] | noun[period], num[card:+] .					//negli anni Settanta	



//------------------------------------------------------------------------------------------------------

// modified 19.8.2008 (Sigrid Maurel, CELI France):
// add of "!number:!" to the rule 10 (ca. 150, 160) for persons

// modified 27.8.2008 (SM):
// added rule for names like "O'Reilly" (ca. 125)

// modified 11.9.2008 (SM):
// add of "!src:!" to rule 10 (ca. 160) for guessed names

// modified 3.10.2008 (SM):
// add of "!number:!" to the rule 9 (ca. 80) for regions, governments, ...

// modified 6.10.2008 (SM):
// add of "!number:!" to the rule 9 (ca. 115) for regions, governments, ...
// add of "verb:~" to rule 9 (ca. 95, 105)

// modified 9.10.2008 (SM):
// add of rule for names after "dicendi" verbs (ca. 155)
// add of rule for maj-words (ca. 20)

// modified 7.11.2008 (SM):
// modified rule for banks (ca. 135)

// modified 24.11.2008 (SM):
// added rule for proper name (ca. 25)

