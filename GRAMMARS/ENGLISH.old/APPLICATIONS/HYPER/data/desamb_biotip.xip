tagging:

verb<species:+>,noun = noun.

// ambiguous noun/verb that are verbs between 2 nouns
noun[verbuse:+] = | noun | verb | noun |.

// function as (if not preceded by an adj or a prep, it is a verb)

noun[function] = | ?[adj:~],?[prep:~] | verb | ?[form:fas] | .
 
//Indium:In is genterm:+. If it is at the beginning if a sentence, it should be a prep

prep,noun<genterm:+,start:+> = prep.

noun<like:+>,adj,verb,prep,conj,adv = | ?[strongbreak:+,form:fhyph] | adj .

//rules for determining if "binding" is a noun or an adjective

?[binding] = | ?[comma:~] | adj[vprog=~] | noun[verb:~] | .

?[binding] = noun[vprog=~] | ?[comma:+];?[sent:+];conj;verb | .

?[binding] = | adj;noun;dig | noun[vprog=~] .

//-ing of -> ing+NOUNING

?[vprog:+] = noun[vprog=~] | ?[form:fof] | .

// adj -ing prep -> NOUNING

?[vprog:+] = | ?[adj:+] | noun[vprog=~] | prep | .

?[vprog:+] = | ?[adj:+] | ?[vprog=~] .

// verb -ing prep -> NOUNING

//?[vprog:+] = | verb | noun[vprog=~] | prep | .

//NADJ+verb -> noun

nadj,noun,adj = noun | verb;aux | .

//det+NADJ+prep -> noun

nadj,noun,adj = | det | noun | prep | .


// an -ing form in a hyphened word cannot be verbal - it has the same behaviour as "binding"

///?[vprog:+] = | ?[form:fhyph,both:+] | adj[vprog=~,verb=~] | noun | .

///?[vprog:+] = | ?[form:fhyph,both:+] | adj<vprog=~,verb=~>,noun<vprog=~,verb=~> .

//?[vprog:+] = | ?[form:fhyph,both:+] | ?[form=fbinding] .

?[vprog:+] = | ?[form:fhyph,both:+] | adj[vprog=~,verb=~] .

//the first part of a compound word cannot be a verb

verb = noun | ?[form:fhyph,both:+] | .

//if "encoding" comes after a determiner, it is an adjective, otherwise not

?[encode] = | ?[det:+,pron:~];?[form:fhyph,both:+] | adj.

?[encode] = | ?[form:fcpar], ?[hyphen:+] | adj.

?[encode] = verb<adj=~>,noun<adj=~> .

//between noun and num the num should remain

noun,num = num.

//"A" at the beginning of a sentence cannot be a measure

det,meas<start:+> = det.

// "repeat" is mostly a noun

?[repeat] = noun | punct;prep;?[protein];?[coord:+] | .

// "code" is a verb when followed by "for"

?[code,start:~] = | ?[det:~] | verb | ?[form:ffor] | .

// an -ed form after a hyphen is always a past participle

?[past:+] = | ?[form:fhyph,both:+] | ?[past=~] .

// those +ing -> those+pron

pron<form:fthose>,det = pron | ?[vprog:+] | .

// if "expressing is between 2 nouns, it is verbal

?[form:fexpress,vprog:+] = | noun;pron[form:fthose] | verb | noun | .

// "for" should never be a conjunction

?[form:ffor] = prep .

// the X marker -> X should be a noun

?[verb:+,past:~,prog:~,noun:~,adj:~] = | det[def:+] | ?[noun=+,guessgeneprot=+] | ?[form:fgene] | .
?[verb:+,past:~,prog:~,noun:~,adj:~] = | det[def:+] | ?[noun=+,guessgeneprot=+] | ?[protein] | .
?[verb:+,past:~,prog:~,noun:~,adj:~] = | det[def:+] | ?[noun=+,guessgeneprot=+] | ?[rnaform] | .
?[verb:+,past:~,prog:~,noun:~,adj:~] = | det[def:+] | ?[noun=+,guessgeneprot=+] | ?[mark:+] | .









