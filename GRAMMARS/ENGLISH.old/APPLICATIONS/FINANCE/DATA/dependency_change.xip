DependencyRules:

| #1[service_provider,changearg=+] |
  ~.

| #1[posit,changearg=+] |
  ~.

| #1[fund,changearg=+] |
  ~.

if ( ^$1(#1[changeind],#2) | ^$1(#1,#2[changeind]) )
   $1[changeind=+](#1,#2).

if ( ^$1(#1[changeind],#2[changearg]) | ^$1(#1[changearg],#2[changeind]) )
   $1[changeind=+,changearg=+](#1,#2).

if ( ^HEAD[changeind](#1,#2) )
   HEAD[changeind=~](#1,#2).

if ( ^HEAD[changearg](#1,#2) )
   HEAD[changearg=~](#1,#2).

////////////////////// CHANGE SG to SG \\\\\\\\\\\\\\\\\\\\\\\

//Effective May 28, 2010, the All - Cap Opportunity Fund changed its name to U.S. Long Short Momentum Fund.
//SUBJ_CHANGEIND_PRE(changed_CHANGEIND:+,Cap Opportunity Fund)
//OBJ_CHANGEIND_POST(changed_CHANGEIND:+,name)
//MOD_CHANGEIND_POST(changed_CHANGEIND:+,U.S. Long Short Momentum Fund)
//PREPD(U.S. Long Short Momentum Fund,to)


if ( SUBJ(#1[change],#2) & OBJ(#1[change],#3) & MOD[post](#1,#4[noun]) & PREPD(#4,#5[lemme:to]) )
   CHANGE[change-to=+](#2,#1,#3,#4).
   
////////////////////// THERE IS/IS NO CHANGE OF SG \\\\\\\\\\\\\\\\\\\\\\\

//Also, there has been a change of leadership in the cable unit, as Neil Smit took over operational responsibilities after the NBC-Universal joint venture was completed.
//MOD_CHANGEIND_POST(change_CHANGEIND:+,leadership)
//NUCL_CHANGEIND_SUBJCOMPL(been,change_CHANGEIND:+)
//PREPD(leadership,of)

if ( MOD[post](#1[change,noun],#2) & NUCL[subjcompl,neg:~](#3,#1) & PREPD(#2,#4[lemme:of]) )
   CHANGE[change-of=+](#1,#2).

if ( MOD[post](#1[change,noun],#2) & NUCL[subjcompl,neg](#3,#1) & PREPD(#2,#4[lemme:of]) )
   CHANGE[change-of=+,neg=+](#1,#2).

////////////////////// THERE IS NO CHANGE IN SG \\\\\\\\\\\\\\\\\\\\\\\

/// no change

//There was no change in the Fund's internal control over financial reporting
//QUANTD_CHANGEIND_NEG(change_CHANGEIND:+,no)
//NUCL_CHANGEIND_SUBJCOMPL_NEG(was,change_CHANGEIND:+)
//MOD_POST(was,control)
//PREPD(control,in)


if ( QUANTD[neg](#1[change],#2) & NUCL[neg](#3,#1[change]) & MOD[post](#3,#4) & PREPD(#4,#5[lemme:in]) )
   CHANGE[neg=+,change-in=+](#2,#1,#4).

////////////////////// NOT ... ANY CHANGE IN \\\\\\\\\\\\\\\\\\\\\\\


//The merger did not result in any change in the portfolio managers or other personnel of the Advisor who are responsible for the Funds ' operations or in the business operations of the Funds or the Advisor .
//QUANTD_CHANGEIND(#1change_CHANGEIND:+,#2any)
//MOD_CHANGEIND_POST(#1change_CHANGEIND:+,#3portfolio managers)
//PREPD(#3portfolio managers,#6in)
//MOD_CHANGEIND_POST(#4result,#1change_CHANGEIND:+)
//MOD_NEG(#4result,#5not)

if ( QUANTD[neg:~](#1[change],#2) & MOD[post](#1[change],#3) & MOD[post](#4[verb],#1[change]) & MOD[neg](#4,#5[lemme:not]) & PREPD(#3,#6[lemme:in]) )
     CHANGE[neg=+,change-in=+](#5,#1,#3). 


/////////////////// General dependency CHANGEIND + CHANGEARG\\\\\\\\\\\

if ( ^$1(#1[changeind],#2[changearg]) | ^$1(#1[changearg],#2[changeind]) )
   $1[changedep=+](#1,#2).



/////////////////////////////////////////////

//The Funds engaged Deloitte & Touche LLP as the principal accountants for the fiscal year ending September 30, 2012, and the auditor-client relationship with KPMG LLP will cease upon completion of the audits of the Funds' financial statements as of and for the year ended September 30, 2011, and the issuance of the independent auditors' report thereon.
//Notwithstanding the merger, the Advisor continues to be controlled by Thomas G. Plumb indirectly through TGP, Inc., and no new person or entity controls the Advisor.
//Effective May 1, 2010, the fund reorganized from a Maryland corporation to a Delaware statutory trust in accordance with a proposal approved by shareholders on November 24, 2009.

