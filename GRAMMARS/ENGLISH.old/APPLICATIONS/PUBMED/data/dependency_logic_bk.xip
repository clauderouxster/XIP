

//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2006 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : English XIP grammar for Pubmed abstracts
//!   filename   : dependency_logic_ps.xip
//!   Date       : 20/03/2006
//!   Author     : Agnes Sandor
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!



DependencyRules:

//scope+level+contr
//mental+level+contr

| #1[contr,nologiccontr:~] |
	LIXO(#1).

if ( ^$1(#1[scope],#2[level,bk:~]) & ~$1[logic](#1,#2) )
	$1[logic=+](#1,#2).

if ( ^$1(#1[level,bk:~],#2[scope]) & ~$1[logic](#1,#2) )
	$1[logic=+](#1,#2).

if ( ^$1(#1[level,bk:~],#2[mental]) & ~$1[logic](#1,#2) )
	$1[logic=+](#1,#2).

if ( ^$1(#1[mental],#2[level,bk:~]) & ~$1[logic](#1,#2) )
	$1[logic=+](#1,#2).

//the cause has not been clarified

| TOP#4{ ?+ } |
if ( $1(#1[contr],#2[level]) & NUCL(#1[contr],#3[mental]) & ~LOGIC(#4) )
	LOGIC[contr=+](#4).

// the cause has been unclear

| TOP#5{ ?+ } |
if ( $1(#1,#2[level]) & NUCL(#1,#3) & NUCL(#3,#4[contr,know]) & ~LOGIC(#5) )
	LOGIC[contr=+](#5).

// the cause is unclear

| TOP#5{ ?+ } |
if ( $1(#1,#2[level]) & NUCL(#1,#3[contr,know]) & ~LOGIC(#5) )
	LOGIC[contr=+](#5).

| TOP#4{ ?+ } |
if ( $1[logic](#1,#2) & LIXO(#3) )
	LOGIC[contr=+](#4).

if ( ^LIXO(#1) )
	~.

if (^$1[logic](#1,#2) )
	$1[logic=~](#1,#2).

//if ( ^IMPSENT(#1) & LOGIC(#1) )
//	IMPSENT[logic=+](#1).
	
if ( ^IMPSENT(#1) & ^LOGIC(#1) )
	~ .
	
//if ( ^IMPSUMMARY(#1) & LOGIC(#1) )
//	IMPSUMMARY[logic=+](#1).
	
if ( IMPSUMMARY(#1) & ^LOGIC(#1) )
	~ .
	
if ( ^SUMMARY(#1) & LOGIC(#1) )
	SUMMARY[logic=+](#1).

if ( SUMMARY[logic](#1) & ^LOGIC(#1) )
	~.
	

// a looser logic_contr sntc: level in syntactic relationship with a negative or forneg word

if ( ^$1(#1[level,strong],#2[negative]) )
	$1[logic=+,contr=+,weak=+](#1,#2).

if ( ^$1(#1[level,strong],#2[forneg]) )
	$1[logic=+,contr=+,weak=+](#1,#2).

if ( ^$1(#1[negative],#2[level,strong]) )
	$1[logic=+,contr=+,weak=+](#1,#2).

if ( ^$1(#1[negative],#2[level,strong]) )
	$1[logic=+,contr=+,weak=+](#1,#2).

if ( ^$1(#1[negat],#2[level,strong]) )
	$1[logic=+,contr=+,weak=+](#1,#2).

if ( ^$1(#1[negat],#2[level,strong]) )
	$1[logic=+,contr=+,weak=+](#1,#2).

if ( ^SUBJ[logic,contr,weak](#1,#2) )
	SUBJ[logic=~,contr=~,weak=~](#1,#2).

| TOP#3{ ?+ } |
if ( $1[logic,contr,weak](#1,#2) & ~IMPSENT(#3) & ~SUMMARY(#3) & ~IMPSUMMARY(#3) & ~LOGIC(#3) )
	LOGIC[contr=+,weak=+](#3).

