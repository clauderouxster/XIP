//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2003 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : English XIP grammar
//!   filename   : localdates.xip
//!   Date       : 13/09/2003
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//!   Author     : C. Brun
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

//The feature "alsoloc" enable to tag expression that
// can be both organisation and location like 
// Washington Hospital" 

// A local Grammar to extract Organisation names
Sequence:
// A & B
//1>  noun[proper:+,mwe:+,org:+] @= noun+[maj], noun[symbol,lemme:"&"], noun*[maj], noun[maj].

//1> noun[proper:+,mwe:+,org:+] @= noun+[maj], noun[symbol,lemme:"&"], ?+[maj], noun[orghead:+].

//1> noun[tutu:+,alsoloc:!] @= nadj[maj], noun[orghead,!alsoloc:!].

1> noun[proper:+,mwe:+,org:+,alsoloc:!] @= noun*[maj,email:~,eventmod:~], noun[maj,email:~];nadj[maj];adj[maj], (nadj[maj];adj[maj]) , noun*[maj,eventmod:~], (punct[comma:+]), noun[orgend];noun[orghead,!alsoloc:!].

1> noun[proper:+,mwe:+,org:+,alsoloc:!] @= noun*[maj], noun[maj,orgend:~,orghead:~];nadj[maj];adj[maj],  conj[coord], noun[maj];nadj[maj];adj[maj] , noun*[maj], (punct[comma:+]), noun[orgend];noun[orghead,!alsoloc:!].

1> noun[proper:+,mwe:+,org:+,alsoloc:!] @= noun*[maj], noun[maj,orgend:~,orghead:~];nadj[maj];adj[maj],  conj[coord], noun[maj];nadj[maj];adj[maj] , part, (punct[comma:+]), noun[orgend];noun[orghead,!alsoloc:!].

1> noun[proper:+,mwe:+,org:+,alsoloc:!] @= noun[country];nadj[country],  noun[orghead:+,!alsoloc:!], noun[country].

1> noun[proper:+,mwe:+,org:+,alsoloc:!] @= noun[country];nadj[country],  noun[orghead:+,!alsoloc:!].

1> noun[proper:+,mwe:+,org:+,alsoloc:!] @= noun[place],  noun[orghead:+,!alsoloc:!].

1>  noun[proper:+,mwe:+,org:+,alsoloc:!] @= |det[def]| noun[maj], adj[maj];nadj[maj] , noun[orghead,!alsoloc:!].

1> noun[proper:+,mwe:+,org:+] @= nadj[country];noun[place],  noun[orgmod:+].

1> noun[proper:+,mwe:+,org:+] @=   noun[maj];dig[rom,lemme:D], noun[symbol,lemme:"&"], noun[maj];dig[rom,lemme:D], noun[orgmod:+].

1>  noun[proper:+,mwe:+,org:+] @= noun[person];noun[org];noun[bus] , noun[symbol,lemme:"&"];conj[coord,lemme:"and"],  noun[famlink];noun[orgend].
 
1> noun[proper:+,mwe:+,org:+,alsoloc:!] @= nadj[maj,guess:~];adj[maj,guess:~], noun[maj], nadj[maj,guess:~];adj[maj,guess:~],noun[orghead:+,!alsoloc:!].

1> noun[proper:+,mwe:+,org:+,alsoloc:!] @= nadj[maj,guess:~];adj[maj,guess:~], noun+[maj], (part;punct[quote:+]),  noun[orghead:+,!alsoloc:!].

1> noun[proper:+,mwe:+,org:+,alsoloc:!] @= nadj[maj,guess:~];adj[proper], noun+[maj], (part;punct[quote:+]), noun[orghead:+,!alsoloc:!].

1> noun[proper:+,mwe:+,org:+,alsoloc:!]  @= adj[maj];nadj[maj], (conj[coord], adj[maj];nadj[maj]), (part;punct[quote:+]),  noun[orghead:+,!alsoloc:!].

//Manchester United
// WARNING : dig ;

1> noun[proper:+,mwe:+,org:+] @= (dig[money:~]),   noun+[proper,title:~,hmm_tit:~], noun[orgmod].

1> noun[proper:+,mwe:+,org:+] @=  (dig[money:~]),  noun+[maj,title:~,hmm_tit:~], noun[orgmod].

//Savage Records
1> noun[proper:+,mwe:+,org:+] @=   (dig[money:~]), nadj+[maj,title:~,hmm_tit:~], noun[orgmod].

1> noun[proper:+,mwe:+,org:+] @=   (dig[money:~]), adj+[maj,title:~,hmm_tit:~], noun[orgmod].

// Air France : I limit to only one Proper name after the word ...

1> noun[proper:+,mwe:+,org:+] @=   (dig[money:~]),  noun[orgmod], noun[proper].

1> noun[proper:+,mwe:+,org:+] @=   (dig[money:~]),  noun[orgmod], noun[maj,countable:~].

1> noun[proper:+,mwe:+,org:+] @=   dig[money:~] , noun[orgmod].

2> noun[proper:+,mwe:+,org:+] @= noun[country];nadj[country], noun[org].
// XXX Inc., 

2> noun[proper:+,mwe:+,org:+,alsoloc:!] @= adj[guess];nadj[guess], noun[orghead,!alsoloc:!];noun[orgend];noun[orgmod].

//Phillip Morris Corp.

2> noun[proper:+,mwe:+,org:+,alsoloc:!] @= (noun[proper,maj]), noun[person], (part;punct[quote:+]), noun[orgend];noun[orghead,!alsoloc:!];noun[orgmod].

2> noun[proper:+,mwe:+,org:+,alsoloc:!] @= noun+[proper], (part;punct[quote:+]), noun[orghead:+,!alsoloc:!].

2> noun[proper:+,mwe:+,org:+,alsoloc:!] @= noun+[maj,email:~], (part;punct[quote:+]), noun[orghead:+,!alsoloc:!]. 

2> noun[proper:+,mwe:+,org:+,alsoloc:!] @=  noun[orghead:+,!alsoloc:!], noun[maj], noun[particlename], noun[maj].

2> noun[proper:+,mwe:+,org:+,alsoloc:!] @=  noun[orghead:+,!alsoloc:!], noun[maj,title:~,hmm_tit:~], ( conj[coord], noun[maj,title:~,hmm_tit:~]).

2>  noun[proper:+,mwe:+,org:+] @=  nadj[lemme:"national"], noun+[maj].

3> noun[proper:+,mwe:+,org:+,alsoloc:!] @=  noun[orghead:+,!alsoloc:!], prep[form:fof], (det[def]), (adj[maj];nadj[maj]) , noun[maj].

3> noun[proper:+,mwe:+,org:+,alsoloc:!] @=  noun[orghead:+,!alsoloc:!], prep[form:ffor], (det[def]), (adj[maj];nadj[maj]) ,  noun[maj].

3> noun[proper:+,mwe:+,org:+] @= noun[country], noun[bus].
#toto

4> noun[proper:+,mwe:+,org:+,alsoloc:!] @= noun[org,!alsoloc:!], prep[form:fof], (nadj+[maj]), noun+[maj] .

4> noun[proper:+,mwe:+,org:+,alsoloc:!]  @= noun[org,!alsoloc:!], prep[form:fof],  prep[form:fof], noun*[proper], noun[proper].

4> noun[proper:+,mwe:+,org:+,alsoloc:!] @= noun[org,!alsoloc:!], prep[form:ffor], (nadj+[maj]), noun+[maj,place:~] .

4> noun[proper:+,mwe:+,org:+,alsoloc:!] @= noun[orghead,!alsoloc:!], prep[form:fof];prep[form:ffor], noun[org].

5>  noun[proper:+,mwe:+,org:+,alsoloc:!] @= (noun[proper]) , noun[person], noun[org,!alsoloc:!,cr:~,cr2:~].

5>  noun[proper:+,mwe:+,org:+,alsoloc:!] @= (noun[proper]) , noun[firstname], noun[org,!alsoloc:!,cr:~,cr2:~].


5> noun[proper:+,mwe:+,org:+,alsoloc:!] @= noun[place], noun[org,!alsoloc:!];noun[bus].

5> noun[proper:+,mwe:+,org:+] @=  noun[org];noun[bus], noun[country].


// Warning
5>  noun[proper:+,mwe:+,org:+] @= |det[def] | noun[org,place:~], conj[coord], noun[org,place:~].


5> noun[proper:+,mwe:+,org:+] @= noun[org], noun[symbol,lemme:"&"], noun[org] .

5> noun[proper:+,mwe:+,org:+] @= noun[org], noun[symbol,lemme:"&"] .

5>  noun[proper:+,mwe:+,org:+] @= | noun[proper,lemme:"CEO"], prep[form:fof] | noun[guess,maj], noun+[guess,maj] |?[maj:~]|.

// 
6> noun[proper:+,mwe:+,person:+] @= noun[org];noun[bus];noun[toutmaj,acron];noun[toutmaj,guess], noun[person,mwe,title].


// Some rule for Foreign entities org (ex: Banque de France) :

6> noun[proper:+,mwe:+,org:+] @= noun[orghead,french],  noun[particlename], (noun[lemme:"la"]),  noun[proper,maj], (noun[maj,guess]).

6> noun[proper:+,mwe:+,org:+] @= noun[orghead,french], (nadj[lemme:"du"]),  noun[proper,maj], (noun[maj,guess]).


// CB TEST WITH TAGGING RULE 
// the Czech National Bank ( CNB ): marquer CNB comme org

Tagging:

7> noun[toutmaj,proper] %= | noun[org], punct[form:fopar] | noun[toutmaj:+,org:+,proper:+,propag:+] | punct[form:fcpar]|. 

7> noun[toutmaj,proper] %= | noun[org], punct[comma] | noun[toutmaj:+,org:+,proper:+,propag:+] | punct[comma]|. 

7>  noun[maj,guess] %= | noun[proper,lemme:"CEO"], prep[form:fof] |  noun[proper:+,org:+] |?[maj:~]|.
