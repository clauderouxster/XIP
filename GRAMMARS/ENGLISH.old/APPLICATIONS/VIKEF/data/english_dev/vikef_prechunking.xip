//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2006 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : English XIP grammar for VIKEF 
//!   filename   : 
//!   Date       : 03/02/2006
//!   Author     : S. Ait-Mokhtar
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

// bib citation labels

Sequence:

// FORMAT:   "[N]"  where N is a number
////////////////////////////////////////////////

// Single number,
// ex. "[12]"

1> NOUN[citlabel=+,mwe=+] = punct[form:fobrack],dig,punct[form:fcbrack],
   where (_DocSection:"Default").

// Sequence of numbers within [ and ]
// ex "[3,7]"

1> NOUN[citlabel=+,mwe=+] = punct[form:fobrack],dig 
     |(punct[comma],dig)+,punct[form:fcbrack]|,
   where (_DocSection:"Default").
   
1> NOUN[citlabel=+,mwe=+] = |punct[form:fobrack],dig,(punct[comma],dig)*|
     punct[comma],dig,punct[form:fcbrack],
   where (_DocSection:"Default").


1> NOUN[citlabel=+,mwe=+] = |punct[form:fobrack],dig,(punct[comma],dig)*|
     punct[comma],dig 
     |(punct[comma],dig)*,punct[form:fcbrack]|,
   where (_DocSection:"Default").


// FORMAT:   Name(s) (YEAR)
////////////////////////////////////////////////

// Single name
// ex. "Pewsey (2002)"


2> NOUN[citlabel=+,mwe=+] =
   noun[maj,initial:~],(punct[comma],noun[maj,initial:~])*,
   noun[coord,symbol];conj[form:fand],noun[maj,initial:~],
   punct[form:fopar],dig[year],punct[form:fcpar],
   where (_DocSection:"Default").

   
3> NOUN[citlabel=+,mwe=+] =
   noun[maj,initial:~],(NOUN[form:fetal]),punct[form:fopar],dig[year],punct[form:fcpar],
   where (_DocSection:"Default").


Marker:
10> NOUN[citlabel]{?*,noun#2[maj,form:~fetal,coord:~,person=+]}}.

//////////////////
// Dependencies

DependencyRules:

20> |noun#1[citlabel]|
    CITATIONLABEL(#1).

20> |noun#1[citlabel]{?*,noun#2[form:~fetal,coord:~]}|
    AUTHOR_OF(#1,#2).
    
20> |noun#1[citlabel]{?*[noun:~],dig#2[year:~]}|
    LABEL_OF(#1,#2).
    
20> |noun#1[citlabel]{?*,dig#2[year]}|
    DATE_OF(#1,#2).    
    

   
   
   
   
   
   
   
   
