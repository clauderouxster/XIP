DependencyRules:


////////////////////// ADDRESS ////////////////

| #1[address] |
  LIXO(#1).

| TOP#1{ ?+ } |
if ( ^LIXO(#2) )
   LOCATION[address=+](#1).

if ( ^LIXO(#1) )
   ~.

//JACKSONVILLE FL
//LOCORG_ITSVALUE_CITY(JACKSONVILLE)

| #1[end,usastate] |
  LIXO(#1).

| TOP#3{ ?+ } |
if ( ^LIXO(#1) & LOCORG[city](#2) )
   LOCATION[address=+](#3).

if ( ^LIXO(#1) )
   ~.

//NC ASHEBORO
//LOCATION_ITSVALUE(Asheboro)
//0>TOP{NP{North_Carolina} NP{Asheboro} ,}

| #1[start,usastate] |
if ( ^LOCATION(#2) )
   LIXO(#2).

| TOP#3{ ?+ } |
if ( ^LIXO(#1) )
   LOCATION[address=+](#3).

if ( ^LIXO(#1) )
   ~.

| #1[start,end,usastate] |
  LOCATION[address=+](#1).

////////////////////// ORGANISATION ////////////////

//GP CONSUMER PRODUCTS
//ORGANISATION_ITSVALUE(consumer Products)
//0>TOP{NP{GP} NP{NOUN{consumer Products}}}

| TOP#1{ ?{#2[maj,start]}, ?+ } |
if ( ^ORGANISATION(#3) & #2~:#3 )
   ORGANISATION(#1).

//PERSON_ITSVALUE(James Corporation)
| #1{#2,#3[orgend]} |
if ( ^PERSON(#1) )
   LIXO(#1).

| TOP#3{ ?+ } |
if ( ^LIXO(#1) )
   ORGANISATION(#3).


////////////////////// DATE ////////////////


| #1[nodate] |
if ( ^DATE(#1) )
   ~.

| #1[nodate] |
if ( ^DATE(#2) )
   ~.

////////////////////// PERSON ////////////////

//Srenaski, Susan J.
//PERSON_ITSVALUE(Susan J.)

| #1[proper], #2[punct] |
if ( ^PERSON(#3[end]) )
   LIXO(#3).

| #1[proper:~] |
if ( ^LIXO(#2) )
   ~.

| TOP#2{ ?+ } |
if ( ^LIXO(#1) )
   PERSON(#2).

if ( ^LIXO(#1) )
   ~.

//PERSON_ITSVALUE(Georg Iowa)

| #1{#2, #3[usastate]} |
if ( ^PERSON(#1) )
   ~.

////////////////////// LABELS and VALUES ////////////////

if ( ^EMAIL(#1) )
   EMAIL[itsvalue=+](#1)

if ( ^LOCORG(#1) )
   LOCORG[itsvalue=+](#1)

if ( ^LOCATION(#1) )
   LOCATION[itsvalue=+](#1)

if ( ^ORGANISATION(#1) )
   ORGANISATION[itsvalue=+](#1)

if ( ^PHONE(#1) )
   PHONE[itsvalue=+](#1)

if ( ^DATE(#1) )
   DATE[itsvalue=+](#1)

if ( ^HOUR(#1) )
   HOUR[itsvalue=+](#1)

if ( ^PERSON(#1) )
   PERSON[itsvalue=+](#1)

if ( ^MONEY(#1) )
   MONEY[itsvalue=+](#1)

if ( ^UNIT(#1) )
   UNIT[itsvalue=+](#1)

//if ( ^$1[itsvalue:~](#1) )
//   $1[label=+](#1).

//to eliminate dubles

| #1[noun]{?+,#2} |
if ( $1(#1) & ^$2(#2) )
   ~.

| #1[noun]{#2, ?+} |
if ( $1(#1) & ^$2(#2) )
   ~.

if ( LOCATION[address](#1) & ^$1(#2) )
   ~.

| TOP#1 { ?+ } |
if ( ^LOCORG(#2) & ORGANISATION(#1) )
   ~.

| TOP#1 { ?+ } |
if ( ^LOCATION(#2) & ORGANISATION(#1) )
   ~.


