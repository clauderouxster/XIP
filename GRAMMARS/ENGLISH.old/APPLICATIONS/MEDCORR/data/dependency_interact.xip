DependencyRules:

// VLINK
if ( SUBJ-N(#1,#2[annot]) & NUCL[vlink,passive:~](#1,#3[iv]) )
	AGENT[interact=+](#3,#2)

// SUBJ-N_INTERACT: "... groEL (gene) that encodes ..."

if (MOD[post,sentence,relativ](#1,#2[iv]) & MOD(#1,#3[annot]) & ~NUCL[passive](#4,#2) )
	AGENT[interact=+](#2,#3)

if (MOD[post,sentence,relativ](#1[annot],#2[iv]) )
	AGENT[interact=+](#2,#1)

// OBJ-N_INTERACT: interact with

if (MOD[post](#1[interact,verb],#2[annot]) & PREPD(#2,#3[form:fwith]) )
	AGORPAT(#1,#3,#2)

if (MOD[post](#1[interact,verb],#2[mwe,proper]) & PREPD(#2,#3[form:fwith]) )
	AGORPAT(#1,#3,#2)

if (MOD[post](#1[interact,verb],#2) & PREPD(#2,#3[form:fwith]) & MOD[pre](#2,#4[annot]) )
	AGORPAT(#1,#3,#4)


// OBJ-N_INTERACT: code for

if (MOD[post](#1verb[code],#2[annot]) & PREPD(#2,#3[form:ffor]) )
	PATIENT[interact=+](#1,#3,#2)

if (MOD[post](#1verb[code],#2[mwe,proper]) & PREPD(#2,#3[form:ffor]) )
	PATIENT[interact=+](#1,#3,#2)

if (MOD[post](#1verb[code],#2) & PREPD(#2,#3[form:ffor,prep]) & MOD[pre](#2,#4[annot]) )
	PATIENT[interact=+](#1,#3,#4)

// OBJ-N_INTERACT: bind to

if (MOD[post](#1verb[bind],#2[annot]) & PREPD(#2,#3[form:finfto,prep]) )
	PATIENT[interact=+](#1,#3,#2)

if (MOD[post](#1verb[bind],#2[mwe,proper]) & PREPD(#2,#3[form:finfto,prep]) )
	PATIENT[interact=+](#1,#3,#2)

if (MOD[post](#1verb[bind],#2) & PREPD(#2,#3[form:finfto,prep]) & MOD[pre](#2,#4[annot]) )
	PATIENT[interact=+](#1,#3,#4)

// ... p53 binding#1 to#2 the mdr1 promoter#3 ...

if ( LIAISON(#1,#2) & prepd(#3,#2) )
	PATIENT[interact=+](#1,#2,#3).


// *** FINITE VERBS - PASSIVE *** 

// duplications of ph suppress -> later: identity words

//if ( SUBJ-N(#1[iv],#2) & PREPD(#3[annot],#4[form:fof]) & MOD(#2,#3[annot]) )
//	AGENT[interact=+](#1,#3).

if ( SUBJ-N(#1,#2[annot]) & NUCL[vlink,passive](#1,#3[iv]) )
	PATIENT[interact=+](#3,#2)

if ( SUBJ-N(#1,#2[allmark]) & NUCL[vlink,passive](#1,#3[iv]) & MOD[pre](#2,#4[annot]) )
	PATIENT[interact=+](#3,#4).

if ( SUBJ-N(#1,#2[annot]) & NUCL(#1,#3) & NUCL[passive](#3,#4[iv]) )
	PATIENT[interact=+](#4,#2)

if ( SUBJ-N(#1,#2[mark]) & NUCL(#1,#3) & NUCL[passive](#3,#4[iv]) & MOD[pre](#2,#5[annot]) )
	PATIENT[interact=+](#4,#5)

if (MOD[post,sentence,relativ](#1,#2[iv]) & MOD(#1,#3[annot]) & NUCL[passive](#4,#2) )
	PATIENT[interact=+](#2,#3)

if (MOD[post,sentence,relativ](#1[annot],#2[iv]) & NUCL[passive](#3,#2) )
	PATIENT[interact=+](#2,#1)

if (MOD[post,sentence,relativ](#1[mark],#2[iv]) & NUCL[passive](#3,#2) & MOD[pre](#2,#4[annot]) )
	PATIENT[interact=+](#2,#4)


//SUBJ-N(regulated_MARK:+,miRNAs_ANNOT:+)

if ( SUBJ-N(#1[mark],#2[annot]) )
   AGENT[interact=+](#1,#2).


// *** INFINITIVE  ***

// "240B1 has been cloned and shown to encode ... "

| nfv{#3,#4}, conj[coord], nfv, iv{prep,verb#5[iv]} |
if ( SUBJ-N(#1,#2[annot]) & NUCL(#1,#3) & NUCL(#3,#4) ) 
	AGENT[interact=+](#5,#2).

// PTH#1 was#2 recently shown#3 to#4 inhibit#5

| ?{?*, #2[partpas,last]}, iv{prep[form:fto], ?*, #5[iv,last]} |
if ( SUBJ-N(#2,#1[annot]) & NUCL[passive](#2,#3) & prepd(#5[iv],#4) )
	AGENT[interact=+](#5,#1).

//SUBJ-N_PRE(was,PTH_ANNOT:+_GENANNOT:+)
//NUCL_VLINK_PASSIVE(was,shown)
//PREPD(inhibit,to)



// *** -ING  *** 

// transform MOD_POST_GERUND into SUBJ-N-INTERACT

if ( MOD[post,gerund](#1[annot],#2[iving]) )
	AGENT[interact=+](#2,#1).

// 1 chunk ending in interaction verb+ing

| ?{?*, ?#1[annot], ?*, ?#2[iving,last]} |
	PATIENT[interact=+](#2,#1).

// OBJ-N_INTERACT dependency between "binding" forms and the previous entity

| ?{?*,?#1[last,annot]}, (punct[hyphen];punct[form:fhyph]), NP{?*,?^#2[binding]} |
	PATIENT[interact=+](#2,#1)

| ?{?*,?#1[last,annot]}, (punct[hyphen];punct[form:fhyph]), AP{?*,?^#2[binding]} |
	PATIENT[interact=+](#2,#1)

| ?#1[annot], (punct[hyphen];punct[form:fhyph]), ?#2[binding] |
	PATIENT[interact=+](#2,#1)

| ?{?*,?#1[last,annot], (punct[hyphen];punct[form:fhyph]), ?^#2[binding]} |
	PATIENT[interact=+](#2,#1)

// OBJ-N_INTERACT dependency between "-encoding" forms and the previous entity

//| ?{(?[form:fopar]) , ?#1[annot], (?[form:fcpar]) , punct[hyphen];punct[form:fhyph], ?#2[hyphmark]} |
//	PATIENT[interact=+](#2,#1)

//| ?{(?[form:fopar]) , ?#1[annot], (?[form:fcpar]) , punct[hyphen];punct[form:fhyph], ?#2[encode,vprog]} |
//	PATIENT[interact=+](#2,#1)

//| ?{(?[form:fopar]) , ?#1[annot], (?[form:fcpar]) , punct[hyphen];punct[form:fhyph], ?#2[ia,vprog]} |
//	PATIENT[interact=+](#2,#1)

// SUBJ-N_INTERACT dependency : sugar transporter-encoding gene gguA : SUBJ-N(encoding,gguA)

//| #1{?,punct[hyphen];punct[form:fhyph],#2[encode,vprog]} |
//if ( MOD[pre](#3[annot],#1) )
//	AGENT[interact=+](#2,#3).

// X-iving -> PATIENT(iv,X)

| ?#1[annot], punct[hyphen];punct[form:fhyph], ?#2[ia,ding] |
	PATIENT[interact=+](#2,#1).


// *** ED ***

// X-ived -> AGENT(iv,X)

| ?#1[annot], punct[hyphen];punct[form:fhyph], ?#2[ia,ded] |
	AGENT[interact=+](#2,#1).

// X-ived Y PATIENT(iv,Y)

| AP{#1{?, punct[hyphen];punct[form:fhyph], ?[ia,last]}}, ?#2[annot] |
if ( MOD[pre](#2[annot],#1) )
	PATIENT[interact=+](#1,#2).

// transform MOD_POST_PASTPART into OBJ-N

if ( MOD[post,pastpart](#1[annot],#2[iv]) )
	PATIENT[interact=+](#2,#1).

if ( MOD[post,pastpart](#1[allmark],#2[iv]) & MOD[pre](#1,#3[annot]) )
	PATIENT[interact=+](#2,#3).

//ADJ+entity+allmark "the downregulated ptb gene" -> OBJ-N(downregulate,ptb)

if ( MOD[pre](#1[allmark],#2[ia]) & MOD[pre](#1,#3[annot]) )
	PATIENT[interact=+](#2,#3).

//MOD_PRE(gene,downregulated)
//MOD_PRE(gene,ptb_ANNOT:+_PROTANNOT:+_GENANNOT:+)
 

// *** NOUNS ***

// *** TION ***

// interaction between X and Y

//a) all the dependencies are extracted by the parser

//if ( MOD[post](#1[interact,noun],#3[annot]) & MOD[post](#1[interact,noun],#5[annot]) & COORD(#4[coord],#5[annot]) & COORD(#4[coord],#3[annot]) & PREPD(#5[annot],#2[sc_pbetween]) & & PREPD(#3[annot],#2[sc_pbetween]) )
//	AGORPAT(#1,#3).

if ( MOD[post](#1[interact,noun],#3[annot]) & COORD(#4[coord],#3[annot]) & PREPD(#3[annot],#2[sc_pbetween]) )
	AGORPAT(#1,#3).

//b) not all the dependencies are extracted by the parser

if ( COORD(#4[coord],#5) & COORD(#4[coord],#1[interact,noun]) & PREPD(#3[annot],#2[sc_pbetween]) )
	AGORPAT(#1,#3).


if ( COORD(#4[coord],#6[allmark]) & COORD(#4[coord],#1[interact,noun]) &PREPD(#3,#2[sc_pbetween]) & MOD[pre](#6[allmark],#5[annot]) & MOD[post](#1[interact,noun],#3) )
	AGORPAT(#1,#5).

if ( MOD[post](#1[interact,noun],#2[annot]) & PREPD(#2[annot],#3[sc_pbetween]) )
	AGORPAT(#1,#2).

if ( AGORPAT(#1[interact,noun],#3[annot]) & COORD(#4[coord],#5[annot]) & #3 < #5 )
	AGORPAT(#1,#5).

// interaction PASSIVE VERB between X and Y

if ( MOD[post](#3,#5[annot]) & SUBJ-N[pre](#2,#1[interact,noun]) & NUCL[vlink,passive](#2,#3) & PREPD(#5[annot],#4[sc_pbetween]) )
	AGORPAT(#1,#5).

// interaction with

if ( MOD[post](#1[interact,noun],#2[annot]) & PREPD(#2[annot],#3[form:fwith]) )
	AGORPAT(#1,#2).

// interaction of

if ( MOD[post](#1[interact,noun],#2[annot]) & PREPD(#2[annot],#3[form:fof]) )
	AGORPAT(#1,#2).

// X interaction

| #1[annot],#2[interact,noun] |
	AGORPAT(#2,#1).


// inhibition

// X inhibition of Y -> SUBJ-N(inhibit,X)

if ( MOD[pre](#1[in],#2[annot]) & MOD[post](#1[in],#3) & PREPD(#3,#4[form:fof]) )
	AGENT[interact=+](#1,#2).

if ( MOD[pre](#2[in],#1[annot]) & MOD[pre](#5[allmark],#4[annot]) & MOD[post](#2[in],#5[allmark]) & PREPD(#5[allmark],#3[form:fof]) )
	AGENT[interact=+](#2,#1).

// if there is a MOD[pre] dependency between an [in] and an [annot] -> PATIENT[interact]([in],[annot])

| ?{?*, ?#2[annot], ?#1[in]} | 
if ( MOD[pre](#1[in],#2[annot])  & ~SUBJ-N(#1,#2) )
	PATIENT[interact=+](#1,#2).

if ( MOD[pre](#1[allmark],#2[in]) & MOD[pre](#1,#3[annot]) & ~AGENT[interact=+](#2,#3) )
	PATIENT[interact=+](#2,#3) .

//OBJ-N_INTERACT dependency between activation nouns and entities in the following PP "activation of X"
 
if ( MOD[post](#1[in],#2[annot]) & PREPD(#2,?[form:fof]) )
	PATIENT[interact=+](#1,#2).

if ( MOD[post](#1[in],#2[annot]) & PREPD(#2,?[form:ffor]) )
	PATIENT[interact=+](#1,#2).

if ( MOD[post](#1[in],#2[allmark]) & PREPD(#2,?[form:fof]) & MOD[pre](#2,#3[annot]) )
	PATIENT[interact=+](#1,#3).

// (GSTp inhibition) of (JNK activity)

| #1{#2,#3[in]} |
if ( MOD[post](#1,#4[annot]) & PREPD(#4[annot],?[form:fof]) )
	PATIENT[interact=+](#3,#4).

//OBJ-N_INTERACT dependency between activation nouns and coordinated entities 

if ( PATIENT[interact](#1,#2) & COORD(?,#2[annot]) & COORD(?,#3[annot]) & ^PATIENT[interact=+](#2) )
	PATIENT[interact=+](#1,#3)


// OBJ-N_INTERACT dependency: X encoding Y : OBJ-N(encoding,Y)
| GV{?#1[encode]}, ?{?*,?^#2[annot]} |
	if (^PATIENT[interact=+](#2) )
	PATIENT[interact=+](#1,#2)


//SUBJ-N_INTERACT dependency between activation nouns and PPs introduced by "by", "with", "through"


| ?{?*,?^#1[last,in]}, (PP), PP{?[agprep],?^#2[annot]} |
	AGENT[interact=+](#1,#2)

// "activation of latent TGF-beta1 protein by both A549 and ANA-1 cells"

| ?{?*,?^#1[in]}, (PP), PREP[agprep], QUANT, ?{?^#2[annot]} |
	AGENT[interact=+](#1,#2)

// "transactivation of IE promoters by the virion protein VP16" - apposition

| ?{?*,?^#1[in]}, (PP), PP{?[agprep],NP^{?*,?[protein,last]}},NP{?^#2[annot]} |
	AGENT[interact=+](#1,#2)

| ?{?*,?^#1[in]}, (PP), PP{?[agprep],NP^{?*,?[allmark,last]}},NP{?^#2[annot]} |
	AGENT[interact=+](#1,#2)

// "... inhibition by adenovirus E1A protein ."

//if (MOD[post](#1[in],#2[allmark]) & MOD[pre](#2,#3[annot]) & PREPD(#2,#4[agprep]) )
//	AGENT[interact=+](#1,#3).

if ( MOD[post](#1[in],#2[annot]) & PREPD(#2[annot],#3[agprep]) )
	AGENT[interact=+](#1,#2).

//X-dependent activation

| #2{?#3[annot], ?[form:fhyph], ?[genact]} |
if ( MOD[pre](#1[in],#2) )
	AGENT[interact=+](#1,#3).

| #4{#1[annot], ?*, #2[in,last]} |
if ( MOD[post](#4[annot],#3) & PREPD(#3,?[form:fof]) )
	AGENT[interact=+](#2,#1).

// #5{L-PAM#1-induced#2} #6{TNF-alpha#3 up-regulation#4} -> AGENT_INTERACT(up-regulation, L-PAM)

| #5{?{#1[annot], ?[form:fhyph], #2[genact]}}, #6{#3[annot],#4[in]} |
	AGENT[interact=+](#4,#1).

// X in

| #1[annot], ?*, #2[in,last] |
if ( ~SUBJ-N(#2,#1) ) 
	PATIENT[interact=+](#2,#1).

// X in by Y -> AGENT(in,Y)

| ?{?*, ?^#1[last,in]},  pp{?[form:fby],#2[annot]} |
	AGENT[interact=+](#1,#2).

// Repression#1 requires#2 the D-raf#3 kinase

// "Glucagon#1 plays#2 a pivotal role#3 in#4 the regulation#5 of metabolism." AGENT_INTERACT(regulate,Glucagon)

if ( SUBJ-N(#2[copverb],#1[annot]) & OBJ-N(#2[copverb],#3[copobj]) & MOD[post](#3[copobj],#5[in]) & PREPD(#5[in],#4) )
	AGENT[interact=+](#5,#1).
	
if ( SUBJ-N(#2[copverb],#1[annot]) & OBJ-N(#2[copverb],#3[copobj]) & MOD[post](#3[copobj],#5[iving]) )
	AGENT[interact=+](#5,#1).
	



//// *** ER-OR ***

// X repressor


| #2[annot], #1[ier] |
if (MOD[pre](#1[ier],#2[annot]) )
	PATIENT[interact=+](#1,#2).

// X as an activator

if (MOD(#1[annot],#2[ier]) & PREPD(#2[ier],?[form:fas]) )
	AGENT[interact=+](#2,#1).

if ( MOD[post](#1[ier],#2[annot]) & PREPD(#2[annot],?[form:fof]) )
	PATIENT[interact=+](#1,#2).

// "GLUT1 is a glucose transporter." SUBJ-N(transporter,GLUT1)

if ( SUBJ-N(#1[form:fbe],#2[annot]) & NUCL[subjcompl](#1,#3[ier]) )
	AGENT[interact=+](#3,#2).

// X functions as a ie -> AGENT(ier,X)

if ( SUBJ-N(#1,#2[annot]) & MOD[post](#1,#3[ier]) & PREPD(#3[ier],#4[form:fas]) )
	AGENT[interact=+](#3,#2).

// " ... the bicoid morphogen, which functions solely as an activator"

if ( MOD[post,sentence,relativ](#2[annot],#1) & MOD[post](#1,#3[ier]) & PREPD(#3[ier],#4[form:fas]) )
	AGENT[interact=+](#3,#2).


// the bicoid morphogen#1, which appears#2 to function#3 solely as#4 an activator#5.

if ( MOD[post,sentence,relativ](#1[annot],#2) & MOD[post](#3,#5[ier]) & PREPD(#5[ier],#4[form:fas]) & OBJ-N[post,infinit](#2,#3) )
//if ( MOD[post,sentence,relativ](#1[annot],#2) & MOD[post](#3,#5[ier]) & OBJ-N[post,infinit](#2,#3) )
	AGENT[interact=+](#5,#1).

//| #1[annot], #2[ier] |
//	PATIENT[interact=+](#2,#1).

//if ( MOD[post](#1[ier],#2[annot]) & ~PREPD(#2[annot],?) )
//	AGENT[interact=+](#1,#2).

//// transporter, AtsRBC

//| ?{?*, ?^#1[ier,last]}, punct[comma], np{#2[annot]} |
//	AGENT[interact=+](#1,#2).

//// transporter cDNA, AtsRBC

//| ?{?*, ?^#1[allmark,last]}, punct[comma], np{#2[annot]} |
//if (MOD[pre](#1[allmark],#3[ier]) )
//	AGENT[interact=+](#3,#2).

//// transporter (AtsRBC)

//| ?{?*, ?^#1[ier,last]}, ins{?, np{#2[annot]}} |
//	AGENT[interact=+](#1,#2).





// *** FINITE VERBS - ACTIVE *** 

// SUBJ-N_INTERACT and OBJ-N_INTERACT dependency: SUBJ-N(E1) verbe[iv] OBJ-N(E2)

if ( SUBJ-N(#1[iv],#2[annot]) & ~NUCL[vlink,passive](#3,#1[iv]) )
	AGENT[interact=+](#1,#2)

if ( SUBJ-N(#1[iving],#2[annot]) )
	AGENT[interact=+](#1,#2)

if ( OBJ-N(#1[iv],#3[annot]) )
	PATIENT[interact=+](#1,#3)

if ( OBJ-N(#1[iving],#3[annot]) )
	PATIENT[interact=+](#1,#3)

// OBJ-N_INTERACT: "binds rsmB RNA"

if ( OBJ-N(#1[iv],#3[allmark]) & MOD(#3,#2[annot]) )
	PATIENT[interact=+](#1,#2)

//  *** NON-FINITE VERBS  *** 

// "The ability of dl to function as both an activator and repressor ... "



// *** dependencies depending on the last dependencies ****

// " ... the icd gene encoding isocitrate ... " if "isocitrate" is OBJ-N, "icd" is SUBJ-N

| ?{?*,?^#3[annot]}, gv{#1} |
if ( OBJ-N(#1[encode,iving],#2) )
	AGENT[interact=+](#1,#3) .

// if a noun is an object of an interaction verb, its premodifier is the object of interaction

if ( OBJ-N(#1[iving],#2) & MOD[pre](#2[allmark],#3[annot]) )
	PATIENT[interact=+](#1,#3).

if ( OBJ-N(#1[iving],#2) & MOD[appos](#2[allmark],#3[annot]) )
	PATIENT[interact=+](#1,#3).

// The entity name is the premodifier of a marked word

if ( SUBJ-N(#1[iv],#2) & MOD[pre](#2[in:~,allmark],#3[annot]) )
	AGENT[interact=+](#1,#3).

if ( SUBJ-N(#1[iv],#2) & MOD[appos](#2[in:~,allmark],#3[annot]) )
	AGENT[interact=+](#1,#3).

if ( SUBJ-N(#1[iving],#2) & MOD[pre](#2[in:~,allmark],#3[annot]) )
	AGENT[interact=+](#1,#3).

if ( SUBJ-N(#1[iving],#2) & MOD[appos](#2[in:~,allmark],#3[annot]) )
	AGENT[interact=+](#1,#3).


if ( AGENT(#1[iv],#2) & MOD[pre](#2[in:~,allmark],#3[annot]) )
	AGENT[interact=+](#1,#3).

if ( AGENT(#1[iv],#2) & MOD[appos](#2[in:~,allmark],#3[annot]) )
	AGENT[interact=+](#1,#3).

if ( AGENT(#1[iv],#2[annot]) )
	AGENT[interact=+](#1,#2) .

if ( MOD[pre](#1[annot],#2[ia]) )
	PATIENT[interact=+](#2,#1) .

if ( MOD[pre](#1[allmark],#2[ia]) & MOD[pre](#1,#3[annot]) )
	PATIENT[interact=+](#2,#3) .

// demander à Claude
if ( $1(#2,#3) & MOD[post,appos](#3,#4[annot]) )
	$1(#2,#4).

// X and Y interact

if ( SUBJ-N(#1[interact],#2[annot]) )
	AGORPAT(#1,#2).

if (^AGENT[interact](#1[interact],#2[annot]) )
	~ .




//|noun#1[genannot]| compute(@biotip.gene[mot:#1[lemme]]->Create() ).
//|noun#1[protannot]| compute(@biotip.protein[mot:#1[lemme]]->Create() ).

//final(@biotip->save("/home/sandor/biotip/essai.xml")).

if ( ^$1(#1,#2) & $1(#1,#2) )
   ~.


if ( AGENT(#1,#2) & PATIENT(#1,#3) )
   INTERACTION(#2,#1,#3).