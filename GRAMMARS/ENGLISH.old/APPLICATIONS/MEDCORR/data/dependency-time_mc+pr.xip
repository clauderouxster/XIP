//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2006 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : english grammar for political risk detection
//!   filename   : dependency-time_pr.xip
//!   Date       : January 2007
//!   Author     : Aude Rebotier, Ágnes Sándor
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

// This file creates the COORDINATE_TIMEs between a core and a date or other temporal information. These are only rough informations (temporal expressions in the text and tenses).



DependencyRules:

//*********** corrections 
//when the issue of security come up, obviously we will have to step in
if (^PRESENT(#1) & MOD(#1,?[form:fwhen]) & EMBED(#1,#3) & FUTURE(#3))
	FUTURE(#1)


//*********** création des coordinates

if (TEMP[pre:~](#1,#2) & CORE(#1) & TIMEX3(#2) )
	COORDINATE[time=+](#1,#2)

//Tenses
if(PAST(#1) & CORE(#1) & ~COORDINATE[time](#1,?))
	COORDINATE[time=+](#1,##NOUN[lemme=PAST])

| #1[surface:had] |
if ( CORE(#1) & ~COORDINATE[time](#1,?))
	COORDINATE[time=+](#1,##NOUN[lemme=PAST])

if(VDOMAIN[progress](#1,#2[pres]) & CORE(#1) & ~COORDINATE[time](#1,?))
	COORDINATE[time=+](#1,##NOUN[lemme=PRESENT])

if(VDOMAIN[modal](#1,#2) & CORE(#1) & ~COORDINATE[time](#1,?))
	COORDINATE[time=+](#1,##NOUN[lemme=PRESENT])


if(PRESENT(#1) & CORE(#1) & ~COORDINATE[time](#1,?))
	COORDINATE[time=+](#1,##NOUN[lemme=PRESENT])

if(FUTURE(#1) & CORE(#1) & ~COORDINATE[time](#1,?))
	COORDINATE[time=+](#1,##NOUN[lemme=FUTURE])


if(NEAR-FUTURE(#1) & CORE(#1) & ~COORDINATE[time](#1,?))
	COORDINATE[time=+](#1,##NOUN[lemme=NEAR-FUTURE])

//Nouns
if (CORE-NOUN(#1) & MOD(#1,#2[lemme:current]) & ~COORDINATE[time](#1,?))
	COORDINATE[time=+](#1,##NOUN[lemme=PRESENT])

if (CORE-NOUN(#1) & MOD(#1,#2[lemme:future]) & ~COORDINATE[time](#1,?))
	COORDINATE[time=+](#1,##NOUN[lemme=FUTURE])

|NP#1, AP#2|
	if (HEAD2(#3,#1) & HEAD2(#4,#2) & CORE-NOUN(#3) & MOD[pre](#4,?[lemme:currently]) & ~COORDINATE[time](#3,?))
	COORDINATE[time=+](#3,##NOUN[lemme=PRESENT])

//if((ARGUMENT[attribute](#1,#2)|ARGUMENT[sv](#1,#2)) & COORDINATE[time](#1,#3) & ~COORDINATE[time](#2,?))
//	COORDINATE[time=+](#2,#3)

if( ARGUMENT[attribute](#1,#2) & COORDINATE[time](#1,#3) & ~COORDINATE[time](#2,?))
	COORDINATE[time=+](#2,#3)




// choose the date between two TEMPs: visit  Iran on 9 July, for the second time this year.

//COORDINATE_TIME(#1visit,#2for the second time this year)
//COORDINATE_TIME(#1visit,#3on 9 July)
//DATE(#49 July)
//PP#3{on NP{NOUN{#49 July}}}

| #3PP{?, np{#4noun{?}}} |
if ( ^COORDINATE[time](#1,#2) & COORDINATE[time](#1,#3) & DATE(#4) )
	~.

