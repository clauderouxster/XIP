DependencyRules:

| TOP#1{ ?+ } |
  SENT(#1).

////////////////////////////////////////////////
///////////////ROLES///////////////////////////
///////////////////////////////////////////////

| #2[doctor=+] |
if ( SUBJ(#1,#2[p1,pronpers]) )
   ~.

| #2[doctor=+] |
if ( SUBJ-N(#1,#2[p1,pronpers]) )
   ~.

| #2[doctor=+] |
if ( OBJ-N(#1,#2[p1,pronpers]) )
   ~.

| #2[doctor=+] |
if ( OBJ(#1,#2[p1,pronpers]) )
   ~.

| #2[colleague=+] |
if ( SUBJ(#1,#2[p2,pronpers]) )
	~.

| #2[colleague=+] |
if ( SUBJ-N(#1,#2[p2,pronpers]) )
	~.

| #3[colleague=+] |
if ( DETD(#2,#3[p2,personal,gen,pron]) )
	~.

| #3[colleague=+] |
if ( DETD(#2,#3[p2,personal,gen,pron]) )
	~.


| #2[patient=+] |
if ( SUBJ(#1,#2[p1:~,p2:~,pronpers,lemme:~it,pl:~]) )
	~.

//| #2[patient=+] |
//if ( SUBJ(#1,#2[lemme:Mary]) )
//	~.

| #2[patient=+] |
if ( SUBJ(#1,#2[lemme:who]) )
	~.

| #2[patient=+] |
if ( SUBJ-N(#1,#2[p1:~,p2:~,pronpers,lemme:~it,pl:~]) )
	~.

//| #2[patient=+] |
//if ( SUBJ-N(#1,#2[lemme:Mary]) )
//	~.

| #2[patient=+] |
if ( SUBJ-N(#1,#2[lemme:who]) )
	~.

| #3[patient=+] |
if ( SUBJ(#1,#2) & DETD(#2,#3[p3,personal,gen,pron,pl:~]) )
	~.

| #3[patient=+] |
if ( SUBJ-N(#1,#2) & DETD(#2,#3[p3,personal,gen,pron,pl:~]) )
	~.

| #2[patient=+] |
if ( MOD[pre](#1[patient],#2) )
	~.

| #3[p3:+,personal:+,gen:+,pron:+,patient=+] |
  ~.

//they are painful
//ATTRIB(They_PATIENT:+,painful)

| #1[patient=~,medical_condition=+,mc=+] |
if ( ATTRIB(#1,#2[lemme:painful]) )
   ~.

//SUBJ-N_PRE(recur,they_PATIENT:+)

| #2[patient=~,medical_condition=+,mc=+] |
if (SUBJ-N(#1[lemme:recur],#2) )
   ~.


| #2[other=+] |
if ( SUBJ(#1,#2[doctor:~,colleague:~,patient:~]) )
	~.

| #2[other=+] |
if ( SUBJ-N(#1,#2[doctor:~,colleague:~,patient:~]) )
	~.

| #1[factual=+] |
if ( $1[factual](#1) |  $1[factual](#1,#2) | $1[factual](#2,#1) )
   ~.

| #1[lemme:Mary] |
if ( ~PATIENT(#1) )
   PATIENT(#1).

| #1[lemme:old] |
if ( ~PATIENT(#1) )
   PATIENT(#1).

| #1[lemme:mother] |
if ( ~PATIENT(#1) )
   PATIENT(#1).

| #1[lemme:she] |
if ( ~PATIENT(#1) )
   PATIENT(#1).



//| #1[lemme:she,Mary=+] |
//if ( PATIENT(#1) & Mary:1 )
//   ~.





/
| #1[todo1=+,todo=+] |
if ( SUBJ(#1,#2[colleague]) )
   TODO1(#1).

| #1[todo1=+,todo=+] |
if ( SUBJ-N(#1,#2[colleague]) )
   TODO1(#1).

| #2[todo2=+,todo=+,todo1:~] |
if ( ( $1(#1[todo1],#2) | $1(#2,#1[todo1]) ) & #1 < #2 )
   TODO2(#2).

| #2[todo3=+,todo=+,todo1:~,todo2:~] |
if ( ( $1(#1[todo2],#2) | $1(#2,#1[todo2]) ) & #1 < #2 )
   TODO3(#2).

| #2[todo4=+,todo=+,todo1:~,todo2:~,todo3:~] |
if ( ( $1(#1[todo3],#2) | $1(#2,#1[todo3]) ) & #1 < #2 )
   TODO4(#2).

if ( TODO1(#1) & TODO2(#2) & TODO3(#3) & TODO4(#4) )
   TODO(#1,#2,#3,#4).

if ( TODO1(#1) & TODO2(#2) & TODO3(#3) )
   TODO(#1,#2,#3).
\

//////////////////////////////////////////////////////////////
////////////////   SIMeD    /////////////////////////////////
////////////////////////////////////////////////////////////


////////context sensitive features

//she states

//SUBJ_PRE(states[subjwpat],she_PATIENT:+)

| #1[subjective=+] |
if ( SUBJ-N(#1[subjwpat],#2[patient]) )
   ~.

//the patient needs

| #1[plan=+] |
if ( SUBJ-N(#1[planwpat],#2[patient]) )
   ~.

//I would continue
//SUBJ-N_PRE(continue,I)

| #1[plan=+,past:~] |
if ( SUBJ-N(#1,#2[lemme:I]) )
   ~.

//Mr. Cadd will be admitted
//MAIN_MODAL_PASSIVE(admitted)

| #1[lemme:admit,subjective=~] |
if ( MAIN[passive](#1) )
   ~.

//We will admit the patient 
//SUBJ-N_PRE(admit,We)

| #1[lemme:admit,subjective=~] |
if ( SUBJ-N(#1,#2[lemme:we]) )
   ~.
//Patient with multiple medical problems admitted
//SUBJ-N_PRE(admitted,Patient)

| #1[lemme:admit,subjective=~] |
if ( SUBJ-N(#1,#2[patient]) )
   ~.


//family history
//MOD_PRE(history,family_FAMILY:+)

| #1[history=~] |
if ( MOD[pre](#1,#2[family]) )
   ~.

// blood in
//MOD_POST(#1blood,#2stool)
//PREPD(#2stool,#3in)

| #1[supporter=+] | 
if ( MOD[post](#1[supporterwplace],#2) & PREPD(#2,#3[lemme:in]) )
   ~.

| NP#2{#1[noun]} |
if ( ^HEAD(#1,#1) )
   ~.

///////////////////////////////////////////////////////////
/////////////////////// ASSESSMENT ////////////////////////
///////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////
/////////////////////// HISTORY ////////////////////////
///////////////////////////////////////////////////////////


//a history of COPD
//MOD_POST(history[assessment],COPD)
//PREPD(#2,#3[lemme:of])


| NP#4 |
if ( MOD[post](#1[history],#2) & PREPD(#2,#3[lemme:of]) & HEAD(#2,#4) )
   ASSESSMENT[history=+](#4).



/////////////COORDINATION//////////////

//Significant for history of diverticulitis x2, arthritis, dyslipidemia, peptic ulcer disease, and glaucoma.
//MOD_POST(history,disease)
//COORD(and,disease)
//ASSESSMENT_HISTORY(diverticulitis)

| NP#4 |
if ( ASSESSMENT[history](#1) & MOD[post](#2[history],#3) & COORD(?,#3) & HEAD(#3,#4) & ~ASSESSMENT[history](#4))
   ASSESSMENT[history=+](#4).


//Past medical history of hypertension, diabetes, hyperlipidemia, obesity, depression:
//PP{of NP{hypertension}} , NP{diabetes} 
//ASSESSMENT_HISTORY(hypertension)

| PP{?,NP#1}, ?[comma], NP#2 |
if ( ASSESSMENT[history](#1) )
   ASSESSMENT[history=+](#2).

| NP#1, ?[comma], NP#2 |
if ( ASSESSMENT[history](#1) )
   ASSESSMENT[history=+](#2).

//No history of alcohol or drug use.

| PP{?,NP#1}, ?[lemme:or], NP#2 |
if ( ASSESSMENT[history](#1) )
   ASSESSMENT[history=+](#2).

/////////////NEGATIVE//////////////

//QUANTD_NEG(history,No)

| NP#1 |
if ( ^ASSESSMENT[history](#1) & QUANTD[neg](#2[history],#3) )
   ASSESSMENT[history,neg=+](#1).

//The patient did not have any history of CVA or TIA.
//NEGAT(have)
//OBJ_POST(have,history)

if ( NEGAT(#1) & OBJ[post](#1,#2) & ^ASSESSMENT[history](#3) )
   ASSESSMENT[history,neg=+](#3).

/////////////OVERWRITTEN//////////////

if (^ASSESSMENT[history](#1) & HEAD(#2,#1) & SUBJ(#3,#2) )
   ~.

//The patient denies any family history of heart disease.
//OBJ_POST(#4denies[subjective],#2history)
//MOD_POST(#2history,#3disease)
//HEAD(#3disease,#1heart disease)
//ASSESSMENT_HISTORY(#1heart disease)

if ( ^ASSESSMENT[history](#1) & MOD[post](#2[history],#3) & HEAD(#3,#1) & OBJ[post](#4[subjective],#2) )
   ~.

//presents with a 5-week history of low energy
if ( ^ASSESSMENT[history](#1) & MOD[post](#2[history],#3) & HEAD(#3,#1) & MOD[post](#4[subjective],#2) )
   ~.


if ( ^ASSESSMENT[history](#1) & HEAD(#2[history],#1) )
   ~.

| #1[family] |
if ( ^ASSESSMENT[history](#2) )
   ~.

//she states she does have a history of chronic diarrhea
//ASSESSMENT_HISTORY(#1chronic diarrhea)
//OBJ_POST_SENTENCE(#3states,#4have)
//OBJ-N(#4have,#5history)

if ( ^ASSESSMENT[history](#1) & OBJ[sentence](#3[subjective],#4) & OBJ-N(#4,#5[history]) )
   ~.



///////////////////////////////////////////////////////////
/////////////////////// DOCTOR'S VIEW ////////////////////////
///////////////////////////////////////////////////////////


//I am highly concerned about primary lung cancer.
//MOD_POST(#1concerned,#2cancer)
//ARGUMENT(#1concerned[assessment],#3I)
//HEAD(#2cancer,#4primary lung cancer)
//PP{about NP#4{primary lung cancer}} 

| NP#4 |
if ( MOD[post](#1,#2) & $1(#1[assessment],#3[lemme:I]) & HEAD(#2,#4) )
   ASSESSMENT(#4).


///////////////////////////////////////////////////////////
/////////////////////// SUBJECTIVE ////////////////////////
///////////////////////////////////////////////////////////




//who presents for progressive dyspnea over the last week.
//MOD_POST(presents[subjective],dyspnea)
//HEAD(dyspnea,for progressive dyspnea)

| NP#3 |
if ( MOD[post](#1[subjective],#2) & HEAD(#2,#3) )
   SUBJECTIVE(#3).

if ( MOD[post](#1[subjective,lemme:present],#2) & HEAD(#2,#3) & ^SUBJECTIVE(#3) & PREPD(#2,#4[lemme:to]) )
   ~.


if ( MOD[post](#1[subjective,lemme:present],#2) & HEAD(#2,#3) & ^SUBJECTIVE(#3) & PREPD(#2,#4[lemme:via]) )
   ~.


//She does admit to productive cough with sputum
//MOD_POST(#5cough,#1sputum)
//PP#1{with NP{sputum}}
//SUBJECTIVE(#2productive cough)
//SUBJECTIVE(#1sputum)
//HEAD(#5cough,#2productive cough)

| PP#3{?, NP#4{?*,#1[last]}} |
if ( ^SUBJECTIVE(#2) & SUBJECTIVE(#4) & MOD[post](#5,#1) & HEAD(#5,#2) )
   SUBJECTIVE(#2,#3).

if ( ^SUBJECTIVE(#4) & MOD[post](#5,#1) & HEAD(#5,#2) & SUBJECTIVE(#2,#3) )
   ~.


//She states that she has had some low grade temperatures in the 99.5 range, but denies any frank fevers.
//EMBED_COMPLTHAT(#1had,#2states[subjwpat])
//COORDINATE(#1had,#3temperatures)
//HEAD(#3temperatures,#4some low grade temperatures)
//MODALITIES(#2states,#1had)
//OBJ_POST(had,temperatures)
//OBJ_POST_SENTENCE(states,had)

if ( OBJ[sentence](#2[subjective],#1[lemme:have]) & OBJ(#1,#3) & HEAD(#3,#4) )
   SUBJECTIVE(#4).

//She states she quit smoking back in November 2011.
//OBJ_POST_SENTENCE(#1states,#2quit)
//OBJ_POST(#2quit,#3smoking)

if ( OBJ[post,sentence](#1[subjective],#2[lemme:quit]) & OBJ[post](#2,#3) )
   SUBJECTIVE(#2,#3).

//The patient admits to having frequent orthostatic dizziness
//MOD_POST_GERUND(admits,having)
//OBJ_POST(having,dizziness)

if ( MOD[post](#1[subjective],#2[lemme:have]) & OBJ[post](#2,#3) & HEAD(#3,#4) )
   SUBJECTIVE(#4).

//but denies any frank fevers.
//OBJ_POST(denies[subjective,nevative],fevers)
//HEAD(fevers,any frank fevers)

if ( OBJ(#1[subjective],#2) & HEAD(#2,#3) )
   SUBJECTIVE(#3).

if ( OBJ(#1[subjective,negative],#2) & HEAD(#2,#3) & ^SUBJECTIVE(#3) )
   SUBJECTIVE[neg=+](#3).

if ( SUBJ[post](#1[subjective],#2) & HEAD(#2,#3) )
   SUBJECTIVE(#3).

if ( SUBJ[post](#1[subjective,negative],#2) & HEAD(#2,#3) & ^SUBJECTIVE(#3) )
   SUBJECTIVE[neg=+](#3).

//He denies harboring suicidal or homicidal ideation.
//OBJ_POST_GERUND(denies,harboring)
//OBJ_POST(harboring,ideation)
//HEAD(ideation,suicidal or homicidal ideation)


if ( OBJ[post,gerund](#1[subjective,negative:~],#2) & OBJ(#2,#3) & HEAD(#3,#4) & ~SUBJECTIVE(#4) )
   SUBJECTIVE(#4).

if ( OBJ[post,gerund](#1[subjective,negative],#2) & OBJ(#2,#3) & HEAD(#3,#4) & ~SUBJECTIVE(#4) )
   SUBJECTIVE[neg=+](#4).

//Denied having any sick contacts.
//MOD(Denied,having)

if ( MOD(#1[subjective,negative],#2[prog]) & OBJ(#2,#3) & HEAD(#3,#4) & ~SUBJECTIVE(#4) )
   SUBJECTIVE[neg=+](#4).

//She denies having had a stroke.
//OBJ_POST(#1had,#2stroke)
//MOD(#3denies,#2had)

if ( OBJ[post](#1,#2) & MOD(#3[subjective,negative],#2) & HEAD(#2,#5) & ~SUBJECTIVE(#5) )
   SUBJECTIVE[neg=+](#5).

//she states she does have a history of chronic diarrhea
//SUBJECTIVE(#1a history)
//MOD_POST(#2history,#3diarrhea)
//HEAD(#2history,#1a history)
//PREPD(#3diarrhea,#4of)#
//HEAD(#3diarrhea,#5chronic diarrhea)

if ( ^SUBJECTIVE(#1) & MOD[post](#2[history],#3) & HEAD(#2[history],#1) & PREPD(#3,#4[lemme:of]) & HEAD(#3,#5) )
   SUBJECTIVE(#5).

//they state that the patient is highly noncompliant with her medication regimen   
//SUBJATTR(#1patient_PATIENT:+,#2noncompliant)
//MOD_POST(#2noncompliant,#3regimen)
//HEAD(#3regimen,with #4her_PATIENT:+ medication regimen)
//MODALITIES(state,is,noncompliant)
//SRC(noncompliant,they[noun])

| PP#4 |
if ( SRC(#2,#5[lemme:~0]) & SUBJATTR(#1[patient],#2) & MOD[post](#2,#3) & HEAD(#3,#4) )
   SUBJECTIVE[report=+](#2,#4).

//This is a 65-year-old female who presented to the emergency room with a complaint of periumbilical pain of one day duration.



/////////////OVERWRITTEN//////////////

//presented to the emergency room
//SUBJECTIVE(#1the emergency room)
//PP#5{#2to NP#1{the emergency room}}
//MOD_POST(#3presented,#4room)
//HEAD(#4room,#5to the emergency room)


| PP#5{#2[lemme:to], NP#1} |
if ( ^SUBJECTIVE(#1) & MOD[post](#3[subjective],#4) & HEAD(#4,#5) )
   ~.

//Patient stated her sleep
//HEAD(#2sleep,#1her sleep)
//SUBJECTIVE(#1her sleep)
//DETD(#2sleep,her[patient])

if ( ^SUBJECTIVE(#1) & HEAD(#2,#1) & DETD(#2,#3[patient]) )
   ~.

//The patient admits to having frequent orthostatic dizziness without any syncope.
//SUBJECTIVE(#1any syncope)
//SUBJECTIVE(#2frequent orthostatic dizziness)
//HEAD(#3dizziness,#2frequent orthostatic dizziness)
//MOD_POST_NEG(#3dizziness,#4syncope)
//HEAD(#4syncope,#5without any syncope)
//PREPD(#4syncope,#6without)

| PP#5 |
if ( SUBJECTIVE(#1) & ^SUBJECTIVE(#2) & HEAD(#3,#2) & MOD[post](#3,#4) & HEAD(#4,#5) )
   SUBJECTIVE(#2,#5).

| PP#5 |
if ( ^SUBJECTIVE(#1) & SUBJECTIVE(#2,#5) & HEAD(#3,#2) & MOD[post](#3,#4) & HEAD(#4,#5) & HEAD(#4,#1) )
   ~.

//This is a 70-year-old male who presents for weakness with a hemoglobin of 6.
//SUBJECTIVE(#1weakness)
//SUBJECTIVE(#2a hemoglobin,#3of 6)
//HEAD(#8hemoglobin,#2a hemoglobin)
//MOD_POST(#4presents,#5weakness)
//MOD_POST(#4presents,#6hemoglobin)
//PREPD(#6hemoglobin,#8with)
//HEAD(#8hemoglobin,#7with a hemoglobin)

| PP#7 |
if ( ^SUBJECTIVE(#1) & SUBJECTIVE(#2,#3) & HEAD(#8,#2) & MOD[post](#4[subjective],#5) & & MOD[post](#4[subjective],#6) & HEAD(#8,#7) )
   SUBJECTIVE(#1,#7,#3)
   
| PP#7 |
if ( SUBJECTIVE(#1,#7,#3) & ^SUBJECTIVE(#2,#3) & HEAD(#8,#2) & MOD[post](#4[subjective],#5) & & MOD[post](#4[subjective],#6) & HEAD(#8,#7) )
   ~.

//Denies any blood in his stool or change in his bowel habits.
//SUBJECTIVE(any blood)
//SUBJECTIVE_NEG(any blood,in his_PATIENT:+ stool)

if ( ^SUBJECTIVE(#1) & SUBJECTIVE(#1,#2) )
   ~.

//The patient presented to the hospital today for cardiac catheterization to evaluate for any coronary artery disease.
//MOD_POST_INFINIT(#1catheterization,#2evaluate)
//MOD_POST(#2evaluate,#3disease)
//SUBJECTIVE(#4cardiac catheterization)
//HEAD(#3disease,#5for any coronary artery disease)
//IV#6{to #2evaluate} PP{for NP{any coronary artery disease}}
//HEAD(#1catheterization,#4cardiac catheterization)


| IV#6{?,#2} |
if ( ^SUBJECTIVE(#4) & HEAD(#1,#4) & MOD[post,infinit](#1,#2) & MOD[post](#2,#3) & HEAD(#3,#5) )
   SUBJECTIVE(#5).



///////////////////////////////////////////////////////////
/////////////////////// OBJECTIVE ////////////////////////
///////////////////////////////////////////////////////////


//demonstrated mediastinal lymphadenopathy and multifocal airspace disease along with 3 focal opacities favoring the middle lobe.
//OBJ_POST(demonstrated,lymphadenopathy)
//HEAD(lymphadenopathy,mediastinal lymphadenopathy)

| NP#3 |
if ( OBJ-N(#1[objective],#2[objective:~]) & HEAD(#2,#3) )
   OBJECTIVE(#3).

//demonstrated mediastinal lymphadenopathy and multifocal airspace disease along with 3 focal opacities favoring the middle lobe.
//MOD_POST(#1disease,#2opacities)
//PREPD(#2opacities,#4along with)
//OBJECTIVE(#5multifocal airspace disease)
//HEAD(#1disease,#5multifocal airspace disease)
//HEAD(#2opacities,#63 focal opacities)
//PP{PREP{along with} NP{3 AP{focal} opacities}} 

| PP{PREP{#4}, NP#6} |
if ( MOD[post](#1,#2) & OBJECTIVE(#5) & HEAD(#1,#5) & HEAD(#2,#6) )
   OBJECTIVE(#6).

//without any evidence,of any intraluminal visible thrombus)
//MOD_POST(evidence,thrombus)
//HEAD(thrombus,of any intraluminal visible thrombus)
//PREPD(evidence,without)
//QUANTD_NEG(evidence,No)


| NP#3 |
if ( MOD[post](#1[noun,objective],#2) & HEAD(#2,#3) )
   OBJECTIVE(#3).

| NP#3 |
if ( MOD[post](#1[noun,objective],#2) & HEAD(#2,#3) & ^OBJECTIVE(#3) & PREPD(#1,#4[lemme:without]) )
   OBJECTIVE[neg=+](#3).

| NP#3 |
if ( MOD[post](#1[noun,objective],#2) & HEAD(#2,#3) & ^OBJECTIVE(#3) & QUANTD[neg](#1,#4) )
   OBJECTIVE[neg=+](#3).

//Upon arrival, the patient was sent for stat CT of the head and brain without contrast with results showing no acute process and an empty sella syndrome.
//QUANTD_NEG(process,no)
//OBJECTIVE(no acute process)
//HEAD(process,no acute process)


if ( ^OBJECTIVE(#1) & QUANTD[neg](#2,#3) & HEAD(#2,#1) )
   OBJECTIVE[neg=+](#1)

//Her tongue does not show any erosion, bruising or laceration.
//MOD_NEG(#2show,#3not)
//OBJECTIVE(#1any erosion)
//OBJ_POST(#2show,#4erosion)

if ( ^OBJECTIVE(#1) & MOD[neg](#2,#3[lemme:not]) & OBJ[post](#2,#4) & HEAD(#4,#1) )
   OBJECTIVE[neg=+](#1).



///////////////////////////////////////////////////////////
/////////////////////// PLAN /////////////////////////////
///////////////////////////////////////////////////////////



//I think this patient needs an endobronchial ultrasound-guided biopsy and BAL
//COORDINATE(needs[plan],biopsy)
//COORDINATE(needs[plan],BAL)
//HEAD(biopsy,an endobronchial ultrasound - guided biopsy)

if ( COORDINATE[time:~](#1[plan],#2) & HEAD(#2[pronpers:~],#3) )
   PLAN(#1,#3).

//I would continue her bronchodilators and the Avelox
//PLAN(#1her bronchodilators)
//PLAN(the Avelox)
//OBJ_POST(#2continue,#3bronchodilators)
//OBJ_POST(continue,Avelox)
//HEAD(#3bronchodilators,#1her bronchodilators)
//HEAD(Avelox,the Avelox)

if ( ^PLAN(#1) & OBJ[post](#2,#3) & HEAD(#3,#1) )
   PLAN(#2,#1).

//I will defer this to the cardiologist.
//PLAN(defer,this)
//PLAN(defer,the cardiologist)
//PLAN(defer,to the cardiologist)

| PP#1{?,NP#2} |
if ( PLAN(#3,#1) & ^PLAN(#3,#2) )
   ~.

//PLAN(defer,this)
//PLAN(defer,to the cardiologist)

| PP#3 |
if ( ^PLAN(#1,#2) & PLAN(#1,#3) )
   PLAN(#1,#2,#3)

if ( PLAN(#1,#2,#3) & ^PLAN(#1,#3) )
   ~.

//I will start her on IV fluids 0.9 normal saline 100 mL an hour.
//SUBJ-N_PRE(start,I)
//MOD_POST(start,100 mL)

| NP#3 |
if ( MOD[post](#1[plan,modplan],#2) & HEAD(#2,#3) )
   PLAN(#1,#3).



/////////////////////////////////////////////////////////
/////////////COORDINATION//////////////////
/////////////////////////////////////////////////////////


//The patient denies any alcohol or illicit street drug use or smoking.
//SUBJECTIVE(#1illicit street drug use)
//NP#1{AP{illicit} street drug use} or NP#2{smoking} 
//HEAD(#3use,#1illicit street drug use)

| NP#1, ?[lemme:or], NP#2 |
if ( SUBJECTIVE(#1) & HEAD(#3,#1) & ~SUBJECTIVE(#2) & ~SUBJECTIVE(#2,#4) )
   SUBJECTIVE(#2).

| NP#1, ?[lemme:or], NP#2 |
if ( SUBJECTIVE[neg](#1) & HEAD(#3,#1) & ^SUBJECTIVE(#2)  & ~SUBJECTIVE(#2,#4) )
   SUBJECTIVE[neg=+](#2).

//At present the patient denies any chest pain, shortness of breath, nausea, vomiting, abdominal pain, dysuria, or blood in stool.
//SUBJECTIVE_NEG(#1shortness,#2of breath)
//NP#1{shortness} PP#2{of NP{breath}} , NP#3{nausea} 

| NP#1, PP#2, ?[comma], NP#3 |
if ( SUBJECTIVE[neg:~](#1,#2) & ~SUBJECTIVE(#3) )
   SUBJECTIVE(#3).


| NP#1, PP#2, ?[comma], NP#3 |
if ( SUBJECTIVE[neg](#1,#2) & ~SUBJECTIVE(#3) )
   SUBJECTIVE[neg=+](#3).
   

| NP#1, ?[comma], NP#2 |
if ( SUBJECTIVE[neg:~](#1)  & ~SUBJECTIVE(#2) )
   SUBJECTIVE(#2).

| NP#1, ?[comma], NP#2 |
if ( SUBJECTIVE[neg](#1)  & ~SUBJECTIVE(#2) )
   SUBJECTIVE[neg=+](#2).



///////////////////////////////////////////////////////
////////////////EXTENSIONS////////////////////////////
///////////////////////////////////////////////////////

//The patient denies any change in the coloration of her skin
//SUBJECTIVE_NEG(#1any change)
//HEAD(#2change,#1any change)
//MOD_POST(#2change[supporter],#3coloration)
//HEAD(#3coloration,#4in the coloration)


| PP#4 |
if ( ^$1(#1) & HEAD(#2[supporter],#1) & MOD[post](#2,#3) & HEAD(#3,#4) )
   $1(#1,#4)


//possessive

//She admits to  shortness of breath
//SUBJECTIVE(#1shortness)
//HEAD(#2shortness,#1shortness)
//MOD_POST(#2shortness,#3breath)
//PREPD(#3breath,#4of)
//HEAD(#3breath,#5of breath)

| PP#5 |
if ( ^$1(#1) & HEAD(#2[history:~],#1) & MOD[post](#2,#3) & PREPD(#3,#4[lemme:of]) & HEAD(#3,#5) )
   $1(#1,#5).

//delete exams in front of subjective symptoms


//The patient is a 27-year-old female with a past medical history significant for a previous appendectomy and cholecystectomy, who presents for evaluation of acute abdominal pain.
//SUBJECTIVE(evaluation,of acute abdominal pain)
//HEAD(pain,acute abdominal pain)
//HEAD(pain,of acute abdominal pain)


| PP#2 |
if ( ^SUBJECTIVE(#1,#2) & HEAD(#3,#4) & HEAD(#3,#2) & HEAD(#5[notwithsubj],#1) )
   SUBJECTIVE(#4).



//The patient denies any change in the coloration of her skin
//SUBJECTIVE_NEG(#1any change,#4in the coloration)
//MOD_POST(#3coloration,#2skin)
//PREPD(#2skin,#5of)
//HEAD(#3coloration,#4in the coloration)
//HEAD(#2skin,#6of her_PATIENT:+ skin)


| PP#6 |
if ( ^$1(#1,#4) & MOD[post](#3,#2) & PREPD(#2,#5[lemme:of]) & HEAD(#3,#4) & HEAD(#2,#6) )
   $1(#1,#4,#6)
   


//Positive and negative

if ( ^SUBJECTIVE[neg:~](#1) & SUBJECTIVE[neg](#1) )
   ~.
   


//cancer with metastasis
//MOD_POST(cancer,metastasis)
//PREPD(metastasis,with)
//HEAD(cancer,small cell lung cancer)

| PP#3 |
if ( MOD[post](#1[lemme:cancer],#2[lemme:metastasis]) & HEAD(#2,#3) & HEAD(#1,#4) & ^$1(#4) )
   $1(#4,#3). 


| PP#3 |
if ( MOD[post](#1[lemme:carcinoma],#2[lemme:metastasis]) & HEAD(#2,#3) & HEAD(#1,#4) & ^$1(#4) )
   $1(#4,#3). 


if ( ^$1(#1) & HEAD(#2[history],#1) )
   ~.

//Subsequently an MRI of the abdomen and pelvis was performed which showed evidence of an abscess in the left gluteal muscle region
//OBJECTIVE(#1an abscess)
//OBJECTIVE(#2the left gluteal muscle region)
//HEAD(#3region,#2the left gluteal muscle region)
//HEAD(#3region,#4in the left gluteal muscle region)
//MOD_POST(#5evidence,#3region)
//MOD_POST(#5evidence,#6abscess)
//HEAD(#6abscess,#1an abscess)

| PP#4 |
if ( ^OBJECTIVE(#1) & OBJECTIVE(#2) & HEAD(#3,#2) & HEAD(#3,#4) & MOD[post](#5[noun,objective],#3) & MOD[post](#5[noun,objective],#6) & HEAD(#6,#1) & #1 < #2 )
   OBJECTIVE(#1,#4).

| PP#4 |
if ( OBJECTIVE(#1,#4) & ^OBJECTIVE(#2) & HEAD(#3,#2) & HEAD(#3,#4) & MOD[post](#5[noun,objective],#3) & MOD[post](#5[noun,objective],#6) & HEAD(#6,#1) & #1 < #2 )
   ~.






/////////////////////////////////////////////////////////
/////////////COORDINATION AFTER EXTENSION//////////////////
/////////////////////////////////////////////////////////

//Denies any blood in his stool or change in his bowel habits.
//SUBJECTIVE_NEG(any blood,in his_PATIENT:+ stool)
//PP{in NP{his_PATIENT:+ stool}} or NP{change} PP{in NP{his_PATIENT:+ bowel habits}}


| PP#2, ?[lemme:or], NP#3 |
if ( SUBJECTIVE(#1,#2) )
   SUBJECTIVE(#3).

| PP#2, ?[lemme:or], NP#3 |
if ( SUBJECTIVE[neg](#1,#2) & ^$1(#3) )
   SUBJECTIVE[neg=+](#3).

| PP#2, ?[lemme:or], NP#3 |
if ( OBJECTIVE(#1,#2) )
   OBJECTIVE(#3).

| PP#2, ?[lemme:or], NP#3 |
if ( OBJECTIVE[neg](#1,#2) & ^$1(#3) )
   OBJECTIVE[neg=+](#3).

//presented to the ER with the complaints of dizziness and persistent nausea and vomiting.
//COORD(#3and,#4nausea)
//COORD(#3and,#2dizziness)
//SUBJECTIVE(#1dizziness)
//HEAD(#2dizziness,#1dizziness)
//HEAD(#4nausea,#5persistent nausea)


| NP#5 |
if ( SUBJECTIVE[neg](#1) & HEAD(#2,#1) & COORD(#3,#2) & COORD(#3,#4) & HEAD(#4,#5) & ~SUBJECTIVE(#5)  & ~SUBJECTIVE(#6,#7) & HEAD(#2,#7) )
   SUBJECTIVE[neg=+](#5).

| NP#1, #2[lemme:and], NP#3 |
if ( SUBJECTIVE[neg](#1)  & ~SUBJECTIVE(#3) )
   SUBJECTIVE[neg=+](#3).

| NP#5 |
if ( SUBJECTIVE[neg:~](#1) & HEAD(#2,#1) & COORD(#3,#2) & COORD(#3,#4) & HEAD(#4,#5) & ~SUBJECTIVE(#5) & ~SUBJECTIVE(#6,#7) & HEAD(#2,#7) )
   SUBJECTIVE(#5).

| NP#1, #2[lemme:and], NP#3 |
if ( SUBJECTIVE[neg:~](#1)  & ~SUBJECTIVE(#3) )
   SUBJECTIVE(#3).


//no evidence of bowel obstruction and pelvic abscess.
//PP{of NP{bowel obstruction}} and NP{AP{pelvic} abscess}
//OBJECTIVE_NEG(bowel obstruction)

| PP{?, NP#1}} , #2[lemme:and], NP#3 |
if ( OBJECTIVE[neg:~](#1) & ~OBJECTIVE(#3) )
   OBJECTIVE(#3).

| PP{?, NP#1}} , #2[lemme:and], NP#3 |
if ( OBJECTIVE[neg](#1) & ~OBJECTIVE(#3) )
   OBJECTIVE[neg=+](#3).

//Her tongue does not show any erosion, bruising or laceration.
//OBJECTIVE_NEG(any erosion)
//NP{any erosion} , GV{bruising} or NP{laceration}

| NP#1, ?[comma], GV#2, ?[lemme:or], NP#3 |
if ( OBJECTIVE[neg:~](#1) )
   OBJECTIVE(#2).

| NP#1, ?[comma], GV#2, ?[lemme:or], NP#3 |
if ( OBJECTIVE[neg:~](#1) & OBJECTIVE[neg:~](#2) )
   OBJECTIVE(#3).

| NP#1, ?[comma], GV#2, ?[lemme:or], NP#3 |
if ( OBJECTIVE[neg](#1) )
   OBJECTIVE[neg=+](#2).

| NP#1, ?[comma], GV#2, ?[lemme:or], NP#3 |
if ( OBJECTIVE[neg](#1) & OBJECTIVE[neg](#2) )
   OBJECTIVE[neg=+](#3).



///////////////////////////////////////////////////////
////////// EXTENSIONS AFTER COORDINATION///////////////
///////////////////////////////////////////////////////


//The patient denies any change in the coloration of her skin
//SUBJECTIVE_NEG(#1any change)
//HEAD(#2change,#1any change)
//MOD_POST(#2change[supporter],#3coloration)
//HEAD(#3coloration,#4in the coloration)

| PP#4 |
if ( ^$1(#1) & HEAD(#2[supporter],#1) & MOD[post](#2,#3) & HEAD(#3,#4) )
   $1(#1,#4)


//possessive

//She admits to  shortness of breath
//SUBJECTIVE(#1shortness)
//HEAD(#2shortness,#1shortness)
//MOD_POST(#2shortness,#3breath)
//PREPD(#3breath,#4of)
//HEAD(#3breath,#5of breath)

| PP#5 |
if ( ^$1(#1) & HEAD(#2[history:~],#1) & MOD[post](#2,#3) & PREPD(#3,#4[lemme:of]) & HEAD(#3,#5) )
   $1(#1,#5).

//The patient denies any change in the coloration of her skin
//SUBJECTIVE_NEG(#1any change,#4in the coloration)
//MOD_POST(#3coloration,#2skin)
//PREPD(#2skin,#5of)
//HEAD(#3coloration,#4in the coloration)
//HEAD(#2skin,#6of her_PATIENT:+ skin)


| PP#6 |
if ( ^$1(#1,#4) & MOD[post](#3,#2) & PREPD(#2,#5[lemme:of]) & HEAD(#3,#4) & HEAD(#2,#6) )
   $1(#1,#4,#6)
   


//Positive and negative

if ( ^SUBJECTIVE[neg:~](#1) & SUBJECTIVE[neg](#1) )
   ~.
   


//cancer with metastasis
//MOD_POST(cancer,metastasis)
//PREPD(metastasis,with)
//HEAD(cancer,small cell lung cancer)

| PP#3 |
if ( MOD[post](#1[lemme:cancer],#2[lemme:metastasis]) & HEAD(#2,#3) & HEAD(#1,#4) & ^$1(#4) )
   $1(#4,#3). 


| PP#3 |
if ( MOD[post](#1[lemme:carcinoma],#2[lemme:metastasis]) & HEAD(#2,#3) & HEAD(#1,#4) & ^$1(#4) )
   $1(#4,#3). 


if ( ^$1(#1) & HEAD(#2[history],#1) )
   ~.



