//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2006 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : english grammar for political risk detection
//!   filename   : dependency_loc_pr.xip
//!   Date       : October 2006
//!   Author     : Aude Rebotier, Ágnes Sándor
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


/***************************** LES LIEUX *************************************\
dependencyRules:

/************ compléments à la grammaire ************\
/relation HEAD2 pour les chunks verbaux\
|FV#1{?*,#2[verb,last]}|
	HEAD2(#2,#1)
|IV#1{?*,#2[verb,last]}|
	HEAD2(#2,#1)
|GV#1{?*,#2[verb,last]}|
	HEAD2(#2,#1)
|NFV#1{?*,#2[verb,last]}|
	HEAD2(#2,#1)

|FV#1{?*,#2[aux,last]}|
	HEAD2(#2,#1)
|IV#1{?*,#2[aux,last]}|
	HEAD2(#2,#1)
|GV#1{?*,#2[aux,last]}|
	HEAD2(#2,#1)
|NFV#1{?*,#2[aux,last]}|
	HEAD2(#2,#1)

|#1[verb]{?*,#2[last]}|
	if (HEAD2(#3,#2))
	HEAD2(#3,#1)

/LOCORG pour les mots inconnus dans certains contextes\
//to return to Parchin (mot inconnu)
if((PREPD(#2[last,noun, guess, maj, proper],#4[p_to])|PREPD(#2[last,noun, guess, maj, proper],#4[form:ffrom])) & (MOD(#3[mvt_to],#2)|RESULT(#3[mvt_to],#2)|RCP(#3[mvt_to],#2)) & ~LOCORG(#3,#2) & ~LOCATION(#3,#2))
	LOCATION(#3,#2)

/on complète les relations LOC\

/at the site\
if (PREPD(#2[commonplace],#4[form:~fof, form:~ffor, p_about:~]) & MOD(#3,#2))
	LOC(#3,#2)

/his meetings with officials in Brussels\
if(MOD[post](#1,#2) & LOC[post](#2,#3))
	LOC[post](#1,#3)


/************** établissement des relations COORDDEP[loc-pr] *******\

/to visit Iran\
if (LOCORG(#1) | LOCATION(#1) & OBJ-N(#2[tr_loc],#1))	
//if (LOCORG(#1) | LOCATION(#1) & OBJ-N(#2[tr_loc],#1) & ~SREL(#3,#2))	
	COORDDEP[loc-pr=+](#2, #1).

/to visit the site\
//if (OBJ-N(#2[tr_loc],#1[commonplace]) & ~SREL(#3,#2))
if (OBJ-N(#2[tr_loc],#1[commonplace]) & HEAD2(#1,#3NP))
	COORDDEP[loc-pr=+](#2,#3).

/to send something to the site\
if ((PREPD(#2[commonplace],#4[p_to]) | PREPD(#2[commonplace],#4[form:ffrom])) & (MOD(#3[mvt_to],#2)|RESULT(#3[mvt_to],#2)|RCP(#3[mvt_to],#2)))
	COORDDEP[loc-pr=+](#3,#2).

//les noms LOCORG précédés de certaines prépositions
	if ((PREPD(#1[place],#3[preploc]) | PREPD(#1[location],#3[preploc])) & ^MOD(#2,#1) & ~COORDDEP[loc-pr](#2,#1))
	COORDDEP[loc-pr=+](#2,#1).

// an attack on Iran
if(PREPD(#2,#3[p_on]) & MOD(#1[loc_on],#2) & (LOCORG(#2)|LOCATION(#2)))
	COORDDEP[loc-pr=+](#1,#2).

/on reprend parmi les relations LOC celles qui rattachent un PP avec EN (in Iran) à un verbe ou un nom \
if (LOC[sentence:~](#1,#2[noplace:~]) & ~COORDDEP[loc-pr](#1,#2))	
	COORDDEP[loc-pr=+](#1,#2).


// on rattache un lieu en début de phrase au premier verbe non subordonné qui suit

if(PREPD(#1[commonplace],#2[firstprep, preploc]) & HEAD2(#3[firstv],#4) & ~NUCL(#3,#5))
	COORDDEP[loc-pr=+](#3,#1)
if(PREPD(#1,#2[firstprep, preploc]) & (LOCORG(#1)|LOCATION(#1)) & HEAD2(#3[firstv],#4) & ~NUCL(#3,#5))
	COORDDEP[loc-pr=+](#3,#1)



/at the Parchin base near Tehran : deux compléments de lieu
il faudra raffiner pour n'avoir qu'un complément 'the town of Darkhuin'
+ la règle ne fonctionne pas pour le moment pour 'in the town of Darkhuin in the southwestern Khuzestan province' car il n'y a pas de dépendance entre Darkhuin et province
\
if (PREPD(#2,?) & (LOCORG(#2)|LOCATION(#2)) & MOD(#3,#2) & COORDDEP[loc-pr](#4,#3))
	COORDDEP[loc-pr=+](#4,#2).



/lorsqu'un lieu peut se rattacher à un nom ou à un verbe, privilégier le nom si c'est un événement\
/ex. Mohamed ElBaradei says uranium enrichment in Iran could lead to nuclear weapons\
if (^COORDDEP[loc-pr](#1[verb],#2) & COORDDEP[loc-pr](#3[noun],#2) & POTCORE(#3) & ~LIGHT(#1,#3))
	~
/lorsqu'un lieu peut se rattacher à deux noms, privilégier celui qui est un événement\
if (^COORDDEP[loc-pr](#1[noun],#2) & COORDDEP[loc-pr](#3[noun],#2) & POTCORE(#3) & ~POTCORE(#1))
	~

/rattacher au verbe support les compléments de lieu du nom qui l'accompagne\
if (~COORDDEP[loc-pr](#1[verb],#2) & LIGHT(#1,#3) & COORDDEP[loc-pr](#3,#2))
	COORDDEP[loc-pr=+](#1,#2)


/remplacer where par son antécédent\
	if (^COORDDEP[loc-pr](#1,#2) & ANTECEDENT(#3,#2))
	COORDDEP[loc-pr=+](#1,#3)

/supprimer la relation COORDDEP[loc-pr] si nécessaire\
	if(^COORDDEP[loc-pr](#1,#2[noplace]))
	~ 

// remplacer le nom par tout le NP 
	if(^COORDDEP[loc-pr](#3,#2) & HEAD2(#2,#1[noun]))
	COORDDEP[loc-pr=+](#3,#1)

//cas où un événement a deux lieux (to go from Russia to Iran)
if (COORDDEP[loc-pr](#1,#2) & COORDDEP[loc-pr](#1,#3) & ~COORDDEP[loc-pr, loc-pr2](#1,#2,#3) & ~COORDDEP[loc-pr, loc-pr2](#1,#3,#2))
	COORDDEP[loc-pr=+, loc-pr2=+](#1,#2,#3)

//ajouter le lieu aux POTPOLEVENT
/quand il y a deux lieux\

if (COORDDEP[loc-pr, loc-pr2](#2,#10,#11) & ^POTPOLEVENT[withmodality:~, loc-pr:~](#1,#2,#3,#4,#5,#6,#7))
	POTPOLEVENT[loc-pr=+, loc-pr2=+](#1,#2,#3,#4,#5,#6,#7,#10,#11)
if (COORDDEP[loc-pr, loc-pr2](#2,#10,#11) & ^POTPOLEVENT[withmodality:~, loc-pr:~](#1,#2,#3,#4,#5,#6))
	POTPOLEVENT[loc-pr=+, loc-pr2=+](#1,#2,#3,#4,#5,#6,#10,#11)
if (COORDDEP[loc-pr, loc-pr2](#2,#10,#11) & ^POTPOLEVENT[withmodality:~, loc-pr:~](#1,#2,#3,#4,#5))
	POTPOLEVENT[loc-pr=+, loc-pr2=+](#1,#2,#3,#4,#5,#10,#11)
if (COORDDEP[loc-pr, loc-pr2](#2,#10,#11) & ^POTPOLEVENT[withmodality:~, loc-pr:~](#1,#2,#3,#4))
	POTPOLEVENT[loc-pr=+, loc-pr2=+](#1,#2,#3,#4,#10,#11)
if (COORDDEP[loc-pr, loc-pr2](#2,#10,#11) & ^POTPOLEVENT[withmodality:~, loc-pr:~](#1,#2,#3))
	POTPOLEVENT[loc-pr=+, loc-pr2=+](#1,#2,#3,#10,#11)


if (COORDDEP[loc-pr, loc-pr2](#2,#10,#11) & ^POTPOLEVENT[withmodality, loc-pr:~](#8,#1,#2,#3,#4,#5,#6,#7))
	POTPOLEVENT[loc-pr=+, loc-pr2=+](#8,#1,#2,#3,#4,#5,#6,#7,#10,#11)
if (COORDDEP[loc-pr, loc-pr2](#2,#10,#11) & ^POTPOLEVENT[withmodality, loc-pr:~](#8,#1,#2,#3,#4,#5,#6))
	POTPOLEVENT[loc-pr=+, loc-pr2=+](#8,#1,#2,#3,#4,#5,#6,#10,#11)
if (COORDDEP[loc-pr, loc-pr2](#2,#10,#11) & ^POTPOLEVENT[withmodality, loc-pr:~](#8,#1,#2,#3,#4,#5))
	POTPOLEVENT[loc-pr=+, loc-pr2=+](#8,#1,#2,#3,#4,#5,#10,#11)
if (COORDDEP[loc-pr, loc-pr2](#2,#10,#11) & ^POTPOLEVENT[withmodality, loc-pr:~](#8,#1,#2,#3,#4))
	POTPOLEVENT[loc-pr=+, loc-pr2=+](#8,#1,#2,#3,#4,#10,#11)
if (COORDDEP[loc-pr, loc-pr2](#2,#10,#11) & ^POTPOLEVENT[withmodality, loc-pr:~](#8,#1,#2,#3))
	POTPOLEVENT[loc-pr=+, loc-pr2=+](#8,#1,#2,#3,#10,#11)

/quand il y a un seul lieu\

if (COORDDEP[loc-pr](#2,#10) & ^POTPOLEVENT[withmodality:~, loc-pr:~](#1,#2,#3,#4,#5,#6,#7))
	POTPOLEVENT[loc-pr=+](#1,#2,#3,#4,#5,#6,#7,#10)
if (COORDDEP[loc-pr](#2,#10) & ^POTPOLEVENT[withmodality:~, loc-pr:~](#1,#2,#3,#4,#5,#6))
	POTPOLEVENT[loc-pr=+](#1,#2,#3,#4,#5,#6,#10)
if (COORDDEP[loc-pr](#2,#10) & ^POTPOLEVENT[withmodality:~, loc-pr:~](#1,#2,#3,#4,#5))
	POTPOLEVENT[loc-pr=+](#1,#2,#3,#4,#5,#10)
if (COORDDEP[loc-pr](#2,#10) & ^POTPOLEVENT[withmodality:~, loc-pr:~](#1,#2,#3,#4))
	POTPOLEVENT[loc-pr=+](#1,#2,#3,#4,#10)
if (COORDDEP[loc-pr](#2,#10) & ^POTPOLEVENT[withmodality:~, loc-pr:~](#1,#2,#3))
	POTPOLEVENT[loc-pr=+](#1,#2,#3,#10)


if (COORDDEP[loc-pr](#2,#10) & ^POTPOLEVENT[withmodality, loc-pr:~](#8,#1,#2,#3,#4,#5,#6,#7))
	POTPOLEVENT[loc-pr=+](#8,#1,#2,#3,#4,#5,#6,#7,#10)
if (COORDDEP[loc-pr](#2,#10) & ^POTPOLEVENT[withmodality, loc-pr:~](#8,#1,#2,#3,#4,#5,#6))
	POTPOLEVENT[loc-pr=+](#8,#1,#2,#3,#4,#5,#6,#10)
if (COORDDEP[loc-pr](#2,#10) & ^POTPOLEVENT[withmodality, loc-pr:~](#8,#1,#2,#3,#4,#5))
	POTPOLEVENT[loc-pr=+](#8,#1,#2,#3,#4,#5,#10)
if (COORDDEP[loc-pr](#2,#10) & ^POTPOLEVENT[withmodality, loc-pr:~](#8,#1,#2,#3,#4))
	POTPOLEVENT[loc-pr=+](#8,#1,#2,#3,#4,#10)
if (COORDDEP[loc-pr](#2,#10) & ^POTPOLEVENT[withmodality, loc-pr:~](#8,#1,#2,#3))
	POTPOLEVENT[loc-pr=+](#8,#1,#2,#3,#10)
