
DependencyRules:

// Ajout a mettre dans gram generale
// I would like to see the girl - pas d'obj entre girl et see
| IV{?[lemme:to],#1[verb]}, NP#2 |
if ( HEAD(#3,#2) & ~SUBJ(?,#3) & ~OBJ(?,#3) & ~MOD(?,#3) & ~OBJ(#1,?) )
   OBJ(#1,#3)

// probleme de chunking
// less noisy
| ?[verb], quant#2[lemme:less], AP{adj#1[first,last]} |
MOD[pre=+](#1,#2)


| #1[printer-part];#1[printer-fonc];#1[printer-assoc];#1[printer-char] |
  LIXO(#1)


// HP should have made the bin deep enough to hold an entire ream (500 sheets).
| TOP#4 |
// <manufacturer> should + Verbe accompli
if ( SUBJ(#2[lemme:should],#1[manufacturer]) & VDOMAIN(#3,#2) & VASPECT[a_perfect](#3) )
ADVICE(#4)


// check if OK
// same as above when subject is "they"
// they + should + Verbe accompli
| TOP#4 |
if ( SUBJ(#2[lemme:should],#1[lemme:they]) & VDOMAIN(#3,#2) & VASPECT[a_perfect](#3) )
ADVICE(#4)

// <printer-part> is missing
| TOP#4 |
if ( SUBJ-N(#1[lemme:miss],?[printer-part]) & NUCL[vlink,progress](?,#1) & ~EMBED[complthat](#1,?[indspeech]))
ADVICE(#4)



// missing <printer-part>
| TOP#4 |
if ( MOD[pre](?[printer-part],?[adj,lemme:"miss",vprog]) )
ADVICE(#4)

// problem with calculation of deep subj and obj
// it's missing a paper holder, paper holder is considered as OBJ
// we deal here with this error
| TOP#4 |
if ( SUBJ-N(#1[lemme:"miss",vprog],?[lemme:it]) & OBJ-N(#1,?[printer-part]) )
ADVICE(#4)



// missing is the ability to fax
// <missing> is <printer-fonc>
| TOP#4 |
if ( ATTRIB(#1[lemme:"miss",vprog],?[printer-fonc]) )
ADVICE(#4)

// the ability to fax is missing
| TOP#4 |
if (SUBJ-N(#1[lemme:miss],?[printer-fonc]) & ~EMBED[complthat](#1,?[belief]))
ADVICE(#4)


// <printer-fonc> subject of "lacks" verb
// printer functions are lacking in reliabilty
| TOP#4 |
if ( SUBJ-N(#1[lemme:lack],?[printer-fonc]) | SUBJ-N(#1[lemme:lack],?[printer-assoc]) & ~EMBED[complthat](#1,?[belief]))
ADVICE(#4)

| TOP#4 |
if ( MOD[post](?[lemme:lack],?[printer-fonc]) | MOD[post](?[lemme:lack],?[printer-assoc]) | 
     MOD[post](?[lemme:absence],?[printer-fonc]) | MOD[post](?[lemme:absence],?[printer-fonc]) 
    | MOD[post](?[lemme:absence],?[printer-part]) 
)
ADVICE(#4)





// I wish the cable were bigger
// LIXO contains anything related with printers
| TOP#4 |
if ( SUBJ-N(#1[lemme:wish],?[pron,nom,p1,sg]) & EMBED(#2[lemme:be],#1) & SUBJ-N(#2,#3) & ATTRIB(#3,?) & LIXO(#3) )
ADVICE(#4)

// I wish for a better cable
| TOP#4  |
if ( MOD(#1[lemme:"wish"],#2) & LIXO(#2) & SUBJ-N(#1,?[pron,nom,p1,sg]) )
ADVICE(#4)

// I wish I had a better cable
// I wish Canon had added this function
// ATTENTION I wish I could tell you all the features but ....
| TOP#4 |
if ( EMBED[complthat](#2,#1[lemme:"wish"]) & SUBJ-N(#1,?[pron,nom,p1,sg]) 
     &
     (  
      ( OBJ-N(#2,#3) & LIXO(#3) & ~VMODAL(#2) ) | SUBJ-N(#2,?[manufacturer])
     )  
)
ADVICE(#4)


// I would like to see higher capacity toner
| TOP#4 |
if ( VMODAL(#1[lemme:like]) & SUBJ-N(#1,?[pron,nom,p1,sg]) & EMBED[infinit](#2,#1) & OBJ(#2,#3) & LIXO(#3) )
ADVICE(#4)

// I would like a longer cable
| TOP#4  |
if ( VMODAL(#1[lemme:"like"]) & SUBJ-N(#1,?[pron,nom,p1,sg]) & OBJ-N(#1,#2) & LIXO(#2)  )
ADVICE(#4)


// I would have liked
| TOP#4 |
if ( VDOMAIN[modal,perfect](?[lemme:like],#1[lemme:would]) & SUBJ(#1,?[pron,nom,p1,sg]) )
ADVICE(#4)

// I would have preferred.
| TOP#4 |
if ( VDOMAIN[modal,perfect](?[lemme:prefer],#1[lemme:would]) & SUBJ(#1,?[pron,nom,p1,sg]) )
ADVICE(#4)

// paper tray could be better|more efficient
| TOP#4 |
if ( ( ATTRIB(#1,#5[adj,compar,positive]) | ( ATTRIB(#1,#5[adj,positive]) & MOD[pre](#5,?[lemme:more]) ) )
      & LIXO(#1) & SUBJ-N(#2,#1) & VMODAL(#2) & VDOMAIN(#2,?[lemme:could]) & ~EMBED[complthat](#2,?[belief]) )
ADVICE(#4)

// fax could be less noisy
| TOP#4 |
if (  ATTRIB(#1,#5[adj,negative]) & MOD[pre](#5,?[lemme:less]) & LIXO(#1) & SUBJ-N(#2,#1) & VMODAL(#2) & VDOMAIN(#2,?[lemme:could]) & ~EMBED[complthat](#2,?[belief]) )
ADVICE(#4)


// improvement would be <printer-part>
// The only improvement would be an LCD display on the printer
// An lcd display would be an improvement
| TOP#4 |
if ( ( ATTRIB(#1[lemme:improvement],#2[printer-part]) | ATTRIB(#1[lemme:improvement],#2[printer-fonc]) | ATTRIB(#1[printer-part],#2[lemme:improvement]) | ATTRIB(#1[printer-fonc],#2[lemme:improvement]) 
     )
     & SUBJ-N(#3[lemme:be],#1) & VMODAL(#3) & ~EMBED[complthat](#3,?[belief])
)
ADVICE(#4)

// A lcd display would improve
| TOP#4 |
if ( ( SUBJ-N(#1[lemme:improve],?[printer-part]) | SUBJ-N(#1[lemme:improve],?[printer-fonc]) )
     & VMODAL(#1) & ~EMBED[complthat](#1,?[belief]))
ADVICE(#4)

// I expected a better performance
| TOP#4 | 
if ( SUBJ-N(#1[lemme:expect],?[pron,nom,p1,sg]) & PAST(#1) & OBJ-N(#1,#2) & LIXO(#2) & MOD[pre](#2,?[compar,positive]) )
ADVICE(#4)
