DependencyRules:

//context-dependent word features



// engage - reflection if not transitive

| #1[lemme:engage,reflection=+] |
if ( ~OBJ(#1,?) | ~OBJ-N(#1,?) )
    ~.

// catch sight of is analysis
//Moreover, the tutorial about the training session in our different countries helped me to understand the differences of the behaviours depending on our different cultures.
//OBJ_POST(catch,sight)

| #1[analysis=+] |
if ( OBJ[post](#1[lemme:catch],#2[lemme:sight]) )
   ~.

| #2[analysis=+] |
if ( OBJ[post](#1[lemme:catch],#2[lemme:sight]) )
   ~.

//Being first generation Australian myself, I’ve been able to relate the concept of transculturation to my current situation and identification.
//SUBJ-N_GRPR(relate,I)

| #1[analysis=+] |
if ( SUBJ-N(#1[lemme:relate],#2[lemme:I]) )
   ~.

//my studies

| #1[analysis=+] |
if ( $1(#1[lemme:study,pl,noun],#2[lemme:I]) )
   ~.

// find out is analysis

| #1[analysis=+] |
if ( NUCL[particle](#1[lemme:find],#2[lemme:out]) )
    ~.

//Then again I can say that knowledge is similar to gears of car, each learning is one of a kind, and is right when utilised to their potential.
//NP{a kind}}

| np{?*, #1[lemme:kind,positivesent=~,last]} |
  ~.

//Not only have the theories informed my educational development, but also the issues discovered during the group case study have informed my professional environment.
//DETD(development,my)

| #1[analysis=+] |
if ( DETD(#1[lemme:development],#2[lemme:I]) )
   ~.


// Missing dependencies

//To use the computational software successfully I was required to apply theoretical engineering knowledge that I had gained through university studies, and which had been further developed during the internship.
//required[sc_np_toinf:+]
//OBJ-N(apply,knowledge)
//NFV{required} IV{to apply}



//The actual outcome from this event was that I was able to complete the task entrusted on me but not within the time period required as the unaccounted external factors obstructed my ability to work effectively .
//MOD_SUBCAT_GRPR(ability,work)
//DETD(ability,my)

if ( DETD(#1[transparent],#2[lemme:I]) & MOD(#1,#3) )
   DEP[grrefl=+](#3,#2).

//As a conclusion , this course definitely changed my way of approaching dialogue .
//DETD(#1way_TRANSPARENT:+,#2my)
//MOD_SUBCAT_POST(#1way,#3approaching)
//OBJ_POST(#4changed,#1way)

if ( DETD(#1[transparent],#2[lemme:I]) & MOD[post](#1,#3) & OBJ[post](#4,#1) )
   OBJ-N[grrefl=+](#4,#3).


//I develop cultural awareness , and I learned a lot through the group work such as being more empathic and patient .
//MOD_POST_GERUND(#1learned,#3being)
//SUBJ_PRE(#1learned,#2I)
//NUCL_SUBJCOMPL(#3being,#4patient)
//NUCL_SUBJCOMPL(#3being,#4empathic)

if ( SUBJ(#1[analysis],#2[lemme:I]) & MOD[gerund](#1,#3) & NUCL[subjcompl](#3,#4) )
   SUBJATTR[grrefl=+](#4,#2).




//However, as a new international student, I realised that English could be a barrier for me in a face-to-face communication because local students speak very fast.
//SUCC(barrier,for)
//PP{for NP{me}}
//PREPD(me_RM:+,for)


| pp{#1, np{#2}} |
if ( SUCC(#3,#1) & PREPD(#2,#1) & ~MOD[post](#3,#2) & #3 < #1 )
   MOD[post=+,grrefl=+,proba=+](#3,#2).



//I am determined to make the company my first point of research, for example the values of the company, their policies, their work culture and how they abide by any standards. 
//NUCL_VLINK_PASSIVE(am,determined)
//SUBJ_PRE(am,I)

if ( NUCL[passive](#1,#2) & SUBJ[pre](#1,#3) & ~SUBJ-N(#2,#3) )
   SUBJ-N[grrefl=+](#2,#3).


//I have also to point out that one of the most important aspects of my learning experience was the teamwork and collaborative learning .
//MOD_POST_INFINIT(have,point)
//PREPD(point,to)
//SUBJ_PRE(have,I)

if ( MOD[post,infinit](#1[lemme:have],#2) & PREPD(#2,#3[lemme:to]) & SUBJ[pre](#1,#4) )
   SUBJ-N[grrefl=+](#2,#4).

//As a result my future action will be to meet difficult situations and learn from my team members on how they approach the challenging task.
//DETD(action,my)
//SUBJ-N_PRE(be,action)
//EMBED_INFINIT(learn,be)

if ( DETD(#1,#2[lemme:I]) & SUBJ-N[pre](#3,#1) & EMBED[infinit](#4,#3) )
   SUBJ-N[grrefl=+](#4,#2).

//My mentor was able to redirect my thinking and stated that the reason I was unable to handle my responsibility in my role was because I had not previously taken advantage of the team and learnt how they dealt with the tasks I had been given.
//OBJ_POST(taken,advantage)
//MOD_NEG(taken,not)

| #3[contrast=+] |
if ( MOD[neg](#1[lemme:take],#2) & OBJ[post](#1,#3[sentiment]) )
   ~.

| #3[contrast=+] |
if ( MOD[neg](#1[lemme:take],#2) & OBJ[post](#1,#3[positivesent]) )
   ~.



//I feel that I did this very poorly and thus impacted the team and as a result, on this occasion, we were very inefficient.
//MANNER_POST(did,poorly)
//SUBJ_PRE(did,I)

if ( MANNER[post](#1,#2[adv]) & SUBJ[pre](#1,#3[lemme:I]) & ~OBJ(#1,?) )
   DEP[grrefl=+](#3,#2).

//In the future however, If faced with a similar problem I will communicate with my fellow colleagues straight away if something of theirs is unclear to me, and from there ask them to be more clear when portraying information as it creates a great amount of confusion and stress.
//PREPD(me,to)
//SUCC(unclear,to)

if ( SUCC(#1[adj],#2) & PREPD(#3,#2) & ~MOD(#1,#3) )
   MOD[grrefl=+](#1,#3).

//For me personally it is about understanding that we live in a civilized society were we are to treat each other with respect and dignity not because it 's the ' nice ' thing to do but because it 's the ' right ' thing to do .
//EMBED_PROG(understanding,is)
//SUBJ-N_PRE(is,me)

if ( SUBJ[pre](#1,#2[lemme:I]) & EMBED[prog](#3[reflection],#1) )
   DEP[grrefl=+](#2,#3).

//One aspect was Leadership was about the impact you have on others, this was a powerful statement to me, because most of the time when you read about leadership it talks about having followers and the influence you have over people to follow you.
//RESULT_POST(was,me)
//NUCL_SUBJCOMPL(was,statement)

if ( RESULT(#1,#2) & NUCL[subjcompl](#1,#3) #2~:#3)
   MOD[post=+,grrefl=+](#3,#2).
  


//I noted down what she said about it, but it hurts to acknowledge it.
//SC{BG{but} NP{it} FV{hurts}} IV{to acknowledge} NP{it} 

| sc{?*, fv{#1}}, iv{?[lemme:to],#2[mental]} |
if ( ~MOD[post,infinit](#1,#2) )
    MOD[post=+,infinit=+,grrefl=+](#1,#2).


//I will list the things that Pam said about my essay and work on them until I really understand what I need to do to write a good essay.
//MANNER_PRE(understand,really)
//PP{until NP{I}} FV{really understand}
//PREPD(I,until)


| pp{?, np{?*,#1[last]}}, fv{adv, #2[last]} |
if ( ~SUBJ(#2,?) )
    SUBJ[grrefl=+](#2,#1), SUBJ-N[grrefl=+](#2,#1).


//I have a level of accountability to ensure employees felt safe in their working environment, yes it's somewhat a legislative 'duty of care' requirement but for me it was more than that.
//CONNECTOR_SUBORD(was,but_RM:+)
//MOD_PRE(was,me)
//MANNER_POST(was,more than)

if ( CONNECTOR[subord](#1,#2) & MOD[pre](#1,#3[lemme:I]) )
   DEP[grrefl=+](#2,#3).

//wrong dependencies

//When I look at the notes I wrote, my anxiety levels shoot up and I know I have to act.
//SUBJ_GRREFL(wrote,I)
//OBJ_POST(look,I)
//OBJ-N(look,I)

if ( ^OBJ(#3,#2[surface:I]) | ^OBJ-N(#3,#2[surface:I]) )
    ~.

//specific features for the refl_conc grammar

| #1[lemme:one,change=~,importance=~] |
    ~.

///////////////////////

////////////////////// SUBJECT \\\\\\\\\\\\\\\\


| #1[lemme:I,subject=+] |
    ~.

| #1[lemme:we,subject=+] |
    ~.

////////////////////// OTHERSUBJECT \\\\\\\\\\\\\\\\


| #1[lemme:he,othersubject=+] |
    ~.

| #1[lemme:she,othersubject=+] |
    ~.

| #1[lemme:they,othersubject=+] |
    ~.

| #1[prof,othersubject=+] |
    ~.

| #1[firstname,othersubject=+] |
    ~.


//Working in a professional environment was something I was used to and I believe I was able to maintain a high level of professionalism .
//AP{professional} environment
//MOD_PRE(environment,professional)


| ap{#1[c_person=~]}, #2noun |
if ( MOD[pre](#2,#1) )
  ~.

//| #1[proper,c_person,othersubject=+] |
| #1[c_person,othersubject=+] |
    ~.

// publication

| #1[noun,publ,othersubject=+] |
    ~.

| #1[lemme:work,publ=~,othersubject=~] |
  ~.

| #1[lemme:outcome,publ=~,othersubject=~] |
  ~.

| #1[noun,course,analysis:~,othersubject=+] |
    ~.

// exceptions

//This was interesting work to me and hearing that places such as Google and Commbank were providing this support to Executives was comforting to hear.
//MOD_POST(providing,Executives)



////////////////////// ANALYSIS \\\\\\\\\\\\\\\\\\

//// needed in add-feature_refl.xip

/

| #1[mental=~,noun,publ] | 
    ~.

| #1[mental,analysis=+] |
    ~.

| #1[scope,analysis=+] |
    ~.

| #1[reflection,analysis=+] |
    ~.
\

//contextual

//This quote really resided with me.
//MOD_POST(resided,me)
//PREPD(me,with)

| #1[analysis=+] |
if ( MOD[post](#1[lemme:reside],#2[lemme:I]) & PREPD(#2,#3[lemme:with]) )
   ~.

//I will develop goals for myself during the week so that each week I have learnt a new skill and can comfortably complete the task and take responsibility for the way it unfolds, whether it be positively or whether it results in complaints.
//OBJ_POST(develop,goals)

| #1[analysis=+] |
if ( OBJ[post](#1[lemme:develop],#2[analysis]) )
   ~.

//I have also to point out that one of the most important aspects of my learning experience was the teamwork and collaborative learning .
//NUCL_PARTICLE(point,out)

| #1[analysis=+] |
if ( NUCL[particle](#1[lemme:point],#2[lemme:out]) )
   ~.

/
| #1[say,analysis=+] |
    ~.

| #1[saythat,analysis=+<] |
    ~.
\
// not SUBJECT and OTHERSUBJECT when (in)direct objects
/
//I need to tell her honestly about the tutorial, the feedback and my disappointment in myself.
//OBJ_POST(tell,her)

| #2[subject=~,othersubject=~] |
if ( OBJ(#1[analysis],#2) )
    ~.

| #2[subject=~,othersubject=~] |
if ( IOBJ(#1[analysis],#2) )
    ~.

// not SUBJECT and OTHERSUBJECT when it is a MOD_SUBCAT_POST of say(that) 

//MOD_SUBCAT_POST(talking,Angie)

| #2[subject=~,othersubject=~] |
if ( MOD[subcat,post](#1[say],#2) |  MOD[subcat,post](#1[saythat],#2) )
    ~.
\
////////////////////// HUMANACTIVITY \\\\\\\\\\\\\\\\

/

| #1[auxbh:~,humanactivity=+,subjectactivity=+,analysis:~] |
//if ( $1(#1[verb],#2[subject]) )
if ( SUBJ-N(#1[verb],#2[subject]) )
    ~.

| #1[time_span_n,humanactivity=+,subjectactivity=+,analysis:~] |
if ( $1(#1[noun],#2[subject]) )
    ~.

| #1[auxbh:~,humanactivity=+,othersubjectactivity=+,analysis:~] |
//if ( $1(#1[verb],#2[othersubject]) )
if ( SUBJ-N(#1[verb],#2[othersubject]) )
    ~.

| #1[time_span_n,humanactivity=+,othersubjectactivity=+,analysis:~] |
if ( $1(#1[noun],#2[othersubject]) )
    ~.
\

////////////////////// CONTRAST \\\\\\\\\\\\\\\\

// don't

| #1[contr_neg,contrast=+] |
  ~.

| #1[wh,pron:~,contrast=+,start] |
  ~.


| ?[contr,contrast=+] |
    ~.

// questions

| ?[form:fquest,contrast=+] |
  ~.


// lexical:
// get over

| #1[contrast=+] |
if ( NUCL[particle](#1[lemme:get],#2[lemme:over]) )
   ~.

// I was required
//NUCL_VLINK_PASSIVE(was,required)
//SUBJ-N_GRREFL(required,I)

| #2[contrast=+] |
if ( NUCL[vlink,passive](#1,#2[lemme:require]) & SUBJ-N(#2,#3[lemme:I]) )
   ~.

/
| ?[form:fwithout,contrast=+] |
  ~.
\

//exception: for a while

| adv{?+, ?[lemme:while,contr=~,contrast=~]} |
  ~.




//NUCL_PARTICLE(look,forward)

| #1[stance=+] |
if ( NUCL(#1[lemme:look],#2[lemme:forward]) )
   ~.

////////////////////// LINK \\\\\\\\\\\\\\\\

| #1[conj,sub,hmm_pronrel:~,time:~,contrast:~,link=+] |
  ~.


/
| #1[lemme:as,start,link=+] |
  ~.
\

| #1[lemme:if,link=~] |
  ~.

| #1[lemme:even_if,link=~] |
  ~.

//SUBJ_PRE(means,this)

| #2[link=+,unary=+] |
if ( SUBJ-N[pre](#1[lemme:mean],#2[pron,masc:~,fem:~]) )
   ~.

| #1[link=+] |
if ( SUBJ[pre](#1[lemme:mean],#2[pron,masc:~,fem:~]) )
   ~.

//Upon reflection I realized that my workplace does not adopt VBM practices.
//PP{Upon NP{reflection}}
/
| pp{#1[start,lemme:upon],np{?*,#2[last,analysis,link=+]}} |
  ~.

| pp{#1[start,lemme:from],np{?*,#2[last,analysis,link=+]}} |
  ~.

| pp{#1[start,lemme:through],np{?*,#2[last,analysis,link=+]}} |
  ~.

| pp{#1[start,lemme:by],np{?*,#2[last,analysis,link=+]}} |
  ~.
\


| pp{#1[start,lemme:upon],np{?*,#2[last,link=+]}} |
  ~.

| pp{#1[start,lemme:from],np{?*,#2[last,link=+]}} |
  ~.

| pp{#1[start,lemme:through],np{?*,#2[last,link=+]}} |
  ~.

| pp{#1[start,lemme:by],np{?*,#2[last,link=+]}} |
  ~.

//Personally , through this collaborative learning I have found that I have natural tendencies to lead and to organise .

| ?[adv,start], (?[comma]), pp{#1[lemme:through],np{?*,#2[last,link=+]}} |
  ~.

| ?[adv,start], (?[comma]), pp{#1[lemme:upon],np{?*,#2[last,link=+]}} |
  ~.

| ?[adv,start], (?[comma]), pp{#1[lemme:from],np{?*,#2[last,link=+]}} |
  ~.

| ?[adv,start], (?[comma]), pp{#1[lemme:by],np{?*,#2[last,link=+]}} |
  ~.



//It gelled with me rather well, that when I went back into the workplace I felt the desire to mention it to one of the Vice Presidents I support, hoping that by planting that seed one day I will get to see the benefits.
//, GV{hoping} BG{that}

| ?[comma], gv{#1[stance,link=+]}, bg{#2[lemme:that]} |
  ~.

| ?[comma], gv{#1[analysis,link=+]}, bg{#2[lemme:that]} |
  ~.


//There was nothing exciting about the meeting it was like a casual conversation between people, it felt real and for that reason I found it inspirational.
//PREPD(reason,for)

| #1[link=+] |
if ( PREPD(#1[lemme:reason],#2) )
   ~.

//The reason for this is that when I am working as a paralegal at a boutique law firm I am still a voiceless follower.
//SUBJ_PRE(is,reason)

| #2[link=+] |
if ( SUBJ[pre](#1[lemme:be],#2[lemme:reason]) )
   ~.

// The final outcome of the task was then affected by the aid of my colleagues, where I eventually asked them for help in deciphering their recorded data which gave me a clearer view on each recording.

// QUALIF(view,clearer)
// OBJ(gave,view)
//IOBJ(gave,me)

| #1[link=+] |
if ( IOBJ(#1[lemme:give],#2[lemme:I]) & OBJ(#1,#3[analysis]) )
   ~.

//From this exercise what I learnt most was not how it impacted employees but more so about the leaders role .
//MOD_POST_SENTENCE_RELATIV(exercise,learnt)
//{PP{From NP{this exercise}}
//SUBJ_PRE(learnt,I)

| pp{?[start], np{#1[lemme:this],#2[link=+]}} |
if ( MOD[post](#2,#3[analysis]) & SUBJ(#3,#4[lemme:I]) )
   ~.

//Two key highlights that I learnt from that information session were about leaders.
//MOD_RCDEP_ANALYSIS_OTHERSUBJECT(learnt,session)
//ARGUMENT_RCDEP_SUBJECT+ANALYSIS(learnt,I)

| #2[link=+] |
if ( MOD(#1[analysis],#2[course]) & $1(#1,#3[lemme:I]) )
   ~.



//exception

|#1[lemme:like,link=~] |
   ~.

//Ethics has a broad definition and it means different things to different people.
//MOD_SUBCAT_POST(means,people)
//PREPD(people,to)

| #1[link=~] |
if (MOD[subcat,post](#1[lemme:mean],#2) & PREPD(#2,#3[lemme:to]) )
   ~.

| #2[link=~] |
if ( SUBJ[pre](#1[lemme:mean,link:~],#2) )
   ~.

//I have noticed this difference in my experiences with business-based communication.
//NADJ{business - based}

| ?{#1,#2[form:fhyph],#3[lemme:base,link=~,rc=~]} |
  ~.

//made me

| #1[link=+] |
if ( $1(#1[lemme:make],#2[lemme:I]) & #1 < #2 )
   ~.

| #1[link=+] |
if ( $1(#1[lemme:lead],#2[lemme:I]) & #1 < #2 )
   ~.

| #1[link=+] |
if ( $1(#1[lemme:enable],#2[lemme:I]) & #1 < #2 )
   ~.

| #1[link=+] |
if ( $1(#1[lemme:help],#2[lemme:I]) & #1 < #2 )
   ~.

//I felt that it gave me confidence to make my decisions so long as I was able to justify my actions and I was willing to accept accountability for it.

////////////////////// TEMPORALITY \\\\\\\\\\\\\\\\

//This has been the only time I have felt inspired working for this organisation.
//DETD(time,the)
//SUBJATTR(This,time)


| #1[temporality=+] |
if ( DETD(#1[lemme:time],#2) & SUBJATTR(#3,#1) )
   ~.

| ?[timefact,temporality=+] |
    ~.

| ?[tempexpr,temporality=+] |
    ~.

//On previous occasions (not as heavy as this) I have just thought about it a bit, and then I suppose I have gone back to the old assumptions that I would be all right because I am really a good student.
//On previous occasions
//PP{On NP{AP{previous} occasions}}

| pp#1{?, np{ap{#2[timefact]}, ?}} |
if ( ~TIMEX3(#1) )
    TIMEX3[grrefl=+](#1).



| #2[temporality=+] |
if ( TEMP[sentence:~](#1,#2) )
    ~.

| #1[temporality=+] |
if ( TIMEX3(#1) & ~TEMP[sentence](?,#1) )
    ~.

// anticipate only with "I"

| ?[anticipate,temporality=+] |
if ( $1(#1[anticipate],#2[lemme:I]) | $1(#1[lemme:I],#2[anticipate]) )
    ~.

//before[reftime]
/
| ?[reftime,temporality=+] |
    ~.
\
//recently

| ?[time,adv,temporality=+] |
    ~.

//present perfect

| #2[temporality=+] |
if ( NUCL[vlink,perfect](#1[pres],#2) & SUBJ-N(#2,#3[lemme:I]) )
   ~.

// verbs with auxiliary

//Pam talked about things I could do.
//NUCL_VLINK_MODAL(could,do)


| #2[temporality=+,futuretemp=+] |
if ( NUCL[modal](#1,#2) & $1(#1,#3[lemme:I]) )
    ~.

//This will enable me to communicate better in business and in general.
//NUCL_VLINK_MODAL(will,enable)
//ARGUMENT(enable,me)

| #2[temporality=+,futuretemp=+] |
if ( NUCL[modal](#1,#2) & $1(#2,#3[lemme:I]) & FUTURE(#2) )
    ~.

//I sincerely hope that this course will support the growth of my leadership skills to be more rounded and less execution oriented .
//SUBJ_PRE(will,course)
//NUCL_VLINK_MODAL(will,support)

| #3[temporality=+,futuretemp=+] |
if ( SUBJ(#1,#2[course]) & NUCL[vlink,modal](#1,#3) )
   ~.


//In order to improve my communication skills and writing techniques, I need to make time to thoroughly proof read my work and allow time for others to read through it as well.
//SUBJ-N_PRE(need,I)
//OBJ_POST_INFINIT(need,make)

| #1[temporality=+,futuretemp=+] |
if ( SUBJ-N(#1[lemme:need,pres],#2[lemme:I]) & OBJ[post,infinit](#1,#3) )
   ~.

//except can't form:fcan,modal:+,contr_neg:+,auxnot:

| #2[temporality=~,futuretemp=~] |
if ( NUCL[modal](#1[form:fcan],#2) )
    ~.

// except when embedded into a past tense main clause

//I assumed that I could just re-engage - but I did not realise that the game had changed.
//EMBED_COMPLTHAT(re - engage,assumed)
//NUCL_VLINK_MODAL(could,re - engage)

| #1[futuretemp,temporality=~,futuretemp=~] |
if ( EMBED(#1,#2[past]) & NUCL(#3,#1) )
    ~.

//As a result, when I felt 'safe' around a harmless circuit board, I would sometimes neglect the strap as I didn't see it as a threat to my safety.
//PAST(felt_RM:+)

| #3[futuretemp,temporality=~,futuretemp=~] |
if ( PAST(#2) & NUCL[modal](#1,#3[futuretemp]) & #2 < #3 )
   ~.

| #3[futuretemp,temporality=~,futuretemp=~] |
if ( PRES-OR-PAST(#2) & NUCL[vlink,perfect](?,#2) & NUCL[modal](#1,#3[futuretemp]) & ~FUTURE(#3) )
   ~.

// verbs embedded in present tense semi-auxiliaries

//ORDER_TEMP_REL(intend,change)
//OBJ_POST_INFINIT(intend,change)

//Why am I concerned with this, and what do I intend to change with regards to myself based on what I have come to understand through being reflective?

| #2[temporality=+,futuretemp=+] |
if ( OBJ[post,infinit](#1[intention,past:~],#2) & PRESENT(#1) & SUBJ(#1,#3) )
    ~.

| #2[temporality=+,futuretemp=+] |
if ( MOD[post,infinit](#1[intention,past:~],#2) & PRESENT(#1) & SUBJ(#1,#3) )
    ~.

//So I made a decision to act ethically and not be a silent bystander at work.
//OBJ_POST(#1made,#2decision)
//SUBJ_PRE(#1made,#3I)
//MOD_SUBCAT_GRPR(#2decision,#4act)
//PREPD(#4act,#5to)

| #2[temporality=+,futuretemp=+] |
if ( OBJ[post](#1[lemme:make],#2[intention]) & SUBJ(#1,#3[lemme:I]) & MOD(#2,#4) & PREPD(#4,#5[lemme:to]) )
   DEP[grrefl=+,rcdep=+](#3,#2).

//I am determined to make the company my first point of research, for example the values of the company, their policies, their work culture and how they abide by any standards. 
//NUCL_VLINK_PASSIVE(am,determined)
//OBJ_POST_INFINIT(determined,make)

| #2[temporality=+,futuretemp=+] |
if ( NUCL[passive](#1[past:~],#2[lemme:determine]) & OBJ[post,infinit](#2,#3) )
   ~.




//Upon finishing the subject, I honestly felt it was a worthwhile subject to do.
//{Upon GV{finishing}

| #1[prep,start], gv{#2[shifter,temporality=+,link=+]} |
  ~.

//


////////////////////// STANCE \\\\\\\\\\\\\\\\


| ?[attitude,stance=+] |
    ~.

| ?[importance,stance=+] |
    ~.

| ?[surprise,stance=+] |
    ~.

| ?[sentiment,stance=+] |
    ~.

| ?[positivesent,stance=+] |
    ~.

| ?[negativesent,stance=+] |
    ~.


//I have a level of accountability to ensure employees felt safe in their working environment, yes it's somewhat a legislative 'duty of care' requirement but for me it was more than that.
//CONNECTOR_SUBORD(was,but_RM:+)
//MOD_PRE(was,me)
//MANNER_POST(was,more than)

|#1[stance=+]{#2[lemme:more],?} |
if ( MANNER[post](#3,#1) )
   ~.

//kind of

//SUCC(NADJ:kind,PREP:of)

| #1[stance=~] |
if ( SUCC(#1[lemme:kind],#2[lemme:of]) )
   ~.

//This has been the only time I have felt inspired working for this organisation.
//QUALIF(time,only)
//DETD(time,the)

//As the work has now been issued to the client for first release, I am keen to see the changes that they will want us to make.
//NUCL_SUBJCOMPL(am,keen)

| #1[stance=+] |
if ( NUCL[subjcompl](#1[lemme:be],#2[lemme:keen]) )
   ~.

| #2[stance=+] |
if ( NUCL[subjcompl](#1[lemme:be],#2[lemme:keen]) )
   ~.



////////////////////// SHIFT \\\\\\\\\\\\\\\\


| ?[change,shift=+] |
    ~.

//shifters are not shift in this grammar

| #1[shifter,shift=~] |
    ~.


// reflection words embedded in shifters

//Now I start to think, things begin to link up.
//EMBED(think,start)
//ARGUMENT(start,I)


| #1[shift=+] |
if ( EMBED(#1[reflection],#2[shifter]) & $1(#2,#3[subject]))
    ~.

//VERB{re - engage}

| #1[shift=+]{#2[lemme:re],?,#3[reflection]} |
    ~.

//Never stop learning – undertaking my MBA has allowed me to broaden my horizons so far – I intend to ensure my mind remains as open as it currently is.

//OBJ_GRPR(broaden,horizons)

| #2[shift=+] |
if ( OBJ(#1[shifter],#2[reflection]) & $1(#2,#3[subject]))
    ~.

//Now I'm thinking that final testing of project must be done in supervising of entire group; we can't give responsibility to one person.

//TEMP_PRE_T_RELAT(thinking,Now)

| #1[shift=+] |
if ( TEMP(#1[analysis,vprog],#2[lemme:now]) )
   ~.

//As a conclusion , this course definitely changed my attitude of approaching dialogue .

| #2[shift=+] |
if ( OBJ(#1[shifter],#2[stance]) & $1(#2,#3[subject]))
    ~.

//shift + past -> temporality

| #1[shifter,past,temporality=+] |
if ( $1(#1,#2[shift]) | $1(#2[shift],#1) )
  ~.

//I can already see that my attitude towards University has changed.
//SUBJ-N_PRE(changed,attitude)

| #2[shift=+] |
if ( SUBJ-N(#1[shifter],#2[stance]) )
    ~.

| #2[shift=+] |
if ( SUBJ-N(#1[shifter],#2[analysis]) )
    ~.

//I have become more empathetic , compassionate , and feel a deeper connection to friends , family and peers .
//NUCL_SUBJCOMPL(become,empathetic)
//NUCL_VLINK_PERFECT(have,become)

| #2[shift=+] |
if ( NUCL[subjcompl](#1[shifter],#2[stance]) & NUCL[perfect](#3,#1))
    ~.


/////////////////////// restrictions \\\\\\\\\\\\\\\\\\\\\\

 //CONTRAST is relevant only if it is in a dependency with analysis, stance or shift
/
| #1[reflconc_contr=+] |
if ( $1(#1[contr],#2[analysis]) | $1(#2[analysis],#1[contr]) )
    ~.

| #1[reflconc_contr=+] |
if ( $1(#1[contr],#2[stance]) | $1(#2[stance],#1[contr]) )
    ~.

| #1[reflconc_contr=+] |
if ( $1(#1[contr],#2[shift]) | $1(#2[shift],#1[contr]) )
    ~.

| #1[reflconc_contr:~,contrast=~] |
    ~.
\


| #2[temporality=~,futuretemp=~] |
if ( ORDER[temp_rel](#1[contrast],#2) & PRESENT(#1) )
    ~.

/
// shift + contrast -> shift

| #1[shift,contrast=~] |
    ~.
\

////////////////////// REFLECTIVE CONCEPTS \\\\\\\\\\\\\\\\


| #1[temporality,rc=+] |
    ~.

| #1[stance,rc=+] |
    ~.

| #1[contrast,rc=+] |
    ~.

| #1[shift,rc=+] |
    ~.

| #1[subject,rc=+] |
    ~.

| #1[othersubject,rc=+] |
    ~.

| #1[humanactivity,rc=+] |
    ~.

| #1[analysis,rc=+] |
    ~.

| #1[link,rc=+] |
    ~.



//New dependencies with new features

//I really enjoyed hearing about a case, then placed into the case and developing an action plan on how to manage that case. (EMBED_POST_GERUND(like,playing))
//{SC{NP{I} FV{really enjoyed}} GV{hearing}

| sc{?*, np, fv{?*,#1[last,stance]}}, gv{#2[reflection]} |
if ( ~EMBED(#1,#2) & SUBJ[pre](#1,#3) )
   EMBED[grrefl=+,post=+,gerund=+](#1,#2), SUBJ-N[grrefl=+](#2,#3).


//This has been the only time I have felt inspired working for this organisation.
//SUBJATTR(I,inspired)
//NUCL_SUBJCOMPL(felt,inspired)
//AP{inspired} GV{working}

| ap{#2}, gv{#3[last]} |
  if ( SUBJATTR(#1,#2) & ~$1(#3,#2) )
SUBJ-N[grrefl=+](#3,#2).

//If the team members have negative attitudes, it is important for him or her to find out the reasons behind it and make some changes so as to cater for their needs.
//SUBJ-N_PRE(have,team_member)
//OBJ_POST(have,attitude)

if ( SUBJ-N(#1[lemme:have],#2[subject]) & OBJ[post](#1,#3[stance]) )
   DEP[grrefl=+](#2,#3).

if ( SUBJ-N(#1[lemme:have],#2[othersubject]) & OBJ[post](#1,#3[stance]) )
   DEP[grrefl=+](#2,#3).


