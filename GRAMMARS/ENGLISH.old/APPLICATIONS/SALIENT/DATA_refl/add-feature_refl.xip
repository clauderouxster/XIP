DependencyRules:

////////////////////// ANALYSIS \\\\\\\\\\\\\\\\\\

//// needed in add-feature_refl.xip


| #1[mental=~,noun,publ] | 
    ~.

| #1[mental,analysis=+] |
    ~.

| #1[scope,analysis=+] |
    ~.

| #1[reflection,analysis=+] |
    ~.




//delete features from words

| #1[ok=+] |
if ( $1(#1[wscope],#2[scope]) | $1(#2[scope],#1[wscope]) )
   ~.

| #1[wscope,ok:~,ksw=~,attitude=~,change=~,contr=~,mental=~,new=~,scope=~,timefact=~,mentsco=~] |
  ~.

| #1[ok=+] |
if ( $1(#1[wmentsco],#2[mentsco]) | $1(#2[mentsco],#1[wmentsco]) )
   ~.

| #1[wmentsco,ok:~,ksw=~,attitude=~,change=~,contr=~,mental=~,new=~,scope=~,timefact=~,mentsco=~] |
  ~.





//import mentsco as reflection
/
| ?[mentsco,noimport:~,reflection=+] |
    ~.
\


//exception
//NEGAT(#1)
//DEP[negative](#1,?) )

| #1[mentsco,contr,shift=~] |
if ( NEGAT(#1) | DEP[negative](#1,?) )
   ~.

//require,need[wmentsco,contr]

| #1[ok=+] |
if ( ^$1(#1[wmentsco,contr],#2[mentsco]) )
   ~.

| #1[ok=+] |
if ( ^$1(#1[mentsco],#2[wmentsco,contr]))
   ~.

| #1[wmentsco,contr,ok:~,contr=~] |
  ~.


///////////////   correcting dependency \\\\\\\\\\\\\\\\\

//During one of the field work I was a part of group conducting surveys by contacting stakeholders related to UTS-2020 master plan, I didn't perform well when it came to communicating with people, asking them questions and describing them about the scenario.
//QUANTD(work,one)
//PREPD(one,During)

if ( QUANTD(#1,#2) & ^PREPD(#2,#3) )
    PREPD[grrefl=+](#1,#3).

///////////////////// missing dependency \\\\\\\\\\\\\\\\\\\



//This made me really wonder why is it acceptable to act unethically?
//NP{me} IV{really wonder_RM:+}
//OBJ_POST(made,me)

| np{#1}, iv{?, #2[reflection]} |
if ( OBJ[post](#3[lemme:make],#1) )
   SUBJ-N[grrefl=+](#2,#1).

//It really made me wonder when will there be a shift in short term thinking to long term thinking. 
//NP{me} IV{wonder when}
//OBJ_POST(made,me)

| np{#1}, iv{#2[reflection],?} |
if ( OBJ[post](#3[lemme:make],#1) )
   SUBJ-N[grrefl=+](#2,#1).


//Throughout these few weeks I have been very interested in understanding what things were, how they worked, where and why the equipment was used etc., however I had not been nearly as interested in understanding the personal safety factors when dealing with equipment.
//VERB{had not been}}

| verb#1[contr=+]{?, #2[lemme:not], ?} |
  ~.



//nearly as NFV{interested_RM}
//MANNER_POST(had not been,nearly)

| #1, (?), nfv{#2} |
if ( MANNER[post](#3,#1) )
   DEP[grrefl=+](#2,#3).


//From this training I realised that although I was following the procedure ( wearing the ESD strap ) , I was not aware of why and how this could be dangerous to equipment .
//not AP{aware}
//NUCL_SUBJCOMPL(was,aware)


| #1[contr], AP{#2[reflection,contr=+]} |
if ( ^NUCL(#3,#2) )
   NUCL[neg=+](#3,#2).


//When I was doing my bachelor on electrical engineering at UTS I thought doing only electrical subjects are not enough for my future career .
//NP{I} FV{thought}

| np{#1[lemme:I,last]}, fv{#2[last]} |
if ( ~SUBJ(#2,#1) )
   SUBJ[grrefl=+](#2,#1), SUBJ-N[grrefl=+](#2,#1).

//The first thing I learnt is how to modify your resume for different companies or area .
//MOD_POST_SENTENCE_RELATIV(thing,learnt)
//SUBJ_PRE(is,thing)
//MOD_POST_INFINIT(is,modify)


if ( SUBJ[pre](#1,#2) & MOD[post,sentence,relativ](#2,#3[reflection]) & MOD[post,infinit](#1,#4) )
   EMBED[grrefl=+](#4,#3).

//my way of thinking
//My studies have greatly expanded my way of thinking .

//MOD_SUBCAT_POST(way,thinking)
//DETD(way,my)

| #1[reflection=+] |
if ( MOD(#1[lemme:way],#2[reflection]) & DETD(#1,#3) )
   DETD[grrefl=+](#2,#3).


//I also prefer a country lifestyle as opposed to city, I enjoyed settling into the community and everyone tried their best to make me feel welcome.
//SC{NP{I} FV{enjoyed}} GV{settling} 

| sc{(?), np{#1[lemme:I]}, fv{?*, #2[trans,reflection]}}, gv{#3[vprog]} |
if ( ~$1(#2,#3) | ~$1(#3,#2) )
    EMBED[post=+,gerund=+,grrefl=+](#2,#3).


//On the other hand, it makes me conscious of the importance of a positive internal dialogue within the company, because of the major impact in can have on the performance and outcomes.
//MOD_SUBCAT_POST(makes,importance)
//MOD_SUBCAT_POST(conscious,importance)
//IOBJ_POST(makes,me)

if ( MOD[subcat,post](#1,#2) & MOD[subcat,post](#3,#2) & IOBJ[post](#1,#4) )
   SUBJATTR[grrefl=+](#4,#3), OBJCOMPL[grrefl=+,post=+](#1,#3).


//I guess I still have a lot of time to think about this , but when the decision is made it will affect my life entirely or even others .
//MOD_POST_INFINIT(#1have,#2think)
//SUBJ-N_PRE(#1have,#3I)

if ( SUBJ-N(#1[lemme:have],#3[lemme:I]) & $1[infinit](#1,#2[reflection]) )
   DEP[grrefl=+](#2,#3).


////////////////////////// contextual reflection words \\\\\\\\\\\\\\\\\\\


//necessary precautions
//As a result I felt very unprofessional and although my behaviour did not do any immediate harm to the equipment, I still felt guilty that I had not taken the necessary precautions or taken any interest in understanding why I was wearing the strap.
//QUALIF(precautions,necessary)

| #2[contr=~] |
if ( QUALIF(#1,#2[lemme:necessary]) )
   ~.


// I show -> not reflective

| #1[reflection=~] |
if ( SUBJ(#1[lemme:show],#2[lemme:I]) | SUBJ-N(#1[lemme:show],#2[lemme:I]) )
   ~.

//It appeared to me that I wasn't very responsible for my work and I did not show the respects I should have shown for my superior and my job.
//MOD_GRPR_POST(appeared,me)

| #1[reflection=+] |
if ( MOD[post](#1[lemme:appear],#2[lemme:I]) )
   ~.

// found a -> not reflection
//OBJ_POST(found,academy)
//DETD(academy,an)

| #1[!reflection=~] |
if ( OBJ[sentence:~](#1[lemme:find],#2) & DETD(#2,#3[indef]) )
   ~.

| #1[!reflection=~] |
if ( OBJ[sentence:~](#1[lemme:find],#2[pron:~,noun]) & ~DETD(#2,#3) )
   ~.

//NUCL_PARTICLE(find_INDICATOR:+,out)

| #1[!reflection=~] |
if ( NUCL(#1[lemme:find],?) )
   ~.

//Pam said that I should have found my own examples.
//OBJ_POST(found,examples)


| NP{?*, #1[last,lastword=+]}, ?[end] |
  ~.

| #1[lemme:find,!reflection=~] |
if ( OBJ[sentence:~](#1[lemme:find],#2[lastword]) )
   ~.


// I found the manual for the device
//MOD_SUBCAT_POST(found,device)
//PREPD(device,for)
//OBJ_POST(found,manual)

//exception
//Oddly enough I found it quite empowering to hear Natalia state the following Real life dilemmas often present choices between equally unfavorable or disagreeable alternatives.
//POTMOD(found,empowering)


| #1[lemme:find,!reflection=~] |
if ( MOD(#1,#2) & PREPD(#2,?) & OBJ[sentence:~](#1,#3[analysis:~]) & ~EMBED(?,#3) & ~POTMOD(#1,#4[vprog]) )
   ~.

//I also found the power of collaboration.
//MOD_POST(#1power,#2collaboration)
//OBJ_POST(#3found,#1power)
//PREPD(#2collaboration,of)

| #3[lemme:find,!reflection=~] |
if ( MOD(#1,#2) & PREPD(#2,?) & OBJ[sentence:~](#3,#1[analysis:~])  & ~EMBED(?,#1) )
   ~.


// realise sg not reflective
//Through our discussions of our ideas we decided to realise our map in the form of a small diorama of the tunnel that represented these ideas in a three-dimensional way, the construction illustrated in image (b) in Appendix A represents also the manner in which group members worked together to complete each stage.
//OBJ_POST(realise,map)

|#1[lemme:realise,!reflection=~] |
if ( OBJ[sentence:~](#1,#2[noun]) )
    ~.

|#1[lemme:realize,!reflection=~] |
if ( OBJ[sentence:~](#1,#2[noun]) & ~CORE-VERB(#3) & #1 < #3 )
    ~.


// indicate: reflective only if the obj. is "I"

| #1[lemme:indicate,!reflection=+] |
if ( OBJ(#1,#2[lemme:I]) | OBJ-N(#1,#2[lemme:I]) | IOBJ(#1,#2[lemme:I])  )
   ~.

| #1[lemme:awaken,!reflection=+,shift=+] |
if ( OBJ(#1,#2[lemme:I]) | OBJ-N(#1,#2[lemme:I]) | IOBJ(#1,#2[lemme:I])  )
   ~.


// exception to "see": it has an OBJ
//Everyone pitches in and if we see an un-recycled item in the normal bin we usually sift through and retrieve the item.
//OBJ_POST(see_INDICATOR:+,item)

| #1[lemme:see,reflection=~] |
if ( OBJ[post,sentence:~](#1,?) )
    ~.

// see sg as
//As a result, when I felt 'safe' around a harmless circuit board, I would sometimes neglect the strap as I didn't see it as a threat to my safety.
//MOD_POST(see,threat)
//PREPD(threat,as)

| #1[!reflection=+] |
if ( MOD[post](#1[lemme:see],#2) & PREPD(#2,#3[lemme:as]) )
   ~.

//Using the basic tools and methods learnt at University, I was able to construct and visualise production components with up to 30 parts and numerous assemblies within assemblies.

| #1[lemme:visualise,reflection=~] |
if ( OBJ[post,sentence:~](#1,?) )
    ~.


| #1[lemme:visualize,reflection=~] |
if ( OBJ[post,sentence:~](#1,?) )
    ~.



///////////////////////////////////

//contextmark

| conj#1[coord:~,form:~fthat,form:~fif,form:~fwhether,form:~fbut,form:~fthan,contextmark=+] |
  ~.


/
| ?[tempexpr,contextmark=+,nocontextmark:~] |
  ~.
\
| ?[lemme:because,contextmark=~] |
  ~.

| ?[lemme:as,contextmark=~] |
  ~.

| ?[lemme:although,contextmark=~] |
  ~.

| ?[lemme:though,contextmark=~] |
  ~.

| ?[lemme:even_though,contextmark=~] |
  ~.

| ?[lemme:so,contextmark=~] |
  ~.

| ?[lemme:however,contextmark=~] |
  ~.

//notice that I
//I even noticed that I wasn't professional enough comparing to my other group members .
//OBJ_POST_SENTENCE(noticed,wasn't)
//SUBJ-N_PRE(wasn't,I)

| #1[lemme:notice,!reflection=+] |
if ( OBJ[sentence](#1,#2) & SUBJ-N(#2,#3[lemme:I]) )
   ~.


// look at
//ARGUMENT_REFLDEP(look,myself)
//PREPD(myself,at)

| #1[reflection=+] |
if ( $1(#1[lemme:look],#2) & PREPD(#2,#3[lemme:at]) )
    ~.

// exception
//I will definitely look for an opportunity at university that will allow me to study management skills.
//MOD_SUBCAT_POST(look,opportunity)
//MOD_POST(look,university)
//PREPD(opportunity,for)

| #1[reflection=~] |
if ( $1(#1[lemme:look],#2) & PREPD(#2,#3[lemme:~at]) )
    ~.






// he taught me

| #1[reflection=~,course=+] |
if ( SUBJ-N(#1[lemme:teach],#2[pronpers]) )
     ~.

| #1[reflection=~,course=+] |
if ( SUBJ-N(#1[lemme:teach],#2[c_person]) )
     ~.

| #1[reflection=~,course=+] |
if ( SUBJ-N(#1[lemme:teach],#2[course]) )
     ~.

//It was during the first two months in the company that taught me to be a independent thinker and doer.
//{BG{that} FV{taught}}

| sc{BG{#1[lemme:that]}, FV{#2[lemme:teach,reflection=~,course=+]}} |
  ~.

//My biggest takeaway from this learning experience is learning how to find YOUR way to your goal as there's no single path.
//DETD(experience,this)

| #1[course=+] |
if ( DETD(#1[lemme:experience],#2[lemme:this]) )
   ~.

//bring to mind

| #1[reflection=+] |
if ( $1(#1[lemme:bring],#2[lemme:mind]) | $1(#1[lemme:mind],#2[lemme:bring]))
    ~.

| #2[reflection=+] |
if ( $1(#1[lemme:bring],#2[lemme:mind]) |  $1(#1[lemme:mind],#2[lemme:bring]) )
    ~.


// open eyes

| #1[shift=+,reflection=+] |
if ( $1(#1[lemme:open],#2[lemme:eye]) | $1(#1[lemme:eye],#2[lemme:open]))
    ~.

| #2[shift=+,reflection=+] |
if ( $1(#1[lemme:open],#2[lemme:eye]) |  $1(#1[lemme:eye],#2[lemme:open]) )
    ~.

//It was eye opening to see the behaviors and feelings of my peers.
//NUCL_SUBJCOMPL(was,eye)
//NUCL_VLINK_PROGRESS(was,opening)

| #1[shift=+,reflection=+] |
if ( NUCL(#1,#2[lemme:open]) &  NUCL(#1,#3[lemme:eye]) )
    ~.

| #2[shift=+,reflection=+] |
if ( NUCL(#1,#2[lemme:open]) &  NUCL(#1,#3[lemme:eye]) )
    ~.

//made me [reflection] -> shift

//This class is the first course I have taken at Georgetown that has made me really feel like I can make a difference in the world and that has helped me to grow as a whole person .
//NFV{made} NP{me} IV{really feel}
//OBJ-N(made,me)

| ?{#1[lemme:make]}, ?{#2[lemme:I]}, iv{?*,#3[reflection,authrefl=+,shift=+]} |
if ( OBJ-N(#1,#2) )
    ~.

//This made me feel like my professor and peers really cared about my life and how I was doing.
//SUBJ-N_REFLDEP(feel,me)
//OBJ-N(made,me)


| #2[authrefl=+,shift=+] |
if ( SUBJ-N(#1[reflection],#2[lemme:I]) & OBJ(#3[lemme:make],#2) )
    ~.

//It made me further think about people around me who may not deviate from social norms in a very outward way, but may be struggling internally and I may not even notice.
//{SC{NP{It} FV{made}} NP{me} IV{further think} 

| sc{?, fv{#1}}, #2[lemme:I], iv{?*,#3[reflection,authrefl=+,shift=+]} |
    ~.

//On the other hand, it makes me conscious of the importance of a positive internal dialogue within the company, because of the major impact in can have on the performance and outcomes.
//IOBJ_POST(makes,me)
//SUBJATTR_GRREFL(me,conscious)

| #3[shift=+] |
if ( IOBJ(#1[lemme:make],#2[lemme:I]) & $1(#2,#3[reflection]) )
   ~.

//I have come to understand that in order to effectively advance women's rights, we need to galvanize a global women's movement.
//OBJ_POST_INFINIT(#2come,#4understand)
//SUBJ-N_PRE(#2come,#1I)
//PREPD(#4understand,#3to)
//SUBJ-N_REFLDEP(#4understand,#1I)

| #2[lemme:come,shift=+] |
if ( SUBJ-N(#2,#1) & SUBJ-N(#4,#1) & OBJ[infinit](#2,#4) & PREPD(#4,#3) )
   ~.

//From this experience I have learnt that is important to not only be interested in asking questions about equipment, how they work and how to fix them, but also to understand all necessary safety precautions (personal and also in respect to keeping equipment safe).
//NUCL_VLINK_PERFECT(have,learnt_RM:+)

| #2[shift=+] |
if (NUCL[perfect](#1[pres],#2[lemme:learn]) )
   ~.


//I gained greater awareness of how passionate Latinos are of their history, and how strongly this influences their current codes and conventions.
//ARGUMENT(gained,awareness)

| #1[shift=+] | 
if ( OBJ(#1[lemme:gain],#2[reflection]) )
   ~.


// no matter
//QUANTD_NEG(matter,no)

| #1[contr=+,ksw=+] |
if ( QUANTD(#1[lemme:matter],#2[lemme:no]) )
    ~.



//exception to capability:
//It showed me that even though I can feel alone sometimes in the issues I'm facing, the odds are someone else is either experiencing it or witnessing someone else experience it.
//NUCL_VLINK_MODAL(can_AUTHCAPA:+,feel)

| #2[capability=~] |
if ( $1(#1[sentiment],#2[capability]) )
    ~.

| #1[capability=~] |
if ( $1(#1[capability],#2[sentiment]) )
    ~.

