# personal pronouns 
# cf. http://www.ucl.ac.uk/dutch/grammatica/personal_pronoun.htm

# +Unstr  = unstressed
# +Spoken = spoken language only

# in Dutch, indirect object pronouns have the same form as (direct) object pronouns, 
# except in the third person plural. 
# Formally, hun is the correct form of the third-person plural indirect object personal pronoun, 
# but you will often come across unstressed ze instead.


define ZELF 	%=:0 {zelf} ;

read regex [

  {ik} 		(ZELF) 	%+Pron:0 %+Pers:0 %+MF:0 %+1P:0 %+Sg:0 %+Nom:0
| {ik}:{'k} 		%+Pron:0 %+Pers:0 %+MF:0 %+1P:0 %+Sg:0 %+Nom:0 %+Unstr:0 %+Spoken:0 
| {ik}:{mij} 	(ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+1P:0 %+Sg:0 %+AccDat:0
| {ik}:{me} 	(ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+1P:0 %+Sg:0 %+Acc:0 %+Unstr:0
| {ik}:{me} 	(ZELF)	%+Pron:0 %+Refl:0 %+MF:0 %+1P:0 %+Sg:0

| {jij} 	(ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+2P:0 %+Sg:0 %+Nom:0
| {jij}:{jou} 	(ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+2P:0 %+Sg:0 %+AccDat:0
| {jij}:{je} 	(ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+2P:0 %+Sg:0 %+NomAcc:0 %+Unstr:0
| {jij}:{je} 	(ZELF)	%+Pron:0 %+Refl:0 %+MF:0 %+2P:0 %+Sg:0

| {gij} 	(ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+2P:0 %+SgPl:0 %+Nom:0
| {gij}:{ge} 	(ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+2P:0 %+SgPl:0 %+NomAcc:0 %+Unstr:0
| {gij}:{ge} 	(ZELF)	%+Pron:0 %+Refl:0 %+MF:0 %+2P:0 %+SgPl:0 

| {hij} 	(ZELF)	%+Pron:0 %+Pers:0 %+M:0 %+3P:0 %+Sg:0 %+Nom:0 
| {hij}:{ie} 		%+Pron:0 %+Pers:0 %+M:0 %+3P:0 %+Sg:0 %+Nom:0 %+Unstr:0 %+Spoken:0 
| {hij}:{hem} 	(ZELF)	%+Pron:0 %+Pers:0 %+M:0 %+3P:0 %+Sg:0 %+AccDat:0 
| {hij}:{'m} 		%+Pron:0 %+Pers:0 %+M:0 %+3P:0 %+Sg:0 %+Acc:0 %+Unstr:0 %+Spoken:0

| {zij} 	(ZELF)	%+Pron:0 %+Pers:0 %+F:0 %+3P:0 %+Sg:0 %+Nom:0
| {zij}:{ze} 		%+Pron:0 %+Pers:0 %+F:0 %+3P:0 %+Sg:0 %+Nom:0 %+Unstr:0
| {zij}:{haar} 	(ZELF)	%+Pron:0 %+Pers:0 %+F:0 %+3P:0 %+Sg:0 %+AccDat:0
| {zij}:{d'r} 		%+Pron:0 %+Pers:0 %+F:0 %+3P:0 %+Sg:0 %+Acc:0 %+Unstr:0 %+Spoken:0

| {het} 	(ZELF)	%+Pron:0 %+Pers:0 %+N:0 %+3P:0 %+Sg:0 %+NomAccDat:0
| {het}:{'t} 		%+Pron:0 %+Pers:0 %+N:0 %+3P:0 %+Sg:0 %+NomAcc:0 %+Unstr:0 %+Spoken:0

| {wij} 	(ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+1P:0 %+Pl:0 %+Nom:0
| {wij}:{we} 	(ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+1P:0 %+Pl:0 %+Nom:0 %+Unstr:0 
| {wij}:{ons} 	(ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+1P:0 %+Pl:0 %+AccDat:0
| {wij}:{ons} 	(ZELF)	%+Pron:0 %+Refl:0 %+MF:0 %+1P:0 %+Pl:0

| {jullie} 	(ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+2P:0 %+Pl:0 %+NomAccDat:0
| {jullie}:{je} (ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+2P:0 %+Pl:0 %+AccDat:0 %+Unstr:0
| {jullie}:{je} (ZELF)	%+Pron:0 %+Refl:0 %+MF:0 %+2P:0 %+Pl:0

| {zij} 	(ZELF)	%+Pron:0 %+Pers:0 %+MFN:0 %+3P:0 %+Pl:0 %+Nom:0
| {zij}:{ze} 		%+Pron:0 %+Pers:0 %+MFN:0 %+3P:0 %+Pl:0 %+NomAcc:0 %+Unstr:0
| {zij}:{hen} 	(ZELF)	%+Pron:0 %+Pers:0 %+MFN:0 %+3P:0 %+Pl:0 %+Acc:0 
| {zij}:{hun} 	(ZELF)	%+Pron:0 %+Pers:0 %+MFN:0 %+3P:0 %+Pl:0 %+Acc:0 %+Spoken:0
| {zij}:{hun} 	(ZELF)	%+Pron:0 %+Pers:0 %+MFN:0 %+3P:0 %+Pl:0 %+Dat:0

| {u} 		(ZELF)	%+Pron:0 %+Pers:0 %+MF:0 %+2P:0 %+SgPl:0 %+NomAccDat:0 %+Formal:0
| {u} 		(ZELF)	%+Pron:0 %+Refl:0 %+MF:0 %+2P:0 %+SgPl:0 %+Formal:0
| {zich}	(ZELF)	%+Pron:0 %+Refl:0 %+MFN:0 %+3P:0 %+SgPl:0

# archaic genitive forms:
# cf. http://oase.uci.kun.nl/~ans/e-ans/05/02/08/01/body.html

| {ik}:{mijner}		%+Pron:0 %+Pers:0 %+MF:0 %+1P:0 %+Sg:0 %+Gen:0 %+FormEr:0
| {ik}:{mijns}		%+Pron:0 %+Pers:0 %+MF:0 %+1P:0 %+Sg:0 %+Gen:0 %+FormS:0
| {hij}:{zijner}	%+Pron:0 %+Pers:0 %+M:0 %+3P:0 %+Sg:0 %+Gen:0 %+FormEr:0
| {hij}:{zijns}		%+Pron:0 %+Pers:0 %+M:0 %+3P:0 %+Sg:0 %+Gen:0 %+FormS:0
| {zij}:{harer}		%+Pron:0 %+Pers:0 %+F:0 %+3P:0 %+SgPl:0 %+Gen:0 %+FormEr:0
| {zij}:{haars}		%+Pron:0 %+Pers:0 %+F:0 %+3P:0 %+SgPl:0 %+Gen:0 %+FormS:0

| {wij}:{onzer}		%+Pron:0 %+Pers:0 %+MF:0 %+1P:0 %+Pl:0 %+Gen:0 %+FormEr:0
| {wij}:{ons}		%+Pron:0 %+Pers:0 %+MF:0 %+1P:0 %+Pl:0 %+Gen:0 %+FormS:0
| {zij}:{hunner}	%+Pron:0 %+Pers:0 %+MF:0 %+3P:0 %+Pl:0 %+Gen:0 %+FormEr:0	
| {zij}:{huns}		%+Pron:0 %+Pers:0 %+MF:0 %+3P:0 %+Pl:0 %+Gen:0 %+FormS:0

| {u}:{uwer}		%+Pron:0 %+Pers:0 %+MF:0 %+2P:0 %+SgPl:0 %+Gen:0 %+FormEr:0
| {u}:{uws}		%+Pron:0 %+Pers:0 %+MF:0 %+2P:0 %+SgPl:0 %+Gen:0 %+FormS:0
] ;


# --------------------------------------------------------------------------------
# possessives
# cf. http://www.ucl.ac.uk/dutch/grammatica/possessive_pronoun.htm
# http://oase.uci.kun.nl/~ans/e-ans/05/05/08/01/body.html

# NOTE: "the first person plural of the possessive, ons/onze, is the only
# 	possessive that behaves like an adjective in the sense that it declines"

read regex [
  {mijn}	%+Det:0 %+Poss:0 %+MF:0 %+1P:0 %+Sg:0	
| {mijn}:{m'n}	%+Det:0 %+Poss:0 %+MF:0 %+1P:0 %+Sg:0 %+Unstr:0 %+Spoken:0
| {jouw}	%+Det:0 %+Poss:0 %+MF:0 %+2P:0 %+Sg:0	
| {jouw}:{je}	%+Det:0 %+Poss:0 %+MF:0 %+2P:0 %+Sg:0 %+Unstr:0 	
| {zijn}	%+Det:0 %+Poss:0 %+MN:0 %+3P:0 %+Sg:0	
| {zijn}:{z'n}	%+Det:0 %+Poss:0 %+MN:0 %+3P:0 %+Sg:0 %+Unstr:0 %+Spoken:0
| {haar}	%+Det:0 %+Poss:0 %+F:0  %+3P:0 %+Sg:0	
| {haar}:{d'r}	%+Det:0 %+Poss:0 %+F:0  %+3P:0 %+Sg:0 %+Unstr:0 %+Spoken:0
| {ons}		%+Det:0 %+Poss:0 %+MF:0 %+1P:0 %+Pl:0 %+Basic:0	 
| {ons}:{onze}	%+Det:0 %+Poss:0 %+MF:0 %+1P:0 %+Pl:0 %+Decl:0	
| {jullie}	%+Det:0 %+Poss:0 %+MF:0 %+2P:0 %+Pl:0	
| {jullie}:{je}	%+Det:0 %+Poss:0 %+MF:0 %+2P:0 %+Pl:0 %+Unstr:0	
| {hun} 	%+Det:0 %+Poss:0 %+MFN:0 %+3P:0 %+Pl:0	

| {uw}		%+Det:0 %+Poss:0 %+MF:0 %+2P:0 %+SgPl:0 %+Formal:0	

# nominal use  (de/het mijne/jouwe/etc.)
# http://oase.uci.kun.nl/~ans/e-ans/05/05/08/01/body.html

| {mijne}	%+Noun:0 %+Poss:0 %+MF:0 [%+Sg:0|%+Pl:n]
| {jouwe} 	%+Noun:0 %+Poss:0 %+MF:0 [%+Sg:0|%+Pl:n]
| {zijne}	%+Noun:0 %+Poss:0 %+MF:0 [%+Sg:0|%+Pl:n]
| {hare}	%+Noun:0 %+Poss:0 %+MF:0 [%+Sg:0|%+Pl:n]
| {hare}	%+Noun:0 %+Poss:0 %+MF:0 [%+Sg:0|%+Pl:n]
| {onze}	%+Noun:0 %+Poss:0 %+MF:0 [%+Sg:0|%+Pl:n]
| {hunne}	%+Noun:0 %+Poss:0 %+MF:0 [%+Sg:0|%+Pl:n]
| {uwe}		%+Noun:0 %+Poss:0 %+MF:0 [%+Sg:0|%+Pl:n]
];

# --------------------------------------------------------------------------------
# reciprocals
read regex [
  {elkaar}	%+Pron:0 %+Rec:0	(%+Gen:s)
| {elkander}	%+Pron:0 %+Rec:0	(%+Gen:s)
| {mekaar}	%+Pron:0 %+Rec:0	(%+Gen:s)
];

# --------------------------------------------------------------------------------
# pronominal adverbs


define PRONADV1 [
  {aantoe} | {aan} | {achter} | {af}
| {beneden} | {benevens} | {bij} | {binnen} | {bovenop} | {boven} | {buiten}
| {doorheen} | {door} 
| {heen}
| {in}
| {langs}
| {mee}
| {naartoe} | {naar} | {naast} | {na} | {nevens}
| {omheen} | {omtrent} | {om} | {onderdoor} | {onderin} | {onderuit} | {onder}
| {opaan} | {opaf} | {opin} | {opna} | {opuit} | {op} | {overheen} | {over}
| {tegenaan} | {tegenin} | {tegenop} | {tegenover} | {tegen} | {toe}
| {tussendoor} | {tussenin} | {tussenuit} | {tussen}
| {uit}
| {vandaan} | {vandoor} | {vanlangs} | {vanonder} | {vantussen} | {van}
| {vooraan} | {voorin} | {vooruit} | {voor}
| {zonder} ];


define PRONADV  [ 
	[ {daar} | {er} | {hier} | {waar} ] %=:0 PRONADV1  %+PronAdv:0 
	| PRONADV1 %+PronAdv:0 %+Partial:0 
];

define MAP [ %+PronAdv -> %+WAdv  || .#. {waar} ?* _ ] ;

read regex MAP.i .o. PRONADV ;


# --------------------------------------------------------------------------------
# demonstratives
# cf. http://oase.uci.kun.nl/~ans/e-ans/05/06/02/body.html

define ADJFLEX	[ %+Basic:0 
		| %+Decl:e  ];

define NADJ	[ 0:e %+MF:0 %+Sg:0 
		| 0:s %+N:0 %+Sg:0 
		| 0:{en} %+MFN:0 %+Pl:0 
		] ;

define PRONDET	[ %+Pron | %+Det ]:0 ;


define ZLF [ {zelfde} ] ;

read regex [

  {het} ZLF		PRONDET %+Dem:0 %+N:0 %+Sg:0
| {een} ZLF		PRONDET %+Dem:0 %+MFN:0 %+Sg:0
| {de} ZLF		PRONDET %+Dem:0 %+MF:0 %+Sg:0
| {de} ZLF 0:n		PRONDET %+Dem:0 %+MF:0 %+Pl:0



| {datgene}		%+Pron:0 %+Dem:0 %+N:0 %+Sg:0
| {dat}	(ZLF)		PRONDET %+Dem:0 %+N:0 %+Sg:0
| {degene}		%+Pron:0 %+Dem:0 %+MF:0 %+Sg:0
| {deze}		PRONDET %+Dem:0 %+MF:0 %+Sg:0
| {deze}:{dezer}	%+Det:0 %+Dem:0 %+MFN:0 %+Pl:0 %+Gen:0
| {deze} 0:n		PRONDET %+Dem:0 %+MFN:0 %+Pl:0
| {diegene}		%+Pron:0 %+Dem:0 %+MF:0 %+Sg:0
| {diegene}		%+Pron:0 %+Dem:0 %+MFN:0 %+Pl:0
| {diegene} 0:n		%+Pron:0 %+Dem:0 %+MFN:0 %+Pl:0
| {die}	(ZLF)		PRONDET %+Dem:0 %+MF:0 %+Sg:0
| {die}:{diens}		%+Pron:0 %+Dem:0 %+MF:0 %+Sg:0 %+Gen:0
| {die} (ZLF 0:n)	PRONDET %+Dem:0 %+MFN:0 %+Pl:0
| {die}:{dier}		%+Det:0 %+Dem:0 %+MF:0 %+Sg:0 %+Gen:0
| {die}:{dier}		%+Det:0 %+Dem:0 %+MFN:0 %+Pl:0 %+Gen:0
| {dit}	(ZLF)		PRONDET %+Dem:0 %+N:0 %+Sg:0
| {gene}		PRONDET %+Dem:0 %+MF:0 %+Sg:0
| {gene}		PRONDET %+Dem:0 %+MFN:0 %+Pl:0
| {gene} 0:n		%+Pron:0 %+Dem:0 %+MFN:0 %+Pl:0
| {gindse}		PRONDET %+Dem:0 %+MF:0 %+Sg:0
| {gindse}		%+Det:0 %+Dem:0 %+MFN:0 %+Pl:0
| {ginds}		PRONDET %+Dem:0 %+N:0 %+Sg:0
| {zo'n}		%+Det:0 %+Dem:0 %+MFN:0 %+Sg:0
| {zo'n}		%+Pron:0 %+Dem:0 %+N:0 %+Sg:0
| {zulk een}		%+Det:0 %+Dem:0 %+MFN:0 %+Sg:0
| {zulk}		PRONDET %+Dem:0 %+N:0 %+Sg:0
| {zulke}		PRONDET %+Dem:0 %+MFN:0 %+Pl:0
| {zulke}		%+Det:0 %+Dem:0 %+MF:0 %+Sg:0
| {zulke} 0:n		%+Pron:0 %+Dem:0 %+MFN:0 %+Pl:0
| {zulks}		%+Pron:0 %+Dem:0 %+N:0 %+Sg:0

#| {dezulke}	 	??

| [ {dergelijk} | {soortgelijk} | {zodanig} | {dusdanig} ]
  [ %+Adj:0 %+Dem:0 ADJFLEX | %+DAdj:0 %+Noun:0 %+Dem:0 NADJ ]

| {zelf}		%+Adv:0 %+Dem:0
| {zelf}:{zélf}		%+Adv:0 %+Dem:0 %+Emph:0 
| {zelf}:{zelve}	%+Adv:0 %+Dem:0	%+Arch:0

];


# -----------------------------------------------------------------------------
# indefinite pronouns/determiners
# cf. 	http://oase.uci.kun.nl/~ans/e-ans/05/09/02/01/body.html
#	http://oase.uci.kun.nl/~ans/e-ans/05/09/03/01/body.html

# !!! CHECK: singular/plural, gender

read regex [
  [d [a|i] t % ] [{en}|{of}] [%  d [a|i] t ]	 PRONDET %+Indef:0
| {die } [{en}|{of}] { die} 			 PRONDET %+Indef:0

| {geen}		[ %+Pron:0 | %+Det:0 ] %+Neg:0 

| {allemaal}		%+Pron:0 %+Indef:0
| {alleman}		%+Pron:0 %+Indef:0

| {andermans}		%+Pron:0 %+Indef:0 %+Gen:0 	# only genitive !

| {iemand} 		%+Pron:0 %+Indef:0
| {iemand} 0:s 		%+Pron:0 %+Indef:0 %+Gen:0

| {iets}		%+Pron:0 %+Indef:0
| {iets}:{íets}		%+Pron:0 %+Indef:0 %+Emph:0

| {niemand} 		%+Pron:0 %+Indef:0
| {niemand} 0:s		%+Pron:0 %+Indef:0 %+Gen:0

| {niets}		%+Pron:0 %+Indef:0
| {niks}		%+Pron:0 %+Indef:0		# collq.

#| {zeker} 		%+Det:0 %+Indef:0		# rather adjective, e.g. "een zeker" = certain
#| {zeker} 0:e		%+Det:0 %+Indef:0

| {zoiets}		%+Pron:0 %+Indef:0

| {men}			%+Pron:0 %+Indef:0


| {ander}		[ %+Adj:0 %+Indef:0 ADJFLEX | %+DAdj:0 %+Noun:0 %+Indef:0 NADJ ]


# cf. http://oase.uci.kun.nl/~ans/e-ans/05/09/03/03/02/body.html

| [ {ener} | {beider} | {aller} | {veler} | {meiger} | {eniger} | {gener} ]	
	[ {lei} | {hande} ]	PRONDET %+Indef:0	# ???

];


# -----------------------------------------------------------------------------
# relative pronouns
# cf. http://oase.uci.kun.nl/~ans/e-ans/05/08/02/body.html

read regex [
  {welk}	%+Det:0 %+Rel:0 %+N:0 %+Sg:0
| {welk} 0:e	%+Det:0 %+Rel:0 %+MF:0 %+Sg:0
| {welke}	PRONDET %+Rel:0 %+MFN:0 %+Pl:0

| {dat}		%+Pron:0 %+Rel:0 %+N:0 %+Sg:0
| {wat}		%+Pron:0 %+Rel:0 %+N:0 %+Sg:0
| {hetwelk}	%+Pron:0 %+Rel:0 %+N:0 %+Sg:0
| {hetgeen}	%+Pron:0 %+Rel:0 %+N:0 %+Sg:0

| {die}		%+Pron:0 %+Rel:0 %+MFN:0 %+Pl:0
| {wie}		%+Pron:0 %+Rel:0 %+MFN:0 %+Pl:0

| {wie}:{wiens}		%+Pron:0 %+Rel:0 %+MFN:0 %+Pl:0 %+Gen:0		# ??
| {wie}:{wier}		%+Pron:0 %+Rel:0 %+MFN:0 %+Pl:0 %+Gen:0		# ??
| {welk} 0:{er}		%+Pron:0 %+Rel:0 %+F:0 %+Sg:0 %+Gen:0		# ??
| {welk} 0:{s}		%+Pron:0 %+Rel:0 %+MN:0 %+Sg:0 %+Gen:0		# ??
| {welke} 0:{r}		%+Pron:0 %+Rel:0 %+MFN:0 %+Pl:0 %+Gen:0		# ??
];

# -----------------------------------------------------------------------------
# interrogative pronouns

read regex [
  {welk}		PRONDET %+Interrog:0 %+N:0 %+Sg:0
| {welk} 0:e		PRONDET %+Interrog:0 %+MF:0 %+Sg:0
| {welke}		PRONDET %+Interrog:0 %+MFN:0 %+Pl:0
| {wat voor}		%+Det:0	%+Interrog:0 %+MFN:0 %+SgPl:0
| {wat voor een}	%+Det:0	%+Interrog:0 %+MFN:0 %+Sg:0	

| {wat}			%+Pron:0 %+Interrog:0 %+N:0 %+Sg:0
| {wie}			%+Pron:0 %+Interrog:0 %+MF:0 %+SgPl:0
| {wie}:{wiens}		%+Pron:0 %+Interrog:0 %+M:0 %+Sg:0 %+Gen:0		# ??
| {wie}:{wier}		%+Pron:0 %+Interrog:0 %+F:0 %+Sg:0 %+Gen:0		# ??
| {wie}:{wier}		%+Pron:0 %+Interrog:0 %+F:0 %+Sg:0 %+Gen:0		# ??
| {wie}:{wier}		%+Pron:0 %+Interrog:0 %+MF:0 %+Pl:0 %+Gen:0		# ??
];



# -----------------------------------------------------------------------------
# Quantifiers

# introducing new feature for -en forms: +NForm

# NOTE: adverbial reading is included in the "basic" reading !
read regex [
  {beide}  		%+Quant:0 		# Ze waren beide licht beschadigd.
| {beide}:{beiden}	%+Quant:0 %+NForm:0	# Ze gaven de kinderen beiden een gulden
| {beide}:{beider}	%+Quant:0 %+Gen:0	# U beider belang wordt niet verwaarloosd

| ({veel en }) {veels}	%+Quant:0 %+Pos:0 %+Basic:0 		# CHECK!
| {veel}		%+Quant:0 %+Pos:0 %+Basic:0
| {veel}:{vele}		%+Quant:0 %+Pos:0 %+Decl:0
| {veel}:{velen}	%+Quant:0 %+Pos:0 %+NForm:0
| {veel}:{veler}	%+Quant:0 %+Pos:0 %+Gen:0

| {veel}:{meer}		%+Quant:0 %+Comp:0 %+Basic:0

| {veel}:{meest}	%+Quant:0 %+Sup:0 %+Basic:0
| {veel}:{meeste}	%+Quant:0 %+Sup:0 %+Decl:0
| {veel}:{meesten}	%+Quant:0 %+Sup:0 %+NForm:0

| {weinig}		%+Quant:0 %+Pos:0 %+Basic:0
| {weinig}:{weinige}	%+Quant:0 %+Pos:0 %+Decl:0
| {weinig}:{weinigen}	%+Quant:0 %+Pos:0 %+NForm:0

| {weinig}:{weiniger}	%+Quant:0 %+Comp:0 %+Basic:0
| {weinig}:{weinigere}	%+Quant:0 %+Comp:0 %+Decl:0	# ?
| {weinig}:{weinigeren}	%+Quant:0 %+Comp:0 %+NForm:0	# ?	

| {weinig}:{weinigst}	%+Quant:0 %+Sup:0 %+Basic:0
| {weinig}:{weinigste}	%+Quant:0 %+Sup:0 %+Decl:0	# ?
| {weinig}:{weinigsten}	%+Quant:0 %+Sup:0 %+NForm:0	# ?	

| {weinig}:{minder}	%+Quant:0 %+Comp:0 %+Basic:0
| {weinig}:{mindere}	%+Quant:0 %+Comp:0 %+Decl:0	
| {weinig}:{minderen}	%+Quant:0 %+Comp:0 %+NForm:0		

| {weinig}:{minst}	%+Quant:0 %+Sup:0 %+Basic:0
| {weinig}:{minste}	%+Quant:0 %+Sup:0 %+Decl:0	
| {weinig}:{minsten}	%+Quant:0 %+Sup:0 %+NForm:0		


| {hoeveel} 		%+Quant:0 %+Basic:0	# Hoeveel ben ik u verschuldigd?
| {hoeveel}:{hoevele}  	%+Quant:0 %+Decl:0	# Hoevele schakeringen!
| {hoeveel}:{hoevelen} 	%+Quant:0 %+NForm:0	# Hoevelen bemerken dit, hoevelen weten dit, ...

| {zoveel} 		%+Quant:0 %+Basic:0	#
| {zoveel}:{zovele}  	%+Quant:0 %+Decl:0	#
| {zoveel}:{zovelen} 	%+Quant:0 %+NForm:0	#


| {al} 			%+Quant:0		# %+Det:0 %+Indef:0
| {alle}		%+Quant:0		# PRONDET %+Indef:0 
| {alle} 0:n		%+Quant:0 %+NForm:0	# %+Pron:0 %+Indef:0
| {alle} 0:r		%+Quant:0 %+Gen:0	# %+Pron:0 %+Indef:0 %+Gen:0
| {alle} 0:s		%+Quant:0		# PRONDET %+Indef:0

#| {alle}:{álle}	PRONDET %+Indef:0 %+Emph:0
#| {alle}:{álles}	PRONDET %+Indef:0 %+Emph:0


| {een of ander}	%+Quant:0 
| {een paar}		%+Quant:0		# PRONDET %+Indef:0	!! ambiguity  with paar = pair (of two)

| {eenieder}		%+Quant:0		# %+Pron:0 %+Indef:0
| {eenieder} 0:s	%+Quant:0 %+Gen:0	# %+Pron:0 %+Indef:0 %+Gen:0

| {elk}			%+Quant:0 %+Basic:0	# PRONDET %+Indef:0
| {elk} 0:e		%+Quant:0 %+Decl:0	# %+Det:0 %+Indef:0 
| {elk} 0:{een}	 	%+Quant:0		# %+Pron:0 %+Indef:0

| {enig}		%+Quant:0 %+Basic:0	# PRONDET %+Indef:0	
| {enig} 0:e		%+Quant:0 %+Decl:0	# PRONDET %+Indef:0	
| {enig} 0:{en}		%+Quant:0 %+NForm:0	# %+Pron:0 %+Indef:0	

| {enkele}		%+Quant:0		# PRONDET %+Indef:0
| {enkele} 0:n		%+Quant:0 %+NForm:0	# %+Pron:0 %+Indef:0
| {ettelijke}		%+Quant:0		# PRONDET %+Indef:0
| {genoeg} 		%+Quant:0		# PRONDET %+Indef:0

| {ieder}		%+Quant:0		# PRONDET %+Indef:0
| {ieder} 0:e		%+Quant:0		# %+Det:0 %+Indef:0 
| {ieder} 0:s		%+Quant:0 %+Gen:0	# %+Pron:0 %+Indef:0 %+Gen:0
| {ieder} 0:{een}	%+Quant:0		# %+Pron:0 %+Indef:0

| {menig} 	 	%+Quant:0		# PRONDET %+Indef:0
| {menig} 0:e	 	%+Quant:0		# PRONDET %+Indef:0
| {menig} 0:{een} 	%+Quant:0		# %+Pron:0 %+Indef:0

| {sommig}		%+Quant:0 %+Basic:0	# PRONDET %+Indef:0
| {sommig} 0:e		%+Quant:0 %+Decl:0	# PRONDET %+Indef:0
| {sommig} 0:{en}	%+Quant:0 %+NForm:0	# %+Pron:0 %+Indef:0

| {verscheidene}	%+Quant:0 %+Decl:0	# PRONDET %+Indef:0
| {verschillende}	%+Quant:0 %+Decl:0	# PRONDET %+Indef:0
| {voldoende} 		%+Quant:0 %+Decl:0	# PRONDET %+Indef:0

| {wat}			%+Quant:0		# PRONDET %+Indef:0
| {zat} 		%+Quant:0		# PRONDET %+Indef:0

];


echo >>> union net

union net




exit

## TO DO:

het	het	+Art+Def+N+Sg



