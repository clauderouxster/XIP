DependencyRules:
/
| #1[lemme:PNUMBER] |
  DATE(#1).

| #1[lemme:ENTTY,financialentity=+] |
  ORGANISATION(#1).
\

//indefinite service providers are filtered

| #3[service_provider=~,rolesec=~] |
if ( $1(#1[service_provider],#2[lemme:such]) & COORD(?,#3) & COORD(?,#1) )
   ~.

| #1[service_provider=~,rolesec=~] |
if ( $1(#1[service_provider],#2[lemme:such]) )
   ~.

| #1[service_provider=~,rolesec=~] |
if ( $1(#1[service_provider],#2[lemme:other]) )
   ~.

| #1[service_provider=~,rolesec=~] |
if ( DETD[indef](#1[service_provider],#2) )
   ~.


/////////////////////// Highly relevant \\\\\\\\\\\\\\\\\\\

| #1[date] |
if ( ~DATE(#1) )
   DATE(#1).

if ( $1(#1[lemme:have],#2) & $2[perfect](#1,#4[hr-ind]) & ^SENT(#3) )
   SENT[high-rel=+](#3).

if ( $1(#1[lemme:have],#2) & $2[perfect](#1,#4[mdachange]) & ^SENT(#3) )
   SENT[high-rel=+](#3).

////////////// reldep4hr //////////////

| #1[relword4hr=+] |
if ( SENT[high-rel](#3) & ^$1(#1[lemme:have],#2) & $2[perfect](#1,#4[hr-ind]) )
   $1[reldep4hr=+](#1,#2).

| #4[relword4hr=+] |
if ( SENT[high-rel](#3) & $1(#1[lemme:have],#2) & ^$2[perfect](#1,#4[hr-ind]) )
   $2[reldep4hr=+](#1,#4).

//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

if ( $1(#1[lemme:have],#2[lemme:we]) & $2[perfect](#1,#4) & ^SENT(#3) )
   SENT[high-rel=+](#3).

///////////// reldep4hr //////////////

if ( SENT[high-rel](#3) & ^$1(#1[lemme:have],#2[lemme:we]) & $2[perfect](#1,#4) )
   $1[reldep4hr=+](#1,#2).

if ( SENT[high-rel](#3) & $1(#1[lemme:have],#2[lemme:we]) & ^$2[perfect](#1,#4) )
   $2[reldep4hr=+](#1,#4).

//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\


if ( ^SENT[allege](#1) )
   SENT[high-rel=+](#1)

if ( ^SENT[violate](#1) )
   SENT[high-rel=+](#1)

//NUM{.4 billion}

| num#1{?=, #2[lemme:billion]} |
if ( ~MONEY(#1) )
   MONEY(#1).

| num#1{?=, #2[lemme:million]} |
if ( ~MONEY(#1) )
   MONEY(#1).



| #2[hr-ind] |
if ( ^SENT(#1) & MONEY(#3) )
   SENT[high-rel=+](#1).


////////////// reldep4hr //////////////

| #2[hr-ind,relword4hr=+] |
if ( SENT[high-rel](#1) & ^MONEY(#3) )
   MONEY[reldep4hr=+](#3)



//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\


| #2[hr-ind] |
if ( ^SENT(#1) & $1(#3[lemme:\$],#4) )
   SENT[high-rel=+](#1).


///////////// reldep4hr //////////////

| #2[hr-ind,relword4hr=+] |
if ( SENT[high-rel](#1) & ^$1(#3[lemme:\$],#4) )
   $1[reldep4hr=+](#3,#4)

//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\


if ( ^SENT(#1) & POSITION(#3) & MONEY(#4) )
   SENT[high-rel=+](#1).


///////////// reldep4hr //////////////


if ( SENT[high-rel](#1) & ^POSITION(#3) & MONEY(#4) )
   POSITION[reldep4hr=+](#3)

if ( SENT[high-rel](#1) & POSITION(#3) & ^MONEY(#4) )
   MONEY[reldep4hr=+](#4)



//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\


| #1[lemme:litigation] |
if ( ^SENT(#2) )
   SENT[high-rel=+](#2).


///////////// reldep4hr //////////////


| #1[lemme:litigation,relword4hr=+] |
if ( SENT[high-rel](#2) )
   ~.


//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

if ( ^SENT(#2) & CHANGE(#1) )
   SENT[high-rel=+](#2).

///////////// reldep4hr //////////////


if ( SENT[high-rel](#2) & ^CHANGE(#1) )
   CHANGE[reldep4hr=+](#1)


//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\





/////////////////////// Relevant \\\\\\\\\\\\\\\\\\\

//This resulted in a one notch upgrade to the issuer ratings, senior debt ratings and long-term deposit ratings of JPMorgan Chase Bank, N.A., and certain other subsidiaries.
//PREPD(JPMorgan Chase Bank, N.A._FINANCIALENTITY:+,of)
/
| #4[rolesec] |
if ( PREPD(#1[financialentity],#2[lemme:of]) & ^SENT(#3) )
   SENT[relev=+](#3).
\

//ASL(as,issuer_SERVICE_PROVIDER:+)

if ( ORGANISATION[financialentity](#1) & ASL(#2,#3[rolesec]) & ^SENT(#4) & MONEY(#5) )
   SENT[relev=+,1=+](#4).

if ( ORGANISATION[financialentity](#1) & ASL(#2,#3[rolesec]) & ^SENT(#4) & DATE(#5) )
   SENT[relev=+,1=+](#4).

///////////// reldep4rel //////////////

if ( ORGANISATION[financialentity](#1) & ^ASL(#2,#3[rolesec]) & SENT[relev](#4) )
   ASL[reldep4rel=+](#2,#3).
   
if ( ^ORGANISATION[financialentity](#1) & ASL(#2,#3[rolesec]) & SENT[relev](#4) )
   ORGANISATION[reldep4rel=+](#1).
   
//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\


| #3[service_provider] |
if ( ORGANISATION(#1) & ^SENT[high-rel:~](#2) & ( SUBJ(?,?) | SUBJ-N(?,?) ) & MONEY(?) )
   SENT[relev=+,2=+](#2).

///////////// reldep4rel //////////////

| #6[service_provider,relword4rel=+] |
if ( ORGANISATION(#1) & SENT[relev](#2) & ^SUBJ(#3,#4) & MONEY(#5) )
   SUBJ[reldep4rel=+](#3,#4).

| #6[service_provider,relword4rel=+] |
if ( ORGANISATION(#1) & SENT[relev](#2) & ^SUBJ-N(#3,#4) & MONEY(#5) )
   SUBJ-N[reldep4rel=+](#3,#4).

| #3[service_provider] |
if ( ORGANISATION(#1) & SENT[relev](#2) & ( SUBJ(?,?) | SUBJ-N(?,?) ) & ^MONEY(#4) )
   MONEY[reldep4rel=+](#4).

| #3[service_provider] |
if ( ^ORGANISATION(#1) & SENT[relev](#2) & ( SUBJ(?,?) | SUBJ-N(?,?) ) & MONEY(#4) )
   ORGANISATION[reldep4rel=+](#1).

//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\


| #3[service_provider] |
if ( ORGANISATION(#1) & ^SENT[high-rel:~](#2) & ( SUBJ(?,?) | SUBJ-N(?,?) ) & DATE(?) )
   SENT[relev=+,3=+](#2).


///////////// reldep4rel //////////////

| #6[service_provider,relword4rel=+] |
if ( ORGANISATION(#1) & SENT[relev](#2) & ^SUBJ(#3,#4) & DATE(#5) )
   SUBJ[reldep4rel=+](#3,#4).

| #6[service_provider,relword4rel=+] |
if ( ORGANISATION(#1) & SENT[relev](#2) & ^SUBJ-N(#3,#4) & DATE(#5) )
   SUBJ-N[reldep4rel=+](#3,#4).

| #3[service_provider] |
if ( ORGANISATION(#1) & SENT[relev](#2) & ( SUBJ(?,?) | SUBJ-N(?,?) ) & ^DATE(#4) )
   DATE[reldep4rel=+](#4).

| #3[service_provider] |
if ( ^ORGANISATION(#1) & SENT[relev](#2) & ( SUBJ(?,?) | SUBJ-N(?,?) ) & DATE(#4) )
   ORGANISATION[reldep4rel=+](#1).

//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\


| #3[service_provider] |
if ( ORGANISATION(#1) & ^SENT[high-rel:~](#2) & ( SUBJ(?,?) | SUBJ-N(?,?) ) & PERCENT(?) )
   SENT[relev=+,4=+](#2).


///////////// reldep4rel //////////////

| #6[service_provider,relword4rel=+] |
if ( ORGANISATION(#1) & SENT[relev](#2) & ^SUBJ(#3,#4) & PERCENT(#5) )
   SUBJ[reldep4rel=+](#3,#4).

| #6[service_provider,relword4rel=+] |
if ( ORGANISATION(#1) & SENT[relev](#2) & ^SUBJ-N(#3,#4) & PERCENT(#5) )
   SUBJ-N[reldep4rel=+](#3,#4).

| #3[service_provider] |
if ( ORGANISATION(#1) & SENT[relev](#2) & ( SUBJ(?,?) | SUBJ-N(?,?) ) & ^PERCENT(#4) )
   PERCENT[reldep4rel=+](#4).

| #3[service_provider] |
if ( ^ORGANISATION(#1) & SENT[relev](#2) & ( SUBJ(?,?) | SUBJ-N(?,?) ) & PERCENT(#4) )
   ORGANISATION[reldep4rel=+](#1).

//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\


| #3[rel-ind] |
if ( DATE(#1) & ^SENT[high-rel:~](#2) )
   SENT[relev=+,5=+](#2).

///////////// reldep4rel //////////////

| #3[rel-ind,relword4rel=+] |
if ( ^DATE(#1) & SENT[relev](#2) )
   DATE[reldep4rel=+](#1).


//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\



| #3[rel-ind,hr-ind:~] |
if ( ^SENT[high-rel:~](#2) )
   SENT[relev=+,6=+](#2).


///////////// reldep4rel //////////////

| #3[rel-ind,hr-ind:~,relword4rel=+] |
if ( SENT[relev](#2) )
   ~.
   
//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\



//| #3[service_provider] |
if ( MONEY(#1) & ^SENT[high-rel:~](#2) )
   SENT[relev=+,7=+](#2).


///////////// reldep4rel //////////////

if ( ^MONEY(#1) & SENT[relev](#2) )
   MONEY[reldep4rel=+](#1).


//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\


if ( $1(#1[service_provider],#2) & ^SENT[high-rel:~](#3) & ORGANISATION(#2) )
   SENT[relev=+,8=+](#3).


///////////// reldep4rel //////////////

if ( ^$1(#1[service_provider],#2) & SENT[relev](#3) & ORGANISATION(#2) )
   $1[reldep4rel=+](#1,#2).

if ( $1(#1[service_provider],#2) & SENT[relev](#3) & ^ORGANISATION(#2) )
   ORGANISATION[reldep4rel=+](#2).


//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\


if ( $1(#1,#2[service_provider]) & ^SENT[high-rel:~](#3) & ORGANISATION(#1) )
   SENT[relev=+,9=+](#3).

///////////// reldep4rel //////////////

if ( ^$1(#1,#2[service_provider]) & SENT[relev](#3) & ORGANISATION(#1) )
   $1[reldep4rel=+](#1,#2).

if ( $1(#1,#2[service_provider]) & SENT[relev](#3) & ^ORGANISATION(#1) )
   ORGANISATION[reldep4rel=+](#1).


//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\




//As of December 31, 2013 , the Guarantors include Ally US LLC and IB Finance Holding Company, LLC (IB Finance), each of which fully and unconditionally guarantee the senior notes on a joint and several basis.
//SUBJ-N_PRE(include,Guarantors_SERVICE_PROVIDER:+)
//OBJ-N(include,Ally US LLC and IB Finance Holding Company , LLC)
//ORGANISATION_SEC-SP_INSENT(Ally US LLC and IB Finance Holding Company , LLC)


if ( SUBJ-N(#1,#2[service_provider]) & OBJ-N(#1,#3) & ORGANISATION(#3) & ^SENT[high-rel:~](#4) )
   SENT[relev=+,10=+](#4).

///////////// reldep4rel //////////////

if ( ^SUBJ-N(#1,#2[service_provider]) & OBJ-N(#1,#3) & ORGANISATION(#3) & SENT[relev](#4) )
   SUBJ-N[reldep4rel=+](#1,#2).


if ( SUBJ-N(#1,#2[service_provider]) & ^OBJ-N(#1,#3) & ORGANISATION(#3) & SENT[relev](#4) )
   OBJ-N[reldep4rel=+](#1,#3).

if ( SUBJ-N(#1,#2[service_provider]) & OBJ-N(#1,#3) & ^ORGANISATION(#3) & SENT[relev](#4) )
   ORGANISATION[reldep4rel=+](#3).

//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\



//Securities and Commodities Regulation Citi conducts securities underwriting, brokerage and dealing activities in the U.S. through Citigroup Global Markets Inc. (CGMI), its primary broker-dealer, and other broker-dealer subsidiaries, which are subject to regulations of the U.S. Securities and Exchange Commission (SEC), the Financial Industry Regulatory Authority and certain exchanges.
//SUBJ-N_PRE(conducts,Citi)
//OBJ-N(conducts,underwriting_SERVICE_PROVIDER:+)

if ( OBJ-N(#1,#2[service_provider]) & ^SENT[high-rel:~](#4) )
   SENT[relev=+,11=+](#4).

///////////// reldep4rel //////////////


if ( ^OBJ-N(#1,#2[service_provider]) & SENT[relev](#4) )
   OBJ-N[reldep4rel=+](#1,#2).

//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

/////////////////////// Neutral \\\\\\\\\\\\\\\\\\\




/////////////////////// Irrelevant \\\\\\\\\\\\\\\\\\\


| #1[lemme:see] |
if ( ^SENT(#2) )
   SENT[high-rel=~,relev=~,irrel=+](#2).


if ( NUCL[subjcompl](#1,#2[service_provider:~,c_person:~]) & ^SENT[high-rel:~,relev:~,neutr:~](#3) )
   SENT[irrel=+](#3).

if ( SUBJ-N(#1,#2) | SUBJ(#1,#2) )
   LIXO(#1,#2)

if ( MAIN(#1) )
   LIXO(#1,#1)

if ( ~LIXO(?,?) & ^SENT[high-rel:~,relev:~,neutr:~](#3) )
   SENT[irrel=+](#3).

if ( ^LIXO(#1,#2) )
   ~.

///////////// reldep4rel //////////////


if ( SENT[irrel](#3) & ^NUCL[subjcompl](#1,#2[service_provider:~,c_person:~]) )
   NUCL[reldep4irrel=+](#1,#2).


//\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\



if ( ^HEAD(?,?) )
   ~.

///////////

// filter file names:

if ( MOD(#1[lemme:name],#2[lemme:file]) & ^SENT(#3) )
   SENT[irrel=~,relev=~,high-rel=~,proba=~,neutr=~](#3).

//////////////

| #2[rolesec] |
if ( ORGANISATION[financialentity](#1) & ^$1(#1,#2) )
   $1[rolesec=+](#1,#2).


| #2[rolesec] |
if ( ORGANISATION[financialentity](#1) & ^$1(#2,#1) )
   $1[rolesec=+](#2,#1).

if ( ^SENT(#1) & $1[rolesec](#2,#3) )
   SENT[rolesec=+](#1).


//


//sec features:
//$FINANCE_GRAM/features_md.xip,
//$FINANCE_GRAM/features_10K.xip,
//$FINANCE_GRAM/features_aaer.xip,
//$FINANCE_GRAM/features_fei.xip,
//$FINANCE_GRAM/features_change.xip,
//$FINANCE_GRAM/features_mda.xip,
//$FINANCE_GRAM/features_finance.xip,


| #1[info,secfeature=+] |
	~.


| #1[deict,secfeature=+] |
	~.


| #1[indicate,secfeature=+] |
	~.


| #1[md-dep,secfeature=+] |
	~.


| #1[allege,secfeature=+] |
	~.


| #1[violate,secfeature=+] |
	~.


| #1[fraud,secfeature=+] |
	~.


| #1[fraudadv,secfeature=+] |
	~.


| #1[fail,secfeature=+] |
	~.


| #1[legal-act,secfeature=+] |
	~.


| #1[aaer-dep,secfeature=+] |
	~.


| #1[failword,secfeature=+] |
	~.


| #1[violationword,secfeature=+] |
	~.


| #1[inventory,secfeature=+] |
	~.


| #1[revenue,secfeature=+] |
	~.


| #1[prop-equ,secfeature=+] |
	~.


| #1[cash-ms,secfeature=+] |
	~.


| #1[rel-ind,secfeature=+] |
	~.


| #1[hr-ind,secfeature=+] |
	~.


| #1[change,secfeature=+] |
	~.


| #1[explanation,secfeature=+] |
	~.


| #1[mdachange,secfeature=+] |
	~.


| #1[fact,secfeature=+] |
	~.


| #1[forecast,secfeature=+] |
	~.


| #1[opinion,secfeature=+] |
	~.


| #1[pres-data-verb,secfeature=+] |
	~.

| #1[rel-ind,secfeature=+] |
	~.

| #1[hr-ind,secfeature=+] |
	~.



