DependencyRules:

//noconcept


//NUCL_SUBJCOMPL(are,low)

| #2[noconcept=+] |
if ( NUCL[subjcompl](#1,#2) )
   ~.

| #1[adj,partpas,last,noconcept=+] |
  ~.

//NP{a AP{wide} variety}} PP{of 
//MOD_POST(variety,purposes_MD:+)
//PREPD(purposes_MD:+,of)

| #1[noconcept=+] |
if ( MOD[post](#1[lemme:variety],#2) & PREPD(#2,#3[lemme:of]) )
   ~.

| #1[fam,proper,noconcept=+] |
  ~.

| #1[particlename,proper,noconcept=+] |
  ~.

| #1[let,noconcept=+] |
  ~.

| #1[transparent,noconcept=+] |
  ~.

| #1[initial,noconcept=+] |
  ~.

//NP{Tinto} , NP{1998}

| NP{#1[maj,noconcept=+]}, #2[comma], NP{#3} |
if ( DATE(#3) )
   ~.

//NP{Macfadyen} & NP{Dawson}

| NP{#1[maj,noconcept=+]}, #2[conj], NP{#3[noconcept=+]} |
  ~.

| #1[noconcept=+] |
if ( PREPD(#1[lemme:press],#2[lemme:in]) )
   ~.

| NP#1{#2[first,last,maj,noconcept=+]} |
  ~.

| ?[noconcept=+]{#1[first,last,noconcept]} |
  ~.

// using one tag for num, dig, quant

| ?[num,!enumer=+] |
  ~.

| ?[quant,!enumer=+] |
  ~.

| ?[dig,!enumer=+] |
  ~.





//NP{some}

| np{#1[quant,noconcept=+]} |
  ~.

//PP{in NP{order}} IV{to better integrate}

| PP{#1[lemme:in], NP{#2[noconcept=+,lemme:order]}}, IV{#3[lemme:to], ?+} |
  ~.

//NP{this AP{ADJ_ENUMER:+{3_ENUMER:+ - year}} research phase}}

| adj#1[enumer,!noconcept=+]{#2[first,enumer,!noconcept=+],?+} |
  ~. 

//

//| np#1{?*,#2[last,ksw:~,rf:~,dig:~,num:~]} |
//if ( ~CONCEPT(#1) & ( $1(#2,#3[ksw]) | $1(#3[ksw],#2) ) )
//  CONCEPT(#1).

| np#1{?*,#2[last,ksw:~,rf:~,enumer:~,tempexpr:~,noconcept:~]} |
if ( ~CONCEPT(#1) )
  CONCEPT(#1).


| np#1{?,#2[last,ksw,maj]} |
if ( ~CONCEPT(#1) )
  CONCEPT(#1).

//| noun#1 |
//if ( ~CONCEPT(#1) )
//  CONCEPT(#1).

//NP{a questionnaire survey}

| np#1{?[det],#2[last,ksw:~,rf:~]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#2).

| np#1{?[det],#2,#3[last]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#2,#3).

if ( ^CONCEPT(#1[ksw,rf],#2[ksw,rf]) )
   ~.

| np#1{?[det],#2,#3,#4[last]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#2,#3,#4).

if ( ^CONCEPT(#1[ksw,rf],#2[ksw,rf],#3[ksw,rf]) )
   ~.

| np#1{?[det],#2,#3,#4,#5[last]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#2,#3,#4,#5).



| np#1{?[det],#2[last]} |
if ( ^CONCEPT(#1) )
   ~.

| np#1{?[enumer],#2[last]} |
if ( ^CONCEPT(#1) )
   ~.

//CONCEPT_IMPSENT_VIS(less effective schools)
//NP{less AP{effective} schools}

| NP#1{#2[enumer], #3, #4} |
if ( ^CONCEPT(#1) )
   CONCEPT(#3,#4).

| np#1{?[pron],#2[last]} |
if ( ^CONCEPT(#1) )
   ~.

| np#1{#2[pron,last]} |
if ( ^CONCEPT(#1) )
   ~.

| np#1{#2[enumer,last]} |
if ( ^CONCEPT(#1) )
   ~.

| np#1{#2[enumer,last]} |
if ( ^CONCEPT(#1) )
   ~.

//NP{a AP{major} AP{current} public policy agenda}

| #1np{?[det],ap+,#2,#3,#4[last]} |
if ( ^CONCEPT(#1) & ( $1(#5[ksw],#4) | $1(#4,#5[ksw]) ) )
   CONCEPT(#2,#3,#4).

//AP{generally positive} response}

| #1{#2adv,#3}, #4 |
if ( MOD(#4,#3) & ^CONCEPT(#1,#4) & ( $1(#5[ksw],#4) | $1(#4,#5[ksw]) ) )
   CONCEPT(#3,#4).

//NP{AP{such} AP{available} datasets}
//such available datasets

| NP#1{AP{#2[lemme:such]}, (?), #3[last]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#3).

//CONCEPT_IMPSENT_CONTRIBUTION_VIS(such a highly decentralized institution)
//NP{AP{such} a AP{highly decentralized} institution}

| NP#1{AP{#2[lemme:such]}, det, #3, #4} |
if ( ^CONCEPT(#1) )
   CONCEPT(#3,#4).


//other entities
//NP{AP{other} entities}

| NP#1{AP{#2[lemme:other]}, (?), #3[last]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#3).

//CONCEPT_IMPSENT_CONTRIBUTION_VIS(numerous Faculties)
//NP{AP{numerous} Faculties}

| NP#1{AP{#2[lemme:numerous]}, #3} |
if ( ^CONCEPT(#1) )
   CONCEPT(#3).


//CONCEPT_IMPSENT_VIS(an Institutional Strategic Plan)
//NP{an AP{Institutional} AP{Strategic} Plan}}

| NP#1{#2[det], AP#3, AP#5, #4[last]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#3,#5,#4).


//CONCEPT_IMPSENT_VIS(a working group)
//NP{a AP{working} group}

| NP#1{#2[det], AP#3, #4[last]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#3,#4).

//CONCEPT_IMPSENT_CONTRIBUTION(more comprehensive,overview)
//NP{a AP{more AP{comprehensive}} overview}

| AP#1{#2[enumer], ?} |
if ( ^CONCEPT(#1,#3) )
   CONCEPT(#3).

//NP{4,661 students}
//CONCEPT_IMPSENT_VIS(4,661 students)

| #1np{#2[enumer], #3[last]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#3).

//CONCEPT_IMPSENT_VIS(at least one fully online course)
//NP{AP{at least one} AP{fully online} course}

| NP#1{AP{#2[lemme:at_least_one]}, #3,#4 } |
if ( ^CONCEPT(#1) )
   CONCEPT(#3,#4).

//35 doctoral degree programs
//NP{35 AP{doctoral} degree programs}
//CONCEPT_IMPSENT_CONTRIBUTION(35 doctoral degree programs)

| NP#1{?[enumer], AP{#2}, #3, #4[last]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#2,#3,#4).

//CONCEPT_IMPSENT_CONTRIBUTION(60 master 's degree programs)
//NP{6,000 graduate students}

| NP#1{?[enumer], #2, #3[last]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#2,#3).

//CONCEPT_IMPSENT_CONTRIBUTION(60 master 's degree programs)
//NP{60 master 's degree programs}

| NP#1{?[enumer], #2, #3, #4, #5[last]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#2,#3,#4,#5).

//CONCEPT_IMPSENT_CONTRIBUTION(more than 70 bachelor 's degree programs)
//NP{NUMP{ADV{more than} 70} bachelor 's degree programs}

| NP#1{nump, #2, #3, #4, #5[last]} |
if ( ^CONCEPT(#1) )
   CONCEPT(#2,#3,#4,#5).


//NP{AP{lesser[compar]} than Student E 's T} 
//CONCEPT_IMPSENT_CONTRIBUTION_VIS(lesser than Student E 's T)

| NP#1{AP{#2[compar]}, ?+} |
if ( ^CONCEPT(#1) )
   ~.

//CONCEPT_IMPSENT_CONTRIBUTION_VIS(more,active,participant)

if ( ^CONCEPT(#1[enumer],#2,#3) )
   ~.

//CONCEPT_IMPSENT_CONTRIBUTION_VIS(Student D' s number)
//NP{Student D' s number}

| NP#1{?+, #2[particlename,proper], ?+} |
if ( ^CONCEPT(#1) )
   ~.

////////////////////////////////
// refinements
//////////////////////////////

//add SUBCAT
//CONCEPT(ARG1,1,PRED,is,ARG2,of the major factors)

| pp#9{?,?{?*,#8[last]}} |
if ( ^CONCEPT(#6) &  HEAD(#7,#6) MOD[subcat](#7,#8) & HEAD(#8,#9) & ( $1(#10[ksw],#8) | $1(#8,#10[ksw]) ) )
   CONCEPT(#6,#9).

| pp#9{?,#10{?*,#8[last]}} |
if ( CONCEPT(#6,#9) &  HEAD(#7,#6) MOD[subcat](#7,#8) & HEAD(#8,#9) & ^CONCEPT(#10) & ( $1(#11[ksw],#8) | $1(#8,#11[ksw]) ) )
   ~.

| pp#8 |
if ( ^CONCEPT(#6) & MOD[subcat](#6,#7) & HEAD(#7,#8) )
   CONCEPT(#6,#8).

//PP#1{to NP{#2life}}
//MOD_SUBCAT_POST(#4right,#3life) MOD_SUBCAT_POST(focuses,children)
//CONCEPT(#2life)
//HEAD(#3life,#1to life)
//HEAD(#3,#2)

| pp#1 |
if ( ^CONCEPT(#2) & MOD[subcat](#4[noun],#3) & HEAD(#3,#1) & HEAD(#3,#2) )
   CONCEPT(#4,#1).

//in GV{protecting} NP{AP{prenatal} life} 
//CONCEPT(prenatal life)

| prep, GV{#1}, #2NP |
if ( ^CONCEPT(#2) & OBJ(#1,#3) & HEAD(#3,#2) )
   CONCEPT(#1,#2).


// Final cleanup



  
// CONCEPT(3_ENUMER:+_NOCONCEPT:+ - year,research,phase)
//AP{ADJ{3 - year}} 

| #1{#4[enumer]} |
if ( ^CONCEPT(#1,#2,#3) )
   CONCEPT(#2,#3).

//CONCEPT(first,priority)

if ( ^CONCEPT(#1[enumer],#2) )
   CONCEPT(#2).

if ( ^CONCEPT(#1[tempexpr],#2) )
   CONCEPT(#2).

//CONCEPT(2008,study,Reliability,statistics)

if ( ^CONCEPT(#1[enumer],#2,#3,#4) )
   CONCEPT(#2,#3,#4).



/////////////////////

/////////////

| #1[conceptword=+] |
if ( CONCEPT(#1) )
    ~.

| #1[conceptword=+] |
if ( CONCEPT(#1,#2) )
    ~.

| #2[conceptword=+] |
if ( CONCEPT(#1,#2) )
    ~.

| #1[conceptword=+] |
if ( CONCEPT(#1,#2,#3) )
    ~.

| #2[conceptword=+] |
if ( CONCEPT(#1,#2,#3) )
    ~.

| #3[conceptword=+] |
if ( CONCEPT(#1,#2,#3) )
    ~.

| #1[conceptword=+] |
if ( CONCEPT(#1,#2,#3,#4) )
    ~.

| #2[conceptword=+] |
if ( CONCEPT(#1,#2,#3,#4) )
    ~.

| #3[conceptword=+] |
if ( CONCEPT(#1,#2,#3,#4) )
    ~.

| #4[conceptword=+] |
if ( CONCEPT(#1,#2,#3,#4) )
    ~.


