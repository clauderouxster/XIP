
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//! Xerox Research Centre Europe - Grenoble Laboratory
//!
//! Copyright (c) 2006 Xerox Corporation.  All Rights Reserved.
//!
//! --- CONTENTS ---
//!   Project    : English XIP grammar for EERQI
//!   filename   : dependency_eerqi.xip
//!   Date       : August 2008
//!   Author     : Agnes Sandor
//!  
//!
//!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

DependencyRules:


//////////////////////////////////////////////////////////////////////////////
/////								    //////////
///// 		       ***** PRESUPPOSITION ******		    /////////
/////						                   //////////
//////////////////////////////////////////////////////////////////////////////

if ( ^PREPD(#1,#2) & PREPD(#3,#2) & #2 < #3 & #3 < #1 )
   ~.


if ( MOD(#3[noun,last],#1[noun,last]) & ~PRESUPPOSITION(#3,#1) & PREPD(#1,?[lemme:~without]) & #3 < #1  )
   PRESUPPOSITION(#3,#1).


//////////////////////////////////////////////////////////////////////////////
/////								    //////////
/////             ****** typing presuppositions *****               //////////
/////						                   //////////
//////////////////////////////////////////////////////////////////////////////

if ( ^PRESUPPOSITION(#3[contr],#1) | ^PRESUPPOSITION(#3,#1[contr]) )
   PRESUPPOSITION[problem=+](#3,#1).

if ( ^PRESUPPOSITION(#3[mentsco],#1) | ^PRESUPPOSITION(#3,#1[mentsco]) )
   PRESUPPOSITION[conceptual=+](#3,#1).

if ( ^PRESUPPOSITION(#3[importance],#1) | ^PRESUPPOSITION(#3,#1[importance]) )
   PRESUPPOSITION[subjectivity=+](#3,#1).

if ( ^PRESUPPOSITION(#3[attitude],#1) | ^PRESUPPOSITION(#3,#1[attitude]) )
   PRESUPPOSITION[subjectivity=+](#3,#1).

if ( ^PRESUPPOSITION(#3[education],#1) | ^PRESUPPOSITION(#3,#1[education]) )
   PRESUPPOSITION[education=+](#3,#1).



if ( ^PRESUPPOSITION(#3,#1) & ( DETD(#3,?[lemme:these]) | DETD(#1,?[lemme:these]) | DETD(#3,?[lemme:those]) | DETD(#1,?[lemme:those]) | DETD(#3,?[pron]) | DETD(#1,?[pron]) | DETD(#3,?[deict]) | DETD(#1,?[deict]) | $1(#3,?[lemme:any]) | $1(#1,?[lemme:any]) | $1(#3,?[lemme:before]) | $1(#1,?[lemme:before]) | $1(#3,?[lemme:after]) | $1(#1,?[lemme:after]) | $1(#3,?[lemme:that]) | $1(#1,?[lemme:that]) ) )
   ~.

if ( ^PRESUPPOSITION(#3[lemme:kind],#1) | ^PRESUPPOSITION(#3,#1[lemme:kind]) )
   ~.

//////////////////////////////////////////////////////////////////////////////
/////								    //////////
/////           ****** extension to chunks *****                  ////////////
/////						                   //////////
//////////////////////////////////////////////////////////////////////////////


| pp{?*,np#3{?*,#1}} |
if ( ^PRESUPPOSITION(#1,#2) & HEAD(#1,#3) & ~PRESUPPOSITION[extension](#3,#2) )
   PRESUPPOSITION[extension=+](#3,#2).
  
| pp[attitude]{?*,np#3{?*,#1}} |
if ( ^PRESUPPOSITION(#3,#2) ) 
   PRESUPPOSITION[subjectivity=+](#3,#2).
  
| pp[importance]{?*,np#3{?*,#1}} |
if ( ^PRESUPPOSITION(#3,#2) ) 
   PRESUPPOSITION[subjectivity=+](#3,#2).
  
| np#3{?*,#1} |
if ( ^PRESUPPOSITION(#1,#2) & HEAD(#1,#3) & ~PRESUPPOSITION[extension](#3,#2) )
   PRESUPPOSITION[extension=+](#3,#2).

| np#3[attitude]{?*,#1} |
if ( ^PRESUPPOSITION(#3,#2) )
   PRESUPPOSITION[subjectivity=+](#3,#2).
   
| np#3[importance]{?*,#1} |
if ( ^PRESUPPOSITION(#3,#2) )
   PRESUPPOSITION[subjectivity=+](#3,#2).
   
| pp#3{?*,np{?}} |
if ( ^PRESUPPOSITION(#1,#2) & HEAD(#2,#3) & ~PRESUPPOSITION[extension](#1,#3) )
   PRESUPPOSITION[extension=+](#1,#3).

| pp#3[attitude]{?*,np{?}} |
if ( ^PRESUPPOSITION(#1,#3) )
   PRESUPPOSITION[subjectivity=+](#1,#3).
   
| pp#3[importance]{?*,np{?}} |
if ( ^PRESUPPOSITION(#1,#3) )
   PRESUPPOSITION[subjectivity=+](#1,#3).
   
  
//PRESUPPOSITION_EXTENSION(#1serious debate,#2about the nature)
//PRESUPPOSITION_EXTENSION(#3the nature,#4of democracy)

| pp#2{?*,np#3{?+}} |
if ( ^PRESUPPOSITION(#1,#2) & PRESUPPOSITION(#3,#4) )
   PRESUPPOSITION(#1,#2,#4).

if ( PRESUPPOSITION(#1,#2,#4) & ^PRESUPPOSITION(#3,#4) )
   ~.

| pp#2[attitude]{?*,np#3{?+}} |
if ( ^PRESUPPOSITION(#1,#2,#4) )
   PRESUPPOSITION[subjectivity=+](#1,#2,#4).

| pp#2[importance]{?*,np#3{?+}} |
if ( ^PRESUPPOSITION(#1,#2,#4) )
   PRESUPPOSITION[subjectivity=+](#1,#2,#4).

//But, all the more surprising given Pring's championing of Dewey's work, there is no systematic engagement with the interface between democratic theory and the common school.
//PP{between NP{AP{democratic} theory}} and NP{the AP{common} school} 
//PRESUPPOSITION_EXTENSION(#1the interface#2,between democratic theory)

| pp#2{?[lemme:between],?+}, #3[lemme:and], np#4 |
if ( ^PRESUPPOSITION(#1,#2) )
    PRESUPPOSITION(#1,#2,#3,#4).

| pp#2[attitude]{?[lemme:between],?+}, #3[lemme:and], np#4 |
if ( ^PRESUPPOSITION(#1,#2,#3,#4) )
   PRESUPPOSITION[subjectivity=+](#1,#2,#3,#4).

| pp#2[importance]{?[lemme:between],?+}, #3[lemme:and], np#4 |
if ( ^PRESUPPOSITION(#1,#2,#3,#4) )
   PRESUPPOSITION[subjectivity=+](#1,#2,#3,#4).


//The second set of concerns, the silences and absences that merit more substantial attention, cluster round the surprisingly uneven presence of democratic theory within the intellectual narrative of the paper.
//PRESUPPOSITION_EXTENSION(the surprisingly uneven presence,of democratic theory,within the intellectual narrative)
//PRESUPPOSITION_EXTENSION(the surprisingly uneven presence,within the intellectual narrative,of the paper)

| pp#2{?[lemme:of],?+} |
if ( PRESUPPOSITION(#1,#2,#3) & ^PRESUPPOSITION(#1,3,#4) )
   ~.

//Nonetheless, within the context of deliberations about the nature of education in and for democracy, and about the place of the common school within that wider project, the significance of the radical traditions of state education is substantial.
//PRESUPPOSITION_EXTENSION(#4the context,#3of deliberations,#5about the nature)
//PRESUPPOSITION_EXTENSION(#2deliberations,#5about the nature,#6of education)
//HEAD(#1deliberations,#2deliberations)
//HEAD(#1deliberations,#3of deliberations)


if ( HEAD(#1,#2) & HEAD(#1,#3) & ^PRESUPPOSITION(#4,#3,#5) & PRESUPPOSITION(#2,#5,#6) )
   PRESUPPOSITION[difficult=+](#4,#3,#5,#6).

if ( PRESUPPOSITION(#4,#3,#5,#6) & ^PRESUPPOSITION(#2,#5,#6) )
   ~.


//////////////////////////////////////////////////////////////////////////////
/////								    //////////
/////    ********  only leave presuppositions that have a feature  ***   /////
/////						                   //////////
//////////////////////////////////////////////////////////////////////////////


if ( ^PRESUPPOSITION[conceptual](#1,#2) | ^PRESUPPOSITION[subjectivity](#1,#2) | ^PRESUPPOSITION[problem](#1,#2) | ^PRESUPPOSITION[education](#1,#2) )
   PRESUPPOSITION[relevant=+](#1,#2).

if ( ^PRESUPPOSITION[relevant:~](#1,#2) )
   PRESUPPOSITION[notype=+](#1,#2).
   
if ( ^PRESUPPOSITION[conceptual](#1,#2,#3) | ^PRESUPPOSITION[subjectivity](#1,#2,#3) | ^PRESUPPOSITION[problem](#1,#2,#3) | ^PRESUPPOSITION[education](#1,#2,#3) )
   PRESUPPOSITION[relevant=+](#1,#2,#3).

if ( ^PRESUPPOSITION[relevant:~](#1,#2,#3) )
   PRESUPPOSITION[notype=+](#1,#2,#3).
   
if ( ^PRESUPPOSITION[conceptual](#1,#2,#3,#4) | ^PRESUPPOSITION[subjectivity](#1,#2,#3,#4) | ^PRESUPPOSITION[problem](#1,#2,#3,#4) | ^PRESUPPOSITION[education](#1,#2,#3,#4) )
   PRESUPPOSITION[relevant=+](#1,#2,#3,#4).

if ( ^PRESUPPOSITION[relevant:~](#1,#2,#3,#4) )
   PRESUPPOSITION[notype=+](#1,#2,#3,#4).


| #1[presupposition=+] |
if ( PRESUPPOSITION(#1,#2) )
   ~.

| #2[presupposition=+] |
if ( PRESUPPOSITION(#1,#2) )
   ~.

| #1[presupposition=+] |
if ( PRESUPPOSITION(#1,#2,#3) )
   ~.

| #2[presupposition=+] |
if ( PRESUPPOSITION(#1,#2,#3) )
   ~.

| #3[presupposition=+] |
if ( PRESUPPOSITION(#1,#2,#3) )
   ~.

| #1[presupposition=+] |
if ( PRESUPPOSITION(#1,#2,#3,#4) )
   ~.

| #2[presupposition=+] |
if ( PRESUPPOSITION(#1,#2,#3,#4) )
   ~.

| #3[presupposition=+] |
if ( PRESUPPOSITION(#1,#2,#3,#4) )
   ~.

| #4[presupposition=+] |
if ( PRESUPPOSITION(#1,#2,#3,#4) )
   ~.

   

//////////////////////////////////////////////////////////////////////////////
/////								    //////////
///////////           ****** keyword + presupposition ***** /////////////////////
/////						                   //////////
//////////////////////////////////////////////////////////////////////////////


if ( PRESUPPOSITION(#1,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD(#3,#1,#2).
//   KEYWORD(#3).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#2) & PRESUPPOSITION[problem](#1,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[problem=+](#3,#1,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#2) & PRESUPPOSITION[education](#1,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[education=+](#3,#1,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#2) & PRESUPPOSITION[subjectivity](#1,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[subjectivity=+](#3,#1,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#2) & PRESUPPOSITION[conceptual](#1,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[conceptual=+](#3,#1,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#2) & PRESUPPOSITION[difficult](#1,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[difficult=+](#3,#1,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#2) & PRESUPPOSITION[notype](#1,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[notype=+](#3,#1,#2).

if ( PRESUPPOSITION(#1,#4,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#2) & PRESUPPOSITION[problem](#1,#4,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[problem=+](#3,#1,#4,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#2) & PRESUPPOSITION[education](#1,#4,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[education=+](#3,#1,#4,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#2) & PRESUPPOSITION[subjectivity](#1,#4,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[subjectivity=+](#3,#1,#4,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#2) & PRESUPPOSITION[conceptual](#1,#4,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[conceptual=+](#3,#1,#4,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#2) & PRESUPPOSITION[difficult](#1,#4,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[difficult=+](#3,#1,#4,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#2) & PRESUPPOSITION[notype](#1,#4,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[notype=+](#3,#1,#4,#2).

if ( PRESUPPOSITION(#1,#4,#5,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#5,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#5,#2) & PRESUPPOSITION[problem](#1,#4,#5,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[problem=+](#3,#1,#4,#5,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#5,#2) & PRESUPPOSITION[education](#1,#4,#5,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[education=+](#3,#1,#4,#5,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#5,#2) & PRESUPPOSITION[subjectivity](#1,#4,#5,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[subjectivity=+](#3,#1,#4,#5,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#5,#2) & PRESUPPOSITION[conceptual](#1,#4,#5,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[conceptual=+](#3,#1,#4,#5,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#5,#2) & PRESUPPOSITION[difficult](#1,#4,#5,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[difficult=+](#3,#1,#4,#5,#2).

if ( ^PRESUPPOSITION-WITH-KEYWORD(#3,#1,#4,#5,#2) & PRESUPPOSITION[notype](#1,#4,#5,#2) & HEAD(#3,#2) )
   PRESUPPOSITION-WITH-KEYWORD[notype=+](#3,#1,#4,#5,#2).



/////////////////////////////////////////////////////
/////////////////////////////////////////////////////
///////////////////////////////////////////////////





///////////////////////////////////////////////////////////////////////////////
/////								    //////////
///// 	       *****   CONTRIBUTION     *******                     //////////
/////						                   //////////
//////////////////////////////////////////////////////////////////////////////



| TOP#4{ ?+ } |
if ( SUBJ(#1,#2[deict]) & NUCL(#1,#3[publ]) )
	INDICATOR[contribution=+](#4).

// this paper proposes
//DETD(paper,this)
//SUBJ-N_PRE(proposes,paper)

| TOP#4{ ?+ } |
if ( $1(#1[publ],#2[deict]) | $1(#1[deict],#2[publ]) )
	INDICATOR[contribution=+](#4).

//| TOP#4{ ?+ } |
//if ( $1(#1[publ],#2[deict]) & $2(#3[mentsco],#1[publ]) )
//	INDICATOR[contribution=+](#4).

// mentioned earlier in this paper
//MOD_POST(mentioned,paper)
////DETD(paper,this)   

// taken together/altogether/all together + scope -> synth (These results, taken together)

| #1[adv,art] |
	MAIN(#1).

| TOP#4{ ?+ } |
if ( ^MAIN(#1[adv,art]) & SUBJ(#2,#3[scope]) )
	INDICATOR[contribution=+](#4).

| TOP#4{ ?+ } |
if ( ^MAIN(#1[adv,art]) & SUBJ(#2[mental],#3[deict]) )
	INDICATOR[contribution=+](#4).

if ( MAIN(#1[adv,art]) )
	~ .
| TOP#3{ ?+ } |
if ( $1[contribution](#1) & ~INDICATOR(#2) )
	INDICATOR[contribution=+](#3).
	
| TOP#3{ ?+ } |
if ( $1[contribution](#1,#2) & ~INDICATOR(#4) )
	INDICATOR[contribution=+](#3).

//In conclusion

| TOP#3{ ?+ } |
if ( $1(#1[relword],#2[prep,maj]) )
	INDICATOR[contribution=+](#3).

| #1[contribution=+] |
if ( $1(#1[deict],#2[mentsco]) & ^INDICATOR(#3) )
   INDICATOR[contribution=+](#3).

| #2[contribution=+] |
if ( $1(#1[deict],#2[mentsco]) & ^INDICATOR(#3) )
   INDICATOR[contribution=+](#3).

| #1[contribution=+] |
if ( $1(#2[mentsco],#1[deict]) & ^INDICATOR(#3) )
   INDICATOR[contribution=+](#3).

| #2[contribution=+] |
if ( $1(#2[mentsco],#1[deict]) & ^INDICATOR(#3) )
   INDICATOR[contribution=+](#3).


///////////////////////////////////////////////////////////////////////////////
/////								    //////////
///// 	       *****   REFERENCE       *******                     //////////
/////						                   //////////
//////////////////////////////////////////////////////////////////////////////


// his/their work

| #1[other=+] |
if ( ^PERSON(#1) )
   ~.

// authors hold

| #1[reference=+] |
if ( SUBJ-N(#1,#2[lemme:author]) )
   ~.
   
| #2[reference=+] |
if ( SUBJ-N(#1,#2[lemme:author]) )
   ~.
   

| #1[lemme:p\.,reference=+] |
  ~.

| #1[lemme:pp\.,reference=+] |
  ~.

| #1[reference=+] |
if ( $1(#1[other],#2[mentsco]) )
	~.

| #2[reference=+] |
if ( $1(#1[other],#2[mentsco]) )
	~.

| #1[reference=+] |
if ( $1(#2[mentsco],#1[other]) )
	~.

| #2[reference=+] |
if ( $1(#2[mentsco],#1[other]) )
	~.

| #1[reference=+] |
if ( $1(#1[other],#2[publ]) )
	~.

| #2[reference=+] |
if ( $1(#1[other],#2[publ]) )
	~.

| #1[reference=+] |
if ( $1(#2[publ],#1[other]) )
	~.

| #2[reference=+] |
if ( $1(#2[publ],#1[other]) )
	~.

//Richard Pring's paper 
//MOD_PRE(paper,Richard Pring)

// see Davies
// OBJ-N(see,Davies)

| #1[lemme:see,bf,reference=+] |
if ( OBJ-N(#1,#2[maj]) )
   ~.

//if ( MOD


//David Held has argued 

| #1[reference=+] |
if ( SUBJ-N(#1[mental],#2[proper]) )
	~.

| #2[reference=+] |
if ( SUBJ-N(#1[mental],#2[proper]) )
	~.

//( Fielding , 2006 ) 
//INS{( NP{Fielding} , NP{2006} )}

| ins#1[reference=+]{ ?, np{?*,?[last,proper]}, ?*} |
  INS[reference=+](#1).

| ins#1[reference=+]{ ?, np{?*,?[dig]}, ?*} |
  INS[reference=+](#1).

| TOP#4{ ?+ } |
if ( ^INS[reference](#1) & ~INDICATOR[reference](#4) )
  INDICATOR[reference=+](#4).  

  


| TOP#4{ ?+ } |
if ( $1(#1[reference],#2) | $1(#1,#2[reference]) &  ~INDICATOR[reference](#4) )
   INDICATOR[reference=+](#4).


/////////////////////////////////
///contribution + reference
////////////////////////////////

if ( ^INDICATOR[contribution](#1) & INDICATOR[reference](#1) )
   INDICATOR[contribution,reference=+](#1)

if ( INDICATOR[contribution,reference](#1) & ^INDICATOR[reference](#1) )
   ~.




///////////////////////////////////////////////////////////////////////////////
/////								    //////////
///// 	       *****   COLORING       *******                     //////////
/////						                   //////////
//////////////////////////////////////////////////////////////////////////////



| TOP#4{ ?+ } |
if ( ~INDICATOR(#4) )
   SENT(#4).




| #1[presuptocolor=+] |
if ( PRESUPPOSITION[relevant](#1,#2) | PRESUPPOSITION[relevant](#1,#2,#3) | PRESUPPOSITION[relevant](#1,#2,#3,#4) )
   ~.

| #2[presuptocolor=+] |
if ( PRESUPPOSITION[relevant](#1,#2) | PRESUPPOSITION[relevant](#1,#2,#3) | PRESUPPOSITION[relevant](#1,#2,#3,#4) )
   ~.

| #3[presuptocolor=+] |
if ( PRESUPPOSITION[relevant](#1,#2,#3) | PRESUPPOSITION[relevant](#1,#2,#3,#4) )
   ~.

| #4[presuptocolor=+] |
if ( PRESUPPOSITION[relevant](#1,#2,#3,#4) )
   ~.


| #1[presupposition]{#3[presupposition=+],#4[presupposition=+]} |
   ~.

| #1[presupposition]{#2[presupposition=+],#3[presupposition=+],#4[presupposition=+]} |
   ~.

| #1[presupposition]{#2[presupposition=+],#3[presupposition=+],#4[presupposition=+],#4[presupposition=+]} |
   ~.

| #1[presupposition]{#3[presupposition=+]} |
   ~.


