# -------------------------------------------------------------------------
# Xerox Research Center Europe - Grenoble
#
# Copyright (C)  Xerox Corporation
#
# -------------------------------------------------------------------------
# TYPE:		(x)fst
# CONTENT:	mappings for XIP added to lookup output
# AUTHOR:	Anne Schiller
# DATE:		01-Dec-2005
# -------------------------------------------------------------------------
# USAGE: 	fst -l [ThisFile]
# -------------------------------------------------------------------------


read regex [

# change tag for infinitive particle 
  %+Izu -> %+Infzu

# decompose person
.o. %+1P3P -> [ %+1P | %+3P ]

# decompose case
.o. %+AccDat		-> %+Acc | %+Dat
.o. %+AccDatGen		-> %+Acc | %+Dat | %+Gen
.o. %+AccGen		-> %+Acc | %+Gen
.o. %+DatGen		-> %+Dat | %+Gen
.o. %+NomAcc		-> %+Nom | %+Acc
.o. %+NomAccDat		-> %+Nom | %+Acc | %+Dat
.o. %+NomAccDatGen	-> %+Nom | %+Acc | %+Dat | %+Gen
.o. %+NomAccGen		-> %+Nom | %+Acc | %+Gen

# decompose number
.o. %+SgPl		-> %+Sg | %+Pl


# gender
.o. %+FN		-> %+Fem | %+Neut
.o. %+MF		-> %+Masc | %+Fem
.o. [ %+Undef | %+MFN ] -> %+Masc | %+Fem | %+Neut
#.o. %+MFN		-> 0
.o. %+MN		-> %+Masc | %+Neut

# invariant adjectives
.o. %+Invar -> %+Pos [%+Masc|%+Fem|%+Neut] [%+Sg|%+Pl] [%+Nom|%+Acc|%+Acc|%+Dat] [%+St|%+Wk] || _ $"+hmm_ADJA" 
.o. %+Invar -> %+Pos %+Pred || _ $"+hmm_ADJD" 

# ordinals
.o. %+Num -> %+Adj 	|| _ $[%+Sg | %+Pl ]

# cardinals
.o. [..] -> %+Pl	|| %+Card _
.o. %+Pl -> %+Sg	|| .#. [1|I|i] %+Dig (?) %+Card _

# determiner 


.o. [..] -> %+AgrWk	|| %+Det %+Def _

.o. %+St -> %+AgrWk	|| %+Det ?* _
.o. %+Wk -> %+AgrSt	|| %+Det ?* _
.o. [..] -> [%+AgrWk|%+AgrSt]	|| %+Det _ ~$[%+AgrWk|%+AgrSt] .#.

# temporary solution for "etliche" 
.o. %+AgrWk -> %+AgrSt	|| {etlich} %+Det ?*  _

# missing inflectional features (e.g. "wenige")
.o. [..] -> [%+St|%+Wk] || %+PAdj _ ~$[%+St|%+Wk] .#.

# adjectives nouns
.o. [..] -> [%+Wk|%+St]	|| [ %+Adj | %+Noun | %+Prop ] _ ~$[%+Wk|%+St] .#.


].i ;

