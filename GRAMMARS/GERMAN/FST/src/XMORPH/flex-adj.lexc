! -------------------------------------------------------------------------
! TYPE:		lexc
! CONTENT:	German morphology: adjective inflection
!
! AUTHOR:	Anne Schiller, XRCE
! DATE:		13-Jan-2002
! -------------------------------------------------------------------------


LEXICON Adv_cmp		+Comp		inv ;	! öfter, eher
LEXICON Adv_sup		@C.sup@+Sup	inv ;	! höchstens
LEXICON Adv_sup2	@C.sup@+Sup2	inv ;	! (am) öftesten


! ----------------------------------------------------------------------------
! Adjectives
! ----------------------------------------------------------------------------

LEXICON A_reg
			A_pos ;
 0:^+^AdjEr		A_cmp ;
 0:^+^SupEst		A_sup ;

LEXICON A_$	
		A_pos ;
 0:^$^+^AdjEr	A_cmp ;
 0:^$^+^SupEst	A_sup ;

LEXICON A_inv		
 +Invar:0		Low ;
 +Invar+Pred:0		Low ;



LEXICON A_pos/flex	+Pos			NAdjFlex ;
LEXICON A_pos/pred	+Pos+Pred		Low ;
LEXICON A_pos/cpd	+Pos			Trunc ;

LEXICON A_cmp/flex	+Comp			NAdjFlex ;
LEXICON A_cmp/pred	+Comp+Pred	      	Low ;
LEXICON A_cmp/cpd	+Comp		      	Trunc ;

LEXICON A_sup/flex	@C.sup@+Sup			NAdjFlex ;	!
LEXICON A_sup/pred	@C.sup@+Sup+Pred		Low ;		! höchst
			@C.sup@+Sup2+Pred:@C.sup@00en	Low ;		! [am] höchsten
LEXICON A_sup/cpd	@C.sup@+Sup			Trunc ;		! Höchst-[gebot]

LEXICON A_pos0		A_pos/pred; A_pos/cpd;
LEXICON A_cmp0		A_cmp/pred; A_cmp/cpd;
LEXICON A_sup0		A_sup/pred; A_sup/cpd;

LEXICON A_pos		A_pos/flex; A_pos0;
LEXICON A_cmp		A_cmp/flex; A_cmp0;
LEXICON A_sup		A_sup/flex; A_sup0;

LEXICON A_loc		A_pos/flex; A_pos/cpd;	! ober-e; Ober-[seite]
	0:^+st		A_sup;			! oberst- 


LEXICON NAdjFlex
	@U.frm.low@		AdjFlex ;	! [ein] schnelles [Auto]
	@U.frm.upp@^]+Noun	AdjFlex ;	! [ein] Schneller
	^]+Noun+MFN+Pl		adj/Cp ;

 ! derivations:
	0:^+^AdjEr			}weise ;	! schnellerweise 


! ----------------------------------------------------------------------
!			Nom	Acc	Dat	Gen
! ----------------------------------------------------------------------
! St	Masc Sg.	+er 	+en	+em	+en
!	Neut Sg.	+es	+es	+em	+en
!	Fem Sg.		+e	+e	+er	+er
!
! Wk	Masc. Sg	+e	+en	+en	+en
!	Neut Sg.	+e	+e	+en	+en
!	Fem Sg.		+e	+e	+en	+en

! St	MFN Pl.		+e	+e	+en	+er
! Wk	MFN Pl.		+en	+en	+en	+en


LEXICON AdjFlex
	+Masc:0	adj/MascSg ;
	+Neut:0	adj/NeutSg ;
	+Fem:0	adj/FemSg ;
	+MFN:0	adj/Pl ;	


! ---------------------------------------------------------------------------
! nouns inflecting like adjectives

LEXICON Nn_Adj	@U.frm.upp@	adj/NeutSg ;	! [das] Junge; [ein] Junges
		@U.frm.upp@	adj/Pl ;
		+Pl:0		adj/Cp ;

LEXICON Nm_Adj	@U.frm.upp@	adj/MascSg ;	! [der] Beamte; [ein] Beamter
		@U.frm.upp@	adj/Pl ;
		+Pl:0		adj/Cp ;

LEXICON Nmf_Adj	@U.frm.upp@+Masc	adj/MascSg ;	! [der] Angestellte; [ein] Angestellter
		@U.frm.upp@+Fem		adj/FemSg ;	! [die] Angestellte; [eine] Angestellte
		@U.frm.upp@+MF		adj/Pl ;
		+MF+Pl:0		adj/Cp ;
	

! ---------------------------------------------------------------------------
LEXICON adj/MascSg
	+Sg+Nom+Wk:^+^AdjE		Fin ;	! [der] kleine [Mann]
	+Sg+Nom+St:^+^AdjEr		Fin ;	! [ein] kleiner [Mann]
	+Sg+AccGen:^+^AdjEn		Fin ;	! [den/ohne;des/trotz] kleinen [Mann(es)]
	+Sg+Dat+St:^+^AdjEm		Fin ;	! [mit] kleinem [Mann]
	+Sg+Dat+Wk:^+^AdjEn		Fin ;	! [dem] kleinen [Mann]

LEXICON adj/NeutSg
	+Sg+NomAcc+St:^+^AdjEs		Fin ;	! [ein/ohne] kleines [Kind]
	+Sg+NomAcc+Wk:^+^AdjE		Fin ;	! [das] kleine [Kind]
	+Sg+Dat+St:^+^AdjEm		Fin ;	! [mit] kleinem [Kind]
	+Sg+Dat+Wk:^+^AdjEn		Fin ;	! [dem] kleinen [Kind]
	+Sg+Gen:^+^AdjEn		Fin ;	! [des/trotz] kleinen [Kindes]

LEXICON adj/FemSg
	+Sg+NomAcc:^+^AdjE		Fin ;	! [eine/die] kleine [Frau]
	+Sg+DatGen+St:^+^AdjEr		Fin ;	! [trotz/mit] kleiner [Frau]
	+Sg+DatGen+Wk:^+^AdjEn		Fin ;	! [trotz/mit einer] kleinen [Frau]

LEXICON adj/Pl
	+Pl+NomAccGen+Wk:^+^AdjEn	Fin ;	! [die/der/den] kleinen [Leute/Leuten]
	+Pl+NomAcc+St:^+^AdjE		Fin ;	! [als/für] kleine [Leute]
	+Pl+Gen+St:^+^AdjEr		Fin ;	! [trotz] kleiner [Leute]
	+Pl+Dat:^+^AdjEn		Fin ;	! [mit] kleinen [Leuten]

LEXICON adj/Cp
	0:^+^AdjEn			Trunc ;


LEXICON AdjFlex/WkSg			! only weak inflection (for function words)
	+MFN+Sg+Nom+Wk:^+^AdjE			Fin ;
	+FN+Sg+NomAcc+Wk:^+^AdjE		Fin ;
	+Masc+Sg+Acc+Wk:^+^AdjEn		Fin ;
	+MFN+Sg+DatGen+Wk:^+^AdjEn		Fin ;

LEXICON AdjFlex/WkPl			! only weak inflection (for function words)
	+MFN+Pl+NomAccDatGen+Wk:^+^AdjEn	Fin ;

LEXICON AdjFlex/Wk	AdjFlex/WkSg ; AdjFlex/WkPl ;

LEXICON AdjFlex/MixSg			! inflection after "ein" (for function words)
	+Masc+Sg+Nom+St:^+^AdjEr		XX ;	! [ein] mancher
	+Masc+Sg+Acc+Wk:^+^AdjEn		XX ;	! [einen] manchen
	+Fem+Sg+NomAcc+Wk:^+^AdjE		XX ;	! [eine] manche
	+Neut+Sg+NomAcc+St:^+^AdjEs		XX ;	! [ein] manches
	+MFN+Sg+DatGen+Wk:^+^AdjEn		XX ;	! [einer/einem] manchen

! ---------------------------------------------------------------------------
! irregular adjectives
LEXICON A.groß
	groß+Adj:groß		A_pos ;
	groß+Adj:größer		A_cmp ;
	groß+Adj:größt		A_sup ;

LEXICON A.gut
	gut+Adj:gut		A_pos ;
	gut+Adj:besser		A_cmp ;
	gut+Adj:best		A_sup ;

LEXICON A.hoch
	hoch+Adj:hoch		A_pos/pred ;
	hoch+Adj:hoch		A_pos/cpd ;
	hoch+Adj:hoh		A_pos/flex ;
	hoch+Adj:höher		A_cmp ;
	hoch+Adj:höchst		A_sup ;

LEXICON A.nahe
	nah+Adj:nah		A_pos ;
	nah+Adj:nahe		A_pos/pred ;
	nah+Adj:näher		A_cmp ;
	nah+Adj:nächst		A_sup ;

