# -------------------------------------------------------------------------
# Xerox Research Center Europe - Grenoble
#
# Copyright (C)  Xerox Corporation
#
# -------------------------------------------------------------------------
# TYPE:		(x)fst
# CONTENT:	mappings for XIP added to lookup output
# AUTHOR:	Anne Schiller
# DATE:		01-Dec-2005
# -------------------------------------------------------------------------
# USAGE: 	fst -l [ThisFile]
# -------------------------------------------------------------------------

define MASC	%+Masc | %+MF | %+MN | %+MFN  ;
define FEM	%+Fem | %+FN | %+MF | %+MFN  ;
define NEUT	%+Neut | %+FN | %+MN | %+MFN ;

define NOM	%+Nom  ;
define ACC	%+Acc  ;
define DAT	%+Dat | %+Dat2 ;
define GEN	%+Gen | %+GenS ;

define SG	%+Sg ;
define PL	%+Pl ;

read regex [

# add feature
    [..] -> %+Common || %+Noun _ \[%+Title|%+NAdj|%+PPast|%+PPres|%+NInf]


# change tag for infinitive particle 
.o. %+Izu -> %+Infzu

# change tag name (otherwise the token consedered as end of sentence!)
.o. %+Sent -> %+SAdv	|| %+Ptcl _

# decompose person
.o. %+1P3P -> [ %+1P | %+3P ]

# decompose case
.o. %+NomAccDatGen	-> %+Nom | %+Acc | %+Dat | %+Gen 

.o. %+AccDat		-> %+Acc | %+Dat
.o. %+AccDatGen		-> %+Acc | %+Dat | %+Gen
.o. %+AccGen		-> %+Acc | %+Gen
.o. %+DatGen		-> %+Dat | %+Gen
.o. %+NomAcc		-> %+Nom | %+Acc
.o. %+NomAccDat		-> %+Nom | %+Acc | %+Dat
.o. %+NomAccGen		-> %+Nom | %+Acc | %+Gen
.o. %+NomGen		-> %+Nom | %+Gen

# decompose number
.o. %+SgPl		-> %+Sg | %+Pl

# introduce combined feature for number+gender

.o. %+MF -> [ %+Masc| %+Fem ] 
.o. %+MN -> [ %+Masc| %+Neut ] 
.o. %+FN -> [ %+Fem| %+Neut ] 
.o. %+MFN -> [ %+Fem | %+Masc | %+Neut ] || $[%+Noun|%+Prop]  _ %+Sg

# add gender feature for last names
.o. [..] -> [ %+Masc| %+Fem ] || %+Prop %+Last _ 

.o. [..] -> %+fnom	|| FEM SG NOM _
.o. [..] -> %+facc	|| FEM SG ACC _
.o. [..] -> %+fdat	|| FEM SG DAT _
.o. [..] -> %+fgen	|| FEM SG GEN _

.o. [..] -> %+mnom	|| MASC SG NOM _
.o. [..] -> %+macc	|| MASC SG ACC _
.o. [..] -> %+mdat	|| MASC SG DAT _
.o. [..] -> %+mgen	|| MASC SG GEN _

.o. [..] -> %+nnom	|| NEUT SG NOM _
.o. [..] -> %+nacc	|| NEUT SG ACC _
.o. [..] -> %+ndat	|| NEUT SG DAT _
.o. [..] -> %+ngen	|| NEUT SG GEN _

.o. [..] -> %+xnom	|| PL NOM _
.o. [..] -> %+xacc	|| PL ACC _
.o. [..] -> %+xdat	|| PL DAT _
.o. [..] -> %+xgen	|| PL GEN _


# ordinals
.o. %+Num -> %+Adj 	|| _ [ %+Ord | %+Fract ]  $[%+Sg | %+Pl ]


].i ;

