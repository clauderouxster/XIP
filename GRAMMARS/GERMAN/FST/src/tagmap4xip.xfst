# ============================================================================
# TYPE:		(x)fst script 
# CONTENT:	mapping of morph. categories to standard POS tags
#
# LANGUAGE:	GERMAN
# TAGSET:	1
#
# AUTHOR:	Anne Schiller
#
# CREATED:	31-Jul-2003
#
# ============================================================================
# USAGE:	xfst -l ThisFile
# ============================================================================



echo >>> define special characters


define DS	"^}" ;    # derivation suffix marker
define DP	"^{" ;    # derivation prefix marker
define CM	"^]" ;    # conversion marker

define SB	"^." ;    # segment boundary		drei^.und^.zwanzig
define VB	"^=" ;    # verb prefix boundary	an^=kommen
define TB	"^&" ;    # token boundary		zu+Prep^&dem+Det...

define CB	"^#" ;	  # compound boundary
define CH	"^-" ;	  # compound hyphen

define ANY	\[SEP|TB] ;

	       
# ---- sub catgeories -----------------------------------
define NounTYPE	[
  %+Curr	# currency			DM, Euro
| %+Lang	# language			(das) Deutsch
| %+Unit	# physical unit			km/h
| %+Music	# music class			A-Dur		
| %+Month	# name of month			Januar
| %+Day		# name of day			Sonntag
| %+Title	# title	(mainly for abbr)	Herr, Dr.
| %+Poss	# possessive			(der) Meine
| %+Symbol	# symbol name			Ypsilon, Eszett
| %+Acron	# acronym			AG, TU
];

define PropTYPE	[
  %+Continent	# continent			Europa
| %+Country	# country, nation		Deutschland
| %+State	# Bundesland, Kanton, ...	Hessen, Uri, Arizona
| %+Region	# region, mountain, ...	
| %+Town	# town, city
| %+TownCountry	# town and/or country		Monaco
| %+TownState	# town and/or state		Bremen
| %+Sea		# ocean				Nordsee, Mittelmeer
| %+Lake	# lake				Bodensee
| %+River	# river				Rhein, Donau
| %+First	# first name			Hans, Mria
| %+Last	# family name			Schiller, Maier
| %+Org		# organisation			Bundestag, UNO
| %+Bus		# business name			Xerox, Daimler
| %+Prod	# product name			(ein) VW
| %+Doc		# law, document name		StVO (=Straßenverkehrsordnung)
| %+Part	# name part			San (Francisco)
];

define AdvTYPE	[
  %+Wh		# interrogative or relative	worauf, wie
| %+Quant	# quantifying			viel
];

define VTYPE	[
  %+Trans	# transitive (*)		er hängte es an die Wand
| %+Intr	# intransitive (*)		er hing an der Wand
		# (*) marked only if inflection differs#
];

define PunctTYPE [
  %+Angle [ %+Left | %+Right ]
| %+Backslash
| %+Begin
| %+Bracket [ %+Left | %+Right ]
| %+Bullet
| %+Colon
#| %+Comma
| %+Dash
| %+Dots
| %+Hyphen
| %+Paren [ %+Left | %+Right ]
| %+Quote [ %+Double | %+Left | %+Right | %+Single ]
| %+Semicolon
#| %+Sent
| %+Slash
];

#--- inflectional features --------------------------------------------------
define CPD	[
  %+Cpd		# truncated compound part	pseudo-, Haus-
];

define ADJPRED	[
  %+Pred	# predicative or adverbial	(er ist/geht) schnell
];

define VINF	[
  %+Inf		# simple infinitive		ankommen, spielen
| %+Izu		# infinitive with "zu"		anzukommen
| %+PPres	# simple present participle	ankommend, spielend
| %+PPrzu	# present prtaiciple with "zu"	anzuwendend
| %+PPast	# past participle		gespielt, angekommen
];

define VMOOD	[
  %+Indc	# indicative			(er) kommt
| %+Subj	# subjunctive			(er) komme
| %+IndcSubj	# indicative or subjunctive	(ich) komme
| %+Imp		# imperative			komm (rein!)
];

define VTENSE	[
  %+Pres	# present			(er) kommt, spielt
| %+Past	# past				(er) kam, spielte
];

define VPOS	[
  %+V1		# main clause position		(er) kommt (an) 
| %+V2		# subordinate position		(weil er) ankommt
];

define PPERS	[
  %+Pers	# personal			ich, mich
| %+Refl	# reflexiv			sich  
| %+Recp	# reciprocal			einander
];

define GENDER	[
  %+Masc	# masculine
| %+Fem		# feminine
| %+Neut	# neuter
| %+MF		# masc or fem
| %+MN		# masc or neut
| %+FN		# fem or neut
| %+MFN		# any gender
| %+Undef	# undefined (for plurals only)
];

define NUMBER	[
  %+Sg		# singular
| %+Pl		# plural
| %+SgPl	# singular or plural
];

define PERSON	[
  %+1P		# first person			ich gehe
| %+2P		# second person			du gehst, ihr geht
| %+3P		# third person			er geht
| %+1P3P	# first or third person		(wir/sie) gehen; (ich/er) kam
];

define CASE	[
  %+Nom		# nominative			der Fall
| %+Acc		# accusative			den Fall
| %+Dat		# dative			dem Fall
| %+Dat2	# "old" dative "-e"		im Falle
| %+Gen		# genitive			des Falles
| %+NomAcc	# nom or acc			mein Kind
| %+AccDat	# acc or dat			in (die/der Stadt)
| %+AccGen	# acc or gen			laut (dem/des Antrag/s)
| %+DatGen	# dat or gen			der Frau
| %+NomAccDat	# nom, acc or dat		(der/den/dem) Fall
| %+NomAccGen	# nom, acc or gen		(die/der) Fälle
| %+AccDatGen	# acc, dat or gen		(des/dem/den) Menschen
| %+NomAccDatGen	# any case			(die/der/den) Frauen
];

define DEGREE	[
  %+Pos		# positive, basic form		groß, der kleine 
| %+Comp	# comparative			größer, der kleinere
| %+Sup		# superlative			größt, der kleinste
| %+Sup2	# superlative with "am"		(am) größten
];

define FLEX	[
  %+St		# strong ("marked") inflection	der, kleinem
| %+Wk		# weak ("unmarked") inflection	ein, (dem) kleinen
];

define INVAR	[
  %+Invar	# invariant for case/number/gender/...
];

# --- additional features ---------------------------------------
define SHORT	[
  %+Abbr	# abbreviation
];

# usage
define USAGE	[
  %+Coll	# colloquial
];

# elision marker
define ELID	[
  %+Elid	# elided 'e"			woll'n
| %+ElidS	# elided e before "'s"		wär ('s)
];

# spelling
define SPELL	[
  %^Old	| %^OldLem | %^OldSrf	# old spelling,
| %^New	| %^NewLem | %^NewSrf	# new spelling
| %^Err	| %^ErrLem | %^ErrSrf	# erroneous, but frequent spelling
];

#  ---- special tokens -----------------------------------
define EXPL	[      # 's after verb
       e s %+Pron %+Pers %+3P %+Neut %+Sg %+NomAcc
];

define PDET	[	# determiner with preposion "eg. zum
       {der} %+Det %+Def %+Masc
|      {die} %+Det %+Def %+Fem
|      {das} %+Det %+Def %+Neut
] %+Sg CASE %+St ;

define FUGE	 [ "^/" [ e | (e) s | (e) n  ] ];

define AGR  [ GENDER NUMBER CASE (FLEX) | %+Invar ] ;

define NADJ [ CM %+Noun GENDER NUMBER CASE (FLEX) ] ;

#  ---- main catgeories -----------------------------------
define MRK  (SHORT) (USAGE) (ELID) (SPELL) ;

define MAP  [ANY]+ [
  %+Cpd							MRK %+hmm%_TRUNC:0

| %+Noun	# common noun
	[ (NounTYPE) (GENDER) (NUMBER) (CASE) (FLEX)	MRK %+hmm%_NOUN:0
	]

| %+Prop	# proper name
	[ (PropTYPE) (GENDER) (NUMBER) (CASE) (FLEX)	MRK %+hmm%_NOUN:0
	]

| %+Adj		# adjective
	[ %+Pos GENDER NUMBER CASE (FLEX)		MRK %+hmm%_ADJA:0
	| %+Pos ADJPRED					MRK %+hmm%_ADJD:0
	| %+Comp GENDER NUMBER CASE (FLEX)		MRK %+hmm%_ADJA2:0
	| %+Comp ADJPRED				MRK %+hmm%_ADJD2:0
	| %+Sup GENDER NUMBER CASE (FLEX)		MRK %+hmm%_ADJA3:0
	| [%+Sup|%+Sup2] ADJPRED			MRK %+hmm%_ADJD3:0
	| (INVAR)					MRK [%+hmm%_ADJA:0|%+hmm%_ADJD:0 ]

# upper case adjectives (in proper names)
##	| %+Pos NADJ					MRK %+hmm%_ADJA:0
##	| %+Comp NADJ					MRK %+hmm%_ADJA2:0
##	| %+Sup NADJ					MRK %+hmm%_ADJA3:0
	]				

| %+Adv		# adverb
	[ (%+Quant) (DEGREE)				MRK %+hmm%_ADV:0
	| %+Wh						MRK %+hmm%_WADV:0
	]				

| %+Verb	# verb
	[ (VTYPE) %+Inf					MRK %+hmm%_VVINF:0
	| (VTYPE) %+Izu					MRK %+hmm%_VVIZU:0
	| (VTYPE) %+PPres				MRK %+hmm%_ADJD:0
	| (VTYPE) %+PPast				MRK %+hmm%_VVPP:0
	| (VTYPE) VMOOD PERSON NUMBER (VTENSE) (VPOS)	MRK %+hmm%_VVFIN:0 

	| %+Modal %+Inf					MRK %+hmm%_VMINF:0
	| %+Modal %+PPres				MRK %+hmm%_ADJD:0
	| %+Modal %+PPast				MRK %+hmm%_VMPP:0
	| %+Modal VMOOD PERSON NUMBER (VTENSE) (VPOS)	MRK %+hmm%_VMFIN:0 

	]

| %+Det	 	# determiner
	[ %+Indef AGR					MRK %+hmm%_INDDET:0
	| %+Def	AGR					MRK %+hmm%_ART:0
	| %+Wh	AGR					MRK %+hmm%_WDET:0
	| %+Dem	AGR					MRK %+hmm%_DEMDET:0
	| %+Quant AGR					MRK %+hmm%_INDDET:0
	| %+Poss AGR					MRK %+hmm%_POSDET:0
	]
	
| %+Predet	# pre-determiner
	[ %+Indef 					MRK %+hmm%_INDINV:0
	| %+Wh						MRK %+hmm%_WINV:0
	| %+Dem						MRK %+hmm%_DEMINV:0
	| %+Quant 					MRK %+hmm%_INDINV:0
	]

| %+Pron	# pronoun
	[ %+Pers PERSON GENDER NUMBER CASE		MRK %+hmm%_PERSPRO:0
	| %+Refl					MRK %+hmm%_REFLPRO:0
	| %+Recp					MRK %+hmm%_REZPRO:0
	| %+Rel AGR					MRK %+hmm%_RELPRO:0
	| %+Indef AGR					MRK %+hmm%_INDPRO:0
	| %+Wh	AGR					MRK %+hmm%_WPRO:0
	| %+Dem	AGR					MRK %+hmm%_DEMPRO:0
	| %+Quant AGR					MRK %+hmm%_INDPRO:0
	| %+Poss AGR					MRK %+hmm%_POSPRO:0
	]

| %+PAdj	# pronoun-like adjective
	[ %+Indef (DEGREE) AGR				MRK %+hmm%_INDADJ:0
	| %+Wh (DEGREE)	AGR				MRK %+hmm%_WDET:0
	| %+Dem	(DEGREE) AGR				MRK %+hmm%_DEMADJ:0
	| %+Quant (DEGREE) AGR				MRK %+hmm%_INDADJ:0
	| %+Quant %+Sup2 %+Pred				MRK %+hmm%_ADJD3:0	# am meisten/wenigsten
	| [%+Indef|%+Dem] %+Pred			MRK %+hmm%_ADV:0	# derartig
	]

| %+Adpos	# pre- or postposition
	[ [%+Prep|%+Crc1] (CASE)			MRK %+hmm%_PREP:0
	| %+Post (CASE)					MRK %+hmm%_POSTP:0
	| %+Crc2 (CASE)					MRK %+hmm%_CIRCP:0
	| %+Prep TB PDET				MRK %+hmm%_PREPART:0
	]

| %+Conj	# conjunction
	[ %+Coord					MRK %+hmm%_COORD:0
	| %+Coord1					MRK %+hmm%_COP1:0
	| %+Coord2					MRK %+hmm%_COP2:0
	| %+Subord					MRK %+hmm%_COSUB:0
	| %+SAdv					MRK %+hmm%_COADV:0
	| %+Infzu					MRK %+hmm%_COINF:0
	]

| %+Ptkl	# particle
	[ %+Neg						MRK %+hmm%_PTKNEG:0
	| %+Sent					MRK %+hmm%_PTKANT:0
	| %+Izu						MRK %+hmm%_PTKINF:0
	| %+PAdv					MRK %+hmm%_ADV:0
	| %+Pos						MRK %+hmm%_PTKPOS:0
	| %+Comp					MRK %+hmm%_PTKCOM:0
	| %+Sup						MRK %+hmm%_PTKSUP:0
	]

| %+Itj	  	# interjection
	[						MRK %+hmm%_ITJ:0
	] 

| %+Pref	# verb or compound prefix
	[ %+Sep						MRK %+hmm%_VPREF:0 
	]

| %+Num		# spelled out numeral
	[ %+Card (GENDER) (NUMBER) (CASE) (FLEX)	MRK %+hmm%_CARD:0
	| %+Ord	 (GENDER) (NUMBER) (CASE) (FLEX)	MRK %+hmm%_ORD:0
	| %+Ord  (ADJPRED)				MRK %+hmm%_ORD:0
	| %+Fract					MRK %+hmm%_ADJA:0
	]

| %+Symbol	# special symbol
	[	[ %+Music
		| %+Letter [%+Upp|%+Low] 
		| %+Init
		| %+Curr
		| %+Unit  ]				MRK %+hmm%_NOUN:0
	|	[ %+Math
		| %+Percent
		| %+Paragraph
		| %+Copyright
		| %+Email
		| %+Url
		| %+Degree
		| %+Underscore | 0 ]			MRK %+hmm%_SYMB:0
	| %+Sgml					MRK %+hmm%_SGML:0
	| %+Sgml %+Sent					MRK %+SENT:0
	]

| %+Dig		# digital numeral
	[ [%+Dec|%+Rom] %+Card				MRK %+hmm%_CARD:0
	| [%+Dec|%+Rom] %+Ord				MRK %+hmm%_ORD:0
	]

| %+Punct	# punctuation (-> special.xfst)
	[ PunctTYPE					MRK %+hmm%_PUNCT:0
	| %+Sent					MRK %+SENT:0
	| %+Comma					MRK %+hmm%_CM:0
	]

| %+Expr	# expressions (-> special.xfst)
	[ %+Date					MRK %+hmm%_DATE:0
	| %+Time 					MRK %+hmm%_DATE:0
	]

| %+Foreign						MRK %+hmm%_FM:0
];

# -----------------------------------------------------------------------
echo >>> special words
define ART	[ {ein} ];
define AUX	[ {haben} | {sein} | {werden} ] %+Verb (%+Aux) ;

define SPECIAL [
  ART %+Det %+Indef GENDER NUMBER CASE (FLEX)		MRK %+hmm%_ART:0

| AUX %+Inf						MRK %+hmm%_VAINF:0
| AUX %+PPres						MRK %+hmm%_ADJD:0
| AUX %+PPast						MRK %+hmm%_VAPP:0
| AUX VMOOD PERSON NUMBER (VTENSE) (VPOS)		MRK %+hmm%_VAFIN:0 

| {wie} %+Conj [%+Subord|%+Coord]			MRK %+hmm%_COWIE:0
| {als} %+Conj [%+Subord|%+Coord]			MRK %+hmm%_COALS:0
];

# -----------------------------------------------------------------------
echo >>> combine
read regex	 SPECIAL .p. MAP ;

optimize net


