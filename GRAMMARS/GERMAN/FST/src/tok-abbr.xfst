# -------------------------------------------------------
#  Abbreviations for standard tokenizer (GERMAN)
#
# AUTHOR:	A. Schiller
# ENCODING:	UTF-8
#
# UPDATE:	29-Jun-1999
# UPDATE:	02-Aug-2006	(converted to UTF8)
# -------------------------------------------------------

echo >>> list of abbreviations

define LIST [
# week days
  {Mo.} | {Di.} | {Mi.} | {Do.} | {Fr.} | {Sa.} | {So.}

# months
| {Jan.} | {Feb.} | {Febr.} | {Mrz.} | {Apr.} | {Jun.}
| {Jul.} | {Aug.} | {Sep.} | {Sept.} | {Okt.} | {Nov.} | {Dez.}

# titles
| {Dr.} | {Mag.} | {Prof.} | {Stud.}
| ({Dr.})
  [ {dent.} | {jur.} | {math.} | {med.} | {phil.}
  | {rer.} (%-) {nat.} | {theol.} ]
| {rer.} | {nat.}
| {Dipl} (%.) %- [{Inform}|{Ing}] %.

| {Fr.} | {Frl.} | {Hr.}
| {Mlle.} | {Mme.} | {Mr.} | {Mrs.} | {Ms.}

# other

| ([a|n|v] %.) {Chr.}
| {Abb.} | {Abk.} | {Adj.} | {Av.} | {Ave.}
| {Bd.} | {bes.} | {best.} | {bzw.} 
| {ca.} | {corp.} | {crp.}
| {etc.} 
| {gem.} | {ggf.}
| {inc.} 
| {Kl.}
| {ltd.} 
| {max.} | {min.}
| {NN.} | {Nr.}
| {Obj.}
| {Pl.}
| {Skt.} | {sog.} | {St.} | {Str.} | {Subj.}
| {Tel.} 
| {usf.} | {usw.} 
| {vgl.}
| {wörtl.}
];

# ---------------------------------------------------------------------
# letters (Latin-1)
# ---------------------------------------------------------------------
echo >>> regular abbreviations (Latin-1)

regex
[
  A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z
| a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|x|y|z

| Á|Â|Ã|Ä|Æ|Ç|È|Ï|Ð|É|Ê|Ë|Ì|Í|Î|Å|£|¥|Ñ|Ò|Ó|Ô|Õ|Ö|À|Ø|Þ|Ù|Ú|Û|Ü|Ý
| á|â|ã|ä|æ|ç|è|ï|ð|é|ê|ë|ì|í|î|µ|å|ñ|ò|ó|ô|õ|ö|à|ø|þ|ù|ú|û|ü|ý
] ;
define Letter

define INIT  [ Letter %. ]+ ;

# ---------------------------------------------------------------
# numerals
# ---------------------------------------------------------------
echo >>> numerals ending with "."

define Dig	[%0|1|2|3|4|5|6|7|8|9] ;

#define Dig1	Dig - %0 ;
#define Day	[ (%0) Dig1 | [1|2] Dig | 3 [%0|1] ] %.  ;
#define Month	[ (%0) Dig1 | 1 [%0 | 1 | 2 ] ] %. ;
#define Year1	[ ([1|2] Dig) Dig Dig ] ;
#define Date	Day %. Month %. [Year1|Year2] ;

define Year	[ [1|2] Dig Dig Dig ];


define DigOrd	[ Dig+ - Year ] %. ;

regex
[ [
	[ ( [ x c | l | x l | (l) (x (x (x))) ] )
	  ( [ i x | v | i v | (v) (i (i (i))) ] )
	]
|	[ ( [ X C | L | X L | (L) (X (X (X))) ] )
	  ( [ I X | V | I V | (V) (I (I (I))) ] )
	]
] - [] ] %. ;
define RomOrd

define NUM  [ DigOrd | RomOrd ];


# ------------------------------------------------------------------------
echo >>> initial upper case
define CAP [
     a (->) A, b (->) B, c (->) C, d (->) D, e (->) E, f (->) F,
     g (->) G, h (->) H, i (->) I, j (->) J, k (->) K, l (->) L,
     m (->) M, n (->) N, o (->) O, p (->) P, q (->) Q, r (->) R,
     s (->) S, t (->) T, u (->) U, v (->) V, w (->) W, x (->) X,
     y (->) Y, z (->) Z, ä (->) Ä, ö (->) Ö, ü (->) Ü,
     à (->) [A|a]	  || .#. _ 
] ;

# ------------------------------------------------------------------------
echo >>> combine
read regex [
    [ LIST .o. CAP ].l
|   INIT
|   NUM
] ;



