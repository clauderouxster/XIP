# -------------------------------------------------------------------------
# Xerox Research Center Europe - Grenoble
#
# Copyright (C)  Xerox Corporation
#
# -------------------------------------------------------------------------
# TYPE:		(x)fst
# CONTENT:	mappings for XIP added to lookup output
# AUTHOR:	Anne Schiller
# DATE:		27-Jul-2005
# -------------------------------------------------------------------------
# USAGE: 	fst -s multi-chars.fst
# -------------------------------------------------------------------------

# NOTES:
# -  XIP requires the following input format:
#	word <tab> lemma <tab> tags
#    where tags must start with a "+" and must not be mixed with normal characters
# - tags are interpreted from left to right,
#   derivational tags must therefore be removed or renamed


# identify tags and markers
define SIG

read regex SIG & [ %+ ?+ ];
multi char sigma net
define MORTAG

read regex SIG & [ %^ ?+ ];
multi char sigma net
define MARKER

# derivation and compound markers
define DRV   "^{" | "^}" | "^]"  ;
define SEP   "^=" ;
define CPD   ["^#" | "^-" ];

# new tags:
define NEWTAG	[ %+NAdj | %+NInf ];

# boundary for moving tags
define BD	CPD | "^}" ;

read regex [
# move some derivational markers
	[..] -> %+PPres || %+PPres ~$[BD] [%+Adj|%+Noun] _
.o.	[..] -> %+PPast || %+PPast ~$[BD] [%+Adj|%+Noun] _
.o.	[..] -> %+PPrzu || %+PPrzu ~$[BD] [%+Adj|%+Noun] _
.o.	[..] -> %+NInf	|| %+Inf %^%] %+Noun _
.o.	[..] -> %+NAdj	|| %+Adj ~$[BD] %+Noun _

# remove others
.o.	[MORTAG|NEWTAG] -> 0 || _ $[DRV|SEP|CPD|%+Cpd]

# change boundary symbols
.o.	"^]" ->  0
.o.	"^}" ->  %}

.o.	"^{" ->  %{
.o.	"^=" ->  %=
.o.	"^/" ->  %/
.o.	"^#" ->  %#
.o.	"^-" ->  %-
.o.	"^_" ->  %  

.o.	"[*" ->  0
.o.	"*]" ->  0
#.o.	"^|" ->  %|
.o.	"^|" -> 0

# remove others that appear *before* tags
.o.	MARKER -> 0 || .#. ~$[MORTAG|NEWTAG] _ 

# add a plus before remaining markers
.o.	[..] -> %+ ||_ MARKER
].i;
