! -------------------------------------------------------------------------
! CONTENT:	German morphology : multicharacter definitions
! -------------------------------------------------------------------------


Multichar_Symbols

! ---- main catgeories -----------------------------------
+Noun	! common noun					Haus, Technik
	!:: (NounTYPE) (GENDER) (NUMBER) (CASE) (FLEX) 
	!:: (NounTYPE) (GENDER) (NUMBER) CPD
+Prop	! proper name					Hans, Maier, Xerox
	!:: (PropTYPE) (GENDER) (NUMBER) (CASE) (FLEX) 
	!:: (PropTYPE) (GENDER) (NUMBER) CPD
+Adj	! adjective					klein, lila
	!:: DEGREE ATTR GENDER NUMBER CASE (FLEX) 
	!:: DEGREE ADJPRED
	!:: DEGREE CPD
	!:: INVAR
+Adv	! adverb					heute, wo
	!:: (AdvTYPE)
+Verb	! verb						spielen, ankommen
	!:: (VerbTYPE) VINF
	!:: (VerbTYPE) CPD
	!:: (VerbTYPE) VMOOD PERS NUMBER (VTENSE) (VPOS) 
+Det	! determiner					der, ein, kein
	!:: DTYPE GENDER NUMBER CASE (FLEX)
	!:: DTYPE INVAR
+Predet	! pre-determiner				manch (ein)
	!:: PronTYPE 
+Pron	! pronoun					ich, keiner
	!:: PPERS (PERSON GENDER NUMBER CASE)
	!:: PronTYPE GENDER NUMBER CASE
	!:: PronTYPE INVAR
+PAdj	! pronoun-like adjective			(ein) solcher
	!:: PronTYPE GENDER NUMBER CASE (FLEX)
	!:: PronTYPE ADJPRED
	!:: PronTYPE INVAR
+Adpos	! pre- or postposition				in, an, zufolge
	!:: AdposTYPE
+Conj	! conjunction					und, weil, statt (zu)
	!:: ConjTYPE
+Ptcl	! particle					nicht, ja, zu
	!:: PtclTYPE
+Itj	! interjection					ach, oh, bravo
	!:: 
+Pref	! verb or compound prefix			(er kommt) an, be- (und entladen)
	!:: (PrefType) (CPD)
+Num	! spelled out numeral				zweiundzwanzig
	!:: NumTYPE GENDER NUMBER CASE (FLEX)
	!:: NumTYPE ADJPRED
+Symbol	! special symbol				Cis, @
	!:: SymbolTYPE
+Dig	! digital numeral				22
	!:: CPD
	! Note: used for compounds and derivations only
	! otherwise, digitals are treated in "special.xfst"

	       
! ---- sub catgeories -----------------------------------
!! NounTYPE ==
+Curr		! currency				DM, Euro
+Lang		! language				(das) Deutsch
+Unit		! physical unit				km/h
+Music		! music class				A-Dur		
+Month		! name of month				Januar
+Day		! name of day				Sonntag
+Title		! title	(mainly for abbr)		Herr, Dr.
+Symbol		! (music) symbol			(ein) Cis
+Acron		! acronym				AKW

!! PropTYPE ==
+Continent	! continent				Europa
+Country	! country, nation			Deutschland
+State		! Bundesland, Kanton, ...		Hessen, Uri, Arizona
+Region		! region, mountain, ...			Zugspitze, Sahara
+Town		! town, city				Stuttgart
+TownCountry	! town and/or country			Monaco
+TownState	! town and/or state			Bremen
+Sea		! ocean					Nordsee, Mittelmeer
+Lake		! lake					Bodensee
+River		! river					Rhein, Donau
+First		! first name				Hans, Mria
+Last		! family name				Schiller, Maier
+Org		! organisation				Bundestag, UNO
+Bus		! business name				Xerox, Daimler
+Prod		! product name				(ein) VW
+Doc		! law, document name			StVO (=Straßenverkehrsordnung)
+PrePart	! name part				San (Francisco)
+PostPart	! name part				(Mair) GmbH	
!! AdvTYPE ==
+Wh		! interrogative or relative		worauf, wie
+Quant		! quantifying				viel

!! VerbTYPE ==
+Aux		! auxiliar				sein, haben, werden
+Modal		! modal					können. sollen, ...
+Trans		! transitive (*)			er hängte es an die Wand
+Intr		! intransitive (*)			er hing an der Wand
		! (*) marked only if inflection differs!

!! AdposTYPE ==
+Prep		! preposition				"in (der Stadt)"
+Post		! postposition				"(den Angaben) zufolge"
+Crc1		! circumposition part1			"um (des Friedens willen)"
+Crc2		! circumposition part2			"(um des Friedens) willen"

!! ConjTYPE ==
+Coord		! coordinating				(er) und (sie)
+Coord1		! coordination part 1			entweder (... oder)
+Coord2		! coordination part 2			(weder ...) noch
+Subord		! subordinating				(er sagt,) dass
+SAdv		! sentence adverbial			aber    
+Infzu		! infinitival				ohne (zu fragen)

!! PronTYPE ==
+Indef		! indefinitie				ein (Mann)
+Def		! definite				der (Mann)
+Quant		! quantifying				viele
+Wh		! interrogative (or relative)		wann, worin
+Dem		! demonstrative				dieser
+Rel		! relative				(der Mann,) der
+Poss		! possessive				mein

!! NumTYPE ==
+Card		! cardinal				eins, dreizehn
+Ord		! ordinal				zweiter
+Fract		! fractal				sechzehntel

!! PtclTYPE ==
+Neg		! negation				nicht
+Sent		! sentential				ja, nein
+Izu		! infinitival 'zu'			zu (kommen)
+Prenum		! prenumeral				über (20 Stück)
+AMod		! adjective od adverb modifier		zu (klein), am (besten)
+Pos		! with positive (adj or adv)		zu (groß)
+Comp		! with comparative			umso (besser)
+Sup		! with superlative			am (schnellsten)

!! PrefTYPE ==
+Sep		! separable prefix			(er kommt) an
+Unsep		! unseparable prefix			be- und entladen
		! (only for truncated parts)		
+Unit		! for phys. units			centi-

!! SymbolTYPE ==
+Music	      	! music symbol				Cis
		! Note: other symbols are defined in "special xfst"

!--- inflectional features --------------------------------------------------
!! CPD ==
+Cpd		! truncated compound part		pseudo-, Haus-

!! ADJPRED ==
+Pred		! predicative or adverbial		(er ist/geht) schnell
+Attr		! attributive				(ein) lila (Kleid)

!! VINF ==
+Inf		! simple infinitive			ankommen, spielen
+Izu		! infinitive with "zu"			anzukommen
+PPres		! simple present participle		ankommend, spielend
+PPrzu		! present prtaiciple with "zu"		anzuwendend
+PPast		! past participle			gespielt, angekommen

!! VMOOD ==
+Indc		! indicative				(er) kommt
+Subj		! subjunctive				(er) komme
+IndcSubj	! indicative or subjunctive		(ich) komme
+Imp		! imperative				komm !

!! VTENSE ==
+Pres		! present				(er) kommt, spielt
+Past		! past					(er) kam, spielte

!! VPOS ==
+V1		! main clause position			(er) kommt (an) 
+V2		! subordinate position			(weil er) ankommt

!! PPERS == 
+Pers		! personal				ich, mich
+Refl		! reflexiv				sich  
+Recp		! reciprocal				einander

!! GENDER == 
+Masc		! masculine				der Mann
+Fem		! feminine				die Frau
+Neut		! neuter				das Haus
+MF		! masc or fem				(der/die) Angestellte
+MN		! masc or neut				(der/das) Junge
+FN		! fem or neut				(die/das) kleine (Frau/Kind)
+MFN		! any gender				die (Leute)
+Undef		! undefined (for plurals only)		Leute

!! NUMBER ==
+Sg		! singular				ein Mann
+Pl		! plural				die Männer
+SgPl		! singular or plural			(die) Iris

!! PERSON ==
+1P		! first person				ich gehe
+2P		! second person				du gehst, ihr geht
+3P		! third person				er geht
+1P3P		! first or third person			(wir/sie) gehen; (ich/er) kam

!! CASE ==
+Nom		! nominative				der Fall
+Acc		! accusative				den Fall
+Dat		! dative				dem Fall
+Dat2		! "old" dative "-e"			im Falle
+Gen		! genitive		 		des Falles
+GenS		! s-genitive (proper names)		das Werk Goethes (c.f. das Werk des alten Goethe)
+NomAcc		! nom or acc				mein Kind
+AccDat		! acc or dat				in (die/der Stadt)
+AccGen		! acc or gen				laut (dem/des Antrag/s)
+DatGen		! dat or gen				der Frau
+NomAccDat	! nom, acc or dat			(der/den/dem) Fall
+NomAccGen	! nom, acc or gen			(die/der) Fälle
+AccDatGen	! acc, dat or gen			(des/dem/den) Menschen
+NomAccDatGen	! any case				(die/der/den) Frauen

!! DEGREE ==
+Pos		! positive, basic form			groß, der kleine 
+Comp		! comparative				größer, der kleinere
+Sup		! superlative				größt, der kleinste
+Sup2		! superlative with "am"			(am) größten

!! FLEX ==
+St		! strong ("marked") inflection		der, kleinem
+Wk		! weak ("unmarked") inflection		ein, (dem) kleinen

!! INVAR ==
+Invar		! invariant for case/number/gender/...

! --- additional features ---------------------------------------
+Abbr		! abbreviation				Tel.
+Upp		! upper case
+Low		!
! usage
+Coll		! colloquial
+Elid		! elided 'e"				woll'n
+ElidS		! elided e before "'s"			wär ('s)

+WordPart	! part of multi-word expression		ab (und zu)


+Deriv		! derivation
+Dim		! diminutive

! spelling
^OldLem ^OldSrf	! old spelling,				Fluß
^NewLem	^NewSrf	! new spelling				Fluss
^ErrLem	^NewLem	! erroneous, but frequent spelling	erstmal

! ----------------------------------------------------
! INTERNAL DIACRITICS

! boundaries
^/	! compound linking element
^+	! inflection boundary,
^# ^-	! composition boundary
^} ^{	! derivation boundary
^]	! conversion boundary
^.	! segment boundary	(eg. zwei^.und^.zwanzig)
^$	! umlaut trigger
^* 	! exception marker
^|	! morpheme boundary
^~	! doubling marker
^_	! space
^&	! token boundary	(eg. geht+Verb^&es+Pron)
^=	! marker for separable verb prefix

! deletion markers
^1 ^2 ^3
^V

! compound markers
[*]	! no compound head (eg. Hab (und Gut) )
[-]	! only hyphenated compound (eg. Piano-Fischer+Prop)
[&]	! only compound affix (eg. (hoch)-rangig)
[^]	! compound only with hyphen and without case change
[=]	! for lexical compounds with case change (in derivations)
	! eg. Baden-Württemberg + }isch -> baden-württembergisch
[_]	! optional space (eg. "u.U.", "u. U."

! verb markers
[G]	! "ge" prefix for past particple (eg. [G]spielen)
[P]	! separable prefix (within word: nach[P][Z][G]ahmen)
[S]	! prefix marker (beginning of word: [S]ahmen)
[Z]	! "zu" infix (eg. miss[Z]verstehen)

! lexical spelling markers
[old]
[new]
[err]

! optional "e"
^e	! "e" may be omitted (eg. beinah^e -> beinah)
^E	! "e" may be added (eg. Kupp^Elung -> Kuppelung)
^a	! "a" may be omitted (eg. daran -> dran)

^AdjE	! "e" in adjective ending
^OptE	! "e" before genitive -s
^ImpE	! "e" e for imperative singular
^InsE	! "e" before verbal -(s)t
^InsE2	! "e" before verbal -st in irregular past tense
^SupE	! "e" before superlative -st

^PGen	! genitive "s" (or "'" or "ens") with proper names
^PPl	! plural "s" (or "-ens") with proper names

! -----------------------------------
! flag diacritics 

! spelling markers
@U.lem.old@ @U.lem.new@ @U.lem.err@ @U.lem.any@
@R.lem.old@ @R.lem.new@ @R.lem.err@
@U.srf.old@ @U.srf.new@ @U.srf.err@ @U.srf.any@
@R.srf.old@ @R.srf.new@ @R.srf.err@

! negative prefix "un-"
@C.un@ @D.un@ @P.un.on@

! superlative prefix "aller-"
@C.sup@ @D.sup@ @P.sup.on@

! verb control
@C.ge@ @U.ge.on@ @U.ge.off@ @P.ge.on@ @P.ge.off@ @R.ge.on@ @R.ge.off@
@C.zu@ @U.zu.on@ @U.zu.off@ @P.zu.on@ @P.zu.off@ @R.zu.on@ @R.zu.off@
@C.sep@ @U.sep.on@ @U.sep.off@ @P.sep.on@ @P.sep.off@ @R.sep.on@ @R.sep.off@ @U.sep.any@
@U.vpref.1@ @U.vpref.2@

! first letter upper/lower case 
@U.frm.upp@ @U.frm.low@

! compounding
@P.cpd.yes@ @U.cpd.yes@	! direct compound 
@P.cpd.dsh@ @U.cpd.dsh@	! compound with hyphen
@U.cpd.no@		! no compound
@D.cpd.no@

! ------------------------------------------------------------
LEXICON Root
!#!						FuncWords ;
						SingleStems ;
< ( [ "@U.cpd.yes@" "^#"
    | ("*PREF*") "@U.cpd.dsh@" "^-" ] ) >	Root1 ;

LEXICON Root1
 < [ "@U.frm.upp@" 0:%^Upp | "@U.frm.low@" 0:%^Low ]
   ( "@P.sup.on@" a l l e r "^{" )
   ( "@P.un.on@" u n "^{" ) >		Stems ;

LEXICON Stems
@C.zu@@C.ge@@C.sep@		VerbStems ;
@C.zu@@C.ge@@C.sep@		VerbPrefix1 ;
@C.zu@@C.ge@@C.sep@		VerbPrefix2 ;
				OtherStems ;
				Numerals ;


! ----------------------------------------------
! FINAL LEXICONS
! ----------------------------------------------

LEXICON Fin	
! prefix control flag must be "consumed"
	< "@D.sup@" "@D.un@" (%^ELID) >		XX ;

LEXICON XX
! control spelling markers
	< "@R.lem.old@" %^OldLem:0 >	SPELL2 ;
	< "@R.lem.new@" %^NewLem:0 >	SPELL2 ;
	< "@R.lem.err@" %^ErrLem:0 >	SPELL2 ;
	< "@U.lem.any@" >		SPELL2 ;

LEXICON SPELL2
	< "@R.srf.new@" %^NewSrf:0 >	# ;
	< "@R.srf.old@" %^OldSrf:0 >	# ;
	< "@R.srf.err@" %^ErrSrf:0 >	# ;
	< "@U.srf.any@" >		# ;

LEXICON Upp		@U.frm.upp@	Fin ;
LEXICON Low		@U.frm.low@	Fin ;


LEXICON inv		@U.cpd.no@	Low ;
LEXICON Inv		@U.cpd.no@	Upp ;

! -------------------------------------------------------------------
! UNSEPARABLE VERB PREFIXES (may occur truncated)
LEXICON PUnsep		
  +Unsep@U.cpd.no@	Trunc0 ;	! be- und entladen
					! das Be- und Entladen

! -------------------------------------------------------------------
! SEPARABLE VERB PREFIXES
! (A) simple ("unproductive") prefixes, i.e.
!     i.e. verb forms must be lexicalised with their prefixes
! LX: 	ein+Pref  PSep ;
!	[old]eis+Pref	PSep ;
! ==>	eis		:: eis+Pref+Sep^OldLem^OldSrf
!	eis-		:: eis+Pref+Sep+Cpd^OldLem^OldSrf
!	ein		:: ein+Pref+Sep
!	ein-		:: ein+Pref+Cpd

LEXICON PSep		
  +Sep:0		inv ;		! er kommt an
  +Sep@U.cpd.no@	Trunc0 ;	! an- und abfahren
					! An- und Abreise

! -------------------------------------------------------------------
! SEPARABLE VERB PREFIXES
! (B) prefixes which combine with "any" verb
! (Note: other readings must be lexicxalised separately !)
! LX: 	hinaus+Pref    VSep1 ;
!	hinaus+Adv     XX ;
!
! ==>	hinaus		:: hinaus+Pref+Sep
!	hinaus-		:: hinaus+Pref+Sep+Cpd
!	hinausstellen	:: hinaus+Pref^=stellen...
!	hinausgestellt	:: hinaus+Pref^=stellen+Verb+PPast...
!	Hinausstellung	:: hinaus+Pref^=Stellung+Noun...
!
! (C) "old" prefixes which correspond to adverbs
!     (always separeted in new spelling!)
! LX: aufeinander+Adv  VSepAdv ;
! ==>   aufeinander		:: aufeinander+Adv
!	aufeinander-		:: aufeinander+Adv+Cpd
!	aufeinanderstellt	:: aufeinander+Adv^=stellen...^Old
!	aufeinandergestellt	:: aufeinander+Adv^=stellen+Verb+PPast...^Old
!				:: aufeinander+Adv^#stellen+Verb+PPast^]+Adj...
!	Aufeinanderstellung	:: aufeinander+Adv^#Stellung+Noun...
!				:: aufeinander+Adv^=Stellung+Noun...^Old
!
! (D) multiple prefix
! LX:	wieder+Adv  VSep2 ;
! ==>   wieder			:: wieder+Adv
!	wieder-			:: wieder+Adv+Cpd
!	wiederkommen		:: wieder+Adv^=kommen+Verb...^Old
!	wiederhereinkommen	:: wieder+Adv^=herein+Pref^=kommen+Verb...^Old
! -------------------------------------------------------------------------------
LEXICON VSep0
 [P][Z]			VerbStems ;	! rück-[stellen]

LEXICON VSep1		
 @U.vpref.1@		PSep ;
 [P][Z]			VerbStems ;

LEXICON VSep2
 @U.vpref.1@		inv ;		! wieder+Adv	
 @U.vpref.1@		Trunc ;		! wieder+Adv+Cpd
 @U.vpref.2@[old][P][Z]	VerbPrefix1 ;	! wiederhereinkommen
 @U.vpref.2@[P][Z]	VerbStems ;	! wiederkommen

LEXICON VSepAdv
 @U.vpref.1@		inv ;		! aufeinander+Adv	
 @U.vpref.1@		Trunc ;		! aufeinander+Adv+Cpd
! [old][P][Z]		VerbStems ;
 [P][Z]			VerbStems ;


LEXICON Compound	< "@D.cpd.no@" "@P.cpd.yes@" "^#" >	Root1 ;
LEXICON Hyphen		< "@D.cpd.no@" "@P.cpd.dsh@" "^-" >	Root1 ;

LEXICON Trunc0		+Cpd:-		Fin ;

LEXICON Trunc		Trunc0 ; Compound ; Hyphen ;
LEXICON Trunc-		Trunc0 ; Hyphen ;


