! -------------------------------------------------------------------------
! CONTENT:	German morphology: noun inflection
! -------------------------------------------------------------------------

! ----------------------------------------------------------------------------
! COMPOUNDING
! ----------------------------------------------------------------------------

LEXICON Cp		+Sg 		Trunc ;
LEXICON Cp_s		+Sg^/s		Trunc ;
LEXICON Cp_Es		+Sg^/^OptEs	Trunc ;
LEXICON Cp_en		+Sg^/en		Trunc ;

LEXICON Cp_Pl		+Pl		Trunc ;


! ----------------------------------------------------------------------------
! NOUN INFLECTION 
! ----------------------------------------------------------------------------

! ----------------------------------------------------------------------------
! singular
! ----------------------------------------------------------------------------
LEXICON	N=Sg		
	+Sg+NomAccDatGen:0		Upp ;	! [die/der] Frau
	+Sg:0				Trunc- ;

LEXICON N_Sg		N=Sg ;			! Tür, Geburt
			Cp ;			! Tür-[klinke]
			Cp_s ;			! Geburts-[stunde]

! ----------------------------------------------------------------------------
LEXICON	N=SgEs		
	+Sg+NomAccDat:0		Upp ;	! [der/den/dem] Mann
	+Sg+Gen:00^+^OptEs 	Upp ;	! [des] Mann(e)s
	+Sg+Dat2:00^+e		Upp ;	! [dem] Manne
	+Sg:0			Trunc- ;

LEXICON N_SgEs		N=SgEs ;		! Mann, Bild, Tisch
			Cp ;			! Tisch-[tuch]
			Cp_Es ;		! Mann(e)s-[kraft]

! ----------------------------------------------------------------------------
LEXICON	N=Sg(Es)	
	+Sg+NomAccDatGen:0 	Upp ;	! [der/den/dem/des] Korpus 
	+Sg+Gen:00^+^OptEs 	Upp ;	! [des] Korpusses
!?	+Sg+Dat2:00^+e		Upp ;	! [dem] Korpusse
	+Sg:0			Trunc- ;
	
LEXICON N_Sg(Es)	N=Sg(Es) ;
			Cp ;			! Korpus-[teil]


! ----------------------------------------------------------------------------
LEXICON	N=Sgs		
	+Sg+NomAccDat:0 	Upp ;	! [der/den/dem] Bikini
	+Sg+Gen:00^+s		Upp ;	! [des] Bikinis
	+Sg:0			Trunc- ;

LEXICON N_Sgs
			N=Sgs ;		! Bikini, Kino
			Cp ;		! Bikini-[teil], Kino-[kasse]

LEXICON N_SgS
			N_Sgs ;		! Überleben
			Cp_s ;		! Überlebens-[kampf]

! ----------------------------------------------------------------------------
LEXICON	N=Sg(s)	
	+Sg+NomAccDatGen:0 	Upp ;	! [der/den/dem/des] Ainu, Barock
	+Sg+Gen:00^+s		Upp ;	! [des] Ainus, Barocks
	+Sg:0			Trunc- ;

LEXICON N_Sg(s)
			N=Sg(s) ;		! 
			Cp ;			! Ainu-[kunst], Barock-[kirche]

		
! ----------------------------------------------------------------------------
! Plural
! ----------------------------------------------------------------------------

LEXICON	N=Pln 	
	+Pl+NomAccGen:0 	Upp ;	! [die/der] Lehrer, Leute
	+Pl+Dat:00^+n		Upp ;	! [den] Lehrern, Leuten
	+Pl:0			Trunc- ;

LEXICON	N=Pl 		
	+Pl+NomAccDatGen:0 	Upp ;	! [die/den/der] Retinae
	+Pl:0			Trunc- ;

! ----------------------------------------------------------------------------


LEXICON N_Pl		N=Pl ;			! [die/den] Kosten
			Cp_Pl ;			! kosten-[günstig]

LEXICON N_Pln		N=Pln ;			! [die] Leute; [den] Leuten
			Cp_Pl ;			! Leute-[schinder]


! ----------------------------------------------------------------------------
! invariant
! ----------------------------------------------------------------------------
LEXICON N=inv	
	+SgPl+NomAccDatGen:0 	Upp ;	! Iris ;
	+Sg:0			Trunc- ;

LEXICON N_inv		N=inv ;			! Iris ;
			Cp ;			! Iris-[blüte]


! ----------------------------------------------------------------------------
! combinations
! ----------------------------------------------------------------------------
LEXICON Nf=en		N=Sg ;			! [die/der] Frau, Zeitung
			0:^+en N=Pl ;		! [die/der/den] Frauen, Zeitungen

LEXICON Nf_en		Nf=en ;			!
			Cp ; 			! Technik-[fan]
			Cp_s ;			! Zeitungs-[blatt]
			0:^+en Cp_Pl ;		! Frauen-[verein]

LEXICON Nf=n		N=Sg ;			! [die/der] Tafel
			0:^+n N=Pl ;		! [die/der/den] Tafeln

LEXICON Nf_n		Nf=n ;			!
			Cp ; 			! Tafel-[kreide]
			0:^+n Cp_Pl ;		! Nummern-[konto]



! ----------------------------------------------------------------------------
LEXICON Nf=s		N=Sg ;			! [die/der] Pizza, Saison
			0:^+s N=Pl ;		! [die/der/den] Pizzas, Saisons

LEXICON Nf_s		Nf=s ;			!
			Cp ; 			! Pizza-[kauf], saison-[bereinigt]
!!!			0:^+s Cp_Pl ;		! Pizzas-[???] 

! ----------------------------------------------------------------------------
LEXICON Nf=e		N=Sg ;	 		! [die/der] Reling, Kenntnis, Mühsal
			0:^+e N=Pln ;		! [die/der] Relinge, Kenntnisse; [den] -n

LEXICON Nf_e		Nf=e ;			!
			Cp ; 			! Kenntnis-[prüfung]
			Cp_s ;			! mühsals-[voll]
!!!			0:^+e Cp_Pl ;		! Relinge-[???]

! ----------------------------------------------------------------------------
LEXICON Nf=$e		N=Sg ; 			! [die/der] Nacht, Auskunft
			0:^$^+e N=Pln ;		! [die/der] Nächte, Auskünfte; [den] -n

LEXICON Nf_$e		Nf=$e ;
			Cp ; 			! Nacht[-wache]
			Cp_s ;			! Auskunfts-[tafel]
			0:^$^+e Cp_Pl ;		! nächte-[lang]

! ----------------------------------------------------------------------------
LEXICON Nf=$		N=Sg ;	 		! [die/der] Mutter
			0:^$^+ N=Pln ;		! [die/der] Mütter, [den] -n

LEXICON Nf_$		Nf=$ ;	 		!
			Cp ;  			! Mutter-[tag]
			0:^$^+ Cp_Pl ;		! Mütter-[heim]

! ----------------------------------------------------------------------------
LEXICON N=0		N=Sgs ;			! [der] Wagen; [des] -s
						! [das] Zeichen; [des] -s
			N=Pl ;			! [die/den] Wagen, Zeichen;

LEXICON N_0		N=0 ;
			Cp ;			! Wagen-[teil], Zeichen-[größe]
			Cp_s ;			! Lebens-[zeit]


LEXICON N=0n		N=Sgs ;			! [der] Lehrer, [des] -s
						! [das] Alter; [des] -s
			N=Pln ;			! [die] Lehrer, [den] -n

LEXICON N_0n		N=0n ;
			Cp ;			! Lehrer-[zimmer]
			Cp_s ;			! Lehrers-[gattin]
						! Alters-[vorsorge]

! ----------------------------------------------------------------------------
LEXICON N=$		N=Sgs ;			! [der] Garten, [des] -s
			0:^$^+ N=Pl ;		! [die, den] Gärten

LEXICON N_$		N=$ ;
			Cp ;			! Garten-[möbel]
			0:^$^+ Cp_Pl ;		! Gärten-[???]

LEXICON N=$n		N=Sgs ;			! [der] Vater, [des] -s
						! [das] Kloster, [des] -s
			0:^$^+ N=Pln ;		! [die] Väter, [den] -n
						! [die] Klöster, [den] -n

LEXICON N_$n		N=$n ;
			Cp ;			! Vater-[mord]
						! Kloster-[garten]
			Cp_s ;			! Handels-[schule]
			0:^$^+ Cp_Pl ;		! Väter-[schule]


! ----------------------------------------------------------------------------
LEXICON N=n		N=Sgs ; 		! [der] Muskel, [des] -s
			0:^+n N=Pl ;		! [die/den] Muskeln

LEXICON N_n		N=n ;	 		!
			Cp ;			! Muskel-[kater]
			0:^+n Cp_Pl ;		! Vettern-[wirtschaft]

! ----------------------------------------------------------------------------
LEXICON N=s		N=Sgs  ;		! [der/den/dem] Scheck, Bankier; [des] -s
						! [das/dem] Büro, Atelier; [des] -s
			0:^+s N=Pl ;		! [die] Schecks
						! [die] Büros, Ateliers

LEXICON N_s		N=s  ;			! 
			Cp ;			! Scheck-[betrug]
						! Büro-[möbel]
			Cp_s ;			! Bankiers-[gattin]
						! Ateliers-[wohnung]


LEXICON N=(s)		N=Sg(s)  ;		! [der/den/dem] Ainu; [des] -(s)
			N=Pl ;			! [die] Ainu
			0:^+s	N=Pl ;		! [die] Ainus

LEXICON N_(s)		N=(s)  ;		! [der] Bantu ; [des] Bantu(s)
			Cp ;			! Bantu-[volk]


! ----------------------------------------------------------------------------
LEXICON N=en		N=SgEs ;		! [der/den/dem] Staat; [des] -(e)s; [dem] -e
						! [das] Bett, Auge; [des] -(e)s; [dem] -e
			0:^+en N=Pl ;		! [die/den] Staaten, Doktoren
						! [die/den] Augen, Betten

LEXICON N_en		N=en ;			!
			Cp ;			! Doktor-[titel]	
						! Bett-[tuch]
			Cp_Es ;			! Staats-[macht]
						! Hemds-[ärmel]
			0:^+en Cp_Pl ;		! Doktoren[-titel], Staaten-[bund]
						! Augen[-lid], Betten-[verkauf]

! ---------------------------------------------------------------------------- 
LEXICON N=e		N=SgEs ;		! [der] Brief, [des] -(e)s; [dem] -e 
						! [das] Denkmal, Bier, -(e)s; [dem] -e 
			0:^+e N=Pln ;		! [die] Briefe, [den] Briefen
						! [die] Denkmale, Biere, [den] -n

LEXICON N_e		N=e ;			!
			Cp ;			! Brief[-umschlag]
						! Bier[-glas]
			Cp_Es ;			! Angriffs-[ziel]
						! Denkmals-[amt]
			0:^+e Cp_Pl ;		! Briefe-[schreiber]
						! Geräte-[beschreibung]

! ---------------------------------------------------------------------------- 
LEXICON N=$e		N=SgEs ;		! [der/den] Fall, [dem] -(e), [des] -(e)s
						! [das] Floß, [dem] -(e), [des] -es
			0:^$^+e N=Pln ;		! [die] Fälle, [den] -n
						! [die] Flöße, [den] -n

LEXICON N_$e		N=$e ; 			!
			Cp ;			! Fall-[beispiel]
						! Floß-[besitzer]
			Cp_Es ;			! Gesang(e)s-[einlage]
			0:^$^+e Cp_Pl ;		! Gäste-[buch]
						! Flöße-[???]

! ---------------------------------------------------------------------------- 
LEXICON N=$er		N=SgEs ;		! [der] Mann, [dem] -(e), [des] -(e)s
						! [das] Bild, Haus; [dem] -(e); [des] -(e)s
			0:^$^+er N=Pln ;	! [die] Männer, [den] -n
						! [die] Bilder, Häuser; [den] -n

LEXICON N_$er		N=$er ; 		!
			Cp ;			! Mann-[deckung]
						! Haus-tier
			Cp_Es ;		! Grab(e)s-[stimme]
						! Mann(e)s-[lust]
			0:^$^+er Cp_Pl ;	! Männer-[chor]
						! Bilder-[galerie]

! ---------------------------------------------------------------------------- 
LEXICON Nm_ns
	+Sg+Nom:0		Upp ;		! [der] Name
	+Sg+Gen:00^+ns		Upp ;		! [des] Namens
	+Sg+NomAccDat:00^+n	Upp ;		! [der/dem/den] Namen	
		0:^+n		N=Pl ;		! [die/den] Namen
		0:^+n		Cp_Pl;		! Namen-[buch]
		0:^+n		Cp_s;		! Namens-[träger]

LEXICON Nn.Herz
	+Sg+NomAccDat:0 	Upp ;		! [das] Herz
	+Sg+Gen:00ens		Upp ;		! [des] Herzens
	+Sg+Gen:00es		Upp ;		! [des] Herzes	(med.)
	+Sg+Dat:00en		Upp ;		! [dem] Herzen
	+Pl+NomAccDatGen:00en 	Upp ;		! [die/den] Herzen
	+Pl+NomAccGen:00e 	Upp ;		! [die] Herze (med.)
				Cp;		! Herz-[förmig]
		0:^+en		Cp_s;		! Herzens-[brecher]


LEXICON Nf.Langeweile
 0weile+Noun+Fem+Sg+NomAccDatGen	Upp ;	! [die/der] Langeweile
 0weile+Noun+Fem+Sg+DatGen+Wk:nweile 	Upp ;	! [der] Langenweile
 0weile+Noun+Fem+Sg+DatGen+St:rweile 	Upp ;	! [aus/trotz] Langerweile

LEXICON Nf.Langezeit				! schweiz.
 0zeit+Noun+Fem+Sg+NomAccDatGen		Upp ;	! [die/der] Langezeit
 0zeit+Noun+Fem+Sg+DatGen+Wk:nzeit 	Upp ;	! [der] Langenzeit
 0zeit+Noun+Fem+Sg+DatGen+St:rzeit 	Upp ;	! [aus/trotz] Langerzeit




! ----------------------------------------------------------------------------
! special classes


LEXICON	N_s/sse 	0:^~ N=Sg(Es) ;		! [der] Fokus; [des] Fokus(ses)
			Cp ;			! Fokus-[kamera]
			0:se	N=Pln ;		! [die] Fokusse; [den] Fokussen

LEXICON	N_us/en 	0:^~ N=Sg(Es) ;		! [der] Globus; [des] Globus(ses)
			Cp ;			! Globus-[verkauf]
			0:^2en	N=Pl ;		! [die/den] Globen
			0:^2en	Cp_Pl ;	! Globen-[handel]

LEXICON	N_us/i 		0:^~ N=Sg(Es) ;		! [der] Kumulus; [des] Kumulus(ses)
			Cp ;			! Kumulus-[wolke]
			0:^2i	N=Pl ;		! [die/den] Kumuli
!!!			0:^2i	Cp_Pl ;		! Kumuli-[???]


LEXICON	N_os/en 	N=Sg ;			! [der/des] Mythos
			Cp ;			! Mythos-[träger]
			0:^2en	N=Pl ;		! [die/den] Mythen
			0:^2en	Cp_Pl ;		! Mythen-[glaube]


LEXICON Nf_a/en		N=Sg ;	 		! [die/der] Firma, Thuja
			0:^1en N=Pl ;		! [die/den] Firmen, Thujen
			Cp ; 			! Thuja-[hecke]
			0:^1en Cp_Pl ;		! Firmen-[sitz]

LEXICON Nf_is/en	N=Sg ;			! [die] Dosis, Basis
			0:^2en N=Pl ;		! [die/den] Dosen, Basen
			Cp ; 			! Basis-[lager]
			0:^2en Cp_Pl ;		! Praxen-[besuch]

LEXICON Nf_s/den	N=Sg ;			! [die] Glottis, Hepatitis
			0:^1den N=Pl ;		! [die/den] Glottiden, Hepatiden
			Cp ; 			! Hepatitis-[fall]
			0:^1den Cp_Pl ;		! Glottiden-[???]




LEXICON	N_um/en 	N=Sgs ;			! [das] Datum, Album; [des] Datums, Albums
			Cp ;			! Album-[bild]
			Cp_s ;			! Datums-[grenze]
			0:^2en	N=Pl ;		! [die/den] Daten, Alben
			0:^2en	Cp_Pl ;		! Daten-[angabe]

LEXICON	N_um/a 		N=Sgs ;			! [das] Maximum; [des] Maximums
			Cp ;			! Maximum-[wert]
			Cp_s ;			! Maximums-[angabe]
			0:^2a	N=Pl ;		! [die/den] Maxima
			0:^2a	Cp_Pl ;		! Maxima-[angabe]

LEXICON N_on/a		N_um/a ;
LEXICON N_on/en		N_um/en ;

LEXICON	N_o/i/os	N=Sgs ;			! [das] Solo; [des] Solos
			Cp ;			! Solo-[stimme]				
			0:^1i 	N=Pl ;		! [die/den] Soli
			0:^1i 	Cp_Pl ;		! Soli-[???]
			0:s	N=Pl ;		! Solos ;


LEXICON N_a/en		N=Sgs ;			! [das] Thema; [des] Themas
			Cp ;			! Thema-[suche]
			0:^1en	N=Pl ;		! [die/den] Themen
			0:^1en	Cp_Pl ;		! Themen-[wahl]

LEXICON N_en/ina	N=Sgs ;			! [das] Pronomen; [des] Pronomens
			Cp ;			! Pronomen-[bindung]
			0:^2ina	N=Pl ;		! [die/den] Pronomina
			0:^2ina	Cp_Pl ;		! Pronomina-[gruppe]


LEXICON N_x/e/izes	N=Sg(Es) ;		! [der] Index; des -(es)
			Cp ;			! Index-[seite]
			0:^+e	N=Pln ;		! Indexe
			0:^2izes N=Pl ;		! Indizes
			0:^2izes Cp_Pl ;	! Indizes-[???]

! ----------------------------------------------------------------------------
! "WEAK" noun inflection
! ----------------------------------------------------------------------------

LEXICON	Nm=Wk	
	+Sg+Nom:0			Upp ;	! [der] Automat
	+Sg+AccDatGen:00^+en		Upp ;	! [den/dem/des] Automaten
	+Pl+NomAccDatGen:00^+en		Upp ;	! [die/den] Automaten

LEXICON	Nm=Wk2	
	+Sg+Nom:0			Upp ;	! [der] Nachbar
	+Sg+AccDatGen:00^+n		Upp ;	! [den/dem/des] Nachbarn
	+Pl+NomAccDatGen:00^+n		Upp ;	! [die/den] Nachbarn
			

LEXICON	Nf=Wk	
	+Sg+NomAcc:0			Upp ;	! [die] Gerade
	+Sg+DatGen:00^+en		Upp ;	! [der] Geraden
	+Pl+NomAccDatGen:00^+en		Upp ;	! [die/den] Geraden
			

! with compounding:
LEXICON Nm_Wk
				Nm=Wk ;
				Cp ;		! ??
	0:^+en			Cp_Pl ;

LEXICON Nm_Wk2
				Nm=Wk2 ;
				Cp ;		! ??
	0:^+n			Cp_Pl ;		! ??

LEXICON Nm_WkS					! cf. Duden Grammatik [436]
				Nm_Wk ;		! der Fürst, dem/den/des/die Fürsten
				N=SgEs ;	! der/dem/den Fürst, des Fürsts
			
LEXICON Nf_Wk
				Nf=Wk ;
				Cp ;		! ??
	0:^+en			Cp_Pl ;

			
! ----------------------------------------------------------------------------
! PROPER NAMES

LEXICON Name=Sg
  +Sg+NomAccDatGen:0		Upp ;	! Peter, Anna, Hans; der kleinen Anna
  +Sg+GenS:^PGen		Upp ;	! Peters, Annas, Hans'
  +Sg:0				Trunc- ;  

LEXICON Name_Sg
	Name=Sg ;
	Compound ;


! ------------------------------------------------------------------------------
! Last names
LEXICON P=Last
  +Sg+NomAccDatGen:0		Upp ;		! das Werk des jungen Goethe
  +Pl+NomAccDatGen:^PPl		Upp ;		! bei Müllers
  +Sg+GenS:^PGen		Upp ;		! das Werk Goethes
  ^}sch+Adj			A_reg ;		! schillersche
  +Sg:0				Trunc- ;	! Schiller-[Schule]

LEXICON P_Last			P=Last ;
				Compound ;	! Roentgen[apparat]



